 2 REM # CALENDAR
 4 REM # JOHN R.HONNIBALL
 6 REM # 08/08/1980
 100 DIMD$(7),M$(12),D(12),S(12)
 110 FORM=1TO12:READM$(M),D(M),S(M):NEXT
 120 FORN=1TO7:READD$(N):NEXT
 130 POKE11,0:POKE12,253
 140 PRINTCHR$(12)"CALENDAR":FORN=1TO8:PRINTCHR$(135);
 145 NEXT:PRINT:PRINT
 150 INPUT"Which Month ";M$:M=0
 160 FORN=1TO12:IFLEFT$(M$,3)=LEFT$(M$(N),3)THENM=N:N=12
 170 NEXT:IFM=0THENPRINT"TRY AGAIN!":GOTO150
 180 INPUT"Which Year  ";Y:C=S(M)+Y+INT(Y/4)+1
 190 C=INT(C-7*INT(C/7))+1
 200 D(2)=28:IFY/4=INT(Y/4)THEND(2)=29
 205 IFY/4<>INT(Y/4)ANDM<3THENC=C+1
 210 PRINTCHR$(12)TAB(10)M$(M);Y
 220 PRINTTAB(10);:FORN=1TOLEN(M$(M)+STR$(Y)):PRINTCHR$(135);:NEXT
 230 PRINT:D=2-C
 240 FORX=1TO7:Y=11:IFX<CTHENY=Y+4
 250 PRINTD$(X);:D1=D
 260 IFX<CTHEND1=D+7
 270 PRINTTAB(Y)D1;:D1=D1+7:Y=Y+4
 280 IFD1<=D(M)THEN270
 290 PRINT:IFX<>7THENPRINT
 300 D=D+1:NEXT:X=USR(X):GOTO140
 310 DATAJANUARY,31,3,FEBRUARY,28,6
 320 DATAMARCH,31,7,APRIL,30,3
 330 DATAMAY,31,5,JUNE,30,1,JULY,31,3
 340 DATAAUGUST,31,6,SEPTEMBER,30,2
 350 DATAOCTOBER,31,4,NOVEMBER,30,7
 360 DATADECEMBER,31,2
 370 DATAMonday,Tuesday,Wednesday,Thursday
 380 DATAFriday,Saturday,Sunday
