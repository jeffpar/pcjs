0 REM *Graphplotter*
1 POKE11,233:POKE12,255:GOSUB15
2 DEFFNY(X)=:REM......................................................
3 INPUT" Plotting interval ";A,B
4 GOSUB26
5 FORX=ATOBSTEPSX:FX=FNY(X)/MX*13
6 POKE54159-A/SX+X/SX-64*INT(FX),128+8*(FX-INT(FX)):NEXT
7 PRINT" Press 'space'";
8 X=USR(X):IFPEEK(533)<>32THEN8
9 PRINTC$:PRINT"  PRESS '1' FOR ANOTHER FUNCTION"
10 PRINTTAB(8)"'2' FOR ANOTHER PLOT."
11 X=USR(X):IFPEEK(533)=49THENRUN
12 IFPEEK(533)=50THEN3
13 GOTO11
14 :
15 C$=CHR$(26):PRINTC$:INPUT" f(X)";A$
16 LN=LEN(A$):I=768
17 IFPEEK(I)<>171THENI=I+1:GOTO17
18 FORJ=1TOLN:RESTORE:ST$=MID$(A$,J,1)
19 FORK=1TO11:READOP$,OP
20 IFST$=OP$THENPOKEI+J,OP:NEXTJ
21 NEXTK:POKEI+J,ASC(ST$):NEXTJ
22 POKEI+J,58:POKEI+J+1,142:RETURN
23 DATA+,163,-,164,*,165,/,166,^,167
24 DATAL,181,E,182,C,183,S,184,T,185,A,186
25 :
26 PRINTC$;:SX=(B-A)/43:MX=FNY(A)
27 FORX=ATOBSTEPSX
28 IFABS(FNY(X))>MXTHENMX=ABS(FNY(X))
29 NEXT:FORX=1TO43
30 POKE54158+X,132:NEXT:FORX=0TO31
31 POKE53263+64*X-A/SX,139:NEXT
32 POKE54159-A/SX,219:RETURN
