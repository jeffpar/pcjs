100 REM Kaleidoscope program
110 REM This program is inspired by material appearing in
120 REM   The Apple II Reference Manual, January 1978, p. 55
130 KEY OFF:WIDTH 40:SCREEN 0,1:COLOR 7,0,0:LOCATE ,,0:CLS
140 LOCATE 25,11:PRINT "PRESS ANY KEY TO EXIT";
150 FOR L=4 TO 40:FOR I=1 TO 12:FOR J=0 TO 11
160 K=I+J:COLOR (J*4/(I+2)+L) MOD 16,0
170 LOCATE K,I+9:PRINT CHR$(219)
180 LOCATE I,K+9:PRINT CHR$(219)
190 LOCATE 24-I,33-K:PRINT CHR$(219)
200 LOCATE 24-K,33-I:PRINT CHR$(219)
210 LOCATE I,33-K:PRINT CHR$(219)
220 LOCATE 24-K,I+9:PRINT CHR$(219)
230 LOCATE K,33-I:PRINT CHR$(219)
240 LOCATE 24-I,K+9:PRINT CHR$(219)
250 IF INKEY$<>"" THEN 280
260 NEXT J:NEXT I:NEXT L
270 GOTO 150
280 COLOR 7,0,0:CLS
290 END
