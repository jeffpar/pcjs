---
layout: page
title: "PC-SIG Library Disk #260"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0260/
machines:
  - id: ibm5160
    type: pcx86
    config: /machines/pcx86/ibm/5160/cga/256kb/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk 0260"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5160" %}

{% comment %}info_begin{% endcomment %}

## Information about "GAME SERIES NO 17"

    This is a good game disk with some neat stuff like JETSET, a flight
    simulator program, and a couple of cribbage games.  SIMONSEZ is a fun
    way for the youngest of kids to get hooked on computers, while
    training their memories at the same time.  Dueling Starships and the
    Tron game are made for two people and are both highly recommended.
    
    Usage:  Entertainment.
    
    System Requirements: Some programs require color graphics, BASIC
    
    How to Start: To read DOC files, enter TYPE filename.ext and press
    <ENTER>.  To run an EXE program, just type its name and press <ENTER>.
    For instructions on running BASIC programs, please refer to the
    GETTING STARTED section in this catalog.
    
    File Descriptions:
    
    DSS      EXE  Dueling Starships
    DSS      DOC  Documentation for Dueling Starships
    CRIB2    BAS  Game of Cribbage, enjoy
    JETSET   BAS  Real Time Simulation of B747 jet flight
    CRIBBAGE BAS  Game of cribbage
    JETSET   DOC  Documentation for JETSET.DOC
    SIMONSEZ BAS  Remember the sequence game for young children
    SCROLL1  ML   Data file for CRIBBAGE.BAS
    SLOTMACH BAS  Nevada style
    SIMONSEZ DOC  Documentation for SIMONSEZ.BAS
    TRONLC   EXE  Compiled version of TRON
    TRONLC   BAS  Game of TRON
    MORSE    BAS  Morse code learning program
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## CRIB2.BAS

```bas
10      REM===============================================================
20      REM * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
30      REM                CRIBBAGE FOR THE IBM PC
40      REM            BY CHRIS LINDELL, L.C SYSTEMS
50      REM                 P. O. BOX 5360
60      REM            CORALVILLE,LA 52241
70      REM            (319) 354-2630
80      REM * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
90      REM===============================================================
100     CLS:KEY OFF:LOCATE 12,25:PRINT " CRIBBAGE ":LOCATE 14,37:PRINT " INITIALIZING VARABLES"
110     I = 0:J = 0:RANDOMIZE VAL(RIGHT$(TIME$,2))
120     DIM D(52,4),C$(52),I(52),W(6,4),M(6,4),Y(6,4),C(4,4),D$(6),V(15,7),Q(11,6),R(4,5),S(4),J(52)
130     REM=================INITIALIZING VARIABLES =======================
140     FOR N = 1 TO 15:FOR M = 1 TO 7:READ V(N,M):NEXT M:NEXT N
150     FOR N = 1 TO 11:FOR M = 1 TO 6:READ Q(N,M):NEXT M:NEXT N
160     FOR N = 1 TO 4:FOR M = 1 TO 5:READ R(N,M):NEXT M:NEXT N
170     FOR N = 1 TO 4:READ S(N):NEXT N
180     FOR N = 1 TO 6:READ D$(N):NEXT N
190     FOR I = 1 TO 13
200     READ C$:C$ = C$ +"  "
210     MID$(C$,3,12)=CHR$(6)
220     C$(I)=C$
230     MID$(C$,3,12)=CHR$(4)
240     C$(I+13)=C$
250     MID$(C$,3,12) = CHR$(3)
260     C$(I+26)=C$
270     MID$(C$,3,12) =CHR$(5)
280     C$(I+39) = C$
290     NEXT I
300     S1=0: S2=0
310     REM =========shuffle the deck then cut for deal,low card deals ========
320     GOSUB 5640
330     GOSUB 5800
340     REM ========shuffle the deck and deal ================================
350     GOSUB 5640
360     GOSUB 6080
370     REM=========find the best four cards, discard the other two============
380     GOSUB 3380
390     REM====================DISCARDS =======================================
400     I1 = V(B9,5)
410     I2 = V(B9,6)
420     LOCATE 22,1:PRINT  "YOUR DISCARDS (INPUT 2 VALUES);       ";
430     LOCATE 22,31:INPUT;I3,I4
440     IF I3 < 1 GOTO 460
450     IF I3 < 7 GOTO 480
460     LOCATE 24,1:PRINT " now really ";:BEEP
470     GOTO 420
480     IF INT(I3)<>I3 GOTO 460
490     IF I4 =I3   GOTO 460
500     IF I4 < 1 GOTO 460
510     IF I4 > 6 GOTO 460
520     IF I4 <> INT(I4) GOTO 460
530     PST = ((I3-1)*10)+13:LIN=4:LOCATE 3,PST-2:PRINT  "  ";:GOSUB 6480
540     PST = ((I4-1)*10)+13:LIN=4:LOCATE 3,PST-2:PRINT  "  ";:GOSUB 6480
550     REM==================== CRIB =======================================
560     FOR J =1 TO 4
570     C(1,J) = M(I1,J)
580     C(2,J) = M(I2,J)
590     C(3,J) = Y(I3,J)
600     C(4,J) = Y(I4,J)
610     NEXT J
620     REM===============GENERATE THE UP CARD=============================
630     GOSUB 4180
640     REM=============== PLAY THE HAND ==================================
650     GOSUB 1470
660     REM=================COUNT THE POINTS ==============================
670     LOCATE 23,1:PRINT SPC(39):IF M = 0 GOTO 710
680     LOCATE 23,2:PRINT " You score first ";
690     X1=1
700     GOTO 930
710     LOCATE 23,2:PRINT "I score first   ";
720     X1 = 2
730     GOTO 1200
740     LOCATE 13,28:PRINT  " --THE CRIB CONTAINS --"
750     FOR I = 1 TO 4
760     PST = ((I-1)*10)+13:LIN = 16:CARD$=C$(C(I,1)):GOSUB 6320
770     NEXT I
780     FOR I = 1 TO 4
790     FOR J = 1 TO 4
800     W(I,J)=C(I,J)
810     NEXT J
820     NEXT I
830     C =1
840     W(5,4)=T9
850     GOSUB 4390
860     ON X1 GOTO 870,910
870     S1=S1+P
880     LOCATE 23,1:PRINT SPC(39):LOCATE 23,2:PRINT "the crib has ";P;"points":LOCATE 21,37:PRINT USING"###";S1
890     IF S1>=121 GOTO 1400
900     GOTO 1370
910     X1=3
920     GOTO 1050
930     K=1
940     FOR I=1 TO 6
950     IF I = I3 GOTO 1010
960     IF I=I4 GOTO 1010
970     FOR J = 1 TO 4
980     W(K,J)=Y(I,J)
990     NEXT J
1000    K=K+1
1010    NEXT I
1020    W(5,4)=T9
1030    C = 0
1040    GOSUB 4390
1050    LOCATE 23,1:PRINT SPC(39):LOCATE 23,2:PRINT  "How many points do you have";
1060    INPUT P9
1070    D = P-P9
1080    IF D>= 0 GOTO 1110
1090    LOCATE 24,1:PRINT "Not with that hand -try again";:BEEP
1100    GOTO 1050
1110    S2=S2+P9
1120    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING"###";S1
1130    IF S2>= 121 GOTO 1430
1140    IF D = 0 GOTO  1190
1150    S1=S1+D
1160    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING "###";S1
1170    LOCATE 24,1:PRINT SPC(39):LOCATE 24,1:PRINT "Muggins for ";D;"points";
1180    IF S1>=121 GOTO 1400
1190    ON X1 GOTO 1200,740,1370
1200    FOR K=1 TO 4
1210    L=V(B9,K)
1220    FOR J = 1 TO 4
1230    W(K,J)=M(L,J)
1240    NEXT J
1250    NEXT K
1260    FOR K= 1 TO 4
1270    L = W(K,1)
1280    NEXT K
1290    W(5,4)=T9
1300    C = 0
1310    GOSUB 4390
1320    S1=S1+P
1330    IF S1 >=121 GOTO 1400
1340    LOCATE 23,1:PRINT SPC(39):LOCATE 23,2:PRINT "I have ";P;"points";
1350    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING "###";S1
1360    ON X1 GOTO 740,930
1370    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING "###";S1
1380    GOTO 340
1390    REM ================ end of the game ==============================
1400    CLS
1410    LOCATE 12,30:PRINT  "I win ";S1;" to ";S2
1420    END
1430    CLS
1440    LOCATE 12,30:PRINT "You win ";S2;" to ";S1
1450    END
1460    REM =============================
1470    REM      play of the hand
1480    REM ==============================
1490    Y5=0:M5=0:C=0:S9=0:G=0
1500    IF M = 0 GOTO 1910
1510    IF Y5<>4 GOTO 1540
1520    IF M5=4 GOTO 2470
1530    GOTO 1910
1540    LOCATE 20,1:PRINT SPC(39);:LOCATE 20,2:PRINT  "YOUR PLAY,WHAT CARD NUMBER";
1550    INPUT C$
1560    IF C$= "go" OR C$ = "GO" GOTO 1910
1570    FOR C6= 1 TO 6
1580    IF C$=D$(C6) GOTO  1620
1590    NEXT C6
1600    BEEP:LOCATE 24,2:PRINT  "INVALID PLAY";
1610    GOTO 1540
1620    IF C6= I3 GOTO 1850
1630    IF C6=I4 GOTO 1850
1640    IF Y5 = 0 GOTO 1680
1650    FOR J = 1 TO Y5
1660    IF I(10+J) =C6 GOTO 1870
1670    NEXT J
1680    IF S9+Y(C6,2) >31 GOTO 1890
1690    S9=S9+Y(C6,2)
1700    Y5=Y5+1
1710    I(10+Y5)=C6
1720    C=C+1
1730    J(C)=Y(C6,4)
1740    GOSUB 2920
1750    PST = ((C6-1)*10)+13:LIN = 4:LOCATE 3,PST-2:PRINT "X ";
1760    GOSUB 6370
1770    LOCATE 22,19:PRINT USING "##";S9:LOCATE 22,37:PRINT USING "###";P
1780    F = 1
1790    S2 = S2+P
1800    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING "###";S1
1810    IF S2 >=121 GOTO 1430
1820    IF S9 <> 31 GOTO 1910
1830    F=0:C=0:S9=0:G=0
1840    GOTO 1910
1850    LOCATE 24,1:PRINT "YOU DISCARDED THAT CARD TRY AGAIN ";:BEEP
1860    GOTO 1540
1870    LOCATE 24,1:PRINT "already played - try again ";:BEEP
1880    GOTO 1540
1890    LOCATE 24,1:PRINT "that totals more than 31, try again";:BEEP
1900    GOTO 1540
1910    IF M5<> 4 GOTO 2050
1920    IF Y5 = 4 GOTO 2470
1930    IF C$ <>"go" AND C$<> "GO" GOTO 1510
1940    IF F = 2 GOTO 2000
1950    LOCATE 23,2:PRINT "You get one point for last card";
1960    S2 = S2+1:GOSUB 6370
1970    IF S2>= 121 GOTO 1430
1980    F = 0:C = 0:S9 = 0
1990    GOTO 1510
2000    LOCATE 23,2:PRINT "I get one point for last card";
2010    S1=S1+1:GOSUB 6370
2020    IF S1>=121 GOTO 1400
2030    F=0:C=0:S9=0
2040    GOTO 1510
2050    K9=0:P9=0
2060    C9=C
2070    C=C+1
2080    H9=S9
2090    FOR I9 = 1 TO 6
2100    I(I9)=0
2110    IF I9=I1 GOTO 2250
2120    IF I9=I2 GOTO 2250
2130    IF M5=0 GOTO 2170
2140    FOR J9=1 TO M5
2150    IF I9=I(20+J9) GOTO 2250
2160    NEXT J9
2170    IF H9+M(I9,2)>31 GOTO 2250
2180    K9=K9+1
2190    S9=H9+M(I9,2)
2200    J(C)=M(I9,4)
2210    GOSUB 2920
2220    IF P >P9 THEN P9 = P
2230    I(I9)=P
2240    I(K9+30)=I9
2250    NEXT I9
2260    C = C9
2270    S9 = H9
2280    IF K9<> 0 GOTO 2570
2290    IF C$<> "go" AND C$<> "GO" GOTO 2360
2300    IF G = 1 GOTO 2370
2310    LOCATE 23,2:PRINT"I get one point for last card";
2320    C=0:S9=0
2330    S1=S1+1:GOSUB 6370
2340    IF S1 >=121 GOTO 1400
2350    GOTO 1510
2360    IF Y5<> 4 GOTO 2430
2370    LOCATE 23,2:PRINT "I'LL give you 1 point for last card ";
2380    S2=S2+1:GOSUB 6370
2390    IF S2>=121 GOTO 1430
2400    C=0:S9=0:G=0
2410    C$ =""
2420    GOTO 1910
2430    IF G = 1 GOTO 1510
2440    LOCATE 24,1:PRINT SPC(39):LOCATE 24,18:PRINT "GO";
2450    G = 1
2460    GOTO 1510
2470    IF F = 0 GOTO 2560
2480    IF F=1 GOTO 2530
2490    LOCATE 23,1:PRINT "I get 1 point for last card";
2500    S1=S1+1:GOSUB 6370
2510    IF S1>= 121 GOTO 1400
2520    GOTO 2560
2530    LOCATE 23,2:PRINT  "You get 1 point for last card ";
2540    S2=S2+1:GOSUB 6370
2550    IF S2>=121 GOTO 1430
2560    RETURN
2570    C = C+1
2580    M5=M5+1
2590    IF C<>1 GOTO 2740
2600    FOR J9 = 1 TO 4
2610    I9=V(B9,J9)
2620    FOR VV%= 1 TO M5-1
2630    IF I (VV%+20) = I9 GOTO 2710 ELSE NEXT VV%
2640    REM============don't play a five first =========================
2650    IF M(I9,2)=5 GOTO 2710
2660    I(M5+20)=I9
2670    J(C)=M(I9,4)
2680    P9=0
2690    S9=M(I9,2)
2700    GOTO 2810
2710    NEXT J9
2720    L = V(B9,1)
2730    GOTO 2660
2740    FOR J9=1 TO K9
2750    I9=I(J9+30)
2760    IF I(I9)=P9 GOTO 2780
2770    NEXT J9
2780    I(M5+20)=I9
2790    J(C)=M(I9,4)
2800    S9=S9+M(I9,2)
2810    LOCATE 7,30:PRINT "--My cards are --";
2820    PST = ((I9-1)*10)+13:LIN = 10:LOCATE 10,PST-2:CARD$=C$(M(I9,1)):GOSUB 6320:IF Q$<> "s"THEN GOSUB 6370
2830    F=2
2840    S1=S1+P9
2850    LOCATE 21,18:PRINT USING "###";S2:LOCATE 21,37:PRINT USING "###";S1
2860    IF S1>= 121 GOTO 1400
2870    IF S9<> 31 GOTO 2900
2880    F=0:C=0:S9=0
2890    GOTO 1510
2900    IF C$="go" OR C$="GO" GOTO 1910
2910    GOTO 1510
2920    REM ==============================================
2930    REM CHECK FOT 15,31 OR
2940    REM 2,3,4 OF A KIND AND RUNS
2950    REM ==============================================
2960    P=0
2970    IF C = 1 GOTO 3200
2980    IF S9 <> 15 GOTO 3010
2990    P = P+2
3000    GOTO 3030
3010    IF S9<> 31 GOTO 3030
3020    P=P+2
3030    IF C-2> 2 THEN MAX = C-2 ELSE MAX = 2
3040    FOR I = C TO MAX STEP -1
3050    IF J(I)<> J(I-1) GOTO 3130
3060    ON C-I+1 GOTO 3070,3090,3110
3070    P=P+2
3080    GOTO 3120
3090    P=P+4
3100    GOTO 3120
3110    P=P+6
3120    NEXT I
3130    REM ==================RUNS ===============================
3140    IF C=2 GOTO 3200
3150    R9=0
3160    FOR I = 3 TO C
3170    GOSUB 3210
3180    NEXT I
3190    P=P+R9
3200    RETURN
3210    FOR J = 1 TO C
3220    J(J+10)=J(C-J+1)
3230    NEXT J
3240    FOR K = 1 TO I
3250    FOR L = K+1 TO I
3260    IF J(K+10)<J(L+10) GOTO 3300
3270    X = J(K+10)
3280    J(K+10)=J(L+10)
3290    J(L+10)=X
3300    NEXT L
3310    NEXT K
3320    FOR K = 1 TO I - 1
3330    IF J(K+10)<> J(K+11)-1 GOTO 3360
3340    NEXT K
3350    R9=I
3360    RETURN
3370    REM =======================================================
3380    REM   FIND THE BEST FOUR CARD HAND
3390    REM =======================================================
3400    P9=0
3410    FOR Z9=1 TO 15
3420    I1=V(Z9,1)
3430    I2=V(Z9,2)
3440    I3=V(Z9,3)
3450    I4=V(Z9,4)
3460    FOR J = 1 TO 4
3470    W(1,J)=M(I1,J)
3480    W(2,J)=M(I2,J)
3490    W(3,J)=M(I3,J)
3500    W(4,J)=M(I4,J)
3510    W(5,J)=25
3520    NEXT J
3530    REM =============EVALUATE THE HAND ======================
3540    C = 0
3550    GOSUB 4390
3560    V(Z9,7)=P
3570    IF P >P9 THEN P9 = P
3580    NEXT Z9
3590    REM ========FIND ALL HANDS WITH MAX SCORE (P9) ==========
3600    J = 0
3610    FOR I = 1 TO 15
3620    IF V(I,7)<> P9 GOTO 3650
3630    J = J+1
3640    I(J)=I
3650    NEXT I
3660    IF J > 1 GOTO 3700
3670    REM ===============this is the single best hand =================
3680    B9=I(1)
3690    RETURN
3700    REM =========no siongle best hand search for key cards ==========
3710    REM ================ CHECK FOR FIVES ============================
3720    C9 = 5
3730    Z = 1
3740    GOTO 3950
3750    REM ============= CHECK FOR EIGHTS ===============================
3760    C9=8
3770    Z=2
3780    GOTO 3950
3790    REM =============CHECK FOR SEVENS ================================
3800    C9 = 7
3810    Z = 3
3820    GOTO 3950
3830    REM ============ CHECK FOR JACKS =================================
3840    C9 = 11
3850    Z = 4
3860    GOTO 3950
3870    REM ============CHECK FOR ACES ====================================
3880    C9 = 1
3890    Z = 5
3900    GOTO 3950
3910    REM ====randomly chose a best hand if we reach this point ==========
3920    B9 = INT(J*RND)+1
3930    B9=I(B9)
3940    RETURN
3950    REM ========BEST HAND WILL BE WHICHEVER HAS THE MOST OF CARD C9 ====
3960    P9 = 0
3970    FOR I = 1 TO 15
3980    J(I)=0
3990    NEXT I
4000    FOR I = 1  TO J
4010    FOR K = 1 TO 4
4020    L=V(I(I),K)
4030    IF M(L,4) <> C9 GOTO 4050
4040    J(I)=J(I) +1
4050    NEXT K
4060    IF J(I) >P9 THEN P9 = J(I)
4070    NEXT I
4080    K = 0
4090    FOR I = 1 TO J
4100    IF J(I) <> P9 GOTO 4130
4110    K = K+1
4120    B9 =I(I)
4130    NEXT I
4140    IF K <> 1 GOTO 4160
4150    RETURN
4160    ON Z GOTO 3760,3800,3840,3880,3920
4170    REM ==============================
4180    REM   GENERATE THE UP CARD
4190    REM =============================
4200    U = INT(RND*38) +14
4210    PRINT
4220    LIN=21:PST=63:CARD$=C$(D(U,1)):LOCATE LIN,47:PRINT "The upcard is....":GOSUB 6320
4230    PRINT
4240    FOR I = 1 TO 4
4250    W(5,I)=D(U,I)
4260    NEXT I
4270    T9=W(5,4)
4280    IF W(5,4)<> 11 GOTO 4370
4290    IF M=0 GOTO 4340
4300    PRINT  "two points to me"
4310    S1=S1+2
4320    IF S1 >=121 GOTO 1400
4330    RETURN
4340    PRINT "two points for you"
4350    S2=S2+2
4360    IF S2 >=121 GOTO 1430
4370    RETURN
4380    REM ===================================
4390    REM    SCORE THE FIVE CARD HAND
4400    REM ===================================
4410    REM====check for a jack of the same suit as the up card except crib ==
4420    P=0
4430    IF C = 1 GOTO 4500
4440    FOR I = 1 TO 5
4450    IF W(I,4) <> 11 GOTO 4490
4460    IF W(I,3) <> W(5,3) GOTO 4490
4470    P = P+1
4480    GOTO 4500
4490    NEXT I
4500    REM =======check for a 4 or 5 card flush ============================
4510    FOR I = 1 TO 3
4520    IF W(I,3) <> W(I+1,3) GOTO 4620
4530    NEXT I
4540    REM ========crib scores only for a five card flush =================
4550    IF C <> 0 GOTO 4600
4560    P= P+4
4570    IF W(4,3)<>W(5,3) GOTO 4620
4580    P=P+1
4590    GOTO 4620
4600    IF W(4,3)<> W(5,3) GOTO 4620
4610    P=P+5
4620    REM ==============check for 2 card sums of 15 ======================
4630    FOR I = 1 TO 4
4640    FOR J = I+1 TO 5
4650    IF W(I,2)+W(J,2) <> 15 GOTO 4670
4660    P=P+2
4670    NEXT J
4680    NEXT I
4690    REM======= check for 3 card sums of 15 ============================
4700    FOR I = 1 TO 3
4710    FOR J = I+1 TO 4
4720    FOR K = J+1 TO 5
4730    IF W(I,2)+W(J,2)+W(K,2)<> 15 GOTO 4750
4740    P=P+2
4750    NEXT K
4760    NEXT J
4770    NEXT I
4780    REM ========check for 4 card sums of 15 ===========================
4790    FOR I = 1 TO 2
4800    FOR J = I+1 TO 3
4810    FOR K = J+1 TO 4
4820    FOR L = K+1 TO 5
4830    IF (W(I,2)+W(J,2)+W(K,2)+W(L,2)) <> 15 GOTO 4850
4840    P=P+2
4850    NEXT L
4860    NEXT K
4870    NEXT J
4880    NEXT I
4890    REM ===========check for 5 card sum of 15 ========================
4900    S=0
4910    FOR I = 1 TO 5
4920    S = S+W(I,2)
4930    NEXT I
4940    IF S<> 15 GOTO 4960
4950    P= P+2
4960    REM ======== check for pairs three and four of a kind ===========
4970    FOR I = 1 TO 13
4980    J(I)=0
4990    NEXT I
5000    FOR I = 1 TO 5
5010    J=W(I,4)
5020    J(J)=J(J)+1
5030    NEXT I
5040    FOR I = 1 TO 13
5050    ON J(I)+1 GOTO 5090,5090,5080,5070,5060
5060    P=P+6
5070    P=P+4
5080    P=P+2
5090    NEXT I
5100    REM =========sort hand into ascending sequence ================
5110    FOR I = 1 TO 5
5120    FOR J = I TO 5
5130    IF W(I,4)<=W(J,4) GOTO 5150
5140    SWAP W(I,4),W(J,4)
5150    NEXT J
5160    NEXT I
5170    REM=============check for a five card run ====================
5180    D = W(1,4)-Q(1,1)
5190    FOR I = 1 TO 11
5200    FOR J = 1 TO 5
5210    Q(I,J)=Q(I,J)+D
5220    NEXT J
5230    NEXT I
5240    FOR I = 1 TO 11
5250    FOR J = 1 TO 5
5260    IF W(J,4) <> Q(I,J) GOTO 5310
5270    NEXT J
5280    REM======== a five card run =================================
5290    P=P+Q(I,6)
5300    RETURN
5310    NEXT I
5320    REM =========== check for a 4 card run =====================
5330    FOR L= 1 TO 2
5340    D = W(L,4) - R(1,1)
5350    FOR I = 1 TO 4
5360    FOR J = 1 TO 4
5370    R(I,J)=R(I,J)+D
5380    NEXT J
5390    NEXT I
5400    FOR I = 1 TO 4
5410    FOR K = 1 TO 4
5420    IF W(K+L-1,4)<> R(I,K) GOTO 5470
5430    NEXT K
5440    REM=========== a 4 card run ===============================
5450    P = P+R(I,5)
5460    RETURN
5470    NEXT I
5480    NEXT L
5490    REM ========check for a 3 card run ==========================
5500    FOR L = 1 TO 3
5510    D = W(L,4)-S(1)
5520    FOR I = 1 TO 3
5530    S(I)=S(I)+D
5540    NEXT I
5550    FOR I = 1 TO 3
5560    IF W(L+I-1,4) <> S(I) GOTO 5610
5570    NEXT I
5580    REM ========== a three card run ============================
5590    P = P+S(4)
5600    RETURN
5610    NEXT L
5620    RETURN
5630    REM =========================================
5640    REM    SHUFFLE THE DECK
5650    REM =========================================
5660    FOR I = 1 TO 52
5670    I(I) = 0
5680    NEXT I
5690    FOR I = 1 TO 52
5700    J=INT(RND*52) +1
5710    IF I(J) <> 0 GOTO 5700
5720    D(I,1) = J
5730    D(I,3) = INT((J-1)/13)+1
5740    D(I,4)=J-13*INT((J-1)/13)
5750    IF D(I,4) < 10 THEN D(I,2) =D(I,4) ELSE D(I,2)=10
5760    I(J)=1
5770    NEXT I
5780    RETURN
5790    REM ========================
5800    REM  CUT FOR THE DEAL
5810    REM ========================
5820    CLS:LOCATE 2,10:PRINT " please cut for the deal <1-52)";
5830    INPUT I
5840    IF I< 1 GOTO 5860
5850    IF I<53 GOTO 5880
5860    LOCATE 4,10:PRINT "be serious now !"
5870    GOTO 5820
5880    IF I<>INT (I) GOTO 5860
5890    I1= D(I,1)
5900    LOCATE 10,15:PRINT"YOUR CARD IS......";:PST=30:LIN=10:CARD$=C$(I1):GOSUB 6320
5910    J=INT(RND(I)*52) +1
5920    IF J = I GOTO 5910
5930    J1=D(J,1)
5940    LOCATE 16,15:PRINT "MY CARD IS......";:CARD$=C$(J1):LIN=16:PST=30:GOSUB  6320
5950    IF D(I,4)<D(J,4) GOTO 6030
5960    IF D(J,4)<D(I,4) GOTO 5990
5970    BEEP:LOCATE 4,12:PRINT"please cut again ";
5980    GOTO 5830
5990    REM ,computer deals
6000    LOCATE 16,40:PRINT CHR$(17);"-low card wins cut"
6010    M = 0
6020    RETURN
6030    REM, player deals
6040    LOCATE 10,40:PRINT CHR$(17);"-low card wins cut "
6050    M = 1
6060    RETURN
6070    REM
6080    REM,deal
6090    REM
6100    CLS:LOCATE 22,1:PRINT "PLEASE WAIT.. I'M LOOKING AT MY CARDS ";
6110    LOCATE 20,12:IF M=0 THEN PRINT"I AM DEALING"ELSE PRINT "-YOU ARE DEALING -"
6120    M = 1-M
6130    Y = 1-M
6140    REM
6150    LOCATE 1,29:PRINT "- -YOUR CARDS ARE --"
6160    PRINT
6170    FOR I = 1 TO 6
6180    K=2*I-Y
6190    L = 2*I-M
6200    FOR J = 1 TO 4
6210    REM============= COMPUTERS HAND ================================
6220    M(I,J)=D(K,J)
6230    REM=============== PLAYERS HAND ================================
6240    Y(I,J)=D(L,J)
6250    NEXT J
6260    PST = ((I-1)*10)+13:LIN = 4:LOCATE 3,PST-2:CARD$=C$(Y(I,1)):PRINT USING "#_.";I:GOSUB 6320
6270    NEXT I
6280    RETURN
6290    REM =====================
6300    REM DRAW A CARD
6310    REM ====================
6320    LOCATE LIN,PST:PRINT CHR$(179);" ";LEFT$(CARD$,3);CHR$(179):LIN=LIN -1:LOCATE LIN,PST:PRINT CHR$(179);"    ";CHR$(179)
6330    LIN=LIN+2:LOCATE LIN,PST:PRINT CHR$(179);"    ";CHR$(179):LIN=LIN+1:LOCATE LIN,PST:PRINT CHR$(192);STRING$(4,196);CHR$(217)
6340    LIN=LIN-4:LOCATE LIN,PST:PRINT CHR$(218);STRING$(4,196);CHR$(191);
6350    RETURN
6360    REM ========================
6370    REM,   GAME STATISTICS
6380    REM =======================
6390    LOCATE 19,1:PRINT STRING$(39,205);CHR$(187)
6400    FOR VV= 20 TO 24:LOCATE VV,40:PRINT CHR$(186);:NEXT
6410    LOCATE 21,1:PRINT USING "YOUR SCORE ....._ ###_ MY SCORE ...._ ###";S2,S1
6420    LOCATE 22,1:PRINT USING "SUM OF CARDS ..._  ##_  POINTS......._ ##";S9,P9
6430    LOCATE 24,1:PRINT SPC(39)
6440    RETURN
6450    REM =============================
6460    REM   ERASE A CARD
6470    REM =============================
6480    LOCATE LIN,PST:PRINT "      ":LIN=LIN -1:LOCATE LIN,PST:PRINT "      "
6490    LIN=LIN+2:LOCATE LIN,PST:PRINT "      ":LIN=LIN+1:LOCATE LIN,PST:PRINT "       "
6500    LIN=LIN-4:LOCATE LIN,PST:PRINT "        "
6510    RETURN
6520    REM ===============================================
6530    REM    DATA FOR CARD VALUES
6540    REM ===============================================
6550    DATA 1,2,3,4,5,6,0,1,2,3,5,4,6,0,1,2,3,6,4,5,0
6560    DATA 1,2,4,5,3,6,0,1,2,4,6,3,5,0,1,2,5,6,3,4,0
6570    DATA 1,3,4,5,2,6,0,1,3,4,6,2,5,0,1,3,5,6,2,4,0
6580    DATA 1,4,5,6,2,3,0,2,3,4,5,1,6,0,2,3,4,6,1,5,0
6590    DATA 2,3,5,6,1,4,0,2,4,5,6,1,3,0,3,4,5,6,1,2,0
6600    DATA 1,1,1,2,3,09,1,1,2,2,3,12,1,1,2,3,3,12
6610    DATA 1,1,2,3,4,08,1,2,2,2,3,09,1,2,2,3,3,12
6620    DATA 1,2,2,3,4,08,1,2,3,3,3,09,1,2,3,3,4,08
6630    DATA 1,2,3,4,4,08,1,2,3,4,5,05
6640    DATA 1,1,2,3,6,1,2,2,3,6,1,2,3,3,6,1,2,3,4,4
6650    DATA 1,2,3,3
6660    DATA 1,2,3,4,5,6,A,2,3,4,5,6,7,8,9,10,J,Q,K
```

## CRIBBAGE.BAS

```bas
1 REM  20 SEPT 79  W/ SOUND/SCROLL/STROBE KEYBOARD
150 CLEAR ,&H8000:WIDTH 80
155 KEY 10,"":ON KEY (10) GOSUB 43000:KEY (10) ON
160 COLOR 7,0:CLS:SND=1:LOCATE ,,0
170 DEF FN A(X)= INT (X* RND (1))
175 GOSUB 50009:DEF SEG = AA:BLOAD "SCROLL1.ML",0
185 SCROLL%=0:MENU%= 2:COMMON MENU%
190 Y1=2:X1=2
195 GOSUB 60200:KEY OFF
197 GOSUB 42000
200 LOCATE 1,31:PRINT "C R I B B A G E":LOCATE 7,5
205 DIM P1(15),HH(4):FOR N= 1 TO 15:READ P1(N):NEXT
210 DATA 60,37,39,41,42,44,46,48,49,51,53,54,56,58,60
220 PRINT "+++   STANDBY - OPENING NEW DECK OF CARDS   +++"
230 PRINT CHR$(11):GOSUB 60300:RANDOMIZE VAL(RIGHT$(TIME$,2))
235 FOR VA= 2 TO 13:GOSUB 60000:NEXT :VA= 1:GOSUB 60000
240 DIM D(52,4),I(52),W(5,4),M(6,4),T$(34),H(8,4)
250 DIM Y(6,4),C(4,4),V(15,7),Q(11,6),R(4,5),S(4),J(52)
500 REM      SET DECK
530 DATA 1,2,3,4,5,6,0,1,2,3,5,4,6,0,1,2,3,6,4,5,0,1,2,4,5,3,6,0
540 DATA 1,2,4,6,3,5,0,1,2,5,6,3,4,0,1,3,4,5,2,6,0,1,3,4,6,2,5,0
550 DATA 1,3,5,6,2,4,0,1,4,5,6,2,3,0,2,3,4,5,1,6,0,2,3,4,6,1,5,0
560 DATA 2,3,5,6,1,4,0,2,4,5,6,1,3,0,3,4,5,6,1,2,0
570 RESTORE 530
580 FOR I= 1 TO 15:FOR J= 1 TO 7:READ V(I,J):NEXT J:NEXT I
600 REM    DATA FOR FOUR CARD RUN W/ PAIRS Q(I,J)
620 DATA 1,1,1,2,3,9,1,1,2,2,3,12,1,1,2,3,3,12
630 DATA 1,1,2,3,4,8,1,2,2,2,3,9,1,2,2,3,3,12
640 DATA 1,2,2,3,4,8,1,2,3,3,3,9,1,2,3,3,4,8
650 DATA 1,2,3,4,4,8,1,2,3,4,5,5
670 REM     DATA FOR THREE CARD RUN W/ PAIRS R(I,J)
690 DATA 1,1,2,3,6,1,2,2,3,6,1,2,3,3,6,1,2,3,4,4
710 REM     DATA FOR THREE CARD RUN S(I)
730 DATA 1,2,3,3
750 REM     PRESET
770 SC= 0:SP= 0:BS= 0:WP= 0:YC= 0:MC= 0:BH= 0
780 GP= 0:M= 0:C= 0:GC= 0:P= 0
790 GOTO 1030
1000 GOTO 150
1005 REM
1010 REM  SHUFFLE//CUT CARDS
1020 REM 
1030 LOCATE 10,5:GOSUB 60300
1040 PRINT "+++   STANDBY   +++   SHUFFLING THE CARDS   +++";
1050 PRINT CHR$(11):GOSUB 14500:GOSUB 17000
1060 LOCATE 12,8:GOSUB 60300
1070 INPUT "PLEASE CUT FOR THE DEAL (1-50)  ";I:GOSUB 60300
1075 IF I< 1 THEN GOSUB 17000:GOTO 6000
1080 IF I= INT (I)AND I> 0 AND I< 51 THEN 1120
1090 GOSUB 17800
1100 PRINT "BE SERIOUS NOW - THERE ARE 52 CARDS";
1110 GOTO 1060
1120 I1= I:VA= D(I,4):ST= D(I,3):GOSUB 22200
1130 GOSUB 17200
1140 LOCATE Y1-1,X1:PRINT "YOURS";
1150 J= FN A(52- I1)+ I1
1160 IF J= 0 THEN J= FN A(52)+ 1
1170 IF J= I1 THEN 1150
1180 J1= J:VA= D(J,4):ST= D(J,3):GOSUB 22300
1190 GOSUB 17200:I= I1:J= J1
1200 LOCATE Y1-1,X1:PRINT "MINE";
1205 FOR TD=1 TO 1500:NEXT
1210 IF D(I,4)< D(J,4) THEN M= 1:GOTO 1250
1220 IF D(J,4)< D(I,4) THEN M= 0:GOTO 1250
1230 GOSUB 17000:PRINT "YOU SURE ARE LUCKY - CUT AGAIN";
1240 GOTO 1060
1250 LOCATE 7,5:PRINT STRING$(48," ");:LOCATE 10,5:PRINT STRING$(48," "):GOSUB 23000
1260 GOSUB 17000:ON M+ 1 GOTO 2050,2000
2000 REM 
2010 REM  START PLAY
2020 REM 
2030 GOSUB 17800
2040 PRINT "YOU ARE DEALING";:GOTO 2070
2050 GOSUB 17800
2060 PRINT "I AM DEALING";
2070 M= 1- M:YY= 1- M
2080 FOR I= 1 TO 6:K= 2* I- YY:L= 2* I- M
2090 FOR J= 1 TO 4
2100 M(I,J)= D(K,J):REM   COMPUTERS HAND
2110 Y(I,J)= D(L,J):REM   PLAYERS   HAND
2120 NEXT J
2130 M(I,0)= 0:Y(I,0)= 0
2140 NEXT I
2150 FOR B= 1 TO 6:ON M+ 1 GOTO 2160,2190
2160 ON B GOSUB 22000,22100,22200,22300,22400,22500
2170 ST= M(B,3):VA= M(B,4):CN= Y1:GOSUB 22600:REM GOSUB 17200FORCARD
2180 ON M+ 1 GOTO 2190,2230
2190 ST= Y(B,3):VA= Y(B,4)
2200 ON B GOSUB 20000,20100,20200,20300,20400,20500
2210 GOSUB 17200:LOCATE 9,X1+ 2:PRINT CHR$(B+48);
2220 ON M+ 1 GOTO 2230,2160
2230 NEXT B
2500 REM  
2510 REM  GET THE UP CARD
2520 REM 
2530 UP= FN A(38)+ 8:V= 1
2540 FOR I= 1 TO 6
2550 IF Y(I,1)= D(UP,1) THEN V= 0
2560 IF M(I,1)= D(UP,1) THEN V= 0
2570 NEXT I:IF V= 0 THEN 2530
2580 FOR I= 1 TO 4:W(5,I)= D(UP,I):NEXT I
3000 REM 
3010 REM  COMPUTER EVALUATE HAND
3020 REM 
3030 REM GOSUB 17000
3040 LOCATE 13,3:PRINT "STANDBY - I TAKE A WHILE TO SORT & DISCARD";
3045 TIME$="00:00:00"
3050 WP= 0:FOR CN= 1 TO 15:VA= CN:GOSUB 60000
3070 I1= V(CN,1):I2= V(CN,2):I3= V(CN,3):I4= V(CN,4)
3110 C= 0:GOSUB 30000:V(CN,7)= P:IF P> WP THEN WP= P
3120 NEXT CN:P= 0
3125 LOCATE 13,3:PRINT STRING$(43," ");:LOCATE 14,9:PRINT STRING$(20," ");
3130 REM 
3140 REM  FIND ALL HANDS W/ MAX SCORE IN WP
3150 REM 
3160 J= 0:FOR I= 1 TO 15:IF V(I,7)< > WP THEN 3180
3170 J= J+ 1:I(J)= I
3180 NEXT I:IF J> 1 THEN 3260
3190 REM  
3200 REM  THIS IS BEST HAND
3210 REM 
3220 BC= I(1):GOTO 4000
3230 REM 
3240 REM  NO SINGLE BEST HAND - SEARCH FOR KEY CARDS
3250 REM 
3260 BH= 5:ZZ= 1:GOTO 3350
3270 BH= 8:ZZ= 2:GOTO 3350
3280 BH= 7:ZZ= 3:GOTO 3350
3290 BH= 11:ZZ= 4:GOTO 3350
3300 BH= 1:ZZ= 5:GOTO 3350
3310 REM 
3320 REM  RANDOMLY CHOOSE A HAND IF CMPTR REACHES HERE
3330 REM 
3340 BC= FN A(J)+ 1:I(BC)= BC:GOTO 4000
3350 REM 
3360 REM  BEST HAND WILL BE WHICHEVER HAS THE MOST OF CARD BH
3370 REM 
3380 WP= 0:FOR I= 1 TO 15:J(I)= 0:NEXT I
3390 FOR I= 1 TO J:FOR K= 1 TO 4:L= V(I(I),K)
3400 IF M(L,4)= BH THEN J(I)= J(I)+ 1
3410 NEXT K:IF J(I)> WP THEN WP= J(I)
3420 NEXT I
3430 K= 0:FOR I= 1 TO J:IF J(I)= WP THEN K= K+ 1:BC= I(I)
3440 NEXT I:IF K= 1 THEN 4000
3450 ON ZZ GOTO 3270,3280,3290,3300,3310
4000 REM  
4001 REM  PLAYER//COMPUTER DISCARDS
4002 REM  
4010 GOSUB 17000
4030 GOSUB 22700:GOSUB 22700
4040 GOSUB 17000:FOR B= 1 TO 2:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT B:GOSUB 60300
4050 I1= V(BC,5):I2= V(BC,6)
4060 GOSUB 17800:PRINT "YOUR DISCARDS PLEASE  ";:LOCATE ,,1
4070 GOSUB 40000:I3=ASC(KB$):GOSUB 60300:PRINT CHR$ (I3)+ "  ";:GOSUB 40000:I4=ASC(KB$):GOSUB 60300:PRINT CHR$ (I4);:LOCATE ,,0
4075 IF I3= 45 OR I4= 45 THEN 6000
4080 I3= I3- 48:I4= I4- 48
4090 IF I3< 1 OR I4< 1 THEN 4130
4100 IF I3< > INT (I3) OR I4< > INT (I4) THEN 4130
4110 IF I3= I4 THEN 4130
4120 IF I3< 7 AND I4< 7 THEN :GOTO 4200
4130 GOSUB 17800:PRINT "TRY A NUMBER FROM 1 TO 6";:GOTO 4060
4200 REM  
4210 REM  SET THE CRIB
4220 REM 
4230 FOR J= 1 TO 4:C(1,J)= M(I1,J):C(2,J)= M(I2,J):C(3,J)= Y(I3,J):C(4,J)= Y(I4,J):NEXT J
4240 M(I1,0)= 1:M(I2,0)= 1:Y(I3,0)= 1:Y(I4,0)= 1
4400 REM  
4410 REM  REPACK PLAYERS HAND//SORT ASCENDING
4420 REM 
4430 FOR K= 1 TO 4
4440 IF Y(K,0)= 0 THEN 4470
4450 FOR J= K+ 1 TO 6:FOR I= 0 TO 4:Y(J- 1,I)= Y(J,I):NEXT I:NEXT J
4460 Y(6,0)= 1:GOTO 4440
4470 NEXT K:Y(5,0)= 1
4480 V= 1:FOR I= 1 TO 3:IF Y(I,4)< = Y(I+ 1,4) THEN 4500
4490 FOR J= 0 TO 4:Y(0,J)= Y(I,J):Y(I,J)= Y(I+ 1,J):Y(I+ 1,J)= Y(0,J):NEXT J:V= 0
4500 NEXT I:IF V= 0 THEN 4480
4700 REM  
4710 REM  REDRAW CARDS
4720 REM 
4725 LOCATE 9,30:PRINT"           ";
4730 GOSUB 20500:FOR B= 1 TO 9:LOCATE Y1+ H1- 1,X1:PRINT SPC( W1);:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT B
4740 GOSUB 20400:FOR B= 1 TO 9:LOCATE Y1+ H1- 1,X1:PRINT SPC( W1);:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT B
4750 FOR B= 1 TO 4:ST= Y(B,3):VA= Y(B,4)
4760 ON B GOSUB 20000,20100,20200,20300:GOSUB 17200
4770 LOCATE 9,X1+ 2:PRINT CHR$(B+48):NEXT B:I3= 5:I4= 6
4780 REM  
4790 REM  GET UP CARD
4800 REM 
4810 VA= D(UP,4):ST= D(UP,3):GOSUB 20600:GOSUB 17200
4820 LOCATE 9,49:PRINT "UP CARD";
4830 FOR I= 1 TO 4:W(5,I)= D(UP,I):NEXT I
4840 IF W(5,4)< > 11 THEN 5000
4850 GOSUB 17000:IF M= 0 THEN 4890
4860 PRINT "2 POINTS FOR ME";:SC= SC+ 2:GOSUB 15500
4870 IF SC> 120 THEN 5500
4880 GOTO 5000
4890 PRINT "2 POINTS FOR YOU";:SP= SP+ 2:GOSUB 15500
4900 IF SP> 120 THEN 5500
5000 REM  
5010 REM  PLAY CONTROL
5020 REM 
5030 GOSUB 7000:LOCATE 6,35:PRINT SPC( 13);:LOCATE 7,35:PRINT SPC( 13);
5035 IF CP= 45 THEN 6000
5040 IF SC> 120 OR SP> 120 THEN 5500
5050 FOR B= 1 TO 4:VA= Y(B,4):ST= Y(B,3):ON B GOSUB 20000,20100,20200,20300
5060 GOSUB 17200:NEXT B
5070 GOSUB 17000:IF M= 0 THEN 5090
5080 PRINT "YOU SCORE FIRST";:XI= 1:GOTO 5100
5090 PRINT "I SCORE FIRST";:XI= 2
5100 GOSUB 10000
5110 IF SC> 120 OR SP> 120 THEN 5500
5120 GOSUB 23000
5130 GOSUB 17000:PRINT "++STANDBY++ SHUFFLING THE CARDS";
5140 GOSUB 14500:GOTO 1260
5500 REM  
5510 REM  SELECT WINNER//END
5520 REM 
5530 GOSUB 17800:IF SC< 121 THEN 5550
5540 GOSUB 17000:PRINT "+++   I WIN   +++";:GOTO 5570
5550 IF SP< 121 THEN 5570
5560 GOSUB 17000:PRINT "+++   YOU WIN   +++";
5570 GOSUB 17000:PRINT "I HAVE  "SC" POINTS";
5580 GOSUB 17000:PRINT "YOU HAVE"SP" POINTS";
5590 IF SC> 120 THEN SC= 121
5600 IF SP> 120 THEN SP= 121
5610 IF SC- SP< = 60 THEN 5630
5620 GOSUB 17800:PRINT "HA - HA - A DOUBLE SKUNK";:GOTO 5690
5630 IF SC- SP< = 30 THEN 5650
5640 GOSUB 17800:PRINT "HA - HA - I SKUNKED YOU";:GOTO 5690
5650 IF SP- SC< = 60 THEN 5670
5660 GOSUB 17800:PRINT "OOOOPS - I GOT DOUBLE SKUNKED";:GOTO 5690
5670 IF SP- SC< = 30 THEN 5690
5680 GOSUB 17800:PRINT "HMMMMM - I GOT SKUNKED";
5690 '
6000 REM  
6010 REM  PLAY AGAIN?
6020 REM 
6030 GOSUB 17800:CALL SCROLL%(SCR%,POS1%,POS2%)
6040 PRINT "WOULD YOU LIKE TO PLAY AGAIN? Y/N ";:LOCATE ,,1
6050 GOSUB 40000:K= ASC(KB$):GOSUB 60300:IF K= 78 OR K= 89 THEN PRINT CHR$(K);
6060 IF K= 89 THEN CLS:RUN 1000
6070 IF K< > 78 THEN 6050
6090 LOCATE ,,0:CLS:LOCATE 16,18
6100 PRINT "G O O D  B Y E";
6110 KEY (10) OFF
6120 END 'CHAIN "MENU",1000
7000 REM  
7010 REM  PLAY THE HAND
7020 REM 
7030 GOSUB 15000
7040 YC= 0:MC= 0:C= 0:BS= 0:GC= 0:GP= 0
7050 IF M= 0 THEN 7830
7060 GOSUB 17000:PRINT "YOUR PLAY  ";:LOCATE ,,1
7070 GOSUB 40000:CP= ASC(KB$):GOSUB 60300:C$= CHR$ (CP):IF CP= 71 OR CP = 13 THEN C$= "GO":CP=71
7075 IF CP= 45 THEN RETURN :REM  ASK IF END
7080 PRINT C$;
7090 IF CP= 71 AND BS< 22 AND MC< 4 THEN 7120
7100 IF CP= 71 THEN 7700
7110 CP= CP- 48:IF CP> 0 AND CP< 7 THEN 7130
7120 GOSUB 17800:PRINT "INVALID PLAY - TRY AGAIN";:GOTO 7060
7130 IF CP> 4 THEN 7530
7140 IF Y(CP,0)= 1 THEN 7540
7150 IF BS+ Y(CP,2)> 31 THEN 7550
7160 BS= BS+ Y(CP,2):YC= YC+ 1:C= C+ 1:GOSUB 15100
7300 REM  
7310 REM  CHECK FOR 15/31 &  PAIRS
7320 REM 
7330 WS= BS:GOSUB 15300
7400 REM 
7410 REM  CHECK FOR RUNS//MOVE CARD
7420 REM 
7430 GOSUB 16000:P= P+ PT:VA= Y(CP,4):ST= Y(CP,3)
7435 GOSUB 17100
7440 ON C GOSUB 21000,21100,21200,21300,21400,21500,21600,21700
7450 GOSUB 17200
7460 LOCATE 6,35:PRINT "SUM    ="BS"  ";:REM
7470 LOCATE 7,35:PRINT "POINTS ="P"  ";
7480 SP= SP+ P:IF P> 0 THEN GOSUB 15500
7490 IF SP> 120 THEN RETURN 
7500 IF BS< > 31 THEN 7700
7510 GP= 0:C= 0:BS= 0:GC= 0
7520 GOSUB 17700:GOTO 7700
7530 GOSUB 17800:PRINT "YOU DISCARDED THAT CARD EARLIER";:GOTO 7060
7540 GOSUB 17800:PRINT "YOU ALREADY PLAYED THIS CARD";:GOTO 7060
7550 GOSUB 17800:PRINT "WHOOPS - THAT IS MORE THAN 31";:GOTO 7060
7700 REM  
7710 REM  CHECK GO STATUS FROM PLAYER
7720 REM 
7730 IF C$= "GO"AND GC= 1 THEN 8880
7740 IF C$= "GO" THEN GP= 1:C$= ""
7750 IF YC> 3 THEN GP= 1
7760 IF GC= 0 THEN 7830
7770 IF GP= 0 AND GC= 1 THEN 7060
7780 GOTO 8880
7800 REM  
7810 REM  COMPUTERS PLAY
7820 REM 
7830 IF YC+ MC> 7 THEN 8880
7840 IF GC= 1 THEN 8800
7850 K= 0:WP= 0:WC= C
7860 FOR I= 0 TO 37:I(I)= 0:NEXT I
7870 K= K+ 1:IF K> 6 THEN 8210
7880 CC= V(BC,K)
7890 IF M(CC,0)= 1 THEN 7870
7900 WS= BS+ M(CC,2)
7910 IF WS> 31 THEN 7870
7920 IF M+ C= 1 AND M(CC,4)= 5 THEN 7870
7930 I(K+ 30)= CC
8000 REM  
8010 REM  CHECK FOR 15/31 & PAIRS
8020 REM 
8030 C= WC+ 1:P= 0:GOSUB 15200:GOSUB 15300
8100 REM  
8110 REM  CHECK FOR RUNS
8120 REM 
8130 GOSUB 16000:P= P+ PT:IF P> WP THEN WP= P
8140 I(K)= WP
8150 FOR L= 0 TO 8
8160 IF H(L,0)< > 2 THEN 8190
8170 H(L,0)= 0:H(L,1)= 0:H(L,2)= 0:H(L,3)= 0:H(L,4)= 0
8180 L= 8
8190 NEXT L
8200 GOTO 7870
8210 WP= 0
8220 FOR K= 1 TO 6
8230 IF WP< I(K) THEN WP= I(K):CC= I(K+ 30)
8240 NEXT K
8250 IF CC= 0 THEN 8530
8260 IF M(CC,0)= 1 THEN 8530
8270 BS= BS+ M(CC,2)
8280 C= WC+ 1:GOSUB 15200:GOTO 8620
8290 GOSUB 17000:PRINT "GO";:GC= 1
8300 C= WC:WP= 0:IF GP= 0 THEN 7060
8410 GOTO 8800
8500 REM  
8510 REM  GET CARD NO POINT VALUE
8520 REM 
8530 K= 0:WP= 0
8540 K= K+ 1:IF K> 6 THEN C= WC:GOTO 8290
8550 IF M(K,0)= 1 THEN 8540
8560 WS= BS+ M(CC,2)
8570 IF WS> 31 THEN 8540
8580 CC= K:C= WC+ 1
8590 GOSUB 15200:GOSUB 15300:GOSUB 16000:WP= WP+ P+ PT
8600 WS= BS+ M(CC,2):IF WS> 31 THEN 8290
8610 BS= WS
8620 MC= MC+ 1:M(CC,0)= 1
8630 VA= M(CC,4):ST= M(CC,3)
8635 GOSUB 22700
8640 ON C GOSUB 21000,21100,21200,21300,21400,21500,21600,21700
8650 GOSUB 17200
8660 LOCATE 6,35:PRINT "SUM    ="BS"  ":REM
8670 LOCATE 7,35:PRINT "POINTS ="WP"  "
8680 SC= SC+ WP:GOSUB 15500
8690 IF SC> 120 THEN RETURN 
8700 IF BS< > 31 THEN 8800
8710 GP= 0:GC= 0:C= 0:BS= 0
8720 GOSUB 17700
8800 REM  
8810 REM  CHECK GO STATUS FROM COMPUTER
8820 REM 
8830 IF GP AND GC= 1 THEN 8940
8840 IF MC> 3 THEN GC= 1
8850 IF GP= 0 THEN 7060
8860 IF GC= 0 AND GP= 1 THEN 7830
8870 GOTO 8940
8880 GOSUB 9200
8890 IF SP> 120 THEN RETURN 
8900 IF YC+ MC> 7 THEN RETURN 
8910 IF MC> 3 THEN GC= 1
8920 IF YC> 3 THEN GP= 1
8930 ON GC+ 1 GOTO 7830,7060
8940 GOSUB 9400
8950 IF SC> 120 THEN RETURN 
8960 IF MC+ YC> 7 THEN RETURN 
8970 IF YC> 3 THEN GP= 1
8980 IF MC> 3 THEN GC= 1
8990 ON GP+ 1 GOTO 7060,7830
9200 REM  
9210 REM  PLAYERS LAST CARD
9220 REM 
9230 GOSUB 17000
9240 ON FN A(3)+ 1 GOTO 9250,9260,9270
9250 PRINT "YOU CAN HAVE 1 POINT FOR LAST CARD";:GOTO 9280
9260 PRINT "I'LL GIVE YOU 1 POINT FOR LAST CARD";:GOTO 9280
9270 PRINT "YOU GET 1 POINT FOR LAST CARD";
9280 SP= SP+ 1:GOSUB 15500:GOSUB 15000
9290 GP= 0:C= 0:GC= 0:BS= 0:GOSUB 17700:RETURN
9400 REM  
9410 REM  COMPUTERS LAST CARD
9420 REM 
9430 GOSUB 17000
9440 ON FN A(3)+ 1 GOTO 9450,9460,9470
9450 PRINT "I'LL TAKE 1 POINT FOR LAST CARD";:GOTO 9480
9460 PRINT "1 POINT FOR ME FOR LAST CARD";:GOTO 9480
9470 PRINT "I GET 1 POINT FOR LAST CARD";
9480 SC= SC+ 1:GOSUB 15500:GOSUB 15000
9490 GP= 0:C= 0:GC= 0:BS= 0:GOSUB 17700:RETURN
10000 REM  
10010 REM  SCORE THE HANDS//PLAYER AND COMPUTER
10020 REM 
10050 IF XI< > 1 THEN 10250
10060 K= 1:FOR I= 1 TO 6
10070 IF I= I3 THEN 10100
10080 IF I= I4 THEN 10100
10090 FOR J= 1 TO 4:W(K,J)= Y(I,J):NEXT J:K= K+ 1
10100 NEXT I
10110 FOR J= 1 TO 4:W(5,J)= D(UP,J):NEXT J
10120 C= 0:GOSUB 11000
10130 GOSUB 17000
10140 GOSUB 17800:PRINT "HOW MANY POINTS DO YOU HAVE?  ";:INPUT;WP:GOSUB 60300
10150 WP= INT (ABS (WP)):IF WP= 19 THEN WP= 0
10160 D= P- WP:IF D= > 0 THEN 10190
10170 GOSUB 17800:PRINT "YOU DON'T HAVE "WP;"POINTS THERE!";
10180 GOTO 10140
10190 SP= SP+ WP:GOSUB 15500:IF SP> 120 THEN RETURN
10200 IF D= 0 THEN 10240
10210 SC= SC+ D:GOSUB 15500:GOSUB 17800
10220 PRINT "MUGGINS FOR"D"POINTS - THANKS";
10230 IF SC> 120 THEN RETURN 
10240 ON XI GOTO 10250,10340,10470
10250 FOR K= 1 TO 4:L= V(BC,K):FOR J= 1 TO 4:W(K,J)= M(L,J):NEXT J:NEXT K
10260 GOSUB 17000:PRINT "MY CARDS ARE :";
10270 FOR K= 1 TO 4:VA= W(K,4):ST= W(K,3)
10280 ON K GOSUB 21000,21100,21200,21300
10290 GOSUB 17200:NEXT K
10300 FOR J= 1 TO 4:W(5,J)= D(UP,J):NEXT J
10310 C= 0:GOSUB 11000:SC= SC+ P:GOSUB 15500:IF SC> 120 THEN RETURN 
10320 GOSUB 17000:PRINT "I HAVE "P" POINTS IN MY HAND";
10330 ON XI GOTO 10340,10060
10340 GOSUB 17000:PRINT "THE CRIB CARDS ARE :";
10350 FOR K= 1 TO 4:VA= C(K,4):ST= C(K,3)
10360 ON K GOSUB 22000,22100,22200,22300
10370 GOSUB 17200:NEXT K
10380 FOR I= 1 TO 4:FOR J= 1 TO 4:W(I,J)= C(I,J):NEXT J:NEXT I
10390 C= 1
10400 FOR J= 1 TO 4:W(5,J)= D(UP,J):NEXT J
10410 GOSUB 11000
10420 ON XI GOTO 10430,10460
10430 GOSUB 17000:PRINT "I HAVE "P" POINTS IN THE CRIB";
10440 SC= SC+ P:GOSUB 15500:IF SC> 120 THEN RETURN
10450 GOTO 10470
10460 XI= 3:GOTO 10130
10470 GOSUB 17000:PRINT "I HAVE A TOTAL SCORE OF"SC"POINTS";
10480 GOSUB 17800:PRINT "YOUR TOTAL SCORE IS"SP"POINTS";
10490 GOSUB 60300:RETURN
11000 REM  
11010 REM  SCORE 5 CARD HAND PLAYER//COMPUTER//CRIB
11020 REM 
11030 REM  CHECK FOR JACK SAME SUIT AS UP CARD
11040 REM  EXCEPT FOR CRIB
11050 REM 
11060 P= 0:IF C= 1 THEN 11100
11070 FOR I= 1 TO 4
11080 IF W(I,4)= 11 AND W(I,3)= W(5,3) THEN P= P+ 1
11090 NEXT I
11100 REM 
11110 REM  CHECK FOR 4 OR 5 CARD FLUSH
11120 REM 
11130 FOR I= 1 TO 3
11140 IF W(I,3)< > W(I+ 1,3) THEN 11240
11150 NEXT I
11160 REM 
11170 REM  CRIB SCORES ONLY FOR 5 CARD FLUSH
11180 REM 
11190 IF C< > 0 THEN 11220
11200 P= P+ 4:IF W(4,3)< > W(5,3) THEN 11240
11210 P= P+ 1:GOTO 11240
11220 IF W(4,3)< > W(5,3) THEN 11240
11230 P= P+ 5
11240 REM 
11250 REM  CHECK FOR 2 CARD SUMS OF 15
11260 REM 
11270 FOR I= 1 TO 4:FOR J= I+ 1 TO 5
11280 IF W(I,2)+ W(J,2)= 15 THEN P= P+ 2
11290 NEXT J:NEXT I
11300 REM 
11310 REM  CHECK FOR 3 CARD SUMS OF 15
11320 REM 
11330 FOR I= 1 TO 3:FOR J= I+ 1 TO 4:FOR K= J+ 1 TO 5
11340 IF W(I,2)+ W(J,2)+ W(K,2)= 15 THEN P= P+ 2
11350 NEXT K:NEXT J:NEXT I
11360 REM 
11370 REM  CHECK FOR 4 CARD SUMS OF 15
11380 REM 
11390 FOR I= 1 TO 2:FOR J= I+ 1 TO 3:FOR K= J+ 1 TO 4:FOR L= K+ 1 TO 5
11400 IF W(I,2)+ W(J,2)+ W(K,2)+ W(L,2)= 15 THEN P= P+ 2
11410 NEXT L:NEXT K:NEXT J:NEXT I
11420 REM 
11430 REM  CHECK FOR 5 CARD SUM OF 15
11440 REM 
11450 S= 0:FOR I= 1 TO 5:S= S+ W(I,2):NEXT I
11460 IF S= 15 THEN P= P+ 2
11470 REM 
11480 REM  CHECK FOR PAIRS//3 AND 4 A KIND
11490 REM 
11500 FOR I= 1 TO 4:FOR J= I+ 1 TO 5
11510 IF W(I,4)= W(J,4) THEN P= P+ 2
11520 NEXT J:NEXT I
11530 REM 
11540 REM  SORT CARDS INTO ASCENDING SEQUENCE
11550 REM 
11560 V= 1:FOR I= 1 TO 4
11570 IF W(I,4)< = W(I+ 1,4) THEN 11610
11580 FOR J= 0 TO 4
11590 W(0,J)= W(I,J):W(I,J)= W(I+ 1,J):W(I+ 1,J)= W(0,J)
11600 NEXT J:V= 0
11610 NEXT I:IF V= 0 THEN 11560
11620 REM 
11630 REM  CHECK//SCORE FOR 5 CARD RUN
11640 REM 
11650 GOSUB 14000:D= W(1,4)- Q(1,1)
11660 FOR I= 1 TO 11:FOR J= 1 TO 5:Q(I,J)= Q(I,J)+ D:NEXT J:NEXT I
11670 PT= 0:FOR I= 1 TO 11:V= 1:FOR J= 1 TO 5
11680 IF W(J,4)< > Q(I,J) THEN J= 5:V= 0
11690 NEXT J:IF V= 1 THEN PT= PT+ Q(I,6)
11700 NEXT I:P= P+ PT:IF PT> 0 THEN RETURN 
11710 REM 
11720 REM  CHECK//SCORE FOR 4 CARD RUN
11730 REM 
11740 PT= 0:FOR L= 1 TO 2:GOSUB 14060:D= W(L,4)- R(1,1)
11750 FOR I= 1 TO 4:FOR J= 1 TO 4:R(I,J)= R(I,J)+ D:NEXT J:NEXT I
11760 FOR I= 1 TO 4:V= 1:FOR K= 1 TO 4:IF W(K+ L- 1,4)< > R(I,K) THEN K= 4:V= 0
11770 NEXT K:IF V= 1 THEN PT= PT+ R(I,5)
11780 NEXT I:NEXT L:P= P+ PT:IF PT> 0 THEN RETURN 
11790 REM 
11800 REM  CHECK//SCORE FOR 3 CARD RUN
11810 REM 
11820 PT= 0:FOR L= 1 TO 3:GOSUB 14120:D= W(L,4)- S(1)
11830 FOR I= 1 TO 3:S(I)= S(I)+ D:NEXT I
11840 V= 1:FOR I= 1 TO 3:IF W(L+ I- 1,4)< > S(I) THEN I= 3:V= 0
11850 NEXT I:IF V= 1 THEN PT= PT+ S(4)
11860 NEXT L:P= P+ PT:RETURN 
14000 REM  
14010 REM  READ Q(I,J)
14020 REM 
14030 RESTORE 620
14040 FOR I= 1 TO 11:FOR J= 1 TO 6:READ Q(I,J):NEXT J:NEXT I
14050 RETURN 
14060 REM 
14070 REM  READ R(I,J)
14080 REM 
14090 RESTORE 690
14100 FOR I= 1 TO 4:FOR J= 1 TO 5:READ R(I,J):NEXT J:NEXT I
14110 RETURN 
14120 REM 
14130 REM  READ S(I)
14140 REM 
14150 RESTORE 730
14160 FOR I= 1 TO 4:READ S(I):NEXT I
14170 RETURN 
14500 REM  
14510 REM  SHUFFLE DECK
14520 REM 
14530 FOR I= 1 TO 52:I(I)= 0:NEXT I
14540 FOR I= 1 TO 52
14550 J= FN A(52)+ 1:IF I(J)= 1 THEN 14550
14555 LOCATE 14,2+ J:PRINT "*";
14560 D(I,1)= J:D(I,3)= INT ((J- 1)/ 13)+ 1
14565 ON D(I,3) GOTO 14570,14572,14574,14576
14570 D(I,4)= J:D(I,2)= D(I,4):GOTO 14580
14572 D(I,4)= J- 13:D(I,2)= D(I,4):GOTO 14580
14574 D(I,4)= J- 26:D(I,2)= D(I,4):GOTO 14580
14576 D(I,4)= J- 39:D(I,2)= D(I,4):GOTO 14580
14580 IF D(I,2)> 10 THEN D(I,2)= 10
14590 D(I,0)= 0:I(J)= 1:NEXT I
14600 FOR I= 1 TO 15:V(I,7)= 0:NEXT I
14605 LOCATE 14,2:PRINT STRING$(54," ");
14610 RETURN 
15000 REM  
15010 REM  CLEAR PLAYING MATRIX
15020 REM 
15030 FOR I= 0 TO 8:FOR J= 0 TO 4:H(I,J)= 0:NEXT J:NEXT I
15040 RETURN 
15100 REM  
15110 REM  PLAYERS CARD INTO PLAYING FIELD
15120 REM 
15130 FOR J= 1 TO 4:H(C,J)= Y(CP,J):NEXT J:Y(CP,0)= 1
15140 RETURN 
15200 REM  
15210 REM  COMPUTERS CARD INTO PLAYING FIELD
15220 REM 
15230 FOR J= 1 TO 4:H(C,J)= M(CC,J):NEXT J
15240 RETURN 
15300 REM  
15310 REM  CHECK FOR 15/31 & PAIRS
15320 REM 
15330 P= 0
15340 IF C< 2 THEN RETURN 
15350 IF H(C,4)= H(C- 1,4) THEN P= P+ 2
15360 IF P< 2 OR C< 3 THEN 15400
15370 IF H(C,4)= H(C- 2,4) THEN P= P+ 4
15380 IF P< 6 OR C< 4 THEN 15400
15390 IF H(C,4)= H(C- 3,4) THEN P= P+ 6
15400 IF WS= 15 THEN P= P+ 2
15410 IF WS= 31 THEN P= P+ 2
15420 RETURN 
15500 REM  
15510 REM  PLOT SCORE
15520 REM 
15530 LOCATE 4,61:PRINT "YOU HAVE";SP;
15535 SCOR=SP:XS=60:YS=5:GOSUB 15700
15540 LOCATE 7,62:PRINT "PC HAS ";SC;
15545 SCOR=SC:XS=60:YS=6:GOSUB 15700
15550 RETURN
15700 LOCATE YS,XS:IF SCOR>30 THEN PRINT"▄▄▄"; ELSE GOTO 15800
15710 XS=XS+4:LOCATE YS,XS:IF SCOR>60 THEN PRINT"▄▄▄"; ELSE GOTO 15800
15720 XS=XS+4:LOCATE YS,XS:IF SCOR>90 THEN PRINT"▄▄▄"; ELSE GOTO 15800
15730 XS=XS+4:LOCATE YS,XS:IF SCOR>120 THEN PRINT"▄▄▄"; ELSE GOTO 15800
15800 PRINT STRING$((SCOR/30-INT(SCOR/30))*3,"▄");:
15810 IF (SCOR/10 -INT(SCOR/10)*10 >4) THEN PRINT"▌";
15820 RETURN
15950 RETURN
16000 REM  
16010 REM  MID/END PAIR CHECK FOR RUNS
16020 REM 
16030 PT= 0
16040 IF C< 3 THEN RETURN 
16050 IF H(C,4)= H(C- 1,4) THEN RETURN
16060 IF H(C,4)= H(C- 2,4) THEN RETURN
16070 IF H(C- 1,4)= H(C- 2,4) THEN RETURN
16080 REM 
16090 REM  LOW END SET
16100 REM 
16110 L= 1:FOR J= C TO 2 STEP - 1:FOR K= J- 1 TO 1 STEP - 1
16120 IF H(J,4)= H(K,4)AND K> L THEN L= K
16130 NEXT K:NEXT J
16140 REM 
16150 REM  CHECK RUNS
16160 REM 
16170 IF C- L< 2 THEN RETURN 
16180 LO= 99:HI= 0:SUM= 0
16190 FOR K= L TO C
16200 IF H(K,4)< LO THEN LO= H(K,4)
16210 IF H(K,4)> HI THEN HI= H(K,4)
16220 SUM= SUM+ H(K,4)
16230 NEXT K
16240 REM 
16250 REM  SCORE RUNS
16260 REM 
16270 MID= (LO+ HI)/ 2
16280 CK= (C- L+ 1)* MID
16290 IF CK= SUM THEN PT= C- L+ 1
16300 IF PT< 3 THEN PT= 0
16310 IF (HI- LO+ 1)/ (C- L+ 1)< > 1 THEN PT= 0
16320 IF PT= 0 THEN L= L+ 1:GOTO 16170
16330 RETURN 
17000 REM 
17010 REM  SCROLL CONTROL FOR PRINT STATEMENTS
17020 REM 
17030 SCR%=6*256+7
17035 X1=44:Y1=19:W1=33:H1=5:GOSUB 64000
17050 GOSUB 17800
17060 RETURN
17100 REM 
17110 REM  ERASE PLAYED CARD - PLAYER FIELD
17120 REM 
17130 ON CP GOSUB 20000,20100,20200,20300,20400,20500
17140 LOCATE Y1+ H1- 1,X1:PRINT SPC( W1)
17145 GOSUB 64000
17150 FOR XX= 2 TO 9:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT XX:GOSUB 60100:RETURN
17200 REM 
17210 REM  CARD GRAPHICS
17220 REM 
17230 FOR I= 0 TO 34:T$(I)= " ":NEXT :REM 
17240 IF ST= 3 THEN U$= CHR$(5):GOTO 17280:REM
17250 IF ST= 4 THEN U$= CHR$(3):GOTO 17280:REM
17260 IF ST= 2 THEN U$= CHR$(4):GOTO 17280:REM
17270 IF ST= 1 THEN U$= CHR$(6):REM
17280 REM PLOT 6,56
17290 ON VA GOTO 17480,17470,17460,17450,17430,17410,17400,17390,17380,17360,17320,17310
17300 T$(17)= "K":GOTO 17330
17310 T$(17)= "Q":GOTO 17330
17320 T$(17)= "J"
17330 T$(8)= U$:T$(7)= U$:T$(6)= U$:T$(11)= U$:T$(16)= U$
17340 T$(21)= U$:T$(26)= U$:T$(27)= U$:T$(28)= U$:T$(23)= U$
17350 T$(18)= U$:T$(13)= U$:GOTO 17490
17360 T$(6)= U$:T$(8)= U$:T$(16)= U$:T$(18)= U$:T$(26)= U$
17370 T$(28)= U$:GOTO 17450
17380 T$(7)= U$:T$(27)= U$:GOTO 17400
17390 T$(12)= U$:T$(22)= U$:GOTO 17410
17400 T$(17)= U$
17410 T$(6)= U$:T$(8)= U$:T$(16)= U$:T$(18)= U$:T$(26)= U$
17420 T$(28)= U$:GOTO 17490
17430 T$(6)= U$:T$(8)= U$:T$(17)= U$:T$(26)= U$:T$(28)= U$
17440 GOTO 17490
17450 T$(11)= U$:T$(13)= U$:T$(21)= U$:T$(23)= U$:GOTO 17490
17460 T$(7)= U$:T$(17)= U$:T$(27)= U$:GOTO 17490
17470 T$(12)= U$:T$(22)= U$:GOTO 17490
17480 T$(17)= U$
17490 IF VA> 1 AND VA< 11 THEN T$= RIGHT$ (STR$ (VA),1)
17500 IF VA= 11 THEN T$= "J"
17510 IF VA= 12 THEN T$= "Q"
17520 IF VA= 13 THEN T$= "K"
17530 IF VA= 1 THEN T$= "A"
17540 IF ST= 2 OR ST= 4 THEN T$= ""+ T$:REM 
17550 IF VA< > 10 THEN 17580
17560 T$= LEFT$ (U$,2):M$= "0":N$= "1":IF ST= 2 OR ST= 4 THEN T$= ""+ T$:REM 
17570 T$(0)= "1":T$(1)= "0":T$(33)= "1":T$(34)= "0":GOTO 17590
17580 T$(0)= T$:T$(34)= T$
17590 CN= Y1+ H1- 1
17595 COLOR 0,7
17600 FOR I= 0 TO 30 STEP 5:CALL SCROLL%(SCR%,POS1%,POS2%):LOCATE CN+1,X1:FOR J= 0 TO 4:PRINT T$(I+ J);:NEXT
17610 NEXT I
17615 COLOR 7,0
17630 GOSUB 60000:RETURN
17700 REM 
17710 REM  ERASE CARDS - BOARD FIELD
17720 REM 
17730 FOR B= 1 TO 8
17735 DEF SEG =0:IF ((PEEK(&H410) AND &H30)<> &H30) THEN DEF SEG = &HB800 ELSE DEF SEG = &HB000:REM CHECK WHICH DISPLAY
17740 ON B GOSUB 21000,21100,21200,21300,21400,21500,21600,21700
17745 IF PEEK (1428 + B*14)= 32 THEN 17770
17750 LOCATE Y1+ H1- 2,X1:PRINT SPC( W1+1);
17755 DEF SEG =AA
17760 FOR XX= 1 TO 7:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT XX:GOSUB 60100
17770 NEXT B:DEF SEG=AA:RETURN
17800 REM 
17810 REM  CALL AND PLOT
17820 REM 
17830 CALL SCROLL%(SCR%,POS1%,POS2%):GOSUB 60300:LOCATE Y1+ H1,X1:RETURN
20000 REM  
20001 REM  PLAYER CARD 1
20002 REM 
20010 X1= 2:Y1= 2:W1= 5:H1= 6:GOSUB 64000
20020 RETURN 
20100 REM  
20101 REM  PLAYER CARD 2
20102 REM  
20110 X1= 9:Y1= 2:W1= 5:H1= 6:GOSUB 64000
20120 RETURN 
20200 REM  
20201 REM  PLAYER CARD 3
20202 REM  
20210 X1= 16:Y1= 2:W1= 5:H1= 6:GOSUB 64000
20220 RETURN 
20300 REM  
20301 REM  PLAYER CARD 4
20302 REM  
20310 X1= 23:Y1= 2:W1= 5:H1= 6:GOSUB 64000
20320 RETURN 
20400 REM  
20401 REM  PLAYER CARD 5
20402 REM  
20410 X1= 30:Y1= 2:W1= 5:H1= 6:GOSUB 64000
20420 RETURN 
20500 REM  
20501 REM  PLAYER CARD 6
20502 REM  
20510 X1= 37:Y1= 2:W1= 4:H1= 6:GOSUB 64000
20520 RETURN 
20600 REM  
20601 REM  UP CARD
20602 REM  
20610 X1= 50:Y1= 2:W1= 4:H1= 6:GOSUB 64000
20620 RETURN 
21000 REM  
21001 REM  COMPUTER CARD 1//BOARD CARD 1
21002 REM  
21010 X1= 2:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21020 RETURN 
21100 REM  
21101 REM  COMPUTER CARD 2//BOARD CARD 2
21102 REM  
21110 X1= 9:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21120 RETURN 
21200 REM  
21201 REM  COMPUTER CARD 3//BOARD CARD 3
21202 REM  
21210 X1= 16:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21220 RETURN 
21300 REM  
21301 REM  COMPUTER CARD 4//BOARD CARD 4
21302 REM  
21310 X1= 23:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21320 RETURN 
21400 REM  
21401 REM  COMPUTER CARD 5//BOARD CARD 5
21402 REM  
21410 X1= 30:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21420 RETURN 
21500 REM  
21501 REM  COMPUTER CARD 6//BOARD CARD 6
21502 REM  
21510 X1= 37:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21520 RETURN 
21600 REM  
21601 REM  BOARD CARD 7
21602 REM  
21610 X1= 44:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21620 RETURN 
21700 REM  
21701 REM  BOARD CARD 8
21702 REM  
21710 X1= 51:Y1= 10:W1= 4:H1= 6:GOSUB 64000
21720 RETURN 
22000 REM  
22001 REM  COMPUTER CARD 1//CRIB CARD 1
22002 REM  
22010 X1= 2:Y1= 18:W1= 4:H1= 6:GOSUB 64000
22020 RETURN 
22100 REM  
22101 REM  COMPUTER CARD 2//CRIB CARD 2
22102 REM  
22110 X1= 9:Y1= 18:W1= 4:H1= 6:GOSUB 64000
22120 RETURN 
22200 REM  
22201 REM  COMPUTER CARD 3//CRIB CARD 3
22202 REM  
22210 X1= 16:Y1= 18:W1= 5:H1= 6:GOSUB 64000
22220 RETURN 
22300 REM  
22301 REM  COMPUTER CARD 4//CRIB CARD 4
22302 REM  
22310 X1= 23:Y1= 18:W1= 4:H1= 6:GOSUB 64000
22320 RETURN 
22400 REM  
22401 REM  COMPUTER CARD 5
22402 REM  
22410 X1= 30:Y1= 18:W1= 4:H1= 6:GOSUB 64000
22420 RETURN 
22500 REM  
22501 REM  COMPUTER CARD 6
22502 REM  
22510 X1= 37:Y1= 18:W1= 4:H1= 6:GOSUB 64000
22520 RETURN 
22600 REM
22601 REM  DRAW BACK OF COMPUTER CARD
22602 REM  
22610 COLOR 0,7:FOR B1= 1 TO 7:LOCATE Y1+ H1-0,X1
22620 CALL SCROLL%(SCR%,POS1%,POS2%)
22630 PRINT "XXXXX";
22640 NEXT B1
22670 COLOR 7,0:GOSUB 60300:RETURN
22700 REM 
22701 REM  ERASE PLAYED CARD - COMPUTER FIELD
22702 REM 
22710 B= FN A(6)+ 1:DEF SEG =0:IF ((PEEK(&H410) AND &H30)<> &H30) THEN DEF SEG = &HB800 ELSE DEF SEG = &HB000:REM CHECK WHICH DISPLAY
22715 IF PEEK(2874+B*14) = 32 THEN GOTO 22710 ELSE DEF SEG = &HF00
22720 ON B GOSUB 22000,22100,22200,22300,22400,22500
22730 LOCATE Y1+ H1- 0,X1:PRINT SPC( W1+1);
22740 FOR B= 1 TO 7:DEF SEG=AA:CALL SCROLL%(SCR%,POS1%,POS2%):NEXT B:GOSUB 60100
22750 RETURN
23000 REM 
23001 REM  SCREEN ERASE
23002 REM  
23005 POKE 21,0:X1= 41:Y1= 2:W1= 14:H1= 14:GOSUB 64000
23007 CALL SCROLL%(SCR%,POS1%,POS2%)
23010 X1= 2:Y1= 2:W1= 39:H1= 22:GOSUB 64000
23020 CALL SCROLL%(SCR%,POS1%,POS2%):GOSUB 60300
23030 POKE 21,1:RETURN
30000 REM  FOUR CARD SCORER
30010 P= 0:
30040 REM  GET THE FOUR CARDS
30050 HH(1)= M(I1,4):HH(2)= M(I2,4):HH(3)= M(I3,4):HH(4)= M(I4,4)
30055 REM  CHECK FOR FLUSH
30060 IF M(I1,3)= M(I2,3) THEN IF M(I2,3)= M(I3,3) THEN IF M(I3,3)= M(I4,3) THEN P= P+ 4
30070 LOCATE 14,9:PRINT "CHECK NUMBER "CN;
30100 REM  SORT FOUR CARDS
30110 FOR X= 1 TO 3:FOR Y= X+ 1 TO 4
30120 IF HH(X)> HH(Y) THEN TE= HH(X):HH(X)= HH(Y):HH(Y)= TE
30130 NEXT Y,X
30200 REM  CHECK FOR PAIRS
30210 FOR X= 1 TO 3:FOR Y= X+ 1 TO 4
30220 IF HH(X)= HH(Y) THEN P= P+ 2
30230 NEXT Y,X
30300 REM  CHECK FOR RUN
30310 IF HH(1)+ 1= HH(2)OR HH(2)+ 1= HH(3)OR HH(3)+ 1= HH(4) THEN 30330
30320 GOTO 30400
30330 IF HH(1)+ 1= HH(2) THEN IF HH(2)+ 1= HH(3) THEN IF HH(3)+ 1= HH(4) THEN P= P+ 4:GOTO 30400
30340 FOR X= 1 TO 2:FOR Y= X+ 1 TO 3:FOR Z= Y+ 1 TO 4
30350 IF HH(X)+ 1= HH(Y) THEN IF HH(Y)+ 1= HH(Z) THEN P= P+ 3
30360 NEXT Z,Y,X
30400 REM  CLIP AT 10
30410 FOR X= 1 TO 4:IF HH(X)> 10 THEN HH(X)= 10
30420 NEXT X
30500 REM  CHECK FOR 15'S
30510 SA= HH(1)+ HH(2)+ HH(3)+ HH(4):IF SA< 15 OR SA> 35 THEN 30600
30520 IF SA= 15 THEN P= P+ 2:GOTO 30600
30530 FOR X= 1 TO 2:FOR Y= X+ 1 TO 3:FOR Z= Y+ 1 TO 4
30540 IF HH(X)+ HH(Y)+ HH(Z)= 15 THEN P= P+ 2
30550 NEXT Z,Y,X
30560 FOR X= 1 TO 3:FOR Y= X+ 1 TO 4
30570 IF HH(X)+ HH(Y)= 15 THEN P= P+ 2
30580 NEXT Y,X
30600 RETURN 
40000 '
40001 ' KEY INPUT ROUTINE
40010 LOCATE ,,1:KB$="":WHILE KB$="":KB$=INKEY$:WEND:IF KB$=CHR$(27) THEN 65000
40020 LOCATE ,,0:RETURN
40100 CHAIN "MENU",1000
42000 REM DRAW TABLE
42001 '
42010 LOCATE 1,1:PRINT"╒═════════════════════════════════════════════════════════════════════════════╕";
42020 FOR N=2 TO 24:LOCATE N,1:PRINT"│";:NEXT
42030 FOR N=2 TO 24:LOCATE N,79:PRINT"│";:NEXT
42040 LOCATE 25,1:PRINT"╘═════════════════════════════════════════════════════════════════════════════╛";
42050 LOCATE 17,43:PRINT"╒═══════════════════════════════════╡";
42060 FOR N=18 TO 24:LOCATE N,43:PRINT"│";:NEXT
42070 LOCATE 25,43:PRINT"╧";
42075 LOCATE 1,57:PRINT"╤";:LOCATE 17,57:PRINT"╧";
42080 FOR N=2 TO 16:LOCATE N,57:PRINT"│";:NEXT
42085 LOCATE 9,57:PRINT"╞═════════════════════╡";
42090 COLOR 0,7:LOCATE 2,63:PRINT"GAME SCORE";
42095 LOCATE 10,62:PRINT"PLAY COMMANDS";
42100 LOCATE 18,48:PRINT" M E S S A G E   B O A R D ";:COLOR 7,0
42110 LOCATE 12,58:PRINT"    G=GO   <cr>=GO";
42120 LOCATE 13,58:PRINT" <esc>=QUIT  121 WINS";
42130 LOCATE 14,58:PRINT"   F10=Sound on/off";
42140 LOCATE 15,58:PRINT"    Patrick Leabo";
42150 LOCATE 16,58:PRINT"      Tucson,Az";
42999 RETURN
43000 REM
43010 REM TOGGLE SOUND
43020 REM
43030 IF SND = 1 THEN SND = 0 ELSE SND = 1
43040 RETURN
50009 DEF SEG=0:AA=PEEK(&H510)+PEEK(&H511)*256+(VARPTR(#1)+16)/16:RETURN
59999 REM 
60000 REM  SCALE SOUND
60005 REM 
60010 IF SND=1 THEN PLAY "L16T120MNN=P1(VA);"
60020 RETURN
60097 REM 
60098 REM  BLEEP SOUND
60099 REM 
60100 IF SND =1 THEN PLAY "L64T200N70"
60110 RETURN
60197 REM 
60198 REM  WHISTLE SOUND
60199 REM 
60200 IF SND=1 THEN PLAY"T255MLL64O5CC#DD#EFF#GG#AA#BMN"
60210 RETURN
60297 '
60298 'CLICK
60299 '
60300 IF SND =1 THEN PLAY"L64T200N46"
60310 RETURN
60997 REM 
64000 REM 
64010 REM  SCROLLING SUBROUTINE
64020 REM 
64030 POS1%=256*(Y1-1)+X1-1:POS2%=256*(Y1+H1-1)+X1+W1:RETURN
65000 END ' CHAIN "MENU",1000
```

## JETSET.BAS

```bas
1 ' *****JETSET*****
2 ' IFR FLIGHT SIMULATOR (BOEING 747)
3 ' CREATED BY GENE SZYMANSKI - BYTE 11-82
4 ' MODIFIED BY R. HOJABOOM - 11-6-82
6 DEF SEG=&H40
7 POKE &H17,(PEEK(&H17) OR 64)
9 ' SYSTEM CLOCK OFF
10 'AND AGAIN BY H.P.REISDORF 11/23/82
12 GOTO 10000
15 ' BEGIN CRUISE MODULE
20 CLEAR,,8000:RANDOMIZE 18
21 KM(0)=7915.704468#
22 KM(1)=23.268932#
23 KM(2)=0.0525
24 KM(3)=0.000213
25 DIM M$(20)
26 KR=57.2958:XQ=64
27 RS=1:IF RND(0)<0.5 THEN RS=-1
28 RW=(15-5)*RND(0)+5
29 RW=RS*RW:TRUE=-1:FALSE=0
30 ' SET UP WIND TABLE
32 DIM WA(10,1)
34 FOR I=0 TO 7:WA(I,0)=RND(359):NEXT
36 FOR I=8 TO 10:WA(I,0)=90*RND(0)+255:NEXT
38 A=0
40 FOR I=1 TO 10:WA(I,1)=25*RND(0)+A:A=A+25:NEXT
41 RS$(0)=STRING$(31,"_")
42 RS$(1)=SPACE$(13)+".   ."+SPACE$(13)
43 RS$(2)=SPACE$(11)+".       ."
44 RS$(3)=SPACE$(9)+STRING$(13,"_")
45 ' VOR FREQ TABLE
46 DIM VF(15)
47 VF(0)=115.9:VF(1)=113.8:VF(2)=112.7
48 VF(3)=117.7:VF(4)=117.8:VF(5)=112.2
49 VF(6)=117.4:VF(7)=115.5:VF(8)=116.4
50 VF(9)=113.6:VF(10)=116.9:VF(11)=117.
51 VF(12)=112.3:VF(13)=117.9:VF(14)=115.7
52 VF(15)=112.8
62 ' ILS CONSTANTS FOR AIRPORTS
63 DIM VG(15,1)
64 VG(0,0)=238:VG(0,1)=28
66 VG(2,1)=240:VG(2,1)=30
69 VG(5,0)=90:VG(5,1)=217
71 VG(7,0)=299:VG(7,1)=164
72 VG(8,0)=166:VG(8,1)=42
79 VG(15,0)=341:VG(15,1)=75
100 ' VOR STATION COORDINATE TABLE
101 DIM VP(15,1)
102 VP(0,0)=40.633:VP(0,1)=73.773
103 VP(1,0)=40.202:VP(1,1)=74.495
104 VP(2,0)=42.358:VP(2,1)=70.993
105 VP(3,0)=41.282:VP(3,1)=70.027
106 VP(4,0)=42.74301:VP(4,1)=73.802
107 VP(5,0)=46.412:VP(5,1)=84.315
108 VP(6,0)=38.35:VP(6,1)=81.77
109 VP(7,0)=40.917:VP(7,1)=77.993
110 VP(8,0)=42.928:VP(8,1)=78.647
111 VP(9,0)=41.358:VP(9,1)=82.162
112 VP(10,0)=42.967:VP(10,1)=83.742
113 VP(11,0)=44.555:VP(11,1)=88.19501
114 VP(12,0)=41.547:VP(12,1)=88.318
115 VP(13,0)=39.495:VP(13,1)=76.978
116 VP(14,0)=42.048:VP(14,1)=83.458
117 VP(15,0)=39.637:VP(15,1)=75.303
155 DATA FUEL,LBS,%,VHF,MHZ,THRUST,MAX,IDLE,REV
160 DATA PITCH," +"," -",DEG,FLAPS,UP,DWN,WHEELS,UP,MID,DOWN
165 DATA COMPASS,AIRSPEED,KTS," VERT",SPEED,FPM
170 DATA ALTITUDE,FEET,CLOCK
175 DIM P$(28)
180 FOR I=0 TO 28:READ P$(I):NEXT
192 FOR I=0 TO 9:F(I)=0:NEXT
195 ' INIT FLIGHT VARIABLES
196 FU=195480.:FP=63:CC=75:AS=380:RC=6704:AL=1900
198 MZ=121.5:TR=4:FL=1:FA=10:BR=10
200 RA=0:AS(1)=380:FA(1)=10:CC(1)=75:AL(1)=1900:RP=40
201 DP(4)=39:DP(5)=52:DP(6)=75:DP(7)=15
202 L1=39.8667:G1=75.25:LL(1)=L1:GL(1)=G1:LS(1)=L1:GS(1)=G1
203 VO(1)=0.:VO$(1)="OUT":VO(2)=8:VO(3)=0:VO(4)=999.9
204 GX(0)=0:GY(0)=0
205 XO=36481.:YO=0
220 TV$=TIME$:GOSUB 11555:TL=TD:' TL=TIME OF DAY IN SECONDS
221 TW(1)=TD
222 GOSUB 335 'DISPLAY PANEL
224 GOSUB 600 'DISPLAY INSTRUMENT READINGS
300 K$=INKEY$:IF K$="" GOTO 304
302 GOSUB 800 'KEY INPUT ROUTINE
304 GOSUB 1000 'UPDATE ROUTINE
305 GOTO 224 'DO IT ALL AGAIN
335 CLS:' DISPLAY PANEL
340 LOCATE 1,3:PRINT P$(0);SPACE$(4);P$(5);SPACE$(3);P$(9);SPACE$(12);P$(20);
341 LOCATE 11,59:PRINT P$(13);SPACE$(3);P$(16);
342 LOCATE 12,61:PRINT P$(14);
343 LOCATE 14,61:PRINT P$(15);
344 LOCATE 2,13:PRINT P$(6);SPACE$(6);P$(10);
345 LOCATE 2,68:PRINT P$(28);
355 LOCATE 3,1:PRINT P$(1);SPACE$(4);P$(2);SPACE$(37);P$(23);
360 LOCATE 4,22:PRINT P$(11);SPACE$(13);P$(21);SPACE$(2);P$(24);SPACE$(2);P$(26);
365 LOCATE 5,1:PRINT P$(3);
370 LOCATE 6,39:PRINT P$(22);SPACE$(6);P$(25);SPACE$(5);P$(27);
375 LOCATE 7,1:PRINT P$(4);SPACE$(10);P$(7);SPACE$(3);P$(12);
380 LOCATE 8,14:PRINT P$(8);
381 LOCATE 4,68:PRINT "VLF OMEGA";:LOCATE 5,65:PRINT "LAT";
382 LOCATE 6,64:PRINT "LONG";
383 LOCATE 7,32:PRINT ". . . . : . . . .";:LOCATE 9,39:PRINT "RUD";
384 LOCATE 10,32:PRINT CHR$(218);STRING$(15,196);CHR$(191);
386 FOR X=32 TO 48 STEP 16
387 FOR Y=11 TO 23
388 LOCATE Y,X:PRINT CHR$(179);
389 NEXT Y
390 NEXT X
391 LOCATE 23,32:PRINT CHR$(192);STRING$(15,196);CHR$(217);
392 LOCATE 11,39:PRINT "VOR":LOCATE 12,44:PRINT "MHZ"
393 LOCATE 15,38:PRINT "RANGE"                
394 LOCATE 17,35:PRINT ".....:....."           
395 LOCATE 20,38:PRINT "RADIAL"    
396 LOCATE 21,39:PRINT "DME":LOCATE 22,44:PRINT "NM";
397 FOR Y=2 TO 8:LOCATE Y,12:PRINT CHR$(196):NEXT
398 FOR Y=2 TO 4:LOCATE Y,21:PRINT CHR$(196):NEXT
400 LOCATE 15,1:PRINT CHR$(218);STRING$(15,196);CHR$(191)
402 FOR X=1 TO 17 STEP 16
404 FOR Y=16 TO 22
406 LOCATE Y,X:PRINT CHR$(179);
408 NEXT Y
410 NEXT X
412 LOCATE 23,1:PRINT CHR$(192);STRING$(15,196);CHR$(217);
414 LOCATE 14,7:PRINT "ILS"
416 LOCATE 18,19:PRINT "MARKER":LOCATE 19,20:PRINT ">  <"
418 GX(1)=0:GY(1)=0
420 GOSUB 2100
422 LOCATE 11,6:COLOR 0,7:PRINT "     "
424 LOCATE 10,4:COLOR 7,0:PRINT "RADAR ALT";SPC(6);"STALL"
425 LOCATE 11,18:PRINT ">     <"
499 RETURN
500 '
501 'FREE BLOCK OF LINE NUMBERS FROM 502 TO 599
599 'END OF FREE BLOCK
600 ' DISPLAY INST. READ
601 ' RUDDER
602 COLOR 0,2,8:LOCATE 8,32:PRINT SPACE$(17)
603 LOCATE 8,RP:PRINT CHR$(24):COLOR 2,0,8
605 YP=2:XP=1:F$="######":V1=FU:GOSUB 11600
610 YP=2:XP=8:F$="##":V1=FP:GOSUB 11600
615 YP=2:XP=39:F$="###":V1=CC:GOSUB 11600
620 YP=5:XP=39:V1=AS:GOSUB 11600
625 YP=5:XP=46:F$="#####":V1=RC:GOSUB 11600
630 YP=5:XP=55:F$="##,###":V1=INT(AL):GOSUB 11600
635 YP=6:XP=1:F$="###.#":V1=MZ:GOSUB 11600
636 IF F(2)=1 THEN YP=11:XP=6:F$="#####":V1=INT(AL):GOSUB 11600
638 LOCATE 1,67:PRINT TIME$
640 ' DISP. THRUST
650 FOR I=2 TO 8
651 LOCATE I,11:COLOR 0,2,8:PRINT " "
652 NEXT
655 LOCATE TR+1,11:PRINT ">";
660 ' DISP PITCH
665 COLOR 0,2,8:FOR I=2 TO 4:LOCATE I,20:PRINT " ":NEXT
670 LOCATE FL+1,20:PRINT ">"
674 F$="+###"
675 YP=6:XP=20:V1=FA:GOSUB 11600
676 IF F(6)=0 THEN SX=25:COLOR 2,0,8:GOTO 679
677 IF SX=25 THEN SX=26:COLOR 0,2,8:GOTO 679
678 SX=25
679 IF F(7)=0 THEN LOCATE 11,19:PRINT "     " ELSE IF F(7)<>0 THEN LOCATE 11,19:COLOR 0,7:PRINT"     ";:COLOR 7,0:BEEP
680 ' DISP FLAPS
690 FOR I=12 TO 14:LOCATE I,59:PRINT " ":NEXT
695 LOCATE BR+2,59:COLOR 0,7:PRINT ">";:COLOR 7,0
700 ' WHEELS
705 IF WH THEN LOCATE 12,68:PRINT"  "+CHR$(186)+" ";:LOCATE 13,69:PRINT CHR$(222)+CHR$(202)+CHR$(221); ELSE COLOR 0,7:LOCATE 12,68:PRINT STRING$(4,47);:COLOR 7,0:LOCATE 13,69:PRINT"   ";
711 IF F(2)=1 GOTO 723
712 ' LAT/LONG
713 YP=5:XP=69:F$="###":V1=DP(4):GOSUB 11600
714 YP=5:XP=73:F$="##.#":V1=DP(5):GOSUB 11600
715 YP=6:XP=69:F$="###":V1=DP(6):GOSUB 11600
716 YP=6:XP=73:F$="##.#":V1=DP(7):GOSUB 11600
717 LOCATE 5,78:PRINT "N"
718 LOCATE 6,78:PRINT "W"
723 IF F(2)=1 THEN RETURN
724 ' DISP VOR
725 YP=12:XP=38:F$="###.#":V1=VO(1):GOSUB 11600
726 COLOR 0,2,8:LOCATE 14,39:PRINT VO$(1):COLOR 2,0,8
727 YP=19:XP=39:F$="###":V1=VO(3):GOSUB 11600
728 YP=22:XP=38:F$="###.#":V1=VO(4):GOSUB 11600
729 COLOR 0,2,8:LOCATE 18,34:PRINT SPACE$(13)
730 IF VO$(1)="OUT " GOTO 732
731 LOCATE 18,VO(2)+1:PRINT CHR$(193)
732 RETURN
800 ' KEY INPUT
805 IF K$="F" OR K$="S" THEN KK=1:GOTO 850
810 IF K$="," OR K$="." THEN KK=2:GOTO 850
811 IF K$="/" THEN RA=0:RETURN
825 IF K$="U" OR K$="D" THEN KK=3:GOTO 850
830 IF K$="W" THEN KK=4:GOTO 850
835 IF K$="V" THEN KK=5:GOTO 850
836 IF K$="R" THEN KK=6:GOTO 850
837 IF K$="L" THEN KK=7:GOTO 850
838 IF K$="A" THEN KK=8:GOTO 850
839 IF K$="Q" THEN KK=9:GOTO 850
840 IF K$="M" THEN KK=10:GOTO 850
841 IF K$="\" THEN KK=11:GOTO 850
850 ON KK GOTO 900,908,914,928,931,935,940,944,946,950,953
855 GOTO 960
900 ' THRUST
902 IF K$="F" THEN TR=TR-1 ELSE TR=TR+1
904 IF TR<1 THEN TR=1
906 IF TR>6 THEN TR=6
907 GOTO 960
908 ' RUDDER
909 IF K$="," GOTO 912 ELSE RA=RA+1
910 IF RA>4 THEN RA=4
911 GOTO 960
912 RA=RA-1:IF RA<-4 THEN RA=-4
913 GOTO 960
914  ' ELEVATOR
915 J=5:IF BR=12 THEN J=1
916 IF K$="U" THEN FA=FA+J ELSE FA=FA-J
918 IF FA>40 THEN FA=40
920 IF FA<-40 THEN FA=-40
922 IF FA>0 THEN FL=1:GOTO 960
924 IF FA=0 THEN FL=2:GOTO 960
926 FL=3:GOTO 960
928 ' WHEELS
929 IF WH THEN WH=FALSE:GOTO 960
930 IF NOT WH THEN WH=TRUE:GOTO 960
931 ' VOR FREQ
932 LOCATE 15,55
933 LINE INPUT;"VOR FREQ ";VX$:VO(1)=VAL(VX$)
934 LOCATE 15,55:PRINT SPC(16);:GOTO 960
935 ' SET VOR RADIAL
936 LOCATE 15,55
937 LINE INPUT;"VOR RADIAL ";VX$:VO(3)=VAL(VX$)
938 LOCATE 15,55:PRINT SPC(14);:GOTO 960
940 ' FLAPS
941 IF BR=10 THEN BR=12:GOTO 960
942 IF BR=12 THEN BR=10:GOTO 960
944 ' AUTO KEY
945 F(3)=1:GOTO 960
946 ' REV THRUST
947 IF F(7)=1 THEN TR=7:GOTO 960
950 ' MISSED APPROACH
951 IF F(2)=1 THEN F(5)=1:GOTO 960
953 ' FLARE
954 IF F(2)=0 THEN FA=0:FL=2:GOTO 960
955 FA=1:FL=1:GOTO 960
960 RETURN
1000 ' UPDATE
1002 TV$=TIME$:GOSUB 11555:TJ=TD-TL:TL=TD
1010 GOSUB 1100
1011 GOSUB 1130
1013 GOSUB 1145
1014 IF TR=7 THEN GOSUB 1800 ELSE GOSUB 1124
1016 GOSUB 1106
1018 GOSUB 1114
1019 IF F(2)=1 GOTO 1600
1020 GOSUB 1400
1021 GOSUB 1500
1030 IF AL<=0 THEN AF=1:GOTO 3000
1031 IF F(2)=0 GOTO 1099
1032 IF AL>0 GOTO 1068
1033 IF F(7)=1 GOTO 1090
1034 F(7)=1
1036 TY=YN
1038 TX=XN-750
1042 IF FA>1 THEN AF=2:GOTO 3000
1044 IF FA<0 THEN AF=3:GOTO 3000
1046 IF F(6)=0 GOTO 1062
1048 IF AX>100 THEN AF=4:GOTO 3000
1050 IF AX>80 THEN AF=5:GOTO 3000
1052 IF WH=FALSE THEN AF=6:GOTO 3000
1054 IF TX>0 THEN AF=7:GOTO 3000
1056 IF TX<-10500 THEN AF=7:GOTO 3000
1058 IF ABS(TY)>100 THEN AF=7:GOTO 3000
1060 GOTO 1099
1062 AF=8:GOTO 3000
1068 IF F(5)=1 GOTO 1082
1070 IF FA>0 GOTO 1076
1072 IF FA<0 THEN F(6)=0
1074 GOTO 1099
1076 IF F(6)=0 THEN AX=AL:F(6)=1
1077 IF TR=6 GOTO 1080
1078 IF FA<2 THEN RC=-1800:GOTO 1099
1079 FA=2:FL=1:RC=-1800:GOTO 1099
1080 FA=1:FL=1:RC=-300:GOTO 1099
1082 IF AL<20 THEN AF=9:GOTO 3000
1084 TR=3:FA=0:FL=2
1086 AF=10:GOTO 3000
1090 IF XN<-9750 OR ABS(YN)>100 GOTO 1094
1091 IF AS>0 GOTO 1099
1092 AF=0:GOTO 3000
1094 AF=9:GOTO 3000
1099 GOTO 1700
1100 ' FUEL
1101 FU=FU-(14-TR)*TJ:IF FU<0 THEN FU=0
1104 FP=FU/3120:RETURN
1106 IF F(7)=1 THEN RC=0:RETURN
1107 IF F(6)=1 THEN RETURN
1108 RC=AS*SIN(ABS(FA/KR))*101.6
1110 IF FA<0 THEN RC=-1*RC
1112 RETURN
1114 ' ALTITUDE
1115 IF F(7)=1 THEN RETURN
1116 AL=INT(AL+TJ*RC/60)
1118 IF AL<0 THEN AL=0:RETURN
1120 IF AL>45000. THEN AL=45000.
1122 RETURN
1124 ' AIRSPEED
1125 AS=800-100*TR
1126 AS=AS-2*FA
1127 IF BR=10 THEN RETURN
1128 AS=AS/3+20:RETURN
1130 IF F(5)=1 GOTO 1132
1131 IF F(2)=1 GOTO 1142
1132 IF RA=0 THEN RT=0:RETURN
1133 IF RA<0 THEN J=-1 ELSE J=1
1134 RA=ABS(RA):IF RA=1 THEN RT=1:GOTO 1137
1135 IF RA=2 THEN RT=10:GOTO 1137
1136 IF RA=3 THEN RT=20 ELSE RT=30
1137 RT=J*RT:RA=J*RA
1138 CC=CC+RT
1139 IF CC=>360 THEN CC=CC-360:RETURN
1140 IF CC<0 THEN CC=360+CC
1141 RETURN
1142 IF RA=0 THEN RETURN
1143 IF RA<0 THEN CC=CC-1 ELSE CC=CC+1
1144 RA=0:GOTO 1139
1145 ' RUDDER POSIT
1146 RP=40+RA+RA:RETURN
1200 ' COMPUTE DISTANCE
1202 VL=ABS(LB-LC):BL(5)=-1:IF LB=>LC THEN BL(5)=1
1204 VG=ABS(GB-GC):BL(4)=1:IF GB=>GC THEN BL(4)=-1
1206 LA=LC:GOSUB 2400
1208 M1=MP
1210 LA=LB:GOSUB 2400
1212 M2=MP:DM=ABS(M2-M1)
1214 IF DM=0 GOTO 1220
1216 QO=VG/(DM/60):IF QO>114.59 GOTO 1220
1218 CA=KR*ATN(QO):DR=VL*(1/COS(CA/KR)):GOTO 1222
1220 CA=90:DR=VG*COS(LC/KR)
1222 DR=DR*60:IF BL(5)=-1 GOTO 1226
1224 IF BL(4)=1 THEN CR=CA ELSE CR=360-CA
1225 RETURN
1226 IF BL(4)=1 THEN CR=180-CA ELSE CR=180+CA
1228 RETURN
1250 ' WIND VECTOR
1252 I =FIX(AL/4000):IF I=>10 THEN I=10
1254 WD=WA(I,0):WV=WA(I,1)
1256 RETURN
1260 ' WIND SOLUTION
1262 A=CC:L=AJ:GOSUB 1300
1264 MX=LX:MY=LY
1266 GOSUB 1250
1268 A=WD+180:IF A=>360 THEN A=A-360
1270 L=WV:GOSUB 1300
1272 MX=MX+LX:MY=MY+LY:GOSUB 1350
1274 TK=MA:GS=VZ
1276 RETURN
1300 ' VECTOR TO RECTANGULAR
1302 IF A<=90 THEN Q=1:B=90-A:GOTO 1310
1304 IF A<=180 THEN Q=2:B=A-90:GOTO 1310
1306 IF A<=270 THEN Q=3:B=270-A:GOTO 1310
1308 Q=4:B=A-270
1310 LX=L*COS(B/KR):LY=L*SIN(B/KR)
1312 IF Q=1 THEN RETURN
1314 IF Q=2 THEN LY=-1*LY:RETURN
1316 IF Q=3 THEN LX=-1*LX:LY=-1*LY:RETURN
1318 LX=-1*LX
1320 RETURN
1350 ' POLAR VECTOR
1352 VZ=SQR(MX^2+MY^2)
1354 IF MX<=0 GOTO 1358
1356 IF MY<=0 THEN Q=2 ELSE Q=1
1357 GOTO 1360
1358 IF MY<=0 THEN Q=3 ELSE Q=4
1360 IF MX=0 THEN MA=90:GOTO 1366
1362 MQ=ABS(MY/MX)
1364 MA=ATN(MQ)*KR
1366 IF Q=1 THEN MA=90-MA:RETURN
1368 IF Q=2 THEN MA=90+MA:RETURN
1370 IF Q=3 THEN MA=270-MA:RETURN
1372 MA=270+MA
1374 RETURN
1400 ' OMEGA POSIT DISPLAY
1402 AS(2)=AS:FA(2)=FA:CC(2)=CC:AL(2)=AL
1404 AS=AS(1):FA=FA(1):CC=CC(1):AL=AL(1)
1406 IF AS(2)<>AS OR FA(2)<>FA GOTO 1440
1408 IF CC(2)<>CC OR AL(2)<>AL GOTO 1440
1410 F(0)=1:IF TD<TW(1)+60 GOTO 1442
1412 AJ=AS*COS(ABS(FA)/KR)
1414 GOSUB 1260
1416 DN=GS*(TD-TW(1))/3600
1418 CN=TK:L1=LL(1):G1=GL(1)
1420 GOSUB 2000
1422 F(0)=0:TW(1)=TD:LL(1)=L2:GL(1)=G2
1424 LS(1)=L2:GS(1)=G2
1426 FOR J=4 TO 7:DP(J)=CP(J):NEXT
1428 AS(1)=AS(2):FA(1)=FA(2):CC(1)=CC(2):AL(1)=AL(2)
1430 AS=AS(2):FA=FA(2):CC=CC(2):AL=AL(2)
1432 RETURN
1440 TW(1)=TD:F(0)=0
1442 AJ=AS*COS(ABS(FA)/KR)
1444 GOSUB 1260
1446 DN=GS*TJ/3600
1448 CN=TK:L1=LS(1):G1=GS(1)
1450 GOSUB 2000
1452 LS(1)=L2:GS(1)=G2
1454 IF F(0)=1 GOTO 1458
1456 LL(1)=L2:GL(1)=G2
1458 GOTO 1426
1500 ' VOR
1502 IF VO(1)=0 GOTO 1540
1504 FOR J=0 TO 15:IF VO(1)=VF(J) GOTO 1506
1505 NEXT:GOTO 1540
1506 LC=VP(J,0):GC=VP(J,1)
1508 LB=L2:GB=G2
1509 AR=VG(J,0):LL=VG(J,1)
1510 LO=360-LL
1512 GOSUB 1200
1514 IF DR>300 GOTO 1540
1516 IF CR=>360 THEN CR=CR-360
1517 GOTO 1578
1518 VO$(1)="FROM"
1519 VO(5)=CR-VO(3):IF VO(5)<=180 GOTO 1522
1520 VO(5)=VO(5)-360:GOTO 1524
1522 IF VO(5)<-180 THEN VO(5)=VO(5)+360
1524 IF ABS(VO(5))>90 THEN I1=VO(3):GOTO 1536
1526 IF VO$(1)="FROM" THEN I=-1 ELSE I=1
1527 IF I=1 THEN VO(3)=I1
1528 VO(2)=40+I*INT(VO(5)):VO(4)=DR
1530 IF VO(2)<35 THEN VO(2)=34
1532 IF VO(2)>45 THEN VO(2)=45
1534 GOTO 1560
1536 VO$(1)=" TO ":VO(3)=VO(3)+180:IF VO(3)=>360 THEN VO(3)=VO(3)-360
1538 GOTO 1519
1540 VO$(1)="OUT ":VO(4)=999.9:F(3)=0:RETURN
1546 IF DR>12 OR DR<10 THEN MK=0:GOTO 1518
1548 IF AL>4000 THEN MK=0:GOTO 1518
1550 IF F(1)=1 THEN J=9 ELSE J=2.5
1552 IF CR>AR+J OR CR<AR-J THEN MK=0:GOTO 1518
1554 F(1)=1:MK=1:GOTO 1518
1560 IF MK=0 THEN COLOR 7,0
1561 IF MK=1 THEN COLOR 0,7
1562 LOCATE 19,21:PRINT "  "
1564 COLOR 2,0,8
1566 IF MK=0 THEN F(1)=0:RETURN
1570 IF CC>LL+1 OR CC<LL-1 THEN RETURN
1572 IF RA<>0 THEN RETURN
1574 F(1)=0:F(2)=1
1576 GOTO 1540
1578 IF F(3)=1 THEN VO(3)=CR:F(3)=0
1580 GOTO 1546
1600 ' ILS
1602 ZN=AL:TH=2.82471:MK=1
1603 DW=1.69*RW*TJ:IF F(7)=1 THEN DW=0
1604 IF F(7)=1 THEN CC=LL:RA=0
1606 CJ=CC
1608 IF CJ>180 THEN CJ=360-CJ:CJ=-1*CJ
1610 HA=LO+CJ
1612 DC=360-HA:TS=1:IF HA<180 THEN DC=HA:TS=-1
1614 IF DC<0 THEN DC=-1*DC:TS=-1*TS
1616 TL$="W":IF TS=1 THEN TL$="E"
1618 DD=1.69*AS*TJ
1620 DY=DD*SIN(DC/KR):DX=DD*COS(DC/KR)
1622 XN=XO-DX:IF TL$="E" THEN DY=-1*DY
1624 YN=YO+DY+DW
1626 LM=KR*ATN(AL/XN)
1628 BE=KR*ATN(ABS(YN)/ABS(XN)):IF BE>2.5 THEN MK=0
1630 J=(LM-TH)/0.25:J=FIX(J)
1632 IF J>3 THEN J=3
1633 IF J<-3 THEN J=-3
1636 BE=BE/0.25:BE=FIX(BE)
1637 IF BE<-7 THEN BE=-7
1638 IF BE>7 THEN BE=7
1639 IF YN=>0 THEN BE=-1*BE
1640 IF MK=0 GOTO 1648
1642 IF XN>34960. AND XN<38000. GOTO 1648
1644 IF XN>2534 AND XN<5574 GOTO 1648
1646 MK=0
1648 GX(1)=BE:GY(1)=J
1650 GOSUB 2100
1654 IF MK=0 THEN COLOR 2,0,8
1655 IF MK=1 THEN COLOR 0,2,8
1656 LOCATE 19,21:PRINT "  "
1658 COLOR 2,0,8
1662 XO=XN:YO=YN
1664 IF F8=1 GOTO 1676
1666 IF XN>12000 GOTO 1676
1668 F8=1
1670 FOR I=0 TO 3
1672 LOCATE 14+I,49:PRINT RS$(I)
1674 NEXT I
1676 IF F8=0 GOTO 1699
1678 LOCATE 18,XQ:PRINT " "
1680 YU=FIX(YN/16.7):XC=64+YU
1682 IF XC<49 THEN XC=49
1684 IF XC>79 THEN XC=79
1686 LOCATE 18,XC:PRINT CHR$(234)
1688 XQ=XC
1699 GOTO 1031
1700 GOTO 1799
1799 GOTO 224
1800 ' REV THRUST
1802 IF F(4)=1 GOTO 1806
1804 VO=AS*1.152*1.47:F(4)=1
1806 V=VO-(3.23636)*TJ
1808 IF V<=0 THEN V=0
1810 VO=V
1812 AS=V/(1.152*1.47)
1814 RETURN
2000 ' COMPUTE POSITION
2004 IF CN=0 THEN CA=0:LB(5)=1:LB(6)=1:GOTO 2014
2006 IF CN>0 AND CN<=90 THEN CA=CN:LB(5)=1:LB(6)=1:GOTO 2014
2008 IF CN>90 AND CN<=180 THEN CA=180-CN:LB(5)=-1:LB(6)=1:GOTO 2014
2010 IF CN>180 AND CN<=270 THEN CA=CN-180:LB(5)=-1:LB(6)=-1:GOTO 2014
2012 CA=360-CN:LB(5)=1:LB(6)=-1
2014 IF CA>89.5 GOTO 2034
2016 DL=DN*COS(CA/KR):DL=DL/60:LB(3)=LB(5)
2018 IF LB(3)=1 THEN L2=L1+DL:GOTO 2024
2020 L2=ABS(L1-DL)
2024 LA=L1:GOSUB 2400
2026 M1=MP
2028 LA=L2:GOSUB 2400
2030 M2=MP
2032 DM=ABS(M1-M2):DG=DM*TAN(CA/KR):GOTO 2036
2034 L2=L1:DG=DN/COS(L1/KR)
2036 DG=DG/60:LB(4)=LB(6)
2038 IF LB(4)=-1 GOTO 2046
2040 G2=ABS(G1-DG)
2044 GOTO 2050
2046 G2=G1+DG
2050 CP(4)=FIX(L2):CP(5)=(L2-CP(4))*60
2052 CP(6)=FIX(G2):CP(7)=(G2-CP(6))*60
2054 RETURN
2100 ' PLOT GLIDESLOPE
2101 IF XO<=750 THEN RETURN
2102 X0=9+GX(0):X1=9+GX(1):Y0=19+GY(0):Y1=19+GY(1)
2103 COLOR 0,2,8
2104 FOR Y=16 TO 22:LOCATE Y,X0:PRINT " ":NEXT
2106 LOCATE Y0,2:PRINT STRING$(15,32)
2108 FOR Y=16 TO 22:LOCATE Y,X1:PRINT CHR$(197):NEXT
2110 LOCATE Y1,2:PRINT STRING$(15,197)
2112 LOCATE Y1,X1:PRINT CHR$(219)
2114 LOCATE 19,9:PRINT "O"
2116 GX(0)=GX(1):GY(0)=GY(1)
2118 RETURN
2400 ' COMP MERIDIONAL PARTS
2404 KM(0)=7915.704468#
2406 KM(1)=23.268932#
2408 KM(2)=0.0525
2410 KM(3)=0.000213
2414 IF LA=0 THEN LA=0+1/60
2416 IF LA>(89+59/60) THEN LA=89+59/60
2418 S1=SIN(LA/KR)
2420 S2=S1*S1:S3=S1*S2
2422 TM(0)=TAN((45+LA/2)/KR)
2424 TM(0)=KM(0)*LOG(TM(0))/LOG(10)
2426 TM(1)=KM(1)*S1
2428 TM(2)=KM(2)*S3
2430 TM(3)=KM(3)*S2*S3
2432 MP=TM(0)-TM(1)-TM(2)-TM(3)
2436 RETURN
3000 ' ABORT
3002 M$(1)="---------- A CRASH HAS OCCURRED ----------"
3004 M$(2)="YOU ACCIDENTLY STALLED THE AIRCRAFT DURING FINAL APPROACH."
3006 M$(3)="THE STALL OCCURRED AT AN ALTITUDE OF"
3008 M$(4)="THE AIRCRAFT STRUCK THE GROUND IN A NOSE UP ATTITUDE."
3010 M$(5)="THE IMPACT RUPTURED THE TAIL SECTION OF THE PLANE."
3012 M$(6)="------ LOCATION OF CRASH ------"
3013 M$(7)="------ LANDING POSITION ------"
3016 M$(8)="     PITCH ANGLE="
3018 M$(9)="     AIRSPEED="
3020 M$(10)="YOU FLARED AT TOO HIGH AN ALTITUDE DURING FINAL APPROACH."
3022 M$(11)="THE RESULTING STALL OCCURRED AT AN ALTITUDE OF"
3024 M$(12)="THE IMPACT RUPTURED THE"
3026 M$(13)="YOU FORGOT TO LOWER THE LANDING GEAR."
3028 M$(14)="YOU FAILED TO TOUCH DOWN INSIDE THE RUNWAY."
3030 M$(15)="-------- AN IMPROPER LANDING WAS MADE --------"
3032 M$(16)="NO DAMAGE OR INJURIES OCCURRED."
3033 RL=0:IF TX>0 THEN RL=1
3034 RW=0:IF ABS(TY)>100 THEN RW=1
3040 N$(2)="FEET INSIDE OF RUNWAY"
3041 IF TX>0 THEN N$(2)="FEET SHORT OF RUNWAY"
3042 N$(3)="FEET LEFT OF RUNWAY CENTERLINE"
3043 IF TY>0 THEN N$(3)="FEET RIGHT OF RUNWAY CENTERLINE"
3044 RX=FIX(TX):RX=ABS(RX)
3045 RY=FIX(TY):RY=ABS(RY)
3050 IF AF=0 GOTO 3600
3055 AX=INT(AX):FA=INT(FA):AS=INT(AS)
3060 ON AF GOTO 3100,3150,3200,3250,3300,3350,3400,3450,3500,3550
3100 GOSUB 3700
3101 YP=4:XP=55:F$="##,###":V1=AL:GOSUB 11600
3102 LOCATE 16,1:PRINT M$(1)
3104 PRINT "YOU FLEW INTO THE GROUND."
3106 PRINT "THE INSTRUMENT READINGS AT THE TIME OF CRASH ARE AS SHOWN ABOVE."
3108 END
3150 CLS
3152 PRINT M$(1)
3154 PRINT M$(2)
3156 PRINT M$(3);AX;"FEET."
3158 PRINT M$(4)
3160 PRINT M$(5)
3162 N$(1)=M$(6)
3164 GOSUB 3800
3166 END
3200 CLS
3202 PRINT M$(1)
3204 PRINT "YOU FLEW INTO THE GROUND DURING FINAL APPROACH."
3206 PRINT "AT TIME OF CRASH, THE AIRCRAFT PROFILE WAS AS FOLLOWS:"
3208 PRINT M$(8);FA;"DEGREES"
3210 PRINT M$(9);AS
3212 N$(1)=M$(6)
3214 GOSUB 3800
3216 END
3250 CLS
3252 PRINT M$(1)
3254 PRINT M$(10)
3256 PRINT M$(11);AX;"FEET."
3258 PRINT M$(12)+"FUSELAGE."
3260 N$(1)=M$(6)
3262 GOSUB 3800
3264 END
3300 CLS
3302 IF NOT WH THEN PRINT M$(1)
3304 IF WH THEN PRINT "------ A MINOR CRASH HAS OCCURRED -----"
3306 PRINT M$(10)
3308 PRINT M$(11);AX;"FEET."
3310 IF NOT WH THEN PRINT M$(13)
3312 IF NOT WH THEN PRINT M$(12)+"FUSELAGE."
3314 IF WH THEN PRINT "THE IMPACT DAMAGED THE LANDING GEAR."
3316 IF WH THEN PRINT "ALL PASSENGERS HAVE BEEN SAFELY EVACUATED."
3318 N$(1)=M$(6)
3320 GOSUB 3800
3322 END
3350 CLS
3352 PRINT M$(1)
3354 PRINT M$(13)
3356 PRINT "THE AIRCRAFT LANDED ON ITS BELLY, CAUSING MODERATE DAMAGE."
3358 PRINT "ALL PASSENGERS HAVE BEEN SAFELY EVACUATED."
3360 N$(1)=M$(6)
3361 GOSUB 3800
3363 END
3400 CLS
3402 PRINT M$(15)
3404 PRINT M$(14)
3406 N$(1)=M$(7)
3408 GOSUB 3800
3410 END
3450 CLS
3452 PRINT M$(15)
3454 PRINT "YOU FAILED TO EXECUTE A FLARE PRIOR TO TOUCHING DOWN."
3456 PRINT "THIS IS A VIOLATION OF COMPANY PROCEDURES."
3458 PRINT M$(16)
3460 N$(1)=M$(7)
3462 GOSUB 3800
3464 END
3500 CLS
3502 PRINT M$(15)
3504 PRINT "YOU ROLLED OFF THE RUNWAY AFTER TOUCHING DOWN."
3506 PRINT M$(16)
3508 N$(1)=M$(7)
3510 GOSUB 3800
3512 PRINT
3514 IF XN<-9750 THEN PRINT "YOU ROLLED PAST FAR END OF RUNWAY"
3516 IF ABS(YN)>100 THEN PRINT "YOU ROLLED THRU RUNWAY SIDE BORDER"
3518 END
3550 CLS
3552 PRINT "----- YOUR MISSED APPROACH SIGNAL IS ACKNOWLEDGED -----"
3554 PRINT "YOU HAVE FOLLOWED PROPER PROCEDURES."
3556 END
3600 CLS
3602 PRINT "----- YOU HAVE SUCCESSFULLY COMPLETED THE FLIGHT -----"
3604 PRINT "ALL PROCEDURES WERE PROPERLY FOLLOWED."
3606 N$(1)=M$(7)
3610 PRINT "CONGRATULATIONS ON A SUCCESSFUL FLIGHT."
3612 GOSUB 3800
3613 PRINT
3614 XN=ABS(XN):JN=FIX(XN+750)
3615 YN=ABS(YN):YN=FIX(YN)
3616 PRINT "YOUR AIRCRAFT CAME TO REST AT THE FOLLOWING POSITION:"
3617 PRINT "     ";JN;"FEET INSIDE THE RUNWAY"
3618 PRINT "     ";YN;"FEET FROM RUNWAY CENTERLINE"
3620 END
3700 ' CLEAR TO LAND
3702 FOR I=10 TO 24
3703 LOCATE I,1:PRINT SPC(79);
3706 NEXT I
3708 RETURN
3800 ' LANDING STATS
3802 PRINT
3804 PRINT SPC(26);N$(1)
3806 PRINT
3808 IF RL=0 THEN PRINT RX;N$(1)
3809 IF RL=1 THEN COLOR 0,2,8:PRINT RX;N$(2):COLOR 2,0,8
3810 PRINT
3812 IF RW=0 THEN PRINT RY;N$(3)
3813 IF RW=1 THEN COLOR 0,2,8:PRINT RY;N$(3):COLOR 2,0,8
3814 PRINT
3816 PRINT "--- SIZE OF RUNWAY IS 10500 FEET BY 200 FEET ---"
3818 RETURN
5085 ' TIME DELAY
5090 FOR I=1 TO TQ
5095 NEXT I
5099 RETURN
10000 ' BEGIN TAKEOFF MODULE HERE
10020 CLS:COLOR 2,0,8:CLEAR,,8000:RANDOMIZE 9
10022 FALSE=0:TRUE=NOT FALSE
10025 KEY OFF:KR=57.2958:DIM XM(13),WM(13),P$(31)
10030 DATA 37,35,32,30,27,25,22,20,17,15,12,10,7,5
10040 DATA 6,11,16,21,26,31,36,41,46,51,56,61,66,71
10041 FOR I=0 TO 28:READ P$(I):NEXT
10045 FOR I=0 TO 13:READ XM(I):NEXT
10050 FOR I=0 TO 13:READ WM(I):NEXT
10055 M$(1)="_":M$(2)="_":M$(3)="_"
10060 DIM XE(13),WE(13)
10065 K1=29:K2=23
10070 FOR I=0 TO 13
10075 XE(I)=K1:WE(I)=K2
10080 K1=K1-2:K2=K2+4
10085 NEXT
10090 IO=40
10095 RN=RND(0)
10100 RS=1:IF RN<0.5 THEN RS=-1
10105 W1=75:IF RS=-1 THEN W1=255
10110 W2=RND(20):W3=RS*W2
10115 DIM XL(14),XR(14)
10120 J=31:K=49
10125 FOR I=0 TO 14
10130 XL(I)=J:XR(I)=K
10135 J=J-2:K=K+2
10140 NEXT
10145 DATA FUEL,LBS,%,VHF,MHZ,THRUST,MAX,IDLE,REV
10150 DATA PITCH," +"," -",DEG,FLAPS,UP,DWN,WHEELS,UP,MID,DOWN
10155 DATA COMPASS,AIRSPEED,KTS," VERT",SPEED,FPM
10160 DATA ALTITUDE,FEET,CLOCK,BRAKE,SET,REL
10170 FOR I=O TO 31:READ P$(I):NEXT
10185 FOR I=0 TO 9:F(I)=0:NEXT
10190 ' INIT. VARIABLES FOR TAKEOFF MODE
10195 FU=200000.:FP=0:CC=75:AS=0:RC=0:AL=0
10200 MZ=121.5:TR=6:FL=2:FA=0:WH=TRUE:IX=40:HY=9:BR=11:BK=11
10205 AB=0
10215 LOCATE 1,24:PRINT "UNITED 312     CLEARED AS FILED"
10217 PRINT:PRINT
10218 PRINT "              SURFACE WINDS VARIABLE, GUSTING TO 15.":PRINT
10219 PRINT "              MINIMUM CEILING CONDITIONS ARE IN EFFECT.":PRINT
10230 PRINT "              SKY CONDITIONS:   20,000 SCATTERED."
10235 PRINT "                                30,000 BROKEN."
10240 PRINT:PRINT
10245 PRINT "      AFTER TAKEOFF, MAINTAIN HEADING 075 TO 3000, THEN PROCEED AS FILED"
10248 LOCATE 24,24:PRINT "STANDBY FOR TAKEOFF CLEARANCE";
10255 TQ=7000:GOSUB 11535
10260 CLS
10265 LOCATE 10,17:PRINT "UNITED 312":PRINT:PRINT
10270 PRINT "    YOU ARE CLEARED FOR TAKEOFF AT 0800 HOURS"
10275 TQ=3000:GOSUB 11535
10280 CLS
10285 TIME$="7:59"
10290 TV$=TIME$:GOSUB 11555:TL=TD
10295 GOSUB 10330:' DISPLAY CONTROL PANEL
10300 GOSUB 10410:' DISPLAY HORIZON LINE
10305 GOSUB 10480:' DISPLAY RUNWAY
10310 GOSUB 10615
10315 K$=INKEY$:IF K$="" GOTO 10325
10320 GOSUB 10790
10325 GOTO 10945
10330 ' DISPLAY PANEL (HEADERS ONLY)
10335 LOCATE 1,3:PRINT P$(0);SPACE$(4);P$(5);SPACE$(3);P$(9);SPACE$(12);P$(20)
10340 LOCATE 11,59:PRINT P$(13);SPACE$(3);P$(16);SPACE$(2);P$(29)
10345 LOCATE 12,61:PRINT P$(14);SPACE$(13);P$(30)
10350 LOCATE 14,61:PRINT P$(15);SPACE$(12);P$(31)
10352 IF F(9)=1 THEN RETURN
10355 LOCATE 2,13:PRINT P$(6);SPACE$(6);P$(10);
10360 LOCATE 2,68:PRINT P$(28)
10365 LOCATE 3,1:PRINT P$(1);SPACE$(4);P$(2);SPACE$(37);P$(23)
10370 LOCATE 4,22:PRINT P$(11);SPACE$(13);P$(21);SPACE$(2);P$(24);SPACE$(2);P$(26)
10375 LOCATE 5,1:PRINT P$(3)
10380 LOCATE 6,39:PRINT P$(22);SPACE$(6);P$(25);SPACE$(5);P$(27)
10385 LOCATE 7,1:PRINT P$(4);SPACE$(10);P$(7);SPACE$(3);P$(12);
10390 LOCATE 8,13:PRINT P$(8)
10395 FOR Y=2 TO 8:LOCATE Y,12:PRINT CHR$(176):NEXT
10400 FOR Y=2 TO 4:LOCATE Y,21:PRINT CHR$(176):NEXT
10405 RETURN
10410 ' DISPLAY HORIZONTAL LINE
10415 IF F(9)=0 GOTO 10430
10420 LOCATE 9,1:PRINT SPACE$(80);
10425 IF HY=20 GOTO 10440
10430 LOCATE HY,1:PRINT STRING$(80,"_")
10435 RETURN
10440 OH=9:HY=10
10445 FOR I=HY TO 22
10450 LOCATE OH,1:PRINT SPACE$(80);
10455 LOCATE I,1:PRINT STRING$(80,"_");
10460 OH=I
10465 NEXT I
10470 LOCATE 23,IO:PRINT " ";
10475 RETURN
10480 ' DISPLAY RUNWAY
10485 IF F(4)=1 GOTO 10525
10490 S=10:X=29
10495 FOR Y=10 TO 22
10500 LOCATE Y,X:PRINT ".";SPACE$(S);" ";SPACE$(S);".";
10505 X=X-2:S=S+2
10510 NEXT
10515 S$=SPACE$(13)
10520 RETURN
10525 ' RUNWAY GRAPHICS
10530 IF N>3 GOTO 10600
10535 LOCATE OY,OX:PRINT SPACE$(WM);
10540 LOCATE NY,NX:PRINT MK$;
10545 OY=NY:OX=NX
10550 WM=FW
10555 RETURN
10560 ' WHEN SHIP IN FINAL ZONE
10565 LOCATE OY,OX:PRINT SPACE$(WM):IF F(9)=1 THEN RETURN
10570 FOR I=10 TO NY-1
10575 LOCATE I,XE(J):PRINT SPACE$(WE(J))
10580 NEXT I
10585 IF N>4 THEN RETURN
10590 LOCATE NY,NX:PRINT MK$;
10595 RETURN
10600 IF F(2)=1 GOTO 10560 ELSE F(2)=1
10605 FOR I=10 TO 13:LOCATE I,59:PRINT SPACE$(21):NEXT
10610 GOTO 10560
10615 ' DISPLAY INSTRUMENT READINGS
10620 YP=2:XP=1:F$="######":V1=FU:GOSUB 11600
10625 YP=2:XP=8:F$="##":V1=FP:GOSUB 11600
10630 YP=2:XP=39:F$="###":V1=CC:GOSUB 11600
10635 YP=5:XP=39:V1=AS:GOSUB 11600
10640 YP=5:XP=46:F$="#####":V1=RC:GOSUB 11600
10645 YP=5:XP=55:F$="##,###":V1=AL:GOSUB 11600
10650 YP=6:XP=1:F$="###.#":V1=MZ:GOSUB 11600
10652 LOCATE 1,67:PRINT TIME$
10655 ' DISPLAY THRUST
10660 FOR I=2 TO 8
10665 LOCATE I,11:COLOR 0,2,8:PRINT " "
10670 NEXT
10675 LOCATE TR,11:PRINT ">";
10680 ' DISPLAY PITCH
10685 FOR I=2 TO 4:LOCATE I,20:COLOR 0,2,8:PRINT " ":NEXT
10690 LOCATE FL+1,20:PRINT ">"
10695 F$="+###"
10700 YP=5:XP=20:V1=FA:GOSUB 11600
10705 ' DISPLAY FLAPS
10710 IF F(2)=1 GOTO 10760
10715 FOR I=12 TO 14:LOCATE I,59:COLOR 0,2,8:PRINT " ":NEXT
10720 LOCATE BR+1,59:PRINT ">"
10725 ' WHEELS
10730 IF WH THEN LOCATE 12,68:PRINT"  "+CHR$(186)+" ";:LOCATE 13,69:PRINT CHR$(222)+CHR$(202)+CHR$(221); ELSE COLOR 0,7:LOCATE 12,68:PRINT STRING$(4,47);:COLOR 7,0:LOCATE 13,69:PRINT"   ";
10740 ' BRAKES
10745 FOR I=12 TO 14:LOCATE I,75:PRINT " ":NEXT
10750 COLOR 0,7:LOCATE BK+1,75:PRINT ">":COLOR 7,0
10760 ' DISPLAY RUNWAY ALIGN INDEX
10765 IF F(9)=1 THEN RETURN
10770 LOCATE 23,IO:PRINT " ";
10775 LOCATE 23,IX:PRINT CHR$(24);
10780 IO=IX
10785 RETURN
10790 ' KEY POLL ROUTINE
10795 IF K$="F" THEN KK=1:GOTO 10820
10800 IF K$="," OR K$="." THEN KK=2:GOTO 10820
10805 IF K$="B" THEN KK=3:GOTO 10820
10810 IF K$="U" OR K$="D" THEN KK=4:GOTO 10820
10815 IF K$="L" THEN KK=5:GOTO 10820
10817 IF K$="W" THEN KK=6:GOTO 10820
10818 IF K$="S" THEN KK=7:GOTO 10820
10820 ON KK GOSUB 10830,10845,10865,10880,10925,10941,10826
10825 RETURN
10826 ' THRUST KEY DECREASE
10827 IF F(7)=1 AND BR=11 THEN TR=4
10829 RETURN
10830 ' THRUST KEY INCREASE
10831 IF F(7)=1 THEN RETURN
10835 IF F(0)=0 THEN RETURN
10840 F(1)=1:TR=2:RETURN
10845 ' RUDDER KEYS
10846 IF F(7)=1 THEN RETURN
10850 IF AS<=50 THEN RETURN
10855 IF K$="," THEN CC=CC-1 ELSE CC=CC+1
10860 RETURN
10865 ' BRAKES
10870 IF F(0)=1 THEN RETURN
10875 F(0)=1:BK=13:RETURN
10880 ' PITCH
10881 IF F(7)=1 THEN RETURN
10885 IF AS<=50 THEN RETURN
10890 FD=-10:IF K$="U" THEN FD=10
10895 FA=FA+FD
10900 IF FA>60 THEN FA=60
10905 IF FA<-60 THEN FA=-60
10910 IF FA>0 THEN FL=1:RETURN
10915 IF FA=0 THEN FL=2:RETURN
10920 FL=3:RETURN
10925 ' FLAPS
10926 IF F(7)=1 AND WH=FALSE THEN BR=11:RETURN
10930 IF F(3)=1 THEN RETURN
10935 IF F(3)=0 THEN F(3)=1:BR=13:RETURN
10940 RETURN
10941 ' WHEELS
10942 IF F(7)=1 THEN WH=FALSE
10944 RETURN
10945 ' SITUATION UPDATE
10950 TV$=TIME$:GOSUB 11555:TJ=TD-TL:TL=TD
10955 IF F(9)=1 GOTO 12000
10960 IF F(0)=1 GOTO 10970
10965 GOSUB 11130:GOTO 10310
10970 IF F(4)=1 GOTO 10990
10975 F(4)=1
10980 XX=0:YY=0
10985 TX=TD:V0=0:N=1:OY=23:OX=5:WM=71
10990 '
10995 A=(118-18*TR)*0.04028
11000 GOSUB 11190
11005 IF AL>0 GOTO 11060
11010 IF YY>10500 OR ABS(XX)>100 GOTO 11390
11015 IF FA<=0 GOTO 11100
11020 IF AS<150 GOTO 11385
11025 IF FA>10 GOTO 11385
11030 IF F(3)=0 GOTO 11100
11035 AL=25
11040 GOSUB 11130
11045 GOSUB 11150
11050 GOSUB 11300
11055 GOTO 10305
11060 F(9)=1
11065 GOSUB 11130
11070 GOSUB 11150
11075 GOSUB 11170
11080 FOR I=10 TO 22:LOCATE I,1:PRINT SPACE$(80);:NEXT
11085 HY=20
11090 GOSUB 10410
11095 GOTO 11115
11100 GOSUB 11130
11105 GOSUB 11300
11110 GOTO 10305
11115 ' DEPARTURE ROUTINES
11120 GOTO 10310
11130 ' UPDATE FUEL
11135 FU=FU-40*TJ
11140 FP=FU/3120
11145 RETURN
11150 ' RATE OF CLIMB
11155 RC=AS*SIN(ABS(FA/57.3))*1.693*60
11160 IF FA<0 THEN RC=-1*RC
11165 RETURN
11170 ' ALTITUDE
11175 AL=AL+TJ*RC/60
11180 IF AL<=0 THEN AL=0
11185 RETURN
11190 ' MOTION EQUATIONS
11195 T=TJ
11200 V=V0+A*T
11205 VB=(V+V0)/2:V0=V
11210 S=VB*T
11215 IF TD<(TX+18) GOTO 11235
11220 TX=TD
11225 WB=RS*RND(2)
11230 CC=CC+WB
11235 DA=(CC-75)/57.3
11240 DY=S*COS(ABS(DA))
11245 DX=S*SIN(ABS(DA))
11250 IF DA<0 THEN DX=-1*DX
11255 YY=YY+DY
11260 XX=XX+DX
11265 AS=V/1.45
11270 IF AS>20 THEN AS=AS+WS
11275 IX=40+FIX(XX*7/20):IF IX>79 THEN IX=79
11280 IF IX<0 THEN IX=0
11285 ZP=FIX(YY-2500*(N-1))
11290 IF ZP>2500 THEN N=N+1:GOTO 11285
11295 RETURN
11300 ' VARIABLES FOR RUNWAY GRAPHICS
11305 MP=FIX(10+(13*ZP)/2500)
11310 IF N>3 GOTO 11350
11315 NY=MP
11320 J=NY-10
11325 NX=XM(J)
11330 FW=WM(J)
11335 S$=SPACE$(J)
11340 MK$=M$(N)+SPACE$(J)+"_"+SPACE$(J)+"_"+SPACE$(J)+"_"+SPACE$(J)+"_"+SPACE$(J)+"_"
11345 RETURN
11350 ' IN ZONE 4 MARKER IS RUNWAY EDGE
11355 NY=MP
11360 J=NY-10
11365 NX=XE(J)
11370 IF N>4 THEN RETURN
11375 MK$=STRING$(WE(J),"_")
11380 RETURN
11385 AB=1:GOTO 11415
11390 AB=2:IF YY<10500 GOTO 11415
11395 FOR I=10 TO 22
11400 LOCATE I,1:PRINT SPACE$(80);
11405 NEXT I
11410 GOTO 11415
11415 ' ABORT
11420 FOR I=10 TO 22
11425 LOCATE I,1:COLOR 0,2,8:PRINT SPACE$(80);
11430 NEXT I
11435 LOCATE 10,29:PRINT "***TAKEOFF FAILED***":PRINT:PRINT
11440 ON AB GOSUB 11455,11480
11445 LOCATE 19,34:COLOR 2,0,8:PRINT "END OF PROGRAM";
11450 END
11455 PRINT "YOU PULLED BACK ON THE STICK AT TOO SLOW A SPEED, OR YOU"
11460 PRINT "PULLED BACK THE STICK TOO FAR WHEN AT PROPER SPEED."
11465 PRINT "AS A RESULT THE TAIL OF THE FUSELAGE SCRAPPED THE RUNWAY"
11470 PRINT "AND THE AIRCRAFT SPUN OUT OF CONTROL."
11475 RETURN
11480 IF YY>10500 GOTO 11505
11485 PRINT "YOU FAILED TO STAY WITHIN THE RUNWAY BOUNDRIES."
11490 PRINT "AS A RESULT YOU VEERED OFF THE RUNWAY AND COLLIDED WITH"
11495 PRINT "THE CONTROL TOWER!!"
11500 RETURN
11505 PRINT "YOU RAN OUT OF RUNWAY.  AS A RESULT YOU ROLLED INTO THE"
11510 PRINT "MARSHLANDS LOCATED";FIX(YY)-10500;"FEET PAST THE END."
11515 IF F(3)=1 THEN RETURN
11520 PRINT:PRINT:PRINT "  YOU FORGOT TO LOWER THE FLAPS."
11525 PRINT "AS A RESULT THE AIRCRAFT COULD NOT DEVELOP SUFFICIENT LIFT."
11530 RETURN
11535 ' TIME DELAY PAD
11540 FOR I=1 TO TQ
11545 NEXT I
11550 RETURN
11555 ' CONVERT TIME TO ABSOLUTE SECONDS
11560 J=7
11565 FOR I=0 TO 2
11570 TC$(I)=MID$(TV$,J,2)
11575 J=J-3
11580 TC(I)=VAL(TC$(I))
11585 TD=(3600*TC(2))+(60*TC(1))+TC(0)
11590 NEXT I
11595 RETURN
11600 ' PLOTS VARIABLE ON REVERSE BACKGROUND
11605 LOCATE YP,XP:COLOR 0,7
11610 PRINT USING F$;V1;
11615 COLOR 7,0
11620 RETURN
12000 ' DEPARTURE ROUTINE
12010 IF F(7)=1 GOTO 12025 ELSE F(7)=1
12015 GOSUB 10340
12020 F(2)=0
12025 IF WH GOTO 12040 ELSE AS=AS+5
12030 IF BR<>11 GOTO 12040 ELSE AS=AS+5
12035 IF TR=4 AND AL>1800 GOTO 15 ELSE GOTO 12090
12040 IF AL<1200 GOTO 12090
12042 CLS:LOCATE 12,1:PRINT "       *********FLIGHT ABORTED*********"
12044 PRINT:PRINT
12046 PRINT "YOU FAILED TO PERFORM CRUCIAL TRIM MANEUVERS FOLLOWING LIFTOFF,"
12048 PRINT "THE PROPER TRIM SEQUENCE, WHICH MUST BE COMPLETED BELOW 1200"
12049 PRINT "FEET, IS AS FOLLOWS:"
12050 PRINT "      1 - RAISE LANDING GEAR"
12052 PRINT "      2 - RETRACT FLAPS"
12054 PRINT "      3 - REDUCE THRUST"
12056 PRINT
12058 PRINT "---------END OF PROGRAM---------"
12060 END
12090 GOSUB 11130
12092 GOSUB 11150
12094 GOSUB 11170
12099 GOTO 10310
12100 ' END OF LISTING
```

## MORSE.BAS

```bas
10    ' Morse Code Practice Program. Elwood Downey, WB0OEW, August, 1983.
20    ' Written for the IBM PC in Microsoft Basica, V1.1, for PC-DOS V1.1.
30    ' Revised November, 1983, to allow typing in responses.
40    '
50    CLS
60    KEY OFF
70    TYPEWAIT = 200     ' time to wait if user typing in responses
80    F = 600            ' initial tone frequency
90    '
100   GOSUB 1400         ' give directions and ask setup questions
110   '
120   ' initialize code strings.
130   ' to add more characters, such as apostrophe, increase numcodes,
140   ' add code string and character at end of current lists and add case
150   ' to main loop, below.
160   NUMCODES = 41  ' . , / ? - plus 26 + 10
170   DIM CODES$(NUMCODES-1)
180   DIM CHARS$(NUMCODES-1)
190   FOR I=0 TO NUMCODES-1
200     READ CODES$(I)
210   NEXT
220  FOR I=0 TO NUMCODES-1
230    READ CHARS$(I)
240  NEXT
250  ' code strings. in one-to-one correspondence with characters, below.
260  DATA ".-", "-...", "-.-.", "-..", ".", "..-.", "--.", "...."
270  DATA "..", ".---", "-.-", ".-..", "--"
280  DATA "-.", "---", ".--.", "--.-", ".-.", "...", "-"
290  DATA "..-", "...-", ".--", "-..-", "-.--", "--.."
300  DATA "-----", ".----", "..---", "...--", "....-", "....."
310  DATA "-....", "--...", "---..", "----."
320  DATA ".-.-.-", "--..--", "-..-.", "..--..", "-...-"
330  ' characters.
340  DATA "A", "B", "C", "D", "E", "F", "G", "H"
350  DATA "I", "J", "K", "L", "M"
360  DATA "N", "O", "P", "Q", "R", "S", "T"
370  DATA "U", "V", "W", "X", "Y", "Z"
380  DATA "0", "1", "2", "3", "4", "5"
390  DATA "6", "7", "8", "9"
400  DATA ".", ",", "/", "?", "-"
410 '
420 ' set up arrow keys to change speed and frequency.
430 PRINT CHR$(24); " "; CHR$(25); " to raise or lower tone,   ";
440 PRINT CHR$(27); " "; CHR$(26); " for slower or faster code."
450 PRINT "Ctrl-Break to quit, F9 to pause."
460 PRINT
470 ON KEY(11) GOSUB 940: KEY(11) ON
480 ON KEY(14) GOSUB 950: KEY(14) ON
490 ON KEY(12) GOSUB 990: KEY(12) ON
500 ON KEY(13) GOSUB 980: KEY(13) ON
510 ON KEY(9) GOSUB 1140: KEY(9) ON
520 '
530 ' init constants and screen.
540 SIL = 32767         ' special code for no tone
550 GOSUB 1020          ' calculate dit, dah and space lengths.
560 GOSUB 1080          ' display wpm and freq
570 '
580 ' define character type checking functions
590  DEF FNLOWER(C$) = "a"<=C$ AND C$<="z"
600  DEF FNUPPER(C$) = "A"<=C$ AND C$<="Z"
610  DEF FNDIGIT(C$) = "0"<=C$ AND C$<="9"
615  DEF FNTOUPPER$(C$) = CHR$(ASC(C$)-32)
620 '
630 ' main loop. read (or generate) each character, sound it and print it.
640  IF RANFILE THEN GOSUB 1210: ELSE C$ = INPUT$(1,#1)
650  IF " "=C$ OR C$=CHR$(13) THEN GOSUB 890: GOTO 760
660  IF "."=C$ THEN MORSE=36: GOTO 750  ' set morse to codes$ array index
670  IF ","=C$ THEN MORSE=37: GOTO 750
680  IF "/"=C$ THEN MORSE=38: GOTO 750
690  IF "?"=C$ THEN MORSE=39: GOTO 750
700  IF "-"=C$ THEN MORSE=40: GOTO 750
710  IF FNLOWER(C$) THEN C$ = FNTOUPPER$(C$)
720  IF FNUPPER(C$) THEN MORSE=ASC(C$)-ASC("A"):  GOTO 750
730  IF FNDIGIT(C$) THEN MORSE=ASC(C$)-ASC("0")+26: GOTO 750
740  GOTO 770   ' non-morse character so skip it
750  IF TYPE>0 THEN GOSUB 1260: ELSE GOSUB 800  ' wait for type in or sound now
760  PRINT C$;
770  GOTO 640
780 '
790 ' sound dit for each ".", dah for each "-" in string codes$(morse)
800 FOR I=1 TO LEN(CODES$(MORSE))
810   IF MID$(CODES$(MORSE),I,1) = "." THEN GOSUB 900 ELSE GOSUB 910
820 NEXT
830 GOSUB 880
840 RETURN
850 '
860 ' produce elemental sounds, or silences.
870  SOUND SIL,DIT: RETURN      ' element space
880 SOUND SIL,ELE*2: RETURN     ' character space, allow for previous trailing
890 SOUND SIL,ELE*6: RETURN   ' word space, allow for trailing.
900 SOUND F,DIT: GOSUB 870: RETURN        ' dit
910 SOUND F,DAH: GOSUB 870: RETURN        ' dah
920 '
930 ' change frequency of tone
940 F = F*1.104: GOSUB 1080: RETURN
950 F = F/1.104: GOSUB 1080: RETURN
960 '
970 ' change speed; update element timings.
980 WPM = WPM+1: GOSUB 1020: GOSUB 1080: RETURN
990 WPM = WPM-1: GOSUB 1020: GOSUB 1080: RETURN
1000 '
1010 ' calculate element timings. units are clock ticks, which are at 18.2hz.
1020 IF WPM<13 THEN CWPM=13 ELSE CWPM=WPM
1030 DIT = 21.84/CWPM: DAH = 3*DIT
1040 IF WPM>=13 THEN ELE=DIT ELSE ELE=DIT*((CWPM/WPM-1)*13+2)/2
1050 RETURN
1060 '
1070 ' display current speed and frequency. return cursor where it was.
1080 COL=POS(0): ROW=CSRLIN: LOCATE 1,64
1090 PRINT "wpm: "; WPM: LOCATE 2,63: PRINT "freq: "; F; "     "
1100 LOCATE ROW,COL
1110 RETURN
1120 '
1130 ' handle F9, the pause control.
1140 COL9=POS(0): ROW9=CSRLIN
1150 LOCATE 24,30: COLOR 16,7: PRINT " Press any key to continue ";
1160 X$=INKEY$: IF X$="" THEN 1160
1170 LOCATE 24,30: COLOR 2,0:  PRINT "                           ";
1180 LOCATE ROW9,COL9: RETURN
1190 '
1200 ' set c$ to random supported character.
1210 ' force a space every fifth time and a newline every 13 groups.
1220 IF NCHRS=5 THEN C$=" ": NCHRS=0: NGRPS=NGRPS+1: RETURN
1230 IF NGRPS=13 THEN C$=CHR$(13): NGRPS=0: NCHRS=0: RETURN
1240 C$ = CHARS$(RND*(NUMCODES-1)): NCHRS=NCHRS+1: RETURN
1250 '
1260 ' sound character in codes$(morse) and let user type in response.
1270 ' repeat until get it right.
1280 RIGHT=0
1290 WHILE RIGHT=0
1300   GOSUB 800       ' sound out character
1310   N=0
1320   N=N+1: X$=INKEY$: IF X$="" AND N<TYPEWAIT GOTO 1320
1330   IF FNLOWER(X$) THEN X$=FNTOUPPER$(X$)
1340   IF C$<>X$ AND X$<>"" THEN SOUND 2000,2: SOUND SIL,5
1350   IF C$=X$ THEN RIGHT=1
1360 WEND
1370 RETURN
1380  '
1390  ' give info and ask setup questions.
1400  PRINT "              Morse Code Practice Program."
1410  PRINT
1420  PRINT "This program sounds out morse code characters. The characters"
1430  PRINT "may come from a file, from the keyboard, or be generated randomly.
1440  PRINT
1450  PRINT "The program may be used in two ways."
1460  PRINT
1470  PRINT "Characters may be continuously sounded and printed on the screen."
1480  PRINT "This form is good practice for copying code by hand."
1490  PRINT
1500  PRINT "The program may also be used to sound one character at a time,"
1510  PRINT "repeating until the correct character is typed on the keyboard."
1520  PRINT "A high tone indicates an incorrect response. This form is good"
1530  PRINT "practice for typing code."
1540  PRINT
1550  PRINT "At any time while the program is running, the left and right"
1560  PRINT "arrow keys change the code speed and the up and down arrows"
1570  PRINT "change the pitch of the tone."
1580  PRINT
1590  PRINT "Type any key to continue ";
1600  X$=INKEY$: IF X$="" GOTO 1600
1610  CLS
1620  '
1630  ' select input source: either from a file, the keyboard or random.
1640  ' if input is not from keyboard, also ask if want to type in responses.
1650  PRINT "Select source of practice characters. Type:
1660  PRINT "      random    for random characters,"
1670  PRINT "      keyboard  to sound characters typed from keyboard,"
1680  PRINT "      or type a file name from which to read characters: "
1690  INPUT F$
1700  IF F$="keyboard" THEN F$="kybd:"
1710  IF F$="kybd:" THEN TYPE=0: GOTO 1750
1720    INPUT "type in each character in response to sound? (y or n) ", X$
1730    IF LEFT$(X$,1)="y" THEN TYPE=1
1740  IF F$="random" GOTO 1770
1750    RANFILE=0: OPEN F$ FOR INPUT AS #1
1760    GOTO 1780
1770    RANFILE=1: RANDOMIZE VAL(RIGHT$(TIME$,2)): NCHRS=0: NGRPS=0
1780  '
1790  ' select speed
1800  INPUT "words per minute? ", WPM
1810  '
1820  CLS
1830  RETURN
```

## SIMONSEZ.BAS

```bas
10 ' SIMONSEZ BY PETER WOLF * JANUARY 4, 1984
20 ' FOR INFORMATION CALL 415-656-5200 EVENINGS
30 DIM GAME(25)
40 DIM PLAYER(25)
50 B=131:G=262:C=523:R=1046
60 KEY OFF
70 COLOR 7,1
80 CLS
90 LOCATE 15,3:PRINT"DO YOU HAVE AN 80"
100 LOCATE 16,1:PRINT"COLUMN DISPLAY? (Y OR N)"
110 I$ = INKEY$: IF I$ = "" THEN 110
120 IF I$ = "Y" OR I$ = "y" THEN L=20 ELSE L=1
130 CLS
140 FOR X=1 TO 15
150 LOCATE 6,L :PRINT"****************************************"
160 LOCATE 7,L :PRINT"*                                      *"
170 LOCATE 8,L :PRINT"*             SIMONSEZ                 *"
180 LOCATE 9,L :PRINT"*           BY PETER WOLF              *"
190 LOCATE 10,L :PRINT"*                                      *"
200 LOCATE 11,L :PRINT"****************************************"
210 NEXT X
220 CLS
230 LOCATE 15,L:PRINT"Do you want instructions (Y or N)?"
240 I$ = INKEY$: IF I$ = "" THEN 240
250 IF I$ = "N" OR I$ = "n" THEN 360
260 CLS
270 REM *************** INSTRUCTIONS *********************
280 LOCATE 4,L:PRINT"MATCH THE COLOR AND SOUND SEQUENCE"
290 LOCATE 5,L:PRINT"    THAT IS GENERATED BY SIMON"
300 LOCATE 7,L:PRINT" YOU PLAY BY PRESSING THE NUMBERS"
310 LOCATE 8,L:PRINT"  THAT CORRESPOND TO THE SEQUENCE"
320 LOCATE 10,L:PRINT"   AFTER MATCHING EACH SEQUENCE   "
330 LOCATE 11,L:PRINT" ONE MORE COLOR/NOTE IS ADDED "
340 LOCATE 13,L:PRINT"NOW DON'T WRITE THE NUMBERS DOWN"
350 LOCATE 14,L:COLOR 14,6:PRINT"        THAT`S CHEATING         "
360 LOCATE 22,L:PRINT"   PRESS ANY KEY WHEN READY   "
370 T=13  '*****************SETS THE TIMER FOR THE NOTES***************
380 SOUND B,T : SOUND G,T : SOUND C,T : SOUND R,T
390 I$ = INKEY$: IF I$ = "" THEN 380
400 CLS
410 N=0
420 RANDOMIZE VAL(RIGHT$(TIME$,2))
430 FOR I = 0 TO 25
440 GAME(I) = INT(4*RND)+1
450 NEXT I
460 REM *************THE DATA IS NOW LOADED - LET'S PLAY*******************
470 FOR P = 0 TO N
480 GOSUB 660
490 NEXT P
500 REM *************CHECK IF PLAYER HAS THE RIGHT SEQUENCE****************
510 FOR P = 0 TO N
520 I$ = INKEY$ :IF I$ = "" THEN 520
530 IF I$ = "1" OR I$ ="2" THEN GOTO 550 ELSE GOTO 540
540 IF I$ = "3" OR I$ ="4" THEN GOTO 550 ELSE GOTO 520
550 PLAYER(P) = VAL(I$)
560 IF PLAYER(P) <> GAME(P) THEN 780 '*******YOU BLEW IT*******************
570 GOSUB 660
580 NEXT P
590 N=N+1
600 FOR J = 1 TO 1000:NEXT J
610 IF N=6 THEN T=8   '*******THIS SHORTENS THE TIME THE NOTE IS PLAYED****
620 IF N=10 THEN T=4
630 IF N=25 GOTO 920  '**************WINNER WINNER WINNER******************
640 GOTO 470
650 REM ****************SETS SCREEN COLOR AND SOUNDS NOTE******************
660 REM
670 IF GAME(P) <> 1 THEN 690
680 COLOR 14,1:CLS: SOUND B,T:LOCATE 12,L+20:PRINT"1"
690 IF GAME(P) <> 2 THEN 710
700 COLOR 14,2:CLS: SOUND G,T:LOCATE 12,L+20:PRINT"2"
710 IF GAME(P) <> 3 THEN 730
720 COLOR 14,3:CLS: SOUND C,T:LOCATE 12,L+20:PRINT"3"
730 IF GAME(P) <> 4 THEN 750
740 COLOR 14,4:CLS: SOUND R,T:LOCATE 12,L+20:PRINT"4"
750 FOR W = 1 TO T*30  '*A SHORT PAUSE*
760 NEXT W
770 RETURN
780 CLS
790 COLOR 7,0
800 FOR X = 1 TO 25
810 LOCATE 12,L+5:PRINT"YOUR SCORE WAS ";N;" CORRECT"
820 LOCATE 14,L+5:PRINT"THE CORRECT SEQUENCE WAS:"
830 NEXT X
840 FOR P = 0 TO N
850 GOSUB 660
860 NEXT P
870 LOCATE 14,L+5:PRINT" DO YOU WANT TO PLAY AGAIN?"
875 WHILE INKEY$<>"":WEND
880 I$ = INKEY$:IF I$ = "" THEN 880
890 IF I$ = "Y" OR I$ = "y" THEN 360
900 REM *SYSTEM
910 END
920 CLS
930 LOCATE 12,L+5:PRINT"  CONGRADULATIONS YOU WON"
940 FOR K=1 TO 20
950    FOR I=50 TO 75 STEP 4
960       PLAY "l64 mf ml t255 n=i;"
970    NEXT
980 NEXT K
990 GOTO 780
```

## SLOTMACH.BAS

```bas
10 M=25
20 RANDOMIZE INT(VAL(MID$(TIME$,7,2)))
30 PRINT "Sound effects (Y/N) ?"
40 NS$=INKEY$: IF NS$="" GOTO 40
50 WIDTH 80:SCREEN 0,1,0,0:COLOR 7,0,2:CLS
60 KEY OFF:GOSUB 1620:COLOR 7,0:CLS
70 REM 'CONVERTED FOR IBM-PC FROM 101 BASIC COMPUTER GAMES BY DIGITAL
80 REM 'EQUIPMENT CORP. SUBMITTED TO DIGITAL BY ROB HOFFBERG OF ROSLYN N.Y.
90 REM 'AUTHOR UNKNOWN
100 REM 'ENHANCED FOR COLOR AND SOUND BY ARNOLD THOMSEN OF SCOTTSDALE, AZ
110 PRINT "OK ";N$;" Here are the odds:"
120 LOCATE 5,10:PRINT "SYMBOL     HOW MANY     PAYOUT"
130 LOCATE 6,10:PRINT "111111111111111111111111111111"
140 LOCATE 7,10:PRINT "CHERRY         3         $2.00"
150 LOCATE 8,10:PRINT "CHERRY         2         $1.00   LEFT & MID WINDOWS ONLY!"
160 LOCATE 9,10:PRINT "CHERRY         1         $0.50   LEFT WINDOW ONLY!"
170 LOCATE 11,10:PRINT "PLUM           3         $5.00"
180 LOCATE 12,10:PRINT "ORANGE         3         $7.50"
190 LOCATE 13,10:PRINT "*BAR*          3        $10.00"
200 LOCATE 15,10:COLOR 6: PRINT "      LEMONS ALWAYS LOOSE":COLOR 7,0
210 LOCATE 20,5:PRINT "When the message PULL appears, press ENTER to PULL."
220 LOCATE 21,5:PRINT "Or type END to end the session."
230 LOCATE 23,5:PRINT "When you are ready to begin press ENTER.";:INPUT Z9$
240 L$=CHR$(186)
250 S$=CHR$(205)
260 B$=CHR$(186)
270 DIM W$(4)
280 W$(1)=" CHERRY ":W$(2)="      ":W$(3)=" *BAR*  ":W$(4)="  PLUM  "
290 REM "WHEEL ONE
300 DIM V$(7)
310 V$(1)=" ORANGE ":V$(2)="  PLUM  ":V$(3)=" LEMON  ":V$(4)=" *BAR*  "
320 V$(7)=" CHERRY ":V$(5)=" CHERRY ":V$(6)=" CHERRY "
330 REM "WHEEL TWO
340 DIM V1$(7)
350 V1$(1)=" *BAR*  ":V1$(2)=" LEMON  ":V1$(3)="  PLUM  "
360 V1$(4)=" CHERRY ":V1$(5)="  PLUM  ":V1$(6)=" ORANGE ":V1$(7)=" CHERRY "
370 REM "WHEEL THREE
380 DIM V3$(5)
390 V3$(1)="  PLUM  ":V3$(2)=" *BAR*  ":V3$(3)=" CHERRY ":V3$(4)=" LEMON  "
400 V3$(5)=" ORANGE "
410 COLOR 15,0:CLS
420 Y=16:FOR X=1 TO 18:LOCATE X,Y:PRINT S$:NEXT X
430 X=18:FOR Y=17 TO 50:LOCATE X,Y:PRINT B$:NEXT Y
440 Y=51:FOR X=18 TO 1 STEP-1:LOCATE X,Y:PRINT S$:NEXT X
450 X=1:FOR Y=50 TO 17 STEP-1:LOCATE X,Y:PRINT L$:NEXT Y
460 COLOR 4,1
470 FOR X = 2 TO 17
480 LOCATE X,17
490 FOR Z = 1 TO 17:PRINT CHR$(36);CHR$(1);:NEXT Z
500 NEXT X
510 Q$ = "QUALITY"
520 LOCATE 3,30:COLOR 15,1:PRINT Q$
530 GOSUB 550:GOTO 590
540 REM "ARM
550 LOCATE 14,52:PRINT "/";
560 Y=52:FOR X=13 TO 8 STEP-1:LOCATE X,Y:PRINT "//";:Y=Y+1:NEXT X
570 FOR X=7 TO 6 STEP-1:LOCATE X,57:PRINT STRING$(3,196):NEXT X
580 FOR X = 1 TO 512:NEXT:RETURN
590 LOCATE 13,28:COLOR 15,1:PRINT "P A Y O U T":LOCATE 14,33
600 X=15:FOR Y=24 TO 42:LOCATE X,Y:PRINT L$:NEXT Y
610 FOR X=15 TO 17:LOCATE X,43:PRINT S$:NEXT X
620 FOR Y=42 TO 24 STEP-1:LOCATE 17,Y:PRINT B$:NEXT Y
630 FOR X=17 TO 15 STEP-1:LOCATE X,23:PRINT S$:NEXT X
640 COLOR 15,0
650 REM "******LEFT WINDOW*****
660 FOR Y=18 TO 26:LOCATE 5,Y:PRINT L$:NEXT Y
670 FOR X=5 TO 7:LOCATE X,27:PRINT S$:NEXT X
680 FOR Y=26 TO 18 STEP-1:LOCATE 7,Y:PRINT B$:NEXT Y
690 FOR X=7 TO 5 STEP-1:LOCATE X,18:PRINT S$:NEXT X
700 REM "*****MID WINDOW*****
710 FOR Y=30 TO 37:LOCATE 5,Y:PRINT L$:NEXT Y
720 FOR X=5 TO 7:LOCATE X,38:PRINT S$:NEXT X
730 FOR Y=37 TO 30 STEP-1:LOCATE 7,Y:PRINT B$:NEXT Y
740 FOR X=7 TO 5 STEP-1:LOCATE X,29:PRINT S$:NEXT X
750 REM "*****RIGHT WINDOW*****
760 FOR Y=41 TO 48:LOCATE 5,Y:PRINT L$:NEXT Y
770 FOR X=5 TO 7:LOCATE X,49:PRINT S$:NEXT X
780 FOR Y=48 TO 41 STEP-1:LOCATE 7,Y:PRINT B$:NEXT Y
790 FOR X=7 TO 5 STEP-1:LOCATE X,40:PRINT S$:NEXT X
800 REM "DO YOU PULL OR END
810 LOCATE 21,24:PRINT "YOU NOW HAVE ";:PRINT USING "$$###.##";M
815 IF M=0 THEN LOCATE 23,12:PRINT "YOU ARE BROKE! AT ";TIME$;" ON ";DATE$:END
820 LOCATE 22,26:PRINT "PULL(ENTER)/END";:INPUT A$
830 IF A$="END" OR A$="E" OR A$="end" OR A$="e" THEN 1570
840 COLOR 0,0:LOCATE 22,26:PRINT "PULL(ENTER)/END? ":GOSUB 550
850 LOCATE 16,24:COLOR 0,0:E$="    ":PRINT E$;E$;E$;E$;"   "
860 LOCATE 6,19:PRINT E$;E$:LOCATE 6,30:PRINT E$;E$:LOCATE 6,41:PRINT E$;E$
870 COLOR 15,1:GOSUB 550
880 REM " * PICK THE WHEELS
890 FOR I=1 TO 2:Z(I)=INT((RND*7)+1):NEXT I
900 Z(3)=INT((RND*5)+1)
910 COLOR 7,0
920 REM "***** ROLL ALL WINDOWS AND PICK LEFT *****
930 FOR X=1 TO 10:FOR Y=1 TO 4:LOCATE 6,19:PRINT W$(Y)
940 LOCATE 6,30:PRINT W$(Y):LOCATE 6,41:PRINT W$(Y):NEXT Y:NEXT X
950 IF V$(Z(1)) = " CHERRY " THEN COLOR 12,0
960 IF V$(Z(1)) = " ORANGE " THEN COLOR 12,6
970 IF V$(Z(1)) = "  PLUM  " THEN COLOR 9,4
980 IF V$(Z(1)) = " LEMON  " THEN COLOR 14,0
990 IF V$(Z(1)) = " *BAR*  " THEN COLOR 31,0
1000 LOCATE 6,19:PRINT V$(Z(1)):COLOR 7,0
1010 IF NS$ = "N" OR NS$ = "n" GOTO 1030 ELSE SOUND 200,2
1020 REM " ***** ROLL MID AND RIGHT AND PICK MID *****
1030 FOR X=1 TO 15:FOR Y=1 TO 4
1040 LOCATE 6,30:PRINT W$(Y):LOCATE 6,41:PRINT W$(Y):NEXT Y:NEXT X
1050 IF V1$(Z(2)) = " CHERRY " THEN COLOR 12,0
1060 IF V1$(Z(2)) = " ORANGE " THEN COLOR 12,6
1070 IF V1$(Z(2)) = "  PLUM  " THEN COLOR 9,4
1080 IF V1$(Z(2)) = " LEMON  " THEN COLOR 14,0
1090 IF V1$(Z(2)) = " *BAR*  " THEN COLOR 31,0
1100 LOCATE 6,30:PRINT V1$(Z(2)):COLOR 7,0
1110 IF NS$ = "N" OR NS$ = "n" GOTO 1130 ELSE SOUND 300,2
1120 REM "***** ROLL RIGHT AND PICK RIGHT *****
1130 FOR X=1 TO 20:FOR Y=1 TO 4
1140 LOCATE 6,41:PRINT W$(Y):NEXT Y:NEXT X
1150 IF V3$(Z(3)) = " CHERRY " THEN COLOR 12,0
1160 IF V3$(Z(3)) = " ORANGE " THEN COLOR 12,6
1170 IF V3$(Z(3)) = "  PLUM  " THEN COLOR 9,4
1180 IF V3$(Z(3)) = " LEMON  " THEN COLOR 14,0
1190 IF V3$(Z(3)) = " *BAR*  " THEN COLOR 31,0
1200 LOCATE 6,41:PRINT V3$(Z(3)):COLOR 7,0
1210 COLOR 7,0
1220 IF NS$ = "N" OR NS$ = "n" GOTO 1240 ELSE SOUND 400,2
1225 FOR X= 1 TO 512:NEXT X 'DELAY
1230 REM "DO I HAVE 3 OF A KIND
1240 IF V$(Z(1))<>V1$(Z(2)) THEN 1370
1250 IF V1$(Z(2))<>V3$(Z(3)) THEN 1370
1260 IF V$(Z(1))<>" CHERRY " THEN 1280
1270 LOCATE 16,27:PRINT "YOU WIN $2.00   ":M=M+2:GOSUB 5100:GOTO 800
1280 IF V$(Z(1))<>"  PLUM  " THEN 1300
1290 LOCATE 16,27:PRINT "YOU WIN $5.00   ":M=M+5:GOSUB 4000:GOTO 800
1300 IF V$(Z(1))<>" ORANGE " THEN 1320
1310 LOCATE 16,27:PRINT "YOU WIN $7.50   ":M=M+7.5:GOSUB 3000:GOTO 800
1320 IF V$(Z(1))<>" *BAR*  " THEN 1340
1330 LOCATE 16,27:PRINT "YOU WIN $10.00  ":M=M+10:GOSUB 2000:GOTO 800
1340 REM "LEMONS
1350 LOCATE 16,28:PRINT "LEMONS LOSE!":M=M-.25:GOTO 800
1360 REM
1370 REM "DO I HAVE CHERRIES
1380 IF V$(Z(1))<>" CHERRY " THEN 1460
1390 IF V1$(Z(2))<>" CHERRY " THEN 1440
1400 REM
1410 REM "2 CHERRIES
1420 LOCATE 16,27:PRINT "YOU WIN $1.00   ":M=M+1:GOSUB 5000:GOTO 800
1430 REM
1440 REM " 1  CHERRY 
1450 LOCATE 16,27:PRINT "YOU WIN $0.50   ":M=M+.5:GOTO 800
1460 REM
1470 REM " GOT SCHMALTZ
1480 ON Z(3) GOSUB 1500,1510,1520,1530,1540,1550,1560
1490 M=M-.25:GOTO 800
1500 LOCATE 16,25:FG=3:COLOR FG,0:PRINT "TRY BINGO INSTEAD ":RETURN
1510 LOCATE 16,25:FG=6:COLOR FG,0:PRINT "HA HA YOU LOSE    ":RETURN
1520 LOCATE 16,25:FG=5:COLOR FG,0:PRINT "ZILCH-O           ":RETURN
1530 LOCATE 16,25:FG=4:COLOR FG,0:PRINT "TRY AGAIN SUCKER  ":RETURN
1540 LOCATE 16,24:FG=1:COLOR FG,7:PRINT "TRY VEGAS INSTEAD ":RETURN
1550 LOCATE 16,25:FG=2:COLOR FG,0:PRINT "YOU GOT SCHMALTZ  ":RETURN
1560 LOCATE 16,25:FG=23:COLOR FG,0:PRINT "BACK TO STAR TREK ":RETURN
1570 LOCATE 16,24:COLOR 15,1:PRINT "     BYE-BYE       "
1580 LOCATE 17,29:PRINT N$
1590 LOCATE 24,1:SYSTEM
1600 END
1620 FOR J=2 TO 22
1630 LOCATE 2,2:PRINT CHR$(201):LOCATE 2,79:PRINT CHR$(187)
1640 LOCATE 23,2:PRINT CHR$(200):LOCATE 23,79:PRINT CHR$(188)
1650 LOCATE J,2:PRINT CHR$(186)
1660 LOCATE J,79:PRINT CHR$(186)
1670 NEXT J
1680 FOR J=3 TO 78
1690 LOCATE 2,J:PRINT CHR$(205)
1700 LOCATE 23,J:PRINT CHR$(205)
1710 NEXT J
1720 COLOR 5
1730 LOCATE 4,4:PRINT "W E L C O M E  T O . . ."
1740 LOCATE 8,30:PRINT "I B M ' S  C A S I N O"
1750 LOCATE 17,4:PRINT "Today you will lose your money at IBM's famous slot machine."
1760 LOCATE 18,4:PRINT "You have been given a credit line of $25.00. Therefore at 25 a pull you"
1770 LOCATE 19,4:PRINT "have a minimum of 100 pulls!"
1780 LOCATE 20,4:PRINT "By the way what is your name";:INPUT N$
1790 RETURN
2000 IF NS$="N" OR NS$="n" THEN RETURN 'STARS AND STRIPES FOREVER
2010 PLAY "T220 L4 C2 O3 B-AA2 G+ A A..... G+A A2 G+ A "
2020 PLAY "O4 C O3 A. O4 C O3 B-1 G P4 G2 F+ G G2 F+G"
2030 PLAY "B-..... A G A O4 C2.D2. D O3 G.... P4"
2040 PLAY "O4 C2 C2 O3 B- A A2 G+ A A..... G+ A A2 G+ A B- AGE"
2050 PLAY "G1 F P4 F2 F2 EF A-2 GF O4 F.... O3 FGA O4 C"
2060 PLAY "P2 P4 O3 CDA G2 F O4 F8"
2070 RETURN
3000 IF NS$="N" OR NS$="n" THEN RETURN 'ENTERTAINER
3010 A$="O3 D8 D+8 E8 O4 C O3 E8 O4 C O3 E8 O4 C2."
3020 PLAY "T160 L4;XA$; O4 L8 CDD+ECDE4 O3 B O4 DC2."
3030 PLAY "XA$;L8 O3 AGF+A O4 C E4 DC O3 A O4 D2."
3040 PLAY "XA$; O4 L8 CDD+ECDE4 O3 B O4 D"
3050 PLAY "C2.CDECDE4CDCECDE4CDCECDE4 O3 B O4 D4 C2."
3060 RETURN
4000 IF NS$="N" OR NS$="n" THEN RETURN 'I LEFT MY HEART IN SAN FRANCISCO
4005 S1$ = "F2..GAB-GC1CO2B O3 C"
4010 PLAY "T100 O3 L4 DE-G;XS1$;G....B-A.F8D....P8"
4020 PLAY "L4 DE-EL8FE-DE-L4F....G"
4030 PLAY "L8 AGFGA2. AG+ A8 B-....    C 03 A. C8 F2.. G8 F8 E- D E-G"
4040 PLAY "O3;XS1$; A.... AB- O4CD.... P8 DC+DE-2. D O3 B O4 CDC"
4050 PLAY "O3 G... P8 GF+G O4 E-... O3 G O4 D2 D2 O3 B-...."
4060 RETURN
5000 IF NS$="N" OR NS$="n" THEN RETURN 'SIDEWALKS
5010 PLAY "T200 L2 O4 G.E.D.C.DC4 O3 A B4 O4 C...":RETURN
5100 IF NS$="N" OR NS$="n" THEN RETURN 'DONNA E MOBILE
5110 PLAY "T200 L4 O4 EEE G8 F8 D2 DDD F8 E8 C2 EDC C8"
5120 PLAY "O3 B8 B2 O4 DC O3 A A8 G8 G2"
5130 RETURN
```

## TRONLC.BAS

```bas
10 KEY OFF
20 SCREEN 1:CLS
30 LINE (1,1)-(3,1),1:LINE (3,1)-(3,3),1:LINE (3,3)-(1,3),1:LINE (1,3)-(1,1),1:PSET (2,2),1
40 DIM SQ(3,3)
50 GET (1,1)-(3,3),SQ
60 CLS
70 LOCATE 2,1:PRINT"           TRON LIGHT CYCLES"
80 PRINT:PRINT"    PROGRAM BY RICHARD G. POSTGATE"
90 PRINT"    OF VANCOUVER, B.C., CANADA."
100 PRINT"       VERSION 1,043,539.67"
110 LOCATE 12,1:PRINT"Do you want Instructions?";
120 A$=INKEY$:IF A$="" THEN 120
130 IF A$="Y" OR A$="y" THEN 960
140 C1=0 : C2=0
150 REM WRITTEN BY D & D ENTERPRISES
160 REM PROGRAMMING
170 REM ON  83-02-26 REVISED ON 84-09-04
180 SCREEN 1:CLS
190 FOR L=1 TO 5
200 X=INT(RND*300)+2:Y=INT(RND*170)+14
210 TX=INT(RND*20)+20:TY=INT(RND*20)+20
220 LINE (X,Y)-(X+TX,Y+TY),(RND*2)+1,BF
230 NEXT L
240 FOR L=191 TO 199
250 LINE (0,L)-(319,L),0
260 NEXT L
270 LINE (1,12)-(1,185),3:LINE -(318,185),3:LINE -(318,12),3:LINE -(1,12),3
280 LINE (0,190)-(319,190),3
290 PAINT (0,0),3
300 PRINT"          TRON  LIGHT  CYCLES           ";
310 LOCATE 1,1:PRINT"P1:";C1;:LOCATE 1,35:PRINT"P2:";C2;
320 LINE (2,99)-(317,99),0:LINE (2,100)-(317,100),0:LINE (2,101)-(317,101),0
330 LOCATE 25,1:PRINT"        PRESS ANY KEY TO START";
340 A1$=INKEY$:IF A1$=""THEN 340
350 LOCATE 25,1:PRINT"                              ";
360 X1=70:Y1=100:X2=230:Y2=100
370 D1=2:D2=4:S1=2:S2=2
380 A$=INKEY$
390 FOR L=1 TO 50:NEXT L
400 IF A$="W"THEN D1=1
410 IF A$="Z"THEN D1=3
420 IF A$="A"THEN D1=4
430 IF A$="D"THEN D1=2
440 IF A$="8"THEN D2=1
450 IF A$="2"THEN D2=3
460 IF A$="4"THEN D2=4
470 IF A$="6"THEN D2=2
480 IF A$="Q" AND S1>1 THEN S1=S1-1
490 IF A$="E" AND S1<3 THEN S1=S1+1
500 IF A$="7" AND S2>1 THEN S2=S2-1
510 IF A$="9" AND S2<3 THEN S2=S2+1
520 IF A$="R"THEN GOTO 140
530 IF A$="B" THEN 1190
540 IF D1=1 THEN Y1=Y1-S1:H1=POINT (X1,Y1-1)
550 IF D1=2 THEN X1=X1+S1:H1=POINT (X1+1,Y1)
560 IF D1=3 THEN Y1=Y1+S1:H1=POINT (X1,Y1+1)
570 IF D1=4 THEN X1=X1-S1:H1=POINT (X1-1,Y1)
580 IF D2=1 THEN Y2=Y2-S2:H2=POINT (X2,Y2-1)
590 IF D2=2 THEN X2=X2+S2:H2=POINT (X2+1,Y2)
600 IF D2=3 THEN Y2=Y2+S2:H2=POINT (X2,Y2+1)
610 IF D2=4 THEN X2=X2-S2:H2=POINT (X2-1,Y2)
620 IF H1<>0 THEN H=1:FOR L=1800 TO 1950:SOUND L,255:SOUND L,0:NEXT L:LOCATE 25,1:PRINT"           PLAYER #1 CRASHED!";:GOTO 670
630 IF H2<>0 THEN H=2:FOR L=1800 TO 1950:SOUND L,255:SOUND L,0:NEXT L:LOCATE 25,1:PRINT"           PLAYER #2 CRASHED!";:GOTO 670
640 PUT (X1-1,Y1-1),SQ,PSET
650 PUT (X2-1,Y2-1),SQ,PSET
660 GOTO 380
670 FOR W=1 TO 20
680 IF H=1 THEN CIRCLE (X1,Y1),W,1
690 IF H=2 THEN CIRCLE (X2,Y2),W,2
700 NEXT W
710 FOR W=1 TO 20
720 IF H=1 THEN CIRCLE (X1,Y1),W,0
730 IF H=2 THEN CIRCLE (X2,Y2),W,0
740 NEXT W
750 FOR W=1 TO 800:NEXT W
760 IF H=1 THEN C2=C2+1
770 IF H=2 THEN C1=C1+1
780 IF C2=10 GOTO 830
790 IF C1=10 GOTO 830
800 FOR L=1 TO 1000:NEXT L
810 FOR R=1 TO 16:Z$=INKEY$:NEXT R
820 GOTO 180
830 SCREEN 0:CLS
840 LOCATE 1,1:PRINT"THE FINAL SCORE IS: PLAYER 1 -";C1
850 PRINT"                    PLAYER 2 -";C2
860 COLOR 17
870 IF C2=10 THEN LOCATE 12,1:PRINT"      PLAYER TWO IS THE WINNER!!!"
880 IF C1=10 THEN LOCATE 12,1:PRINT"      PLAYER ONE IS THE WINNER!!!"
890 FOR L=1 TO 16:A$=INKEY$:NEXT L
900 LOCATE 24,1:PRINT"      DO YOU WANT TO PLAY AGAIN?";
910 A$=INKEY$:IF A$="" THEN 910
920 IF A$="Y" THEN 140
930 IF A$<>"N" THEN 910
940 SCREEN 0:WIDTH 80:KEY ON:CLS:PRINT"THANK YOU FOR PLAYING `TRON LIGHT CYCLES'!"
950 END
960 CLS
970 PRINT"        TRON LIGHT CYCLES GAME"
980 PRINT:PRINT"The object of the game is to force your"
990 PRINT"opponent to crash into either an"
1000 PRINT"obstacle, his own trail, or your trail."
1010 PRINT"You and a friend control a `Light Cycle'";
1020 PRINT"which leaves a trail behind it as it"
1030 PRINT"moves. You can win by forcing your"
1040 PRINT"opponent to crash 10 times."
1050 PRINT"(You get a point every time your"
1060 PRINT" opponent crashes.)"
1070 PRINT:PRINT"The player on the left (player 1)"
1080 PRINT"moves his cycle with the A,W,D,Z keys,"
1090 PRINT"and the player on the right (player 2)"
1100 PRINT"uses either the cursor keys on the"
1110 PRINT"numeric keypad, or the number keys "
1120 PRINT"(4,8,6,2)."
1130 PRINT"Cycle speed is controlled by the Q/E &"
1140 PRINT"7/9 keys."
1150 PRINT:PRINT"Note: The NumLock and CapsLock keys"
1160 PRINT"must be `on'."
1170 LOCATE 24,1:PRINT"      PRESS ANY KEY TO CONTINUE";
1180 A$=INKEY$:IF A$="" THEN 1180 ELSE 140
1190 REM
ust be `on'."
1170 LO
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk 0260

     Volume in drive A has no label
     Directory of A:\

    CRC      TXT      1193  12-26-84   1:35p
    CRCK4    COM      1536  10-21-82   5:50p
    CRIB2    BAS     17939   3-20-84  10:34a
    CRIBBAGE BAS     23513   9-13-83  12:00a
    DSS      DOC     46976   8-21-84  12:04p
    DSS      EXE     70528   8-21-84  11:30a
    JETSET   BAS     25856   1-01-80  12:01a
    JETSET   DOC     46080  10-13-83  10:34a
    MORSE    BAS      6784   2-22-84   8:03p
    SCROLL1  ML        128   3-26-82
    SIMONSEZ BAS      2963   1-12-84  10:50p
    SIMONSEZ DOC      1431   1-13-84  10:01p
    SLOTMACH BAS      9508   3-28-84   8:48a
    TRONLC   BAS      4480   1-01-80  12:01a
    TRONLC   EXE     26864  12-15-84  10:10a
           15 file(s)     285779 bytes
                           29696 bytes free
