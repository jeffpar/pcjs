---
layout: page
title: "PC-SIG Diskette Library (Disk #209)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0209/
machines:
  - id: ibm5160
    type: pcx86
    config: /machines/pcx86/ibm/5160/cga/256kb/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0209"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5160" %}

{% comment %}info_begin{% endcomment %}

## Information about "ARCADE SERIES NO 2"

    Three of our better graphic programs.  DATNOIDS is a very good
    robot-type arcade game, very challenging.  JUMPJOE2 demands a lot of
    hand-eye coordination, and is interesting enough to keep your
    attention for quite a while.  NUKE-NY isnt a game, it's a graphic demo
    of the effect of a nuclear bomb on the N.Y. area.
    
    System Requirements: Some programs require BASIC, color graphics.
    
    How to Start: To read DOC files simply enter TYPE filename.ext and
    press <ENTER>.  Type JUMPJOE2 and press <ENTER> to start that game.
    For instructions on running BASIC programs, please refer to the
    GETTING STARTED section in this catalog.
    
    File Descriptions:
    
    JUMPJOE2 DOC  Documentation & hints  (Tim McClarren)
    JUMPJOE2 EXE  Improved version of Miner 2049'er-like game  (Kevin Bales)
    DATNOIDS BAS  Similar to robot arcade game  (Casey Roche)
    NUKE-NY  BAS  Simulation of nuclear attack on NYC area  (J. R. Dukat)
    HS       DAT  Data file for JUMPJOE2.EXE
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## DATNOIDS.BAS

```bas
5 REM DATNOIDS Copyright (c) 1982 By Casey Roche
10 KEY OFF:WIDTH 80:COLOR 0,1,0:CLS:PLAY"mb":COLOR 4,0:LOCATE 24,1:PRINT" "+STRING$(78,219):SOUND 1000,1:PRINT" ░░░░░░      ░░░░    ░░░░░░░░  ░░   ░░    ░░░░░░   ░░░░░░░░  ░░░░░░      ░░░░░":SOUND 2000,1
80 PRINT" ░▓▓▓▓▓░    ░▓▓▓▓░   ░▓▓▓▓▓▓░  ░▓   ▓░   ░▓▓▓▓▓▓░  ░▓▓▓▓▓▓░  ░▓▓▓▓▓░    ░▓▓▓▓▓░":SOUND 1000,1:PRINT" ░▓    ▓░  ░▓    ▓░     ▓▓     ░▓░  ▓░   ░▓    ▓░     ▓▓     ░▓    ▓░  ░▓▓░    ":SOUND 2000,1
120 PRINT" ░▓    ▓░  ░▓    ▓░     ▓▓     ░▓▓░ ▓░   ░▓    ▓░     ▓▓     ░▓    ▓░ ░▓▓░   ":SOUND 1000,1:PRINT" ░▓    ▓░  ░▓▓▓▓▓▓░     ▓▓     ░▓░▓░▓░   ░▓    ▓░     ▓▓     ░▓    ▓░  ░▓▓░ ":SOUND 2000,1
160 PRINT" ░▓    ▓░  ░▓    ▓░     ▓▓     ░▓ ░▓▓░   ░▓    ▓░     ▓▓     ░▓    ▓░    ░▓▓░":SOUND 1000,1:PRINT" ░▓    ▓░  ░▓    ▓░     ▓▓     ░▓  ░▓░   ░▓    ▓░     ▓▓     ░▓    ▓░      ░▓▓░":SOUND 2000,1
200 PRINT" ░▓▓▓▓▓░   ░▓    ▓░     ▓▓     ░▓   ▓░   ░▓▓▓▓▓▓░  ░▓▓▓▓▓▓░  ░▓▓▓▓▓░   ░▓▓▓▓▓▓░":SOUND 1000,1:PRINT" ░░░░░░    ░░    ░░     ░░     ░░   ░░    ░░░░░░   ░░░░░░░░  ░░░░░░    ░░░░░░░":SOUND 2000,1:PRINT" "+STRING$(78,219)
250 FOR I=1000 TO 4000 STEP 50:SOUND I,0.5:SOUND 5000-I,0.3:NEXT:PLAY"mf":COLOR 7,0:SOUND 32767,0.1:PRINT"                                       "+CHR$(15):SOUND 32767,0.1:PRINT"                                       *
280 SOUND 32767,0.1:PRINT"                                       "+CHR$(179):SOUND 32767,0.1:PRINT"                                       "+CHR$(179):SOUND 32767,0.1:PRINT"                                       "+CHR$(179)
310 SOUND 32767,0.1:PRINT"                                       "+CHR$(179):SOUND 32767,0.1:PRINT"                                       "+CHR$(179):SOUND 32767,0.1:PRINT"                                       "+CHR$(179)
340 SOUND 32767,0.1:LOCATE 23,28:COLOR 0,7:PRINT" DATNOIDS BY CASEY ROCHE ":SOUND 32767,0.1:PRINT STRING$(80,"█"):LOCATE 24,31:PRINT"(C) COPYRIGHT 1982":COLOR 7,0:PLAY"c8c8e8gc8c8e8gf8e8d8g8g8g8g":TIME$="00:00":PLAY"MB":DIM MZ(22,80)
400 ON KEY(1)GOSUB 1250:KEY(1)ON:DEF SEG=0:POKE 1050,PEEK(1052):CLS:LOCATE 10,21:INPUT"PLEASE ENTER YOUR SKILL LEVEL (1-10)";SK:IF SK<1 OR SK>10 THEN CLS:BEEP:LOCATE 10,63:PRINT"ERROR":GOTO 400
420 TK=SK:SK=(11-SK)*20:LOCATE 12,21:INPUT"WHICH MAZE (1,2 OR 3)";WM:IF WM=1 THEN LEV$="1":GOTO 470
440 IF WM=2 THEN LEV$="2":GOTO 470
450 IF WM=3 THEN LEV$="3":GOTO 470
460 CLS:BEEP:LOCATE 13,25:PRINT"ERROR- ONLY 1,2 OR 3 PLEASE":GOTO 420
470 CLS:PLAY"mb":B$=CHR$(219):LOCATE 10,35:PRINT"please wait...":FOR LS=1 TO 20:FOR SL=2 TO 79:MZ(LS,SL)=1:NEXT:NEXT:LOCATE 10,35:PRINT"              ":COLOR 0,7:LOCATE 25,16:PRINT" F1=QUIT  A=UP  Z=DOWN  ;=LEFT  `=RIGHT  [=MISSILE ":COLOR 7,0
500 LOCATE 24,1:PRINT STRING$(80," "):FOR I=2 TO 80:LOCATE 2,I:PRINT B$:MZ(2,I)=1:NEXT:FOR I=2 TO 80:LOCATE 22,I:PRINT B$:MZ(22,I)=1:NEXT:FOR I=2 TO 22:LOCATE I,2:PRINT B$:MZ(I,2)=1:MZ(I,1)=1:NEXT:FOR I=2 TO 22:LOCATE I,80:PRINT B$:MZ(I,80)=1:NEXT
550 LOCATE 4,2:PRINT CHR$(221):LOCATE 6,2:PRINT CHR$(221):LOCATE 23,4,0:PRINT"░ SCORE =                   ░░░░░░░░░░░░░░░░░░░ LEVEL   "+STR$(TK)+"  ░░  MAZE "+LEV$+"  ░░░":S$=STRING$(75,176):LOCATE 24,4,0:PRINT USING"&";S$
610 LOCATE 22,37:COLOR 7,0:PRINT CHR$(16)+"DATNOIDS"+CHR$(17):COLOR 7,0:CH$=CHR$(15):LOCATE 4,1:PRINT" ":LOCATE 4,2:PRINT" ":LOCATE 1,1,0:LOCATE 22,24:PRINT SC:IF LEV$="1"THEN GOSUB 2560
650 IF LEV$="2"THEN GOSUB 4210
660 IF LEV$="3"THEN GOSUB 6720
670 COLOR 15,0:LOCATE 19,72:PRINT CH$:COLOR 1,0:LOCATE 20,72:PRINT"╩":PLAY"MF":FOR R=1 TO 600 STEP 10:SOUND 1000+R,0.1:SOUND 3000+R*1.5,0.1:NEXT:FOR R=1 TO 3:SOUND 32767,10:BEEP:NEXT:PLAY"MB":DEF SEG=0:POKE 1050,PEEK(1052):ON KEY(1)GOSUB 1250:KEY(1)ON
700 X=19:Y=72:A=1:B=0:COLOR 15,0
710 MZ(4,1)=9:FOR UI=70 TO 74:MZ(20,UI)=1:NEXT:SC=INT(SC+VAL(RIGHT$(TIME$,2))+INT((200-SK)/10)):LOCATE 22,24:PRINT SC
720 X=X-A:Y=Y-B:IF MZ(X,Y)=9 GOTO 8940
730 IF MZ(X,Y)<>4 GOTO 1140
750 LOCATE X,Y:PRINT CH$:SOUND 32767,0.05:FOR P=1 TO SK:NEXT:AXN$=INKEY$:IF AXN$="A"OR AXN$="a"THEN GOSUB 1070
762 IF AXN$="Z"OR AXN$="z"THEN GOSUB 950
763 IF AXN$="`"OR AXN$="~"THEN GOSUB 890
764 IF AXN$=";"OR AXN$=":"THEN GOSUB 1010
765 IF AXN$="["OR AXN$="{"THEN GOSUB 1260:GOTO 770
770 IF A=1 THEN LOCATE X+1,Y:PRINT CHR$(179):SOUND 32767,0.1
780 IF A=-1 THEN LOCATE X-1,Y:PRINT CHR$(179):SOUND 32767,0.1
790 IF B=-1 THEN LOCATE X,Y-1:PRINT CHR$(196):SOUND 32767,0.1
800 IF B=1 THEN LOCATE X,Y+1:PRINT CHR$(196):SOUND 32767,0.1
810 AXN$=INKEY$:IF AXN$="A"OR AXN$="a"THEN GOSUB 1070
820 IF AXN$="Z"OR AXN$="z"THEN GOSUB 950
830 IF AXN$="`"OR AXN$="~"THEN GOSUB 890
840 IF AXN$=";"OR AXN$=":"THEN GOSUB 1010
850 IF AXN$="["OR AXN$="{"THEN GOSUB 1260:GOTO 770
860 LOCATE X,Y:PRINT"*":SC=SC+1:GOTO 720
890 IF A=-1 AND X-1<2 THEN LOCATE X-1,Y:PRINT CHR$(219):LOCATE X,Y:PRINT CHR$(192):GOTO 940
900 IF A=-1 THEN LOCATE X-1,Y:PRINT CHR$(179):LOCATE X,Y:PRINT CHR$(192):GOTO 940
910 IF A=1 THEN LOCATE X+1,Y:PRINT CHR$(179):LOCATE X,Y:PRINT CHR$(218)
920 IF B=1 GOTO 1200
930 IF B=-1 GOTO 1200
940 A=0:B=-1:TI=1:Y=Y+1:RETURN 710
950 IF A=1 AND X-1<2 THEN LOCATE X,Y:PRINT CHR$(219):LOCATE X,Y:PRINT CHR$(179):GOTO 1000
960 IF B=-1 THEN LOCATE X,Y-1:PRINT CHR$(196):LOCATE X,Y:PRINT CHR$(191):GOTO 1000
970 IF B=1 THEN LOCATE X,Y+1:PRINT CHR$(196):LOCATE X,Y:PRINT CHR$(218)
980 IF A=1 THEN 1200
990 IF A=-1 THEN 1200
1000 A=-1:B=0:TI=20:X=X+1:RETURN 710
1010 IF X<>4 AND Y=3 THEN 1140
1017 IF A=-1 AND X-1<2 THEN LOCATE X-1,Y:PRINT CHR$(219):LOCATE X,Y:PRINT CHR$(217):GOTO 1060
1020 IF A=-1 THEN LOCATE X-1,Y:PRINT CHR$(179):LOCATE X,Y:PRINT CHR$(217):GOTO 1060
1030 IF A=1 THEN LOCATE X+1,Y:PRINT CHR$(179):LOCATE X,Y:PRINT CHR$(191)
1040 IF B=-1 THEN 1200
1050 IF B=1 GOTO 1200
1060 A=0:B=1:TI=1:Y=Y-1:RETURN 710
1070 IF B=-1 AND Y-1>79 THEN LOCATE X,Y-1:PRINT CHR$(219):LOCATE X,Y:PRINT CHR$(217):GOTO 1120
1080 IF B=-1 THEN LOCATE X,Y-1:PRINT CHR$(196):LOCATE X,Y:PRINT CHR$(217):GOTO 1120
1090 IF B=1 THEN LOCATE X,Y+1:PRINT CHR$(196):LOCATE X,Y:PRINT CHR$(192)
1100 IF A=-1 THEN 1200
1110 IF A=1 THEN 1200
1120 A=1:B=0:TI=20:X=X-1:RETURN 710
1140 PLAY"MB":FOR T=100 TO 10000 STEP 100:SOUND T,0.1:NEXT:SOUND 37,18:LOCATE X,Y:PRINT CHR$(176):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(177):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(178):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(219):SOUND 4700,2
1160 FOR M=1 TO 3:IF X-M<1 OR X+M>24 THEN 1170 ELSE LOCATE X-M,Y:PRINT CHR$(179-M):LOCATE X+M,Y:PRINT CHR$(179-M):NEXT
1170 FOR M=1 TO 3:IF Y-M<1 OR Y+M>80 THEN 1180 ELSE LOCATE X,Y-M:PRINT CHR$(179-M):LOCATE X,Y+M:PRINT CHR$(179-M):NEXT:LOCATE X,Y:PRINT CHR$(254)
1180 SC=INT(SC+VAL(RIGHT$(TIME$,2))+((200-SK)/10))
1190 LOCATE 22,24:PRINT SC:LOCATE 10,15:PRINT" YOU HAVE IMMOLATED YOURSELF ON THE ANTIMATTER WALL ":FOR H=1 TO 3000:NEXT:DD=DD+1:IF DD<4 THEN COLOR 3,0:LOCATE 10,12:PRINT STRING$(55,178):LOCATE 4,2:PRINT" ":GOTO 1500 ELSE 1560
1200 PLAY"mb":FOR T=100 TO 10000 STEP 100:SOUND T,0.1:SOUND 10100-T,0.1:NEXT:SOUND 37,18
1210 LOCATE X,Y:PRINT CHR$(176):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(177):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(178):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(219):SOUND 4700,2
1220 FOR M=1 TO 3:IF X-M<1 OR X+M>24 THEN 1230 ELSE LOCATE X-M,Y:PRINT CHR$(179-M):LOCATE X+M,Y:PRINT CHR$(179-M):NEXT
1230 FOR M=1 TO 3:IF Y-M<1 OR Y+M>80 THEN 1240 ELSE LOCATE X,Y-M:PRINT CHR$(179-M):LOCATE X,Y+M:PRINT CHR$(179-M):NEXT:LOCATE X,Y:PRINT CHR$(254)
1240 SC=INT(SC+VAL(RIGHT$(TIME$,2))+((200-SK)/10)):LOCATE 22,24:PRINT SC:LOCATE 10,16:PRINT" YOU HAVE EXCEEDED THE RESTRICTIONS OF HYPERSPEED ":FOR H=1 TO 3000:NEXT:GOTO 1500
1250 COLOR 7,0:CLS:KEY ON:PRINT"Later.":END
1260 LOCATE X,Y:PRINT CHR$(15):LOCATE X+A,Y+B:PRINT"*":FOR H=5000 TO 100 STEP-250:SOUND H,0.1:NEXT:XT=X:YT=Y:SC=INT(SC+VAL(RIGHT$(TIME$,2))+((200-SK)/10)):LOCATE 22,24:PRINT SC:IF MZ(X,Y)=7 THEN 1340
1300 SOUND 4000,0.1:LOCATE XT-A,YT-B:PRINT CHR$(249):IF MZ(XT-A,YT-B)=4 THEN LOCATE XT-A,YT-B:PRINT" ":XT=XT-A:YT=YT-B:GOTO 1300 ELSE IF MZ(XT-A,YT-B)=7 OR MZ(XT-A,YT-B)=8 THEN 1340
1320 IF MZ(XT-A,YT-B)=9 THEN 1380
1330 IF MZ(XT-A,YT-B)<>4 THEN 1380
1340 PLAY"MF
1350 LOCATE XT-A,YT-B:PRINT CHR$(176):FOR H=37 TO 1500 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B:PRINT CHR$(177):FOR H=1500 TO 3000 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B:PRINT CHR$(178):FOR H=3000 TO 5000 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B
1360 PRINT CHR$(219):SOUND 5000,0.1:LOCATE XT-A,YT-B:PRINT CHR$(178):SOUND 37,4:LOCATE XT-A,YT-B:PRINT CHR$(177):SOUND 37,3:LOCATE XT-A,YT-B:PRINT CHR$(176):SOUND 37,2:LOCATE XT-A,YT-B:PRINT" ":MZ(XT-A,YT-B)=4:PLAY"MB":RETURN
1380 PLAY"MF
1390 LOCATE XT-A,YT-B:PRINT CHR$(176):FOR H=37 TO 1500 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B:PRINT CHR$(177):FOR H=1500 TO 3000 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B:PRINT CHR$(178):FOR H=3000 TO 5000 STEP 100:SOUND H,0.1:NEXT:LOCATE XT-A,YT-B
1400 PRINT CHR$(219):SOUND 5000,0.1:LOCATE XT-A,YT-B:PRINT CHR$(178):SOUND 37,3:LOCATE XT-A,YT-B:PRINT CHR$(177):SOUND 37,3:LOCATE XT-A,YT-B:PRINT CHR$(176):SOUND 37,3
1420 PLAY"MB":SOUND 3800,0.1:LOCATE XT+A,YT+B:PRINT CHR$(249):IF XT=X AND YT=Y THEN 1450
1440 IF XT+A=X AND YT+B=Y THEN 1450 ELSE LOCATE XT+A,YT+B:PRINT" ":XT=XT+A:YT=YT+B:GOTO 1420
1450 KEY(9)OFF:KEY(10)OFF:KEY(12)OFF:KEY(13)OFF:PLAY"mb":FOR T=100 TO 10000 STEP 100:SOUND T,0.1:SOUND 10100-T,0.1:NEXT:SOUND 37,18
1460 LOCATE X,Y:PRINT CHR$(176):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(177):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(178):FOR H=1 TO 199:NEXT:LOCATE X,Y:PRINT CHR$(254):SOUND 4700,2
1470 FOR M=1 TO 3:IF X-M<1 OR X+M>24 THEN 1180 ELSE LOCATE X-M,Y:PRINT CHR$(179-M):LOCATE X+M,Y:PRINT CHR$(179-M):NEXT
1480 FOR M=1 TO 3:IF Y-M<1 OR Y+M>80 THEN 1180 ELSE LOCATE X,Y-M:PRINT CHR$(179-M):LOCATE X,Y+M:PRINT CHR$(179-M):NEXT:LOCATE X,Y:PRINT CHR$(254)
1490 SC=INT(SC+VAL(RIGHT$(TIME$,2))+((200-SK)/10)):LOCATE 22,24:PRINT SC:LOCATE 10,19:PRINT" YOUR MISSILE RICOCHETTED AND DESTROYED YOU ":FOR H=1 TO 3000:NEXT:DD=DD+1
1500 IF DD<4 THEN LOCATE 10,12:COLOR 3,0:PRINT STRING$(55,178) ELSE 1560
1510 LOCATE 4,2:PRINT" ":LOCATE X,Y:PRINT"░":IF LEV$="1"THEN GOSUB 1570
1530 IF LEV$="2"THEN GOSUB 7370
1540 IF LEV$="3"THEN GOSUB 6720
1550 GOTO 670
1560 CLS:LOCATE 10,25:PRINT"FINAL SCORE WAS",SC:FOR H=1 TO 3000:NEXT:SC=0:DD=0:GOTO 400
1570 LOCATE X,Y:PRINT CHR$(176):FOR M=13 TO 20:N=72:LOCATE M,N:PRINT" ":LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=3 TO 10:N=70:LOCATE M,N:PRINT CHR$(32)
1690 LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=13 TO 18:N=56:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32)
1800 LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=3 TO 8:N=31:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR N=3 TO 23:M=4:LOCATE M,N:PRINT CHR$(32)
1930 LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=29 TO 72:M=4:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=40 TO 58:M=18:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32)
2060 LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=54 TO 74:M=14:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=50 TO 72:M=9:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT
2200 FOR N=10 TO 23:M=17:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:N=51:FOR M=8 TO 16:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32)
2300 LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:N=21:FOR M=6 TO 15:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:N=31
2390 FOR M=8 TO 18:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:COLOR 0,7:FOR M=13 TO 15:N=60:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT
2450 FOR M=3 TO 5:N=36:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR M=3 TO 5:N=46:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR M=3 TO 5:N=56:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR M=3 TO 5:N=6:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT
2490 FOR M=3 TO 5:N=12:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR M=17 TO 19:N=46:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR M=16 TO 18:N=15:MZ(M,N)=8:LOCATE M,N:PRINT CHR$(186):NEXT:FOR N=29 TO 33:M=7:MZ(M,N)=7:LOCATE M,N:PRINT CHR$(205):NEXT
2530 FOR N=12 TO 16:M=13:MZ(M,N)=7:LOCATE M,N:PRINT CHR$(205):NEXT:COLOR 4,0:G=1:FOR H=3 TO 21:LOCATE H,G:PRINT" ":NEXT:GOTO 670
2560 COLOR 7,0:LOCATE 10,22:PRINT"WAIT WHILE I PREPARE THE LEVEL ONE TEST":COLOR 7,0:FOR M=13 TO 20:N=72:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=3 TO 10:N=70:MZ(M,N)=4
2740 MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=13 TO 18:N=56:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=3 TO 8
2960 N=31:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR N=2 TO 23:M=4:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3130 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3140 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3150 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3160 NEXT:FOR N=29 TO 72:M=4:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3230 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3240 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3250 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3260 NEXT:FOR N=40 TO 58:M=18:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3340 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3350 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3360 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3370 NEXT:FOR N=54 TO 74:M=14:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3440 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3450 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3460 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3470 NEXT:FOR N=50 TO 72:M=9:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3540 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3550 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3560 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3570 NEXT:FOR N=10 TO 23:M=17:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
3640 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
3650 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
3660 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
3670 NEXT:N=51:FOR M=8 TO 16:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
3740 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
3750 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
3760 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
3770 N=N-1:NEXT:N=21:FOR M=6 TO 15:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
3840 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
3850 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
3860 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
3870 N=N-1:NEXT:N=31:FOR M=8 TO 18:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
3940 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
3950 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
3960 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
3970 N=N-1:NEXT:MZ(3,2)=0:MZ(5,2)=0:FOR M=13 TO 15:N=60:MZ(M,N)=8:NEXT:FOR M=3 TO 5:N=36:MZ(M,N)=8:NEXT:FOR M=3 TO 5:N=46:MZ(M,N)=8:NEXT:FOR M=3 TO 5:N=56:MZ(M,N)=8:NEXT:FOR M=3 TO 5:N=6:MZ(M,N)=8:NEXT:FOR M=3 TO 5:N=12:MZ(M,N)=8:NEXT
4050 FOR M=17 TO 19:N=46:MZ(M,N)=8:NEXT:FOR M=16 TO 18:N=15:MZ(M,N)=8:NEXT:FOR N=29 TO 33:M=7:MZ(M,N)=7:NEXT:FOR N=12 TO 16:M=13:MZ(M,N)=7:NEXT:COLOR 3,0:FOR O=2 TO 20:FOR P=3 TO 79:IF MZ(O,P)=4 THEN LOCATE O,P:PRINT" ":GOTO 4190
4140 IF MZ(O,P)=0 THEN LOCATE O,P:PRINT CHR$(178):GOTO 4190
4150 IF MZ(O,P)=1 THEN LOCATE O,P:PRINT CHR$(176):GOTO 4190
4160 IF MZ(O,P)=3 THEN LOCATE O,P:PRINT CHR$(177):GOTO 4190
4170 IF MZ(O,P)=7 THEN COLOR 0,7:SOUND 120,0.1:LOCATE O,P:PRINT CHR$(205):COLOR 3,0:GOTO 4190
4180 IF MZ(O,P)=8 THEN COLOR 0,7:SOUND 37,0.1:LOCATE O,P:PRINT CHR$(186):COLOR 3,0
4190 NEXT:NEXT:COLOR 7,0:RETURN
4210 COLOR 7,0:LOCATE 10,22:PRINT"WAIT WHILE I PREPARE THE LEVEL TWO TEST":COLOR 7,0:FOR M=15 TO 20:N=72:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=10 TO 17:N=57:MZ(M,N)=4
4430 MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=5 TO 12:N=74:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=9 TO 19
4650 N=41:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-1)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=4 TO 19:N=34:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT
4880 FOR M=3 TO 19:N=3:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N+4)=3:NEXT:FOR M=13 TO 19:N=17:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT
5100 FOR M=9 TO 15:N=26:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3:MZ(M,N+4)=3:NEXT:FOR M=3 TO 11:N=21:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:MZ(M,N+2)=4:MZ(M,N+3)=1:MZ(M,N-3)=1:MZ(M,N-4)=3
5320 MZ(M,N+4)=3:NEXT:FOR N=74 TO 55 STEP-1:M=16:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5410 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5420 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5430 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5440 NEXT:FOR N=55 TO 74:M=11:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5510 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5520 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5530 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5540 NEXT:FOR N=53 TO 76:M=6:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5610 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5620 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5630 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5640 NEXT:FOR N=32 TO 48:M=5:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5710 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5720 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5730 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5740 NEXT:FOR N=15 TO 35:M=18:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5810 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5820 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5830 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5840 NEXT:FOR N=15 TO 28:M=14:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
5910 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
5920 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
5930 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
5940 NEXT:FOR N=19 TO 28:M=10:MZ(M,N)=4:MZ(M+1,N)=4:MZ(M-1,N)=4:IF MZ(M-2,N)<>4 THEN MZ(M-2,N)=1
6010 IF MZ(M+2,N)<>4 THEN MZ(M+2,N)=1
6020 IF MZ(M+3,N)<>4 THEN MZ(M+3,N)=3
6030 IF MZ(M-3,N)<>4 THEN MZ(M-3,N)=3
6040 NEXT:N=54:FOR M=5 TO 17:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
6130 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
6140 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
6150 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
6160 N=N-1:NEXT:N=46:FOR M=4 TO 8:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
6230 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
6240 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
6250 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
6260 N=N-1:NEXT:N=21:FOR M=3 TO 19:MZ(M,N)=4:MZ(M,N+1)=4:MZ(M,N+2)=4:MZ(M,N-1)=4:MZ(M,N-2)=4:IF MZ(M,N-3)<>4 THEN MZ(M,N-3)=1
6330 IF MZ(M,N+3)<>4 THEN MZ(M,N+3)=1
6340 IF MZ(M,N-4)<>4 THEN MZ(M,N-4)=3
6350 IF MZ(M,N+4)<>4 THEN MZ(M,N+4)=3
6360 N=N-1:NEXT:N=60:FOR M=15 TO 17:MZ(M,N)=8:NEXT:N=65:FOR M=15 TO 17:MZ(M,N)=8:NEXT:M=13:FOR N=55 TO 59:MZ(M,N)=7:NEXT:M=8:FOR N=72 TO 76:MZ(M,N)=7:NEXT:M=12:FOR N=40 TO 43:MZ(M,N)=7:NEXT:M=10:FOR N=32 TO 36:MZ(M,N)=7:NEXT
6440 M=16:FOR N=32 TO 36:MZ(M,N)=7:NEXT:M=12:FOR N=24 TO 28:MZ(M,N)=7:NEXT:M=15:FOR N=3 TO 5:MZ(M,N)=7:NEXT:M=10:FOR N=3 TO 5:MZ(M,N)=7:NEXT:M=6:FOR N=3 TO 5:MZ(M,N)=7:NEXT:N=71:FOR M=10 TO 12:MZ(M,N)=8:NEXT:N=66:FOR M=10 TO 12:MZ(M,N)=8:NEXT
6510 N=54:FOR M=5 TO 7:MZ(M,N)=8:NEXT:N=61:FOR M=5 TO 7:MZ(M,N)=8:NEXT:N=66:FOR M=5 TO 7:MZ(M,N)=8:NEXT:N=38:FOR M=4 TO 6:MZ(M,N)=8:NEXT:N=25:FOR M=17 TO 19:MZ(M,N)=8:NEXT:N=20:FOR M=17 TO 19:MZ(M,N)=8:NEXT:N=23:FOR M=13 TO 15:MZ(M,N)=8:NEXT
6600 MZ(3,2)=4:MZ(5,2)=4:COLOR 3,0:FOR O=2 TO 20:FOR P=3 TO 79:IF MZ(O,P)=4 THEN LOCATE O,P:PRINT" ":GOTO 6700
6640 IF MZ(O,P)=0 THEN LOCATE O,P:PRINT CHR$(178):GOTO 6700
6650 IF MZ(O,P)=1 THEN LOCATE O,P:PRINT CHR$(176):GOTO 6700
6660 IF MZ(O,P)=2 THEN LOCATE O,P:PRINT CHR$(176):GOTO 6700
6670 IF MZ(O,P)=3 THEN LOCATE O,P:PRINT CHR$(177):GOTO 6700
6680 IF MZ(O,P)=7 THEN COLOR 0,7:SOUND 100,0.1:LOCATE O,P:PRINT CHR$(205):COLOR 3,0:GOTO 6700
6690 IF MZ(O,P)=8 THEN COLOR 0,7:SOUND 37,0.1:LOCATE O,P:PRINT CHR$(186):COLOR 3,0
6700 NEXT:NEXT:COLOR 7,0:RETURN
6720 COLOR 7,0:LOCATE 10,10:PRINT"               ALLOW ME TO PREPARE THE THIRD TEST         ":COLOR 3,0:LOCATE 3,2:PRINT CHR$(221):LOCATE 5,2:PRINT CHR$(221):G=1:FOR H=3 TO 21:LOCATE H,G:PRINT" ":NEXT:H=2:FOR G=1 TO 21:MZ(G,H)=4:NEXT
6760 H=79:FOR G=1 TO 21:MZ(G,H)=4:NEXT:G=1:FOR H=1 TO 79:MZ(G,H)=4:NEXT:G=21:FOR H=1 TO 79:MZ(G,H)=4:NEXT:MZ(4,2)=9:PLAY"MB":DEF SEG=&H40:RANDOMIZE PEEK(&H6D)*256.+PEEK(&H6C)-32768.:FOR H=2 TO 20:FOR G=3 TO 79:X=INT(RND*(16)):LOCATE H,G:PRINT CHR$(16)
6840 IF X=2 OR X=9 THEN LOCATE H,G:PRINT CHR$(176+INT(RND*(3))):MZ(H,G)=0:SOUND(RND*2000)+37,0.1:GOTO 6860
6850 LOCATE H,G:PRINT" ":MZ(H,G)=4
6860 NEXT:NEXT:H=72:FOR G=20 TO 13 STEP-1:LOCATE G,H:PRINT" ":MZ(G,H)=4:NEXT:G=4:FOR H=2 TO 15:LOCATE G,H:PRINT" ":MZ(G,H)=4:NEXT:COLOR 0,7:SOUND 600,0.1:LOCATE 10,5:PRINT CHR$(201):MZ(10,5)=7
6910 SOUND 600,0.1:G=10:FOR H=6 TO 12:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 10,13:PRINT CHR$(187):MZ(10,13)=7:SOUND 600,0.1:LOCATE 11,13:PRINT CHR$(186):MZ(11,13)=7:SOUND 600,0.1:LOCATE 12,13:PRINT CHR$(186):MZ(12,13)=7
6950 SOUND 600,0.1:LOCATE 13,13:PRINT CHR$(186):MZ(13,13)=7:SOUND 600,0.1:LOCATE 14,13:PRINT CHR$(200):MZ(14,13)=7:SOUND 600,0.1:G=14:FOR H=14 TO 30:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 14,31:PRINT CHR$(187):MZ(14,31)=7
6990 SOUND 600,0.1:H=31:FOR G=15 TO 17:LOCATE G,H:PRINT CHR$(186):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 18,31:PRINT CHR$(188):MZ(13,13)=7:SOUND 600,0.1:G=18:FOR H=30 TO 6 STEP-1:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT
7020 SOUND 600,0.1:LOCATE 18,5:PRINT CHR$(200):MZ(18,5)=7:SOUND 600,0.1:H=5:FOR G=17 TO 11 STEP-1:LOCATE G,H:PRINT CHR$(186):MZ(G,H)=7:NEXT:COLOR 4,0:SOUND 600,0.1:FOR G=11 TO 17:FOR H=6 TO 12:LOCATE G,H:PRINT" ":MZ(G,H)=4:SOUND 1500,0.1:NEXT:NEXT
7050 SOUND 600,0.1:FOR G=15 TO 17:FOR H=13 TO 30:LOCATE G,H:PRINT" ":MZ(G,H)=4:SOUND 1500,0.1:NEXT:NEXT:COLOR 0,7:SOUND 600,0.1:LOCATE 4,53:PRINT CHR$(201):MZ(4,53)=7:SOUND 600,0.1:G=4:FOR H=54 TO 76:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT
7080 SOUND 600,0.1:LOCATE 4,77:PRINT CHR$(187):MZ(4,77)=7:SOUND 600,0.1:H=77:FOR G=5 TO 11:LOCATE G,H:PRINT CHR$(186):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 12,77:PRINT CHR$(188):MZ(12,77)=7
7110 SOUND 600,0.1:G=12:FOR H=76 TO 71 STEP-1:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 12,70:PRINT CHR$(200):MZ(12,70)=7:SOUND 600,0.1:LOCATE 11,70:PRINT CHR$(186):MZ(11,70)=7:SOUND 600,0.1:LOCATE 10,70:PRINT CHR$(186):MZ(10,70)=7
7150 SOUND 600,0.1:LOCATE 9,70:PRINT CHR$(186):MZ(9,70)=7:SOUND 600,0.1:LOCATE 8,70:PRINT CHR$(187):MZ(8,70)=7:SOUND 600,0.1:G=8:FOR H=69 TO 54 STEP-1:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 8,53:PRINT CHR$(200):MZ(8,53)=7
7190 SOUND 600,0.1:LOCATE 7,53:PRINT CHR$(186):MZ(7,53)=7:SOUND 600,0.1:LOCATE 6,53:PRINT CHR$(186):MZ(6,53)=7:SOUND 600,0.1:LOCATE 5,53:PRINT CHR$(186):MZ(5,53)=7:COLOR 4,0
7220 SOUND 600,0.1:FOR G=5 TO 7:FOR H=54 TO 70:LOCATE G,H:PRINT" ":MZ(G,H)=4:SOUND 1500,0.1:NEXT:NEXT:SOUND 600,0.1:FOR G=5 TO 11:FOR H=71 TO 76:LOCATE G,H:PRINT" ":MZ(G,H)=4:SOUND 1500,0.1:NEXT:NEXT
7240 COLOR 0,7:SOUND 600,0.1:LOCATE 9,38:PRINT CHR$(201):MZ(9,38)=7:SOUND 600,0.1:G=9:FOR H=39 TO 43:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT:SOUND 600,0.1:LOCATE 9,44:PRINT CHR$(187):MZ(9,44)=7:SOUND 600,0.1:LOCATE 10,44:PRINT CHR$(186):MZ(10,44)=7
7280 SOUND 600,0.1:LOCATE 11,44:PRINT CHR$(186):MZ(11,44)=7:SOUND 600,0.1:LOCATE 12,44:PRINT CHR$(186):MZ(12,44)=7:SOUND 600,0.1:LOCATE 13,44:PRINT CHR$(188):MZ(13,44)=7:SOUND 600,0.1:G=13:FOR H=43 TO 39 STEP-1:LOCATE G,H:PRINT CHR$(205):MZ(G,H)=7:NEXT
7320 SOUND 600,0.1:LOCATE 13,38:PRINT CHR$(200):MZ(13,38)=7:SOUND 600,0.1:LOCATE 12,38:PRINT CHR$(186):MZ(12,38)=7:SOUND 600,0.1:LOCATE 11,38:PRINT CHR$(186):MZ(11,38)=7:SOUND 600,0.1:LOCATE 10,38:PRINT CHR$(186):MZ(10,38)=7:COLOR 7,0
7360 SOUND 600,0.1:FOR G=10 TO 12:FOR H=39 TO 43:SOUND 1500,0.1:LOCATE G,H:PRINT" ":MZ(G,H)=4:NEXT:NEXT:RETURN
7370 LOCATE 3,2:PRINT CHR$(221):LOCATE 5,2:PRINT CHR$(221):G=1:FOR H=3 TO 21:LOCATE H,G:PRINT" ":NEXT:FOR M=15 TO 20:N=72:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32)
7450 LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=10 TO 17:N=57:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=5 TO 12:N=74:LOCATE M,N:PRINT CHR$(32)
7580 LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=9 TO 19:N=41:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32)
7690 LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=4 TO 19:N=34:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=3 TO 19:N=3:LOCATE M,N:PRINT CHR$(32)
7820 LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=13 TO 19:N=17:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=9 TO 15:N=26
7950 LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR M=3 TO 11:N=21:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32)
8060 LOCATE M,N-2:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):NEXT:FOR N=74 TO 55 STEP-1:M=16:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=55 TO 74:M=11:LOCATE M,N:PRINT CHR$(32)
8190 LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=53 TO 76:M=6:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=32 TO 48:M=5:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32)
8320 LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=15 TO 35:M=18:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:FOR N=15 TO 28:M=14:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT
8460 FOR N=19 TO 28:M=10:LOCATE M,N:PRINT CHR$(32):LOCATE M+1,N:PRINT CHR$(32):LOCATE M-1,N:PRINT CHR$(32):NEXT:N=54:FOR M=5 TO 17:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32)
8580 LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:N=46:FOR M=4 TO 8:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:N=21
8670 FOR M=3 TO 19:LOCATE M,N:PRINT CHR$(32):LOCATE M,N+1:PRINT CHR$(32):LOCATE M,N+2:PRINT CHR$(32):LOCATE M,N-1:PRINT CHR$(32):LOCATE M,N-2:PRINT CHR$(32):N=N-1:NEXT:COLOR 0,7:N=60:FOR M=15 TO 17:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT
8740 N=65:FOR M=15 TO 17:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:M=13:FOR N=55 TO 59:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=8:FOR N=72 TO 76:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=12:FOR N=40 TO 43:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT
8780 M=10:FOR N=32 TO 36:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=16:FOR N=32 TO 36:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=12:FOR N=24 TO 28:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=15:FOR N=3 TO 5:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT
8820 M=10:FOR N=3 TO 5:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:M=6:FOR N=3 TO 5:LOCATE M,N:PRINT CHR$(205):MZ(M,N)=7:NEXT:N=71:FOR M=10 TO 12:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=66:FOR M=10 TO 12:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT
8860 N=54:FOR M=5 TO 7:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=61:FOR M=5 TO 7:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=66:FOR M=5 TO 7:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=38:FOR M=4 TO 6:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT
8900 N=25:FOR M=17 TO 19:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=20:FOR M=17 TO 19:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:N=23:FOR M=13 TO 15:LOCATE M,N:PRINT CHR$(186):MZ(M,N)=8:NEXT:COLOR 4,0:RETURN
8940 IF LEV$="3"THEN LEV$="1":GOTO 8970
8950 IF LEV$="1"THEN LEV$="2":GOTO 8970
8960 IF LEV$="2"THEN LEV$="3
8970 PLAY"MB":CLS:WIDTH 80:KEY OFF:COLOR 4,0:PLAY"MB":CLS:WIDTH 80:KEY OFF:COLOR 4,0:LOCATE 10,9:PRINT"PREPARE YOURSELF FOR ACCELERATION TO HYPERSPEED FOR LEVEL "+LEV$+" PLAY":FOR Y=37 TO 3000 STEP 11:SOUND Y,0.2:SOUND 3037-Y,0.2:NEXT:CLS
8990 ST1$=STRING$(79,176):COLOR 1,0:ST2$=STRING$(79,177):ST3$=STRING$(79,178):ST4$=STRING$(79,219):Q$=CHR$(179):FOR R=1 TO 2:FOR W=2500 TO 1000 STEP-100:SOUND W,0.1:NEXT:COLOR 4,0:LOCATE 12,40:PRINT CHR$(15):LOCATE 11,40:PRINT"*":COLOR 3,0
9070 FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:COLOR 1,0:LOCATE 24,1:PRINT ST1$:COLOR 4,0:LOCATE 12,40:PRINT CHR$(15):LOCATE 11,40:PRINT"*":COLOR 3,0:FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:COLOR 1,0:LOCATE 24,1:PRINT ST2$:COLOR 4,0
9130 LOCATE 12,40:PRINT CHR$(15):LOCATE 11,40:PRINT"*":COLOR 1,0:FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:COLOR 1,0:LOCATE 24,1:PRINT ST3$:COLOR 2,0:LOCATE 12,40:PRINT CHR$(15):LOCATE 11,40:PRINT"*":COLOR 2,0
9190 FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:COLOR 1,0:LOCATE 24,1:PRINT ST4$:COLOR 2,0:FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:NEXT:FOR Z=1 TO 15
9240 SOUND 2000,0.1:SOUND(16-Z)*100,0.1:LOCATE 24,1:PRINT STRING$(80," "):LOCATE 12,40:PRINT CHR$(15):LOCATE 11,40:PRINT"*":FOR I=1 TO 11:LOCATE I,40:PRINT Q$:NEXT:NEXT:FOR P=3500 TO 100 STEP-20:SOUND P,0.2:NEXT:SOUND 100,24
9280 LOCATE 13,6:PRINT"YOU HAVE SUCCESSFULLY MADE THE HYPERSPEED JUMP TO LEVEL "+LEV$+" OF DATNOIDS":FOR I=1 TO 5000:NEXT:IF LEV$="1"THEN LOCATE 15,20:PRINT"YOU ALSO HAVE A FASTER SHIP, GOOD LUCK":FOR I=1 TO 2500:NEXT
9300 CLS:IF LEV$="1"THEN SK=INT(SK-(SK/2)):IF SK<1 THEN SK=1
9310 GOTO 470
```

## NUKE-NY.BAS

```bas

10 REM-Nuclear attack simulator
20 REM-by J.R.DUKAT
30 REM
40 REM-Information presented is as good as my sources allow
50 REM-any corrections or improvements are welcomed........
60 REM
70 REM-start it up
80 SCREEN 1:CLS:KEY OFF
90 LOCATE 10,10,0:PRINT "DO YOU LOVE NEW YORK?";
100 FOR T=1 TO 3000:NEXT T:CLS
110 GOTO 160
120 GOSUB 2970
130 GOSUB 2940
140 SCREEN 1
150 CLS
160 COLOR 1,0
170 KEY OFF
180 LINE (158,3)-(339,20),2,B
190 LINE (158,27)-(338,190),2,B
200 LOCATE 2,21:PRINT " MAP..NEW YORK AREA";
210 SCALE=1
220 XT=-140:YT=120
230 IBMX=150+(XT*SCALE):IBMY=99-((YT)*SCALE)
240 FLG=0
250 READ X,Y
260 IF X=999 AND Y=999 THEN 240
270 IF X=888 AND Y=888 THEN 370
280 X=X+TX:Y=Y+TY
290 X0=TX:Y0=TY
300 X=IBMX+X:Y=IBMY-(Y)
310 PSET (X,Y),1
320 IF FLG=0 THEN 340
330 LINE (X,Y)-(LASTX,LASTY)
340 LASTX=X:LASTY=Y
350 FLG=1
360 GOTO 250
370 READ X,Y
380 IF X=999 AND Y=999 THEN 410
390 PAINT (X,Y),2,3
400 GOTO 370
410 GOSUB 3020 ;DRAW COMPASS ROSE
420 IF NXTBOMB=1 THEN 2330
430 LOCATE 5,21:PRINT "  This map covers ";
440 LOCATE 6,21:PRINT " an area of appx. ";
450 LOCATE 7,21:PRINT " 1700 sq/mi.  The ";
460 LOCATE 8,21:PRINT " long dimension is";
470 LOCATE 9,21:PRINT " about 50 miles - ";
480 LOCATE 10,21:PRINT " short dimension  ";
490 LOCATE 11,21:PRINT " about 34 miles.  ";
500 LOCATE 12,21:PRINT " Manhattan island ";
510 LOCATE 13,21:PRINT " can be seen near ";
520 LOCATE 14,21:PRINT " the map center.  ";
530 LOCATE 15,21:PRINT " The Hudson river ";
540 LOCATE 16,21:PRINT " runs along the w.";
550 LOCATE 17,21:PRINT " side.  The East  ";
560 LOCATE 18,21:PRINT " river on opposite";
570 LOCATE 19,21:PRINT " side. Staten Is. ";
580 LOCATE 20,21:PRINT " is to the SW.    ";
590 FOR T=1 TO 9000:NEXT T
600 GOSUB 2970  ;CLEAR BIG BOX
610 LOCATE 5,21:PRINT "  The south end of";
620 LOCATE 6,21:PRINT " Long Is. Sound is";
630 LOCATE 7,21:PRINT " to top right.    ";
640 LOCATE 8,21:PRINT "  The entrance to ";
650 LOCATE 9,21:PRINT " New York Harbor  ";
660 LOCATE 10,21:PRINT " is to the bottom ";
670 LOCATE 11,21:PRINT " right of the map.";
680 LOCATE 12,21:PRINT " Most of the left ";
690 LOCATE 13,21:PRINT " side would lie in";
700 LOCATE 14,21:PRINT " New Jersey...    ";
710 FOR T=1 TO 8000:NEXT T
720 GOSUB 2970
730 LOCATE 5,21:PRINT "  This program is ";
740 LOCATE 6,21:PRINT " to demonstrate   ";
750 LOCATE 7,21:PRINT " effects from var-";
760 LOCATE 8,21:PRINT " ious nuclear war-";
770 LOCATE 9,21:PRINT " heads which might";
780 LOCATE 10,21:PRINT " be used in attack";
790 LOCATE 11,21:PRINT " ing New York.....";
800 LOCATE 12,21:PRINT " This attack will ";
810 LOCATE 13,21:PRINT " consist of just  ";
820 LOCATE 14,21:PRINT " nine 1 MT. bombs.";
830 LOCATE 15,21:PRINT " They will explode";
840 LOCATE 16,21:PRINT " at an altitude of";
850 LOCATE 17,21:PRINT " 8000 ft. This sim";
860 LOCATE 18,21:PRINT " ulation would be ";
870 LOCATE 19,21:PRINT " considered on the";
880 LOCATE 20,21:PRINT " conservative side";
890 LOCATE 21,21:PRINT " A maximum attack ";
900 LOCATE 22,21:PRINT " would use around ";
910 LOCATE 23,21:PRINT " 60 warheads......";
920 FOR T=1 TO 11000:NEXT T
930 GOSUB 2970
940 TMR=2000
950 GOSUB 2940
960 LOCATE 2,21:PRINT "   Select Targets  ";
970 LINE (158,27)-(338,190),2,B
980 LOCATE 5,21:PRINT "   Manhattan Is.  ";
990 SOUND 500,1:PSET (81,90),1
1000 FOR T=1 TO TMR:NEXT T
1010 LOCATE 6,21:PRINT "   Seacaucus      ";
1020 SOUND 500,1:PSET (53,83),1
1030 FOR T=1 TO TMR:NEXT T
1040 LOCATE 7,21:PRINT "   Bayonne        ";
1050 SOUND 500,1:PSET (56,114),1
1060 FOR T=1 TO TMR:NEXT T
1070 LOCATE 8,21:PRINT "   Staten Is.     ";
1080 SOUND 500,1:PSET (32,136),1
1090 FOR T=1 TO TMR:NEXT T
1100 LOCATE 9,21:PRINT "   Newark         ";
1110 SOUND 500,1:PSET (22,103),1
1120 FOR T=1 TO TMR:NEXT T
1130 LOCATE 10,21:PRINT "   Hackensack     ";
1140 SOUND 500,1:PSET (69,53),1
1150 FOR T=1 TO TMR:NEXT T
1160 LOCATE 11,21:PRINT "   Bronx          ";
1170 SOUND 500,1:PSET (105,60),1
1180 FOR T=1 TO TMR:NEXT T
1190 LOCATE 12,21:PRINT "   Queens         ";
1200 SOUND 500,1:PSET (117,99),1
1210 FOR T=1 TO TMR:NEXT T
1220 LOCATE 13,21:PRINT "   Brooklyn       ";
1230 SOUND 500,1:PSET (91,118),1
1240 FOR T=1 TO TMR:NEXT T
1250 FOR I=5 TO 13
1260 LOCATE I,22:PRINT CHR$(26);
1270 SOUND 1000,1:FOR T=1 TO 50:NEXT T
1280 NEXT I
1290 LOCATE 2,21:PRINT " Targets Confirmed ";
1300 FOR T=1 TO 4000:NEXT T
1310 GOSUB 2970
1320 FOR S=1 TO 50:SOUND 9000,.5:SOUND 8000,.5:NEXT S
1330 LOCATE 2,21:PRINT "   Warhead Size    ";
1340 LOCATE 5,21:PRINT "   Each warhead is ";
1350 LOCATE 6,21:PRINT " one megaton - the ";
1360 LOCATE 7,21:PRINT " equivalent of one ";
1370 LOCATE 8,21:PRINT " million tons of   ";
1380 LOCATE 9,21:PRINT " TNT.              ";
1390 LOCATE 11,21:PRINT "  Warheads are set ";
1400 LOCATE 12,21:PRINT " to airburst at an ";
1410 LOCATE 13,21:PRINT " altitude of 8000  ";
1420 LOCATE 14,21:PRINT " feet.             ";
1430 FOR T=1 TO 5000:NEXT T
1440 GOSUB 2970:GOSUB 2940
1450 SOUND 8000,.5
1460 LOCATE 2,21:PRINT "   Targets Marked  ";
1470 PSET (81,90),Z
1480 PSET (53,83),Z
1490 PSET (56,114),Z
1500 PSET (32,136),Z
1510 PSET (22,103),Z
1520 PSET (69,53),Z
1530 PSET (105,60),Z
1540 PSET (117,99),Z
1550 PSET (91,118),Z
1560 C=C+1:IF C=150 THEN 1590
1570 IF Z=1 THEN Z=0 ELSE Z=1
1580 GOTO 1470
1590 LOCATE 2,21:PRINT " Time to detonate  ";
1600 FOR CD=30 TO 3 STEP -1
1610 LOCATE 6,23:PRINT USING "####";CD;
1620 LOCATE 6,28:PRINT "seconds";
1630 FOR T=1 TO 1000:NEXT T
1640 NEXT CD
1650 GOSUB 2970
1660 FOR S=1 TO 50:SOUND 4000,.5:SOUND 4500,.5:NEXT S
1670 LOCATE 2,21:PRINT " -- DETONATION --  ";
1680 S=1:I=5:R=0:GOSUB 2690
1690 LOCATE 5,21:PRINT "       ZONE 1      ";
1700 LOCATE 7,21:PRINT " Diameter 3 mi.    ";
1710 LOCATE 10,21:PRINT " Overpress. 25 psi ";
1720 LOCATE 11,21:PRINT " Wind Vel. 320 mph ";
1730 LOCATE 13,21:PRINT "   All structures  ";
1740 LOCATE 14,21:PRINT " above ground are  ";
1750 LOCATE 15,21:PRINT " destroyed.        ";
1760 LOCATE 17,21:PRINT " 98 % fatalities   ";
1770 LOCATE 19,21:PRINT " STBY FOR ZONE 2   ";
1780 FOR T=1 TO 5500:NEXT T
1790 GOSUB 2970
1800 S=6:I=10:R=3:GOSUB 2690
1810 LOCATE 5,21:PRINT "       ZONE 2      ";
1820 LOCATE 7,21:PRINT " Diameter 6 miles  ";
1830 LOCATE 10,21:PRINT " Overpress. 9 psi  ";
1840 LOCATE 11,21:PRINT " Wind Vel. 260 mph ";
1850 LOCATE 13,21:PRINT "  Factories/office ";
1860 LOCATE 14,21:PRINT " buildings collapse";
1870 LOCATE 15,21:PRINT " Severe damage to  ";
1880 LOCATE 16,21:PRINT " highway bridges.  ";
1890 LOCATE 17,21:PRINT " 50% dead 40% inj. ";
1900 LOCATE 19,21:PRINT " STBY FOR ZONE 3   ";
1910 FOR T=1 TO 5500:NEXT T
1920 GOSUB 2970
1930 I=19:R=1:GOSUB 2590 ;DRAW CIRCLES
1940 LOCATE 5,21:PRINT "       ZONE 3      ";
1950 LOCATE 7,21:PRINT " Diameter 10 mi.   ";
1960 LOCATE 10,21:PRINT " Overpress. 3 psi  ";
1970 LOCATE 11,21:PRINT " Wind Vel. 98 mph  ";
1980 LOCATE 13,21:PRINT "  Wood frame resid-";
1990 LOCATE 14,21:PRINT " ences severely    ";
2000 LOCATE 15,21:PRINT " damaged. People   ";
2010 LOCATE 16,21:PRINT " are blown around. ";
2020 LOCATE 17,21:PRINT " 2nd degree burns. ";
2030 LOCATE 18,21:PRINT " 10% dead 45% inj. ";
2040 LOCATE 20,21:PRINT " STBY FOR MORE..   ";
2050 FOR T=1 TO 5500:NEXT T
2060 GOSUB 2970
2070 LOCATE 5,21:PRINT " LONG RANGE EFFECT ";
2080 LOCATE 7,21:PRINT " Light door/plaster";
2090 LOCATE 8,21:PRINT " damage to 14.5 mi.";
2100 LOCATE 9,21:PRINT " Windows broken out";
2110 LOCATE 10,21:PRINT " to 30 miles.      ";
2120 LOCATE 12,21:PRINT " Flashblindness to ";
2130 LOCATE 13,21:PRINT " 13 miles daytime. ";
2140 LOCATE 14,21:PRINT " and to 50 miles at";
2150 LOCATE 15,21:PRINT " night clear sky.  ";
2160 FOR T=1 TO 5500:NEXT T
2170 GOSUB 2970:GOSUB 2940
2180 LOCATE 2,21:PRINT " WANNA SEE WORSE ? ";
2190 LOCATE 5,21:PRINT " If you wait just a";
2200 LOCATE 6,21:PRINT " second i'll redraw";
2210 LOCATE 7,21:PRINT " the map and drop  ";
2220 LOCATE 8,21:PRINT " just one 20 MT.war";
2230 LOCATE 9,21:PRINT " head centered on  ";
2240 LOCATE 10,21:PRINT " Manhattan Is.     ";
2250 LOCATE 11,21:PRINT " I will mark zones ";
2260 LOCATE 12,21:PRINT " as before...      ";
2270 LOCATE 13,21:PRINT " Burst altitude for";
2280 LOCATE 14,21:PRINT " this device will  ";
2290 LOCATE 15,21:PRINT " be 17500 feet...  ";
2300 NXTBOMB=1
2310 FOR T=1 TO 9000:NEXT T
2320 RESTORE:GOTO 120
2330 SOUND 2000,1
2340 LOCATE 2,21:PRINT " Target Manhattan  ";
2350 LOCATE 5,21:PRINT "  20 MT. WARHEAD   ";
2360 PSET (81,90),0
2370 FOR T=1 TO 3000:NEXT T
2380 SOUND 5000,1:LOCATE 2,21:PRINT " -- DETONATION --  ";
2390 S=1:I=16:R=0:GOSUB 2830:S=17:I=23:R=1:GOSUB 2830
2400 CIRCLE (81,90),40,1
2410 FOR T=1 TO 3000:NEXT T
2420 GOSUB 2970
2430 LOCATE 6,21:PRINT " -FOR COMPARISON-  ";
2440 LOCATE 8,21:PRINT " I'll drop a 12.5  ";
2450 LOCATE 9,21:PRINT " kiloton bomb on   ";
2460 LOCATE 10,21:PRINT " Staten Island.    ";
2470 LOCATE 11,21:PRINT " (Hiroshima size)..";
2480 S=1:I=2:R=0:GOSUB 2900:S=3:I=4:R=3:GOSUB 2900
2490 FOR T=1 TO 10000:NEXT T:CLS:SCREEN 0:WIDTH 80
2500 LOCATE 10,15,0
2510 PRINT " Projection for U.S. & SOVIET warhead numbers for 1985"
2520 LOCATE 11,15,0
2530 PRINT " is a grand total of roughly 23000 of various sizes..."
2540 LOCATE 12,15,0
2550 PRINT " This total does not include other nuclear powers ..."
2560 LOCATE 17,19,0
2570 PRINT "             BYE/BYE  &  GOOD LUCK"
2580 FOR T=1 TO 9000:NEXT T:LOCATE 1,1,1:SYSTEM
2590 CIRCLE (81,90),I,R
2600 CIRCLE (53,83),I,R
2610 CIRCLE (56,114),I,R
2620 CIRCLE (32,136),I,R
2630 CIRCLE (22,103),I,R
2640 CIRCLE (69,53),I,R
2650 CIRCLE (105,60),I,R
2660 CIRCLE (117,99),I,R
2670 CIRCLE (91,118),I,R
2680 RETURN
2690 FOR GZ=S TO I
2700 CIRCLE (81,90),GZ,R
2710 CIRCLE (53,83),GZ,R
2720 CIRCLE (56,114),GZ,R
2730 CIRCLE (32,136),GZ,R
2740 CIRCLE (22,103),GZ,R
2750 CIRCLE (69,53),GZ,R
2760 CIRCLE (105,60),GZ,R
2770 CIRCLE (117,99),GZ,R
2780 CIRCLE (91,118),GZ,R
2790 FOR T=1 TO 300:NEXT T
2800 NEXT GZ
2810 REM-SINGLE WARHEAD ON MANHATTAN
2820 RETURN
2830 FOR GZ=S TO I
2840 CIRCLE (81,90),GZ,R
2850 FOR T=1 TO 300:NEXT T
2860 NEXT GZ
2870 FOR T=1 TO 300:NEXT T
2880 RETURN
2890 REM-sub to drop 12.5 kiloton bomb on staten island
2900 FOR GZ=S TO I
2910 CIRCLE (49,133),GZ,R
2920 FOR T=1 TO 300:NEXT T
2930 NEXT GZ
2940 REM- to clear small box
2950 LOCATE 2,21:PRINT "                  ";
2960 RETURN
2970 REM-sub to clear big box
2980 FOR I=5 TO 23
2990 LOCATE I,22:PRINT "                  ";
3000 NEXT I
3010 RETURN
3020 REM-sub to draw compass rose
3030 FOR I=20 TO 50 STEP 2:PSET (30,I),1:NEXT I
3040 FOR I=20 TO 40 STEP 4:PSET (I,35),1:NEXT I
3050 PSET (30,20),1:LINE -(27,23),1:PSET (30,20),1:LINE -(33,23),1
3060 RETURN
3070 DATA 32,-138,36,-140,40,-139,46,-138,48,-138,51,-137
3080 DATA 53,-139,53.5,-144,57,-150,43,-166,39,-168,41,-166
3090 DATA 39.5,-165,38,-167,28,-175,25,-174,22,-176,16,-178
3100 DATA 15,-175,18,-171,15.5,-170,18,-168,17,-165,21,-162
3110 DATA 24,-161,25.5,-154,27,-151,27.5,-145,26.5,-143,28,-140
3120 DATA 29,-139,30,-138,32,-138
3130 DATA 999,999
3140 DATA 135,-81, 127,-78, 126,-79, 124,-79, 125,-81, 122.8,-83
3150 DATA 124.5,-87, 125.2,-86, 128,-87, 129,-88, 130.5,-87, 129.5,-90
3160 DATA 129.5,-93, 128,-94, 128.5,-98, 127,-93, 126,-91, 124,-90
3170 DATA 120,-87, 119,-89, 119.5,-90, 118,-93, 118,-95, 120,-97
3180 DATA 119.5,-98, 120,-100, 121,-101, 120,-101, 119,-106, 115.8,-100
3190 DATA 116,-99, 115,-97, 113.5,-98, 114,-99, 111.5,-100, 111,-97
3200 DATA 107,-97, 106.5,-97, 104,-98, 102,-97, 101.5,-98, 101,-97
3210 DATA 100,-98,98,-101, 100,-102, 100,-105, 101,-106, 102,-106
3220 DATA 100,-108,98,-106,98.5,-105,99.5,-106,98.5,-104,95,-102
3230 DATA 95.2,-100,94,-101,93,-103,92,-104,90,-101,90,-99
3240 DATA 88,-99,86,-101,83.5,-102,82,-103,82.5,-105,76.5,-112
3250 DATA 77,-116,75,-120,75,-122,72.5,-121,69,-123,67.5,-126
3260 DATA 67,-127,66,-128,64,-129,66,-132,68,-132,65,-135
3270 DATA 61,-140,59.5,-144,61,-148,64.5,-150,69,-152,70,-155
3280 DATA 66,-156,65.5,-156,68,-158,74,-157,82,-156,82.5,-155
3290 DATA 78,-155,82,-154,87,-155,88,-153,89.5,-155,88.5,-155
3300 DATA 90,-156,93.5,-155,94,-154,92.5,-148,90,-144, 100,-138
3310 DATA 106,-137, 116,-145, 121,-141, 122,-143, 118,-145, 118,-148
3320 DATA 116,-148, 117,-152, 115,-149, 110,-152, 110,-153, 102,-155
3330 DATA 99,-155,89,-160,87,-159,81,-163,89,-162, 103,-156
3340 DATA 112,-154, 119.5,-152, 124,-152, 120,-155, 124,-154, 135,-155
3350 DATA 135,-81,999,999
3360 DATA 96, 0,96,-2,96,-4,96.5,-6,96.5,-8,96,-10
3370 DATA 96,-12,96,-14,96,-16,96,-18,95,-20,96,-22
3380 DATA 96.5,-24,96,-26,96,-28,96,-30,95,-32,94.5,-34
3390 DATA 94.5,-36,94,-38,94,-40,93,-42,93,-44,93,-46
3400 DATA 92.5,-48,92,-50,92,-52,91,-54,90,-56,89.5,-58
3410 DATA 89,-60,88.7,-62,88,-64,87.5,-66,87,-68,86.5,-70
3420 DATA 86,-72,85,-74,84,-76,83,-78,82,-80,81,-82
3430 DATA 80,-84,80,-86,79,-88,78,-90,76.5,-92,76,-94
3440 DATA 74.5,-96,73,-98,72,-100,71,-102,70,-104,68.5,-106
3450 DATA 67.5,-108,67,-110,66.8,-112,66,-114,66,-116,66,-118
3460 DATA 65.2,-120,64.2,-122,65,-123,67,-122,69,-121,73,-120
3470 DATA 74,-114,75.5,-110,80,-103,80.8,-101,84,-97,84,-95
3480 DATA 86,-96,88,-97,96,-95,97,-94, 100,-95, 102,-92
3490 DATA 103,-95, 104,-92, 108,-92, 112,-95, 119,-94, 110,-93
3500 DATA 110,-91, 109,-90, 107,-83, 107,-80, 109,-81, 110,-83
3510 DATA 113.5,-77, 113,-75, 117,-70, 116,-67, 117.5,-65, 118,-68
3520 DATA 122,-65, 121.5,-63, 124,-63, 124,-65, 124.5,-63, 126,-59
3530 DATA 123.5,-58, 128,-58, 130,-57, 133,-54, 131,-60, 135,-55
3540 DATA 135,0,96,0,999,999
3550 DATA  0, 0,86, 0,87.5,-2,88.5,-4,88.5,-6
3560 DATA 88.2,-8,87.5,-10,87,-12,87,-14,87,-16
3570 DATA 87,-18,86.5,-20,86.5,-22,85.8,-24,86,-26
3580 DATA 86,-28,86.2,-30,90.5,-31,88.8,-32,89,-33
3590 DATA 89,-34,89.5,-36,89.5,-38,89.5,-40,89,-42
3600 DATA 89.2,-44,89,-46,88.8,-48,88.2,-50,88,-52
3610 DATA 87,-54,86.5,-56,86,-58,85,-60,85,-62
3620 DATA 84.5,-64,84,-66,83.5,-68,83,-70,82,-72
3630 DATA 81,-74,80.5,-76,79.5,-78,78.5,-80,78,-82
3640 DATA 77,-84,75.8,-86,75,-88,74.8,-90,73.8,-92
3650 DATA 72,-94,71,-96,69.8,-98,69,-100,67.8,-102
3660 DATA 67,-104,66,-105,65,-106,64,-108,64,-110
3670 DATA 63.8,-112,63,-114,62.5,-116,63,-118,62,-120
3680 DATA 61.5,-122,58,-124,57,-126,54,-124,54,-128
3690 DATA 53,-130,54,-132,53,-134,47,-131,47.5,-132
3700 DATA 47.5,-134,50.5,-135,44,-137,42,-138,38,-138
3710 DATA 45,-125,48.5,-118,43,-118,43,-123,39,-129
3720 DATA 40,-130,37,-134,35,-133,32,-137,29,-138
3730 DATA 27,-140,25.5,-143,25.8,-148,26,-150,25,-154
3740 DATA 23,-160,22,-161,20,-160,16,-162,15,-166
3750 DATA 15.8,-170,12,-177, 9,-177, 8,-177,10,-178
3760 DATA 14,-185,21,-188,24.5,-190,25,-193,28,-190
3770 DATA 30,-187,35,-190,39,-187,44,-190,49,-192
3780 DATA 60,-196,66,-198,70,-200,70.5,-198,68,-194
3790 DATA 68,-190,66,-190,65,-184,62.5,-183,63.5,-181
3800 DATA 67,-183,70,-190,71,-198,70,-206,61,-207,0,-207,0,0,888,888
3810 DATA 60,60,140,100,140,30,50,140,77,165,105,137,96,140,123,131
3820 DATA 999,999
3830 STOP




```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0209

     Volume in drive A has no label
     Directory of A:\

    JUMPJOE2 DOC     14427   6-03-84   4:54p
    JUMPJOE2 EXE     62720   6-01-84   3:29a
    DATNOIDS BAS     22803   5-28-84  12:03a
    NUKE-NY  BAS     15360  12-10-83   9:05p
    HS       DAT       128   6-25-84  12:42a
    XXX                512   6-25-84  12:15a
    CRC      TXT       935  11-15-84   9:23a
    CRCK4    COM      1536  10-21-82   7:54p
            8 file(s)     118421 bytes
                           40448 bytes free
