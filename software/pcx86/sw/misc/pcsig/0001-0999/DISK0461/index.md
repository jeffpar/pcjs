---
layout: page
title: "PC-SIG Diskette Library (Disk #461)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0461/
machines:
  - id: ibm5160
    type: pcx86
    config: /machines/pcx86/ibm/5160/cga/256kb/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0461"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5160" %}

{% comment %}info_begin{% endcomment %}

## Information about "RIDGETOWN COLLEGE"

    From Ridgetown College of Ontario comes this fine collection of farm
    management tools. Besides a handy metric conversion program, highlights
    include:
    
    LUMBER -- Welcome to the world of computer carpentry. Enter your basic
    design and constraints, if any. It produces reliable estimates of all
    the construction materials you will need for your project and keep a
    running total of the costs.
    
    USLE -- Deal with the problem of soil loss evaluation on irregular
    slopes with this program. Get a fast, accurate estimate of
    such soil loss. As many, if not most, fields have irregular
    slopes, the farmer/conservation specialist can benefit from
    this aid.
    
    WOODHEAT -- Explore the economics of using wood as a primary source of
    heating.
    
    The documentation also includes the code for Hewlett Packard (HP-41c)
    calculators.
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## CONV.BAS

```bas
10 ' COPYRIGHT 1982, RICHARD M. SCHINNELL
20 ' This Program is called CONVERT.BAS VER 1.1 (2-13-83)
30 DEFINT A-Z:FALSE=0:TRUE = NOT FALSE:ON ERROR GOTO 680
40 KEY OFF:CLS:LOCATE 12,1
50 PRINT"COPYRIGHT 1982, Richard Schinnell Rockville,Maryland 301 949-8848 "
60 PRINT"This program will convert EXE/COM files to a basic program which"
70 PRINT"you then can transmit to someone over your Modem. They
80 PRINT"can then run the basic program and it will re-create the EXE/COM pgm.
90 PRINT"just like you had it.       E N J O Y   RICH......":PRINT
100 INPUT "Name of the COM or EXE File to convert";FIL1$
110 PRINT "To call the program with the same name with .BAS ext hit C/R"
120 INPUT "WHAT do you wish to call the basic program  (.BAS) ";FIL2$
130 IF LEN(FIL2$)<1 THEN FIL2$=MID$(FIL1$,1,(INSTR(FIL1$,".")-1))+".BAS"
140 NUMLIN=1001:X=0:GRAND#=0:CNT=1
150 IF FIL1$=FIL2$ THEN CLS:LOCATE 12,15:PRINT "DUPLICATE FILE NAMES ":GOTO 10
160 OPEN FIL1$ FOR INPUT AS #1:CLOSE #1 ' just checking to make sure its there
170 OPEN  FIL1$  AS #1 LEN=1 ' opening the .COM file
180 V= VARPTR(#1) ' looking at the FCB
190 L0=PEEK(V) ' getting the type of file
200 L1=PEEK(V+17) + 256 * PEEK(V+18) ' this gets the filesize in bytes
210 LIMIT = L1 ' THIS IS THE LIMIT FOR THE FILE SIZE
220 OPEN  FIL2$  FOR OUTPUT AS #2 ' opening up the .bas file this pgm creates
230 GOSUB 470
240 PRINT #2,"1000 DATA ";STR$(LIMIT):
250 LIN$=RIGHT$(STR$(NUMLIN),4)
260 RICH2$=LIN$+" DATA "
270 FIELD #1,1 AS GETS$
280 X =X +1:IF X =< LIMIT THEN GET #1,X ELSE 400
290 RICH1$=HEX$(ASC(GETS$)):IF LEN(RICH1$)<2 THEN RICH1$="0"+RICH1$
300 IF CNT <29 THEN RICH2$=RICH2$+RICH1$:CNT=CNT+1:GRAND#=GRAND#+ASC(GETS$):GOTO 280
310 GOTO 400
320 RICH2$=LIN$+" DATA 000000,9999"
330 PRINT #2,RICH2$
340 PRINT #2,"5000 PRINT ";CHR$(34);"* * ERROR VERIFY FOLLOWING DATA * * * ";CHR$(34)
350 PRINT #2,"5005 PRINT X$;";CHR$(34);",";CHR$(34);";CHECK "
360 PRINT #2,"5010 KEY ON:CLOSE:END"
370 'JUST A COMMENT LINE. BE CAREFULL IF YOU CHANGE ANYTHING IN THIS PGM...
380 LOCATE 20,1:PRINT" You have sucessfully created file named ";FIL2$
390 CLOSE:KEY ON:END
400 GRAND$=MID$(STR$(GRAND#),2,4) 'THIS IS WHERE IT ALL HAPPENS
410 CNT=2:PRINT #2,RICH2$;",";GRAND$:NUMLIN=NUMLIN+1
420 LOCATE 18,1:PRINT SPC(78);:LOCATE 18,1:PRINT RICH2$;",";GRAND#;
425 LOCATE 16,51:PRINT CINT(((LIMIT/28)+1)-(X/28));" LINES LEFT";
430 LIN$=RIGHT$(STR$(NUMLIN),4)
440 IF X>LIMIT THEN GOTO 320
450 RICH2$=LIN$+" DATA "+RICH1$:GRAND#=ASC(GETS$)
460 GOTO 280
470 PRINT #2," 10 CLS:PRINT";CHR$(34);"This basic program was created by CONVERT.BAS";CHR$(34);":KEY OFF:DEFINT A-Z:FALSE=0:TRUE=NOT FALSE"
480 IF INSTR(FIL1$,":")=2 THEN FIL3$=MID$(FIL1$,3,13) ELSE FIL3$=FIL1$
490 GOSUB 720
500 PRINT #2," 20 PRINT";CHR$(34);"Copyright 1983 ,Rich Schinnell Rockville,MD. Not for Sale.";CHR$(34)
510 PRINT #2," 30 PRINT ";CHR$(34);"This program will create  you a program named ";FIL3$;CHR$(34);":PRINT "
520 PRINT #2," 40 ON ERROR GOTO 5000"
530 PRINT #2," 50 INPUT ";CHR$(34);"PLACE the disk to write the file TO in Drive A: Press <ENTER> ";CHR$(34);"; CAPITAL$"
540 PRINT#2," 90 CLS:LOCATE 12,5:PRINT ";CHR$(34);"Now writing file named ";FIL3$;" standby please  ";CHR$(34)
550 PRINT #2,"100 RESTORE"
560 PRINT #2,"110 OPEN ";CHR$(34);"R";CHR$(34);", #1,";CHR$(34);FIL3$;CHR$(34);",1 "
570 PRINT #2,"120 FIELD #1, 1 AS N$
580 PRINT #2,"130 READ N:LL=(N/28)+1"
590 PRINT #2,"140 FOR I = 1 TO N
600 PRINT #2,"150 READ X$,CHECK:FOR C=1 TO LEN(X$) STEP 2:CC$=";CHR$(34);"&H";CHR$(34);"+MID$(X$,C,2):IF CHECK=9999 THEN 170"
610 PRINT #2,"155 LSET N$=CHR$(VAL(CC$)):PUT #1:CHECK=CHECK-VAL(CC$):NEXT C:IF CHECK<>0 THEN 5000
620 PRINT #2,"160 LOCATE 14,5:PRINT TIME$;";CHR$(34);" Reading data line #";CHR$(34);";(1000+I);";CHR$(34);" lines left=";CHR$(34);";LL-I;:NEXT I:CLOSE"
630 PRINT #2,"170 LOCATE 16,5:PRINT ";CHR$(34); FIL3$;" CREATED * *";CHR$(34);":GOTO 5010"
640 CLS:LOCATE 12,5:PRINT "Reading file named ";FIL1$
650 LOCATE 14,5:PRINT     "Writing file named ";FIL2$
660 LOCATE 16,5:PRINT     "The file size of the input file is ";LIMIT
670 RETURN
680 PRINT " you had an error ";ERR;" in line # ";ERL
690 PRINT "Probably you named a file which does not exist "
700 PRINT "try again Charlie........
710 KEY ON:END
720 FOR I = 1 TO LEN(FIL3$)
730 IF ASC(MID$(FIL3$,I,1))>90 THEN MID$(FIL3$,I,1)=CHR$(ASC(MID$(FIL3$,I,1))-32)
740 NEXT I
750 RETURN
```

## LUMBER.BAS

```bas
10 REM NEW SHELTER's Material Estimator
20 REM Copyright 1984 Rodale's NEW SHELTER
30 REM  Written bymRoger Rawlings; Based on "Trade Secrets" interview
31 REM    with Dale McCormick
32 REM  Keyed from the September 1984 issue by Karson Morrison
40 GOSUB 580
50 DIM M$(6),H$(7),Q$(1),L$(5),D$(6),N(7)
60 PRINT "NEW SHELTER'S":PRINT "MATERIAL ESTIMATOR": PRINT "KEYED BY Karson Morrison"
70 PRINT:PRINT "MATERIAL TO BE ESTIMATED = ":PRINT
80 PRINT "1) STUDS":PRINT "2) FLOOR JOISTS":PRINT "3) CEILING JOISTS"
90 PRINT "4) RAFTERS":PRINT "5) SUBFLOORING":PRINT "6) WALL SHEATHING":PRINT "7) ROOF SHEATHING"
100 PRINT "8) QUIT":PRINT "Your Choice":INPUT M
110 IF M < 1 OR M > 8 THEN GOTO 70
120 GOSUB 580
130 ON M GOTO 140,170,170,280,330,330,400,790
140 M$ = "STUD":H$= "WALL": L$ = "WALL":GOSUB 590:GOSUB 600:GOSUB 610
150 T = L * QC:GOSUB 640: GOSUB 670:PRINT:GOSUB 720:GOSUB 580
160 ON Z GOTO 70, 140
170 M$ = "JOIST":L$ = "FAR WALL":H$= "FLOOR"
180 IF M <> 3 THEN GOTO 200
190 H$ = "CEILING"
200 GOSUB 590:GOSUB 600:GOSUB 610
210 PRINT :PRINT "DO THE JOISTS MEET IN MID-SPAN":GOSUB 730
220 IF Q$ = "N" THEN GOTO 240
230 L = L * 2
240 PRINT "HOW MANY STAIRWAYS PASS THROUGH THE "; H$:INPUT SR
250 SR = SR * 3
260 T = (L * (QC / 2)) + 1 + SR:GOSUB 640:GOSUB 670:GOSUB 720: GOSUB 580
270 SR = 0:ON Z GOTO 70,170
280 M$ = "RAFTER":L$ = "RIDGE":H$= "ROOF"
290 GOSUB 590:GOSUB 600:GOSUB 610:PRINT:GOSUB 630
300 T = (L * (QC / 2) + 1) * SL:GOSUB 640:GOSUB 670:GOSUB 720:GOSUB 580
320 ON Z GOTO 70, 280
330 H$ = "FLOOR":M$ = "4' X 8' SHEET":L$ = "FLOOR"
340 IF M <>6 THEN GOTO 360
350 H$ = "WALL":L$ = "WALL"
360 GOSUB 590:GOSUB 600
370 D$ = "WIDTH":IF M<> 6 THEN GOTO 390
380 D$ = "HEIGHT"
390 PRINT D$; " OF ";H$; " (FT)":INPUT W:GOTO 440
400 H$ = "ROOF":M$ = "4' X 8' SHEET":GOSUB 590:PRINT:PRINT "RIDGE LENGTH (FT)":INPUT L
410 PRINT:PRINT "RAFTER LENGTH (FT)": INPUT W
420 PRINT:GOSUB 630:IF SL = 1 THEN GOTO 440
430 W = W * 2
440 A = L * W:PRINT
450 PRINT "ARE THERE ANY OPENINGS IN ":PRINT "THE ";H$:GOSUB 730
460 IF Q$ = "N" THEN GOTO 540
470 D$ = "HEIGHT": IF M = 6 THEN GOTO 490
480 D$ = "LENGTH"
490 PRINT "OPENING ";D$;" (FT.)":INPUT L
500 PRINT:PRINT "OPENING WIDTH (FT.)":INPUT W
510 A = A - (L * W)
520 PRINT :PRINT "ARE THERE ANY MORE OPENINGS IN THE ";H$:GOSUB 730
530 IF Q$<>"N" THEN GOTO 470
540 T = A / 32:GOSUB 670:GOSUB 720:GOSUB 580
550 IF Q$ = "N" THEN GOTO 70
560 ON M GOTO 0,0,0,0,330,330,400
570 REM Line 580 Use your computer's screen-clearing command
580 CLS:RETURN
590 N(M) = N(M) + 1:PRINT H$; " #";N(M):RETURN
600 PRINT:PRINT "LENGTH OF ";L$; " (FT.)":INPUT L:PRINT:RETURN
610 PRINT M$; " SPACING (IN. O.C.)":INPUT OC
620 QC = 1 / (OC / 24):RETURN
630 PRINT "1 ROOF SLOPE OR 2":INPUT SL:RETURN
640 IF T = INT(T) THEN GOTO 660
650 T = INT (T) + 1
660 RETURN
670 PRINT:PRINT "PRICE OF ONE ";M$:PRINT "(OMIT DOLLAR SIGN)":INPUT P
680 TP = INT ((P * T) * 100) / 100:RT = RT + TP
690 PRINT: PRINT "FOR THIS ";H$;" YOU NEED ";T;" ";M$;"S"
700 PRINT: PRINT "COST = $";TP
710 PRINT:PRINT " TOTAL COST FOR ALL ITEMS":PRINT "ESTIMATED SO FAR =$";RT:RETURN
720 PRINT:PRINT "DO ANOTHER ";H$;" "
730 PRINT "(Y OR N)"
740 INPUT Q$
750 IF Q$<>"Y" AND Q$<> "N" THEN GOTO 740
760 Z = 2:IF Q$<> "N" THEN GOTO 780
770 Z = 1
780 PRINT:RETURN
790 PRINT "TOTAL COST = $";RT:PRINT:PRINT "<END>"
800 END
```

## METRICON.BAS

```bas
10 '
20 ' THIS SECTION IS USED TO CONVERT THE METRIC SYSTEM TO THE IMPERIAL
30 ' SYTEM OR TO CONVERT THE IMPERIAL SYSTEM TO THE METRIC SYSTEM WHICHEVER
40 ' TICKLES YOUR FANCY.
50 '
60 CLS : CLEAR : KEY OFF
70 DEF SEG = 0:POKE 1047,96:REM ZERO SEGMENT SHIFT LOCK ON & CURSOR KEYS OFF
80 LOCATE 5,19 : PRINT "RIDGETOWN COLLEGE OF AGRICULTURAL TECNOLOGY"
90 LOCATE 10,26 : PRINT "IMPERIAL/METRIC CONVERSION"
100 LOCATE 14,23 : PRINT "Programmed by:  Randolph W. Ross"
110 LOCATE 16,37 : PRINT "September 1985"
120 LOCATE 18,19 : PRINT "Inspired by a program written by"
130 LOCATE 20,37 : PRINT "Terry Matthews"
140 GOSUB 11250
150 LOCATE 13,30:PRINT "ONE MOMENT PLEASE"
160 GOTO 340
170 '
180 LOCATE 5,30 : PRINT "CONVERSIONS"
190 LOCATE 9,25 : PRINT "1.  --  AREA"
200 LOCATE 10,25 : PRINT "2.  --  CROP YIELDS"
210 LOCATE 11,25 : PRINT "3.  --  FLUID EQUIVALENTS"
220 LOCATE 12,25 : PRINT "4.  --  LENGTH"
230 LOCATE 13,25 : PRINT "5.  --  PROPORTION"
240 LOCATE 14,25 : PRINT "6.  --  SPEED"
250 LOCATE 15,25 : PRINT "7.  --  TEMPERATURE"
260 LOCATE 16,25 : PRINT "8.  --  VOLUME AND CAPACITY"
270 LOCATE 17,25 : PRINT "9.  --  WEIGHT"
280 LOCATE 18,25 : PRINT "10. --  QUIT SYSTEM"
290 LOCATE 21,14 : INPUT "What type of conversion would you like to perform"; PICK
300 IF PICK = 10 THEN SYSTEM
310 IF PICK < 1 OR PICK > 9 THEN BEEP : CLS : LOCATE 24,5 : PRINT "INVALID SELECTION  --  PLEASE ENTER YOUR SELECTION AGAIN" : GOTO 180
320 ON PICK GOSUB 580,2440,3230,5230,5850,7020,7830,8490,9740
330 GOTO 180
340 DIM LENGTH(9,9), TYPE$(9)
350 FOR X = 1 TO 9 : READ TYPE$(X) : NEXT
360 DATA "MILLIMETRES", "CENTIMETRES", "METRES", "KILOMETRES"
370 DATA "INCHES", "FEET", "YARDS", "RODS", "MILES"
380 FOR X = 1 TO 9
390   FOR Y = 1 TO 9
400     READ LENGTH(X,Y)
410   NEXT Y
420 NEXT X
430 DATA 1, .1, .001, .000001, .03937008, .0032808, .0010936, .0001989, .00000062
440 DATA 10, 1, .01, .00001, .3937008, .032808, .0109361, .0019892, .0000062
450 DATA 1000, 100, 1, .001, 39.37008, 3.2808399, 1.0936133, .1989258, .0006214
460 DATA 1000000, 100000, 1000, 1, 393700.8, 3280.8399, 1093.6133, 198.9258, .6213712
470 DATA 25.4, 2.54, .0254, .0000254, 1, .08333333, .027777778, .005050505, .0000157828
480 DATA 304.8, 30.48, .3048, .0003048, 12, 1, .33333333, .06060606, .00018939394
490 DATA 914.4, 91.44, .9144, .0009144, 36, 3, 1, .1818182, .000568182
500 DATA 5027, 502.7, .5027, .0005027, 198, 16.5, 5.5, 1,.003125
510 DATA 1609344, 160934.4, 1609.344, 1.609344, 63360, 5280, 1760, 320, 1
520 CLS:GOTO 180
530 END
540 '
550 ' THIS SECTION CONVERTS IMPERIAL AREA TO METRIC AREA AND
560 ' VICE VERSA.
570 '
580 CLS
590 LOCATE 5,20 : PRINT "AREA CONVERSION - IMPERIAL TO METRIC"
600 LOCATE 9,25 : PRINT "1.   SQ IN"
610 LOCATE 10,25 : PRINT "2.   SQ FT"
620 LOCATE 11,25 : PRINT "3.   SQ YD"
630 LOCATE 12,25 : PRINT "4.   SQ RODS"
640 LOCATE 13,25 : PRINT "5.   SQ MILES"
650 LOCATE 14,25 : PRINT "6.   ACRES"
660 LOCATE 15,25 : PRINT "7.   SQ CM"
670 LOCATE 16,25 : PRINT "8.   SQ M"
680 LOCATE 17,25 : PRINT "9.   SQ KM"
690 LOCATE 18,25 : PRINT "10.  HECTARES"
700 LOCATE 20,25 : PRINT "11.  SYMBOLS CHART"
710 LOCATE 21,25 : PRINT "12   RETURN TO MAIN MENU"
720 LOCATE 24,15 : INPUT "ENTER THE CONVERSION YOU WISH TO PERFORM"; PICK
730 IF PICK < 1 OR PICK > 12 THEN CLS : LOCATE 23,5 : PRINT "INVALID ENTRY  --  PLEASE ENTER A NUMBER BETWEEN 1 AND 12" : GOTO 580
740 IF PICK = 12 THEN CLS:RETURN
750 ON PICK GOSUB 770,880,1010,1250,1140,1380,1550,1640,1790,1920,2120
760 GOTO 580
770 CLS
780 LOCATE 10,5 : INPUT "HOW MANY SQUARE INCHES WOULD YOU LIKE TO CONVERT"; IN
790 V = IN * 6.4516
800 CLS : LOCATE 10,10 : PRINT IN ; "SQUARE INCHES EQUALS";
810 LOCATE 10,40 : PRINT V; "SQUARE CENTIMETRES"
820 V = IN * 645.16
830 LOCATE 11,40 : PRINT V; "SQUARE MILLIMETRES"
840 V = IN / 144
850 LOCATE 12,40 : PRINT V; "SQUARE FEET"
860 GOSUB 11250
870 RETURN
880 CLS
890 LOCATE 10,5 : INPUT "HOW MANY SQUARE FEET WOULD YOU LIKE TO CONVERT"; FT
900 V = FT * 929.088
910 CLS : LOCATE 10,10 : PRINT FT ; "SQUARE FEET EQUALS";
920 LOCATE 10,40 : PRINT V; "SQUARE CENTIMETRES"
930 V = FT * 0.0929088
940 LOCATE 11,40 : PRINT V; "SQUARE METRES"
950 V = FT * 144
960 LOCATE 12,40 : PRINT V; "SQUARE INCHES"
970 V = FT / 9
980 LOCATE 13,40 : PRINT V; "SQUARE YARDS"
990 GOSUB 11250
1000 RETURN
1010 CLS
1020 LOCATE 10,5 : INPUT "HOW MANY SQUARE YARDS WOULD YOU LIKE TO CONVERT"; YD
1030 V = YD * 0.8361
1040 CLS : LOCATE 10,10 : PRINT YD ; "SQUARE YARDS EQUALS";
1050 LOCATE 10,40 : PRINT V; "SQUARE METRES"
1060 V = YD * 8361
1070 LOCATE 11,40 : PRINT V; "SQUARE CENTIMETRES"
1080 V = YD * 9
1090 LOCATE 12,40 : PRINT V; "SQUARE FEET"
1100 V = YD / 30.25
1110 LOCATE 13,40 : PRINT V; "SQUARE RODS"
1120 GOSUB 11250
1130 RETURN
1140 CLS
1150 LOCATE 10,5 : INPUT "HOW MANY SQUARE MILES WOULD YOU LIKE TO CONVERT"; MILES
1160 V = MILES * 2.59
1170 CLS : LOCATE 10,10 : PRINT MILES; "SQUARE MILES EQUALS";
1180 LOCATE 10,40 : PRINT V; "SQUARE KILOMETRES"
1190 V = MILES * 259
1200 LOCATE 11,40 : PRINT V; "HECTARES"
1210 V = MILES * 640
1220 LOCATE 12,40 : PRINT V; "ACRES"
1230 GOSUB 11250
1240 RETURN
1250 CLS
1260 LOCATE 10,5 : INPUT "HOW MANY SQUARE RODS WOULD YOU LIKE TO CONVERT"; RODS
1270 V = RODS * 25.292
1280 CLS : LOCATE 10,10 : PRINT RODS; "SQUARE RODS EQUALS";
1290 LOCATE 10,40 : PRINT V; "SQUARE METRES"
1300 V = RODS * 0.002529
1310 LOCATE 11,40 : PRINT V; "HECTARES"
1320 V = RODS * 30.25
1330 LOCATE 12,40 : PRINT V; "SQUARE YARDS"
1340 V = RODS * 0.00625
1350 LOCATE 13,40 : PRINT V; "ACRES"
1360 GOSUB 11250
1370 RETURN
1380 CLS
1390 LOCATE 10,5 : INPUT "HOW MANY ACRES WOULD YOU LIKE TO CONVERT"; AC
1400 V = AC * 0.4047
1410 CLS : LOCATE 10,10 : PRINT AC; "ACRES EQUALS";
1420 LOCATE 10,40 : PRINT V; "HECTARES"
1430 V = AC * 0.004047
1440 LOCATE 11,40 : PRINT V; "SQUARE KILOMETRES"
1450 V = AC * 43560.
1460 LOCATE 12,40 : PRINT V; "SQUARE FEET"
1470 V = AC * 4840
1480 LOCATE 13,40 : PRINT V; "SQUARE YARDS"
1490 V = AC * 160
1500 LOCATE 14,40 : PRINT V; "SQUARE RODS"
1510 V = AC * 0.0015625
1520 LOCATE 15,40 : PRINT V; "SQUARE MILES"
1530 GOSUB 11250
1540 RETURN
1550 CLS
1560 LOCATE 10,5 : INPUT "HOW MANY SQUARE CENTIMETRES WOULD YOU LIKE TO CONVERT"; CM
1570 V = CM * 0.155
1580 CLS : LOCATE 10,10 : PRINT CM; "SQUARE CENTIMETRES EQUALS";
1590 LOCATE 10,40 : PRINT V; "SQUARE INCHES"
1600 V = CM * 0.00107639
1610 LOCATE 11,40 : PRINT V; "SQUARE FEET"
1620 GOSUB 11250
1630 RETURN
1640 CLS
1650 LOCATE 10,5 : INPUT "HOW MANY SQUARE METRES WOULD YOU LIKE TO CONVERT"; M
1660 V = M * 10.7639
1670 CLS : LOCATE 10,10 : PRINT M; "SQUARE METRES EQUALS";
1680 LOCATE 10,40 : PRINT V; "SQUARE FEET"
1690 V = M * 1.196
1700 LOCATE 11,40 : PRINT V; "SQUARE YARDS"
1710 V = M * 0.03954
1720 LOCATE 12,40 : PRINT V; "SQUARE RODS"
1730 V = M * 0.00024625
1740 LOCATE 13,40 : PRINT V; "ACRES"
1750 V = M * 0.0001
1760 LOCATE 14,40 : PRINT V; "HECTARES"
1770 GOSUB 11250
1780 RETURN
1790 CLS
1800 LOCATE 10,5 : INPUT "HOW MANY SQUARE KILOMETRES WOULD YOU LIKE TO CONVERT";KM
1810 V = KM * 0.3861
1820 CLS : LOCATE 10,10 : PRINT KM; "SQUARE KILOMETRES EQUALS";
1830 LOCATE 10,40 : PRINT V; "SQUARE MILES"
1840 V = KM * 247.1
1850 LOCATE 11,40 : PRINT V; "ACRES"
1860 V = KM * 39536.
1870 LOCATE 12,40 : PRINT V; "SQUARE RODS"
1880 V = KM * 10000
1890 LOCATE 13,40 : PRINT V; "HECTARES"
1900 GOSUB 11250
1910 RETURN
1920 CLS
1930 LOCATE 10,5 : INPUT "HOW MANY HECTARES WOULD YOU LIKE TO CONVERT"; HA
1940 V = HA * 2.471
1950 CLS : LOCATE 10,10 : PRINT HA; "HECTARES EQUALS";
1960 LOCATE 10,40 : PRINT V; "ACRES"
1970 V = HA * 0.003861
1980 LOCATE 11,40 : PRINT V; "SQUARE MILES"
1990 V = HA * 395.36
2000 LOCATE 12,40 : PRINT V; "SQUARE RODS"
2010 V = HA * 11959.6
2020 LOCATE 13,40 : PRINT V; "SQUARE YARDS"
2030 V = HA * 10000
2040 LOCATE 15,40 : PRINT V; "SQUARE METRES"
2050 V = HA * 0.0001
2060 LOCATE 16,40 : PRINT V; "SQUARE KILOMETRES"
2070 GOSUB 11250
2080 RETURN
2090 '
2100 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR AREA
2110 '
2120 CLS
2130 LOCATE 2,31 : PRINT "SYMBOL CHART"
2140 LOCATE 5,1 : PRINT "QUANTITY"
2150 LOCATE 5,15 : PRINT "UNIT"
2160 LOCATE 5,70 : PRINT "SYMBOL"
2170 LOCATE 7,1 : PRINT "Area"
2180 LOCATE 7,15 : PRINT "Square centimetre"
2190 LOCATE 7,70 : PRINT "sq cm"
2200 LOCATE 8,15 : PRINT "Square metre"
2210 LOCATE 8,70 : PRINT "sq m"
2220 LOCATE 9,15 : PRINT "square kilometre"
2230 LOCATE 9,70 : PRINT "sq km"
2240 LOCATE 10,15 : PRINT "hectare"
2250 LOCATE 10,70 : PRINT "ha"
2260 LOCATE 12,15 : PRINT "square inch"
2270 LOCATE 12,70 : PRINT "sq in"
2280 LOCATE 13,15 : PRINT "square foot"
2290 LOCATE 13,70 : PRINT "sq ft"
2300 LOCATE 14,15 : PRINT "square yard"
2310 LOCATE 14,70 : PRINT "sq yd"
2320 LOCATE 15,15 : PRINT "square rod"
2330 LOCATE 15,70 : PRINT "sq rod"
2340 LOCATE 16,15 : PRINT "square mile"
2350 LOCATE 16,70 : PRINT "sq mi"
2360 LOCATE 17,15 : PRINT "acre"
2370 LOCATE 17,70 : PRINT "ac"
2380 GOSUB 11250
2390 RETURN
2400 '
2410 ' YIELD
2420 ' THIS SECTION CONVERTS BUSHELS PER ACRE TO TONNES PER HECTARE.
2430 '
2440 CLS:P=0
2450 LOCATE 10,20 : PRINT "1.  BU/AC"
2460 LOCATE 11,20 : PRINT "2.  TONNES/HA"
2470 LOCATE 12,20 : PRINT "3.  BU"
2480 LOCATE 13,20 : PRINT "4.  TONNES"
2490 LOCATE 15,20 : PRINT "5.  SYMBOLS CHART"
2500 LOCATE 16,20 : PRINT "6.  RETURN TO MAIN MENU"
2510 LOCATE 20,20 : INPUT "ENTER YOUR SELECTION";PICK
2520 IF PICK = 5 THEN GOSUB 3000 : GOTO 2440
2530 IF PICK = 6 THEN CLS:RETURN
2540 IF PICK < 1 OR PICK > 4 THEN CLS : BEEP : LOCATE 23,5 : PRINT "INVALID SELECTION  --  PLEASE REENTER ANOTHER CHOICE" : GOTO 2440
2550 CLS : LOCATE 10,10 : PRINT "ENTER THE NAME OF THE COMMODITY (ie: CORN)"
2560 LOCATE 11,10 : PRINT "ENTER `LIST' FOR LIST OF COMMODITIES"
2570 LOCATE 10,57:INPUT C$
2580 IF C$="LIST" THEN GOSUB 11290:GOTO 2550
2590 IF LEFT$(C$,3)="COR" OR LEFT$(C$,3)="SOR" OR LEFT$(C$,3)="RYE" OR LEFT$(C$,4)="FLAX" THEN P = 0.06276767 : C = 39.36826 : GOTO 2720
2600 IF LEFT$(C$,3)="WHE" OR LEFT$(C$,3)="SOY" OR LEFT$(C$,3)="POT" OR LEFT$(C$,3)="PEA" THEN P = 0.06725108 : C = 36.74371 : GOTO 2720
2610 IF LEFT$(C$,3)="OAT" THEN P = 0.03810895 : C = 64.84185 : GOTO 2720
2620 IF LEFT$(C$,3)="BAR" OR ( LEFT$(C$,3)="GRA" AND ( RIGHT$(C$,1)="Y" OR RIGHT$(C$,1)="G")) THEN P = 0.05380086 : C = 45.92964 :GOTO 2720
2630 IF LEFT$(C$,3)="MAL" OR (LEFT$(C$,3)="BAR" AND (RIGHT$(C$,1)="T" OR RIGHT$(C$,1)="G")) THEN P = 0.03810895 : C = 64.84185 : GOTO 2720
2640 IF LEFT$(C$,4)="RAPE" OR LEFT$(C$,4)="CANO" OR LEFT$(C$,3)="MUS" THEN P = 0.05604257 : C = 44.09245 : GOTO 2720
2650 IF LEFT$(C$,3)="SUN" THEN P=0.02690043 : C=91.85927 : GOTO 2720
2660 IF LEFT$(C$,4)="BUCK" THEN P=0.05380086 : C=45.92964:GOTO 2720
2670 IF P = 0 THEN : CLS : LOCATE 10,10 : PRINT "SORRY, I DO NOT HAVE THE FORMULA TO CONVERT THAT COMMODITY" ELSE 2720
2680 LOCATE 12,10:PRINT "ENTER THE NUMBER OF POUNDS IN A BUSHEL OF ";C$;
2690 INPUT P
2700 C=2204.6225#/P
2710 P=(P/2204.6225#)*(1/0.4046856)
2720 ON PICK GOTO 2730,2800,2870,2920
2730 CLS : LOCATE 10,10 : INPUT "ENTER THE NUMBER OF BUSHELS"; BU
2740 T = BU * P
2750 T1 = BU / C
2760 CLS : LOCATE 10,1 : PRINT BU; "BUSHELS PER ACRE OF "; C$; " EQUALS"; T; "TONNES PER HECTARE"
2770 LOCATE 12,30:PRINT "OR  ";T1;"TONNES PER ACRE"
2780 GOSUB 11250
2790 GOTO 2440
2800 CLS : LOCATE 10,10 : INPUT "ENTER THE NUMBER OF TONNES"; T
2810 BU = T / P
2820 B1 = T * C
2830 CLS : LOCATE 10,1 : PRINT T; "TONNES PER HECTARE OF "; C$; " EQUALS"; BU; "BUSHELS PER ACRE"
2840 LOCATE 12,30:PRINT "OR  ";B1;"BUSHELS PER HECTARE"
2850 GOSUB 11250
2860 GOTO 2440
2870 CLS : LOCATE 10,10 : INPUT "ENTER THE NUMBER OF BUSHELS"; BU
2880 T = BU / C
2890 CLS : LOCATE 10,1 : PRINT BU; "BUSHELS OF "; C$; " EQUALS"; T; "TONNES"
2900 GOSUB 11250
2910 GOTO 2440
2920 CLS : LOCATE 10,10 : INPUT "ENTER THE NUMBER OF TONNES"; T
2930 BU = T * C
2940 CLS : LOCATE 10,1 : PRINT T; "TONNES OF "; C$; " EQUALS"; BU; "BUSHELS"
2950 GOSUB 11250
2960 GOTO 2440
2970 '
2980 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR CROP YIELDS
2990 '
3000 CLS
3010 LOCATE 2,31 : PRINT "SYMBOL CHART"
3020 LOCATE 5,1 : PRINT "QUANTITY"
3030 LOCATE 5,15 : PRINT "UNIT"
3040 LOCATE 5,70 : PRINT "SYMBOL"
3050 LOCATE 8,1 : PRINT "Crop Yields"
3060 LOCATE 8,15 : PRINT "Bushels per Acre"
3070 LOCATE 8,70 : PRINT "bu/ac"
3080 LOCATE 10,15 : PRINT "Kilograms per Hectare"
3090 LOCATE 10,70 : PRINT "kg/ha"
3100 LOCATE 12,15 : PRINT "Tonnes per Hectare"
3110 LOCATE 12,70 : PRINT "t/ha"
3120 LOCATE 14,15 : PRINT "Tonnes"
3130 LOCATE 14,70 : PRINT "t"
3140 LOCATE 16,15 : PRINT "Bushels"
3150 LOCATE 16,70 : PRINT "bu"
3160 GOSUB 11250
3170 CLS:RETURN
3180 '
3190 ' FLUID
3200 ' THIS SECTION IS USED TO SHOW THE FLUID EQUIVALENTS BETWEEN US, IMPERIAL
3210 ' AND METRIC MEASURES.
3220 '
3230 CLS
3240 LOCATE 8,10 : PRINT "CONVERT FROM   1.  IMPERIAL FLUID OUNCES"
3250 LOCATE 9,10 : PRINT "               2.  IMPERIAL PINTS"
3260 LOCATE 10,10 : PRINT "               3.  IMPERIAL QUARTS"
3270 LOCATE 11,10 : PRINT "               4.  IMPERIAL GALLONS"
3280 LOCATE 12,10 : PRINT "               5.  U.S. FLUID OUNCES"
3290 LOCATE 13,10 : PRINT "               6.  U.S. PINTS"
3300 LOCATE 14,10 : PRINT "               7.  U.S. QUARTS"
3310 LOCATE 15,10 : PRINT "               8.  U.S. GALLONS"
3320 LOCATE 16,10 : PRINT "               9.  MILLILITRES"
3330 LOCATE 17,10 : PRINT "              10.  LITRES"
3340 LOCATE 19,10 : PRINT "              11.  SYMBOLS CHART"
3350 LOCATE 20,10 : PRINT "              12.  RETURN TO MAIN MENU"
3360 LOCATE 23,10 : INPUT "               WHAT IS YOUR SELECTION"; PICK
3370 IF PICK = 12 THEN CLS:RETURN
3380 IF PICK < 1 OR PICK > 12 THEN CLS : BEEP : LOCATE 23,5 : PRINT INVALID SELECTION  --  PLEASE REENTER NUMBER FROM 1 - 12" : GOTO 3020
3390 ON PICK GOSUB 3410,3550,3730,3910,4070,4200,4380,4540,4700,4760,4970
3400 GOTO 3230
3410 CLS : LOCATE 10,1 : INPUT "HOW MANY IMPERIAL FLUID OUNCES WOULD YOU LIKE TO CONVERT"; NUM
3420 V = NUM * 0.05
3430 CLS : LOCATE 10,10 : PRINT NUM; "IMPERIAL FLUID OUNCES EQUALS ";
3440 LOCATE 10,40 : PRINT V; "IMPERIAL PINTS"
3450 V = NUM * 1
3460 LOCATE 11,40 : PRINT V; "U.S. FLUID OUNCES"
3470 V = NUM * 0.0625
3480 LOCATE 12,40 : PRINT V; "U.S. PINTS"
3490 V = NUM * 29.573
3500 LOCATE 13,40 : PRINT V; "MILLILITRES"
3510 V = NUM * 0.029573
3520 LOCATE 14,40 : PRINT V; "LITRES"
3530 GOSUB 11250
3540 RETURN
3550 CLS : LOCATE 10,1 : INPUT "HOW MANY IMPERIAL PINTS WOULD YOU LIKE TO CONVERT"; NUM
3560 V = NUM * 20
3570 CLS : LOCATE 10,10 : PRINT NUM; "IMPERIAL PINTS EQUALS ";
3580 LOCATE 11,40 : PRINT V; "FLUID OUNCES"
3590 V = NUM / 2
3600 LOCATE 12,40 : PRINT V; "IMPERIAL QUARTS"
3610 V = NUM / 8
3620 LOCATE 13,40 : PRINT V; "IMPERIAL GALLONS"
3630 V = NUM * 1.25
3640 LOCATE 14,40 : PRINT V; "U.S. PINTS"
3650 V = NUM * 0.625
3660 LOCATE 15,40 : PRINT V; "U.S. QUARTS"
3670 V = NUM * 0.15625
3680 LOCATE 15,40 : PRINT V; "U.S. GALLONS"
3690 V = NUM * 0.59146
3700 LOCATE 16,40 : PRINT V; "LITRES"
3710 GOSUB 11250
3720 RETURN
3730 CLS : LOCATE 10,1 : INPUT "HOW MANY IMPERIAL QUARTS WOULD YOU LIKE TO CONVERT"; NUM
3740 V = NUM * 40
3750 CLS : LOCATE 10,10 : PRINT NUM; "IMPERIAL QUARTS EQUALS ";
3760 LOCATE 10,40 : PRINT V; "FLUID OUNCES"
3770 V = NUM * 2
3780 LOCATE 11,40 : PRINT V; "IMPERIAL PINTS"
3790 V = NUM * 0.25
3800 LOCATE 12,40 : PRINT V; "IMPERIAL GALLONS"
3810 V = NUM * 2.5
3820 LOCATE 13,40 : PRINT V; "U.S. PINTS"
3830 V = NUM * 1.25
3840 LOCATE 14,40 : PRINT V; "U.S. QUARTS"
3850 V = NUM * 0.3125
3860 LOCATE 15,40 : PRINT V; "U.S. GALLONS"
3870 V = NUM * 1.182917
3880 LOCATE 16,40 : PRINT V; "LITRES"
3890 GOSUB 11250
3900 RETURN
3910 CLS : LOCATE 10,1 : INPUT "HOW MANY IMPERIAL GALLONS WOULD YOU LIKE TO CONVERT"; NUM
3920 V = NUM * 8
3930 CLS : LOCATE 10,10 : PRINT NUM; "IMPERIAL GALLONS EQUALS ";
3940 LOCATE 10,40 : PRINT V; "IMPERIAL PINTS"
3950 V = NUM * 4
3960 LOCATE 11,40 : PRINT V; "IMPERIAL QUARTS"
3970 V = NUM * 10
3980 LOCATE 12,40 : PRINT V; "U.S. PINTS"
3990 V = NUM * 5
4000 LOCATE 13,40 : PRINT V; "U.S. QUARTS"
4010 V = NUM * 1.25
4020 LOCATE 14,40 : PRINT V; "U.S. GALLONS"
4030 V = NUM * 4.73167
4040 LOCATE 15,40 : PRINT V; "LITRES"
4050 GOSUB 11250
4060 RETURN
4070 CLS : LOCATE 10,1 : INPUT "HOW MANY U.S. FLUID OUNCES WOULD YOU LIKE TO CONVERT"; NUM
4080 V = NUM * 0.0625
4090 LOCATE 10,40 : PRINT V; "U.S. PINTS"
4100 V = NUM * 1
4110 LOCATE 11,40 : PRINT V; "IMPERIAL FLUID OUNCES"
4120 V = NUM * 0.05
4130 LOCATE 12,40 : PRINT V; "IMPERIAL PINTS"
4140 V = NUM * 29.573
4150 LOCATE 13,40 : PRINT V; "MILLILITRES"
4160 V = NUM * 0.029573
4170 LOCATE 14,40 : PRINT V; "LITRES"
4180 GOSUB 11250
4190 RETURN
4200 CLS : LOCATE 10,1 : INPUT "HOW MANY U.S. PINTS WOULD YOU LIKE TO CONVERT"; NUM
4210 V = NUM * 16
4220 CLS : LOCATE 10,10 : PRINT NUM; "U.S. PINTS EQUALS ";
4230 LOCATE 10,40 : PRINT V; "FLUID OUNCES"
4240 V = NUM / 2
4250 LOCATE 11,40 : PRINT V; "U.S. QUARTS"
4260 V = NUM / 8
4270 LOCATE 12,40 : PRINT V; "U.S. GALLONS"
4280 V = NUM * 0.8
4290 LOCATE 13,40 : PRINT V; "IMPERIAL PINTS"
4300 V = NUM * 0.4
4310 LOCATE 14,40 : PRINT V; "IMPERIAL QUARTS"
4320 V = NUM * 0.1
4330 LOCATE 15,40 : PRINT V; "IMPERIAL GALLONS"
4340 V = NUM * 0.47317
4350 LOCATE 11,40 : PRINT V; "LITRES"
4360 GOSUB 11250
4370 RETURN
4380 CLS : LOCATE 10,1 : INPUT "HOW MANY U.S. QUARTS WOULD YOU LIKE TO CONVERT"; NUM
4390 V = NUM * 2
4400 CLS : LOCATE 10,10 : PRINT NUM; "U.S. QUARTS EQUALS ";
4410 LOCATE 10,40 : PRINT V; "U.S. PINTS"
4420 V = NUM * 0.25
4430 LOCATE 11,40 : PRINT V; "U.S. GALLONS"
4440 V = NUM * 1.6
4450 LOCATE 12,40 : PRINT V; "IMPERIAL PINTS"
4460 V = NUM * 0.8
4470 LOCATE 13,40 : PRINT V; "IMPERIAL QUARTS"
4480 V = NUM * 0.2
4490 LOCATE 14,40 : PRINT V; "IMPERIAL GALLONS"
4500 V = NUM * 0.9463
4510 LOCATE 15,40 : PRINT V; "LITRES"
4520 GOSUB 11250
4530 RETURN
4540 CLS : LOCATE 10,1 : INPUT "HOW MANY U.S. GALLONS WOULD YOU LIKE TO CONVERT"; NUM
4550 V = NUM * 8
4560 CLS : LOCATE 10,10 : PRINT NUM; "U.S. GALLONS EQUALS ";
4570 LOCATE 10,40 : PRINT V; "U.S. PINTS"
4580 V = NUM * 4
4590 LOCATE 11,40 : PRINT V; "U.S. QUARTS"
4600 V = NUM * 6.4
4610 LOCATE 12,40 : PRINT V; "IMPERIAL PINTS"
4620 V = NUM * 3.2
4630 LOCATE 13,40 : PRINT V; "IMPERIAL QUARTS"
4640 V = NUM * 0.8
4650 LOCATE 14,40 : PRINT V; "IMPERIAL GALLONS"
4660 V = NUM * 3.7853
4670 LOCATE 15,40 : PRINT V; "LITRES"
4680 GOSUB 11250
4690 RETURN
4700 CLS : LOCATE 10,1 : INPUT "HOW MANY MILLILITRES WOULD YOU LIKE TO CONVERT"; NUM
4710 V = NUM * 0.0339147
4720 CLS : LOCATE 10,10 : PRINT NUM; "MILLILITRES EQUALS ";
4730 LOCATE 10,40 : PRINT V; "IMPERIAL OR U.S. FLUID OUNCES"
4740 GOSUB 11250
4750 RETURN
4760 CLS : LOCATE 10,1 : INPUT "HOW MANY LITRES WOULD YOU LIKE TO CONVERT TO PINTS"; NUM
4770 V = NUM * 33.8147
4780 CLS : LOCATE 10,10 : PRINT NUM; "LITRES EQUALS ";
4790 LOCATE 10,40 : PRINT V; "FLUID OUNCES"
4800 V = NUM * 0.52835
4810 LOCATE 11,40 : PRINT V; "U.S. PINTS"
4820 V = NUM * 1.0567
4830 LOCATE 12,40 : PRINT V; "U.S. QUARTS"
4840 V = NUM * 0.2642
4850 LOCATE 13,40 : PRINT V; "U.S. GALLONS"
4860 V = NUM * 0.42268
4870 LOCATE 14,40 : PRINT V; "IMPERIAL PINTS"
4880 V = NUM * 0.84536
4890 LOCATE 15,40 : PRINT V; "IMPERIAL QUARTS"
4900 V = NUM * 0.21136
4910 LOCATE 16,40 : PRINT V; "IMPERIAL GALLONS"
4920 GOSUB 11250
4930 RETURN
4940 '
4950 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR FLUID EQUIVALENTS.
4960 '
4970 CLS
4980 LOCATE 2,31 : PRINT "SYMBOL CHART"
4990 LOCATE 5,1 : PRINT "QUANTITY"
5000 LOCATE 5,35 : PRINT "UNIT"
5010 LOCATE 5,70 : PRINT "SYMBOL"
5020 LOCATE 8,1 : PRINT "Fluid"
5030 LOCATE 9,1 : PRINT "Equivalents"
5040 LOCATE 8,15 : PRINT "Litre"
5050 LOCATE 8,70 : PRINT "l"
5060 LOCATE 9,15 : PRINT "Millilitre"
5070 LOCATE 9,70 : PRINT "ml"
5080 LOCATE 11,15 : PRINT "Gallon"
5090 LOCATE 11,70 : PRINT "gal"
5100 LOCATE 12,15 : PRINT "Quart"
5110 LOCATE 12,70 : PRINT "qt"
5120 LOCATE 13,15 : PRINT "Pint"
5130 LOCATE 13,70 : PRINT "pt"
5140 LOCATE 14,15 : PRINT "Fluid Ounce"
5150 LOCATE 14,70 : PRINT "fl oz"
5160 GOSUB 11250
5170 CLS:RETURN
5180 '
5190 ' LENGTH
5200 ' THIS SECTION IS USED TO CONVERT MEASURES OF LENGTH FROM THE IMPERIAL
5210 ' SYSTEM TO THE METRIC SYSTEM AND VICE VERSA.
5220 '
5230 CLS
5240 LOCATE 4,20 : PRINT "CONVERT FROM"
5250 LOCATE 8,20 : PRINT "1.  MILLIMETRES"
5260 LOCATE 9,20 : PRINT "2.  CENTIMETRES"
5270 LOCATE 10,20 : PRINT "3.  METRES"
5280 LOCATE 11,20 : PRINT "4.  KILOMETRES"
5290 LOCATE 12,20 : PRINT "5.  INCHES"
5300 LOCATE 13,20 : PRINT "6.  FEET"
5310 LOCATE 14,20 : PRINT "7.  YARDS"
5320 LOCATE 15,20 : PRINT "8.  RODS"
5330 LOCATE 16,20 : PRINT "9.  MILES"
5340 LOCATE 18,20 : PRINT "10. SYMBOLS CHART"
5350 LOCATE 19,20 : PRINT "11. RETURN TO MAIN MENU"
5360 LOCATE 21,20 : INPUT "ENTER YOUR SELECTION"; PICK
5370 IF PICK = 10 THEN GOSUB 5540 : GOTO 5230
5380 IF PICK = 11 THEN CLS:RETURN
5390 IF PICK < 1 OR PICK > 9 THEN CLS : BEEP : LOCATE 23,5 : PRINT "INVALID ENTRY  --  PLEASE ENTER A NUMBER BETWEEN 1 AND 11" : GOTO 5230
5400 CLS
5410 LOCATE 10,5 : PRINT "HOW MANY "; TYPE$(PICK); " WOULD YOU LIKE TO CONVERT";
5420 LOCATE 10,60 : INPUT NUM
5430 CLS:LOCATE 10,1
5440 FOR I=1 TO 9
5450 IF I=PICK THEN 5480
5460 ANS = NUM * LENGTH(PICK,I)
5470 PRINT TAB(5)NUM; TYPE$(PICK); " EQUALS"; ANS; TYPE$(I)
5480 NEXT I
5490 GOSUB 11250
5500 GOTO 5230
5510 '
5520 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR LENGTH.
5530 '
5540 CLS
5550 LOCATE 2,31 : PRINT "SYMBOL CHART"
5560 LOCATE 5,1 : PRINT "QUANTITY"
5570 LOCATE 5,15 : PRINT "UNIT"
5580 LOCATE 5,70 : PRINT "SYMBOL"
5590 LOCATE 8,1 : PRINT "Length"
5600 LOCATE 8,15 : PRINT "Millimetre"
5610 LOCATE 8,70 : PRINT "mm"
5620 LOCATE 9,15 : PRINT "Centimetre"
5630 LOCATE 9,70 : PRINT "cm"
5640 LOCATE 10,15 : PRINT "Metre"
5650 LOCATE 10,70 : PRINT "m"
5660 LOCATE 11,15 : PRINT "Kilometre"
5670 LOCATE 11,70 : PRINT "km"
5680 LOCATE 13,15 : PRINT "Inch"
5690 LOCATE 13,70 : PRINT "in."
5700 LOCATE 14,15 : PRINT "Foot"
5710 LOCATE 14,70 : PRINT "ft."
5720 LOCATE 15,15 : PRINT "Yard"
5730 LOCATE 15,70 : PRINT "yd."
5740 LOCATE 16,15 : PRINT "Rod"
5750 LOCATE 16,70 : PRINT "rod"
5760 LOCATE 17,15 : PRINT "Mile"
5770 LOCATE 17,70 : PRINT "mile"
5780 GOSUB 11250
5790 CLS:RETURN
5800 '
5810 ' PPORTION
5820 ' THIS SECTION CONVERTS IMPERIAL PROPORTIONS TO METRIC
5830 ' PROPORTIONS AND VICE VERSA.
5840 '
5850 CLS
5860 LOCATE 5,25 : PRINT "PROPORTION CONVERSION"
5870 LOCATE 9,25 : PRINT "1.   GAL / AC"
5880 LOCATE 10,25 : PRINT "2.   LBS / AC"
5890 LOCATE 11,25 : PRINT "3.   LBS / SQ IN"
5900 LOCATE 12,25 : PRINT "4.   BU / AC"
5910 LOCATE 13,25 : PRINT "5.   L / HA"
5920 LOCATE 14,25 : PRINT "6.   KG / HA"
5930 LOCATE 15,25 : PRINT "7.   KG / SQ CM"
5940 LOCATE 16,25 : PRINT "8.   HL / HA"
5950 LOCATE 18,25 : PRINT "9.   SYMBOLS CHART"
5960 LOCATE 19,25 : PRINT "10.  RETURN TO MAIN MENU"
5970 LOCATE 23,15 : INPUT "ENTER THE CONVERSION YOU WISH TO PERFORM"; PICK
5980 IF PICK < 1 OR PICK > 10 THEN CLS : BEEP : LOCATE 23,5 : PRINT "INVALID ENTRY  --  PLEASE ENTER A NUMBER BETWEEN 1 AND 10" : GOTO 5850
5990 IF PICK=10 THEN CLS:RETURN
6000 ON PICK GOSUB 6020,6110,6200,6270,6360,6450,6540,6610,6730
6010 GOTO 5850
6020 CLS
6030 LOCATE 10,1 : INPUT "HOW MANY GALLONS PER ACRE WOULD YOU LIKE TO CONVERT"; GA
6040 V = GA * 9.35365
6050 CLS : LOCATE 10,10 : PRINT GA; "GALLONS PER ACRE EQUALS ";
6060 LOCATE 10,40 : PRINT V; "LITRES PER HECTARE"
6070 V = GA * 3.7853
6080 LOCATE 11,40 : PRINT V; "LITRES PER ACRE"
6090 GOSUB 11250
6100 RETURN
6110 CLS
6120 LOCATE 10,1 : INPUT "HOW MANY POUNDS PER ACRE WOULD YOU LIKE TO CONVERT"; IA
6130 V = IA * 1.12084
6140 CLS : LOCATE 10,10 : PRINT IA; "POUNDS PER ACRE EQUALS ";
6150 LOCATE 10,40 : PRINT V; "KILOGRAMS PER HECTARE"
6160 V = IA * 0.45359
6170 LOCATE 11,40 : PRINT V; "KILOGRAMS PER ACRE"
6180 GOSUB 11250
6190 RETURN
6200 CLS
6210 LOCATE 10,1 : INPUT "HOW MANY POUNDS PER SQUARE INCH WOULD YOU LIKE TO CONVERT"; LI
6220 V = LI * 0.070302
6230 CLS : LOCATE 10,1 : PRINT LI; "POUNDS PER SQUARE INCH EQUALS ";
6240 LOCATE 10,37 : PRINT V; "KILOGRAMS PER SQUARE CENTIMETRE"
6250 GOSUB 11250
6260 RETURN
6270 CLS
6280 LOCATE 10,5 : INPUT "HOW MANY BUSHELS PER ACRE WOULD YOU LIKE TO CONVERT"; BA
6290 V = BA * 0.870757
6300 CLS : LOCATE 10,10 : PRINT BA; "BUSHELS PER ACRE EQUALS ";
6310 LOCATE 10,40 : PRINT V; "HECTOLITRES PER HECTARE"
6320 V = BA * 35.2384
6330 LOCATE 11,40 : PRINT V; "LITRES PER ACRE"
6340 GOSUB 11250
6350 RETURN
6360 CLS
6370 LOCATE 10,5 : INPUT "HOW MANY LITRES PER HECTARE WOULD YOU LIKE TO CONVERT"; LH
6380 V = LH * 0.10691
6390 CLS : LOCATE 10,5 : PRINT LH; "LITRES PER HECTARE EQUALS ";
6400 LOCATE 10,40 : PRINT V; "GALLONS PER ACRE"
6410 V = LH * 0.404687
6420 LOCATE 11,40 : PRINT V; "LITRES PER ACRE"
6430 GOSUB 11250
6440 RETURN
6450 CLS
6460 LOCATE 10,5 : INPUT "KILOGRAMS PER HECTARE WOULD YOU LIKE TO CONVERT"; KH
6470 V = KH * 0.892188
6480 CLS : LOCATE 10,1 : PRINT KH; "KILOGRAMS PER HECTARE EQUALS ";
6490 LOCATE 10,40 : PRINT V; "POUNDS PER ACRE"
6500 V = KH * 0.404687
6510 LOCATE 11,40 : PRINT V; "KILOGRAMS PER ACRE"
6520 GOSUB 11250
6530 RETURN
6540 CLS
6550 LOCATE 10,5 : INPUT "HOW MANY KILOGRAMS PER SQUARE CENTIMETRE WOULD YOU LIKE TO CONVERT"; KC
6560 V = KC * 14.2243
6570 CLS : LOCATE 10,1 : PRINT KC; "KILOGRAMS PER SQUARE CENTIMETRE EQUALS ";
6580 LOCATE 10,45 : PRINT V; "POUNDS PER SQUARE INCH"
6590 GOSUB 11250
6600 RETURN
6610 CLS
6620 LOCATE 10,5 : INPUT "HOW MANY HECTOLITRES PER HECTARE WOULD YOU LIKE TO CONVERT"; HH
6630 V = HH * 1.14843
6640 CLS : LOCATE 10,1 : PRINT HH; "HECTOLITRES PER HECTARE EQUALS ";
6650 LOCATE 10,40 : PRINT V; "BUSHELS PER ACRE"
6660 V = HH * 40.4687
6670 LOCATE 11,40 : PRINT V; "LITRES PER ACRE"
6680 GOSUB 11250
6690 RETURN
6700 '
6710 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR PROPORTION.
6720 '
6730 CLS
6740 LOCATE 2,31 : PRINT "SYMBOL CHART"
6750 LOCATE 5,1 : PRINT "QUANTITY"
6760 LOCATE 5,35 : PRINT "UNIT"
6770 LOCATE 5,70 : PRINT "SYMBOL"
6780 LOCATE 7,1 : PRINT "Proportion"
6790 LOCATE 7,15 : PRINT "Gallons per Acre"
6800 LOCATE 7,70 : PRINT "gal/ac"
6810 LOCATE 8,15 : PRINT "Pounds per Acre"
6820 LOCATE 8,70 : PRINT "lb/ac"
6830 LOCATE 9,15 : PRINT "Bushels per Acre"
6840 LOCATE 9,70 : PRINT "bu/ac"
6850 LOCATE 10,15 : PRINT "Pounds per Square Inch"
6860 LOCATE 10,70 : PRINT "Ib/sq in"
6870 LOCATE 12,15 : PRINT "Litres per Hectare"
6880 LOCATE 12,70 : PRINT "l/ha"
6890 LOCATE 13,15 : PRINT "Hectolitres per Hectacre"
6900 LOCATE 13,70 : PRINT "hl/ha"
6910 LOCATE 14,15 : PRINT "Kilograms per Hectacre"
6920 LOCATE 14,70 : PRINT "kg/ha"
6930 LOCATE 15,15 : PRINT "Kilograms per Square Centimetre"
6940 LOCATE 15,70 : PRINT "kg/sq cm"
6950 GOSUB 11250
6960 CLS:RETURN
6970 '
6980 ' SPEED
6990 ' THIS SECTION IS SET UP TO CONVERT IMPERIAL SPEED TO METRIC SPEED AND
7000 ' VICE VERSA.
7010 '
7020 CLS
7030 LOCATE 8,20: PRINT "SPEED CONVERSION"
7040 LOCATE 10,20 : PRINT "1.   KM / HOUR"
7050 LOCATE 11,20 : PRINT "2.   M / SEC"
7060 LOCATE 12,20 : PRINT "3.   MILES / HOUR"
7070 LOCATE 13,20 : PRINT "4.   FT / SEC"
7080 LOCATE 15,20 : PRINT "5.   SYMBOLS CHART"
7090 LOCATE 16,20 : PRINT "6.   RETURN TO MAIN MENU"
7100 LOCATE 20,20 : INPUT "ENTER YOUR CHOICE"; PICK
7110 IF PICK = 6 THEN CLS:RETURN
7120 IF PICK < 1 OR PICK > 6 THEN CLS : LOCATE 23,10 : PRINT "INVALID ENTRY  --  PLEASE ENTER 1 OR 6" : GOTO 7100
7130 ON PICK GOSUB 7150,7260,7370,7480,7620
7140 GOTO 7020
7150 CLS
7160 LOCATE 10,5 : INPUT "ENTER THE NUMBER OF KILOMETRES / HOUR THAT YOU WOULD LIKE TO CONVERT"; KM
7170 V = KM * 0.6214
7180 CLS : LOCATE 10,5 : PRINT KM; "KILOMETRES PER HOUR EQUALS ";
7190 LOCATE 10,40 : PRINT V; "MILES PER HOUR"
7200 V = KM * 0.91139
7210 LOCATE 11,40 : PRINT V; "FEET PER SECOND"
7220 V = KM * 0.2777778
7230 LOCATE 12,40 : PRINT V; "METRES PER SECOND"
7240 GOSUB 11250
7250 RETURN
7260 CLS
7270 LOCATE 10,5 : INPUT "ENTER THE NUMBER OF METRES / SECOND THAT YOU WOULD LIKE TO CONVERT"; M
7280 V = M * 2.23693
7290 CLS : LOCATE 10,5 : PRINT M; "METRES PER SECOND EQUALS ";
7300 LOCATE 10,40 : PRINT V; "MILES PER HOUR"
7310 V = M * 3.2808
7320 LOCATE 11,40 : PRINT V; "FEET PER SECOND"
7330 V = M * 3.6
7340 LOCATE 12,40 : PRINT V; "KILOMETRES PER SECOND"
7350 GOSUB 11250
7360 RETURN
7370 CLS
7380 LOCATE 10,5 : INPUT "ENTER THE AMOUNT OF MILES /HOUR THAT YOU WOULD LIKE TO CONVERT"; MI
7390 V = MI * 1.6094
7400 CLS : LOCATE 10,10 : PRINT MI; "MILES PER HOUR EQUALS ";
7410 LOCATE 10,40 : PRINT V; "KILOMETRES PER HOUR"
7420 V = MI * 0.44704
7430 LOCATE 11,40 : PRINT V; "METRES PER SECOND"
7440 V = MI * 1.46667
7450 LOCATE 12,40 : PRINT V; "FEET PER SECOND"
7460 GOSUB 11250
7470 RETURN
7480 CLS
7490 LOCATE 10,5 : INPUT "ENTER THE AMOUNT OF FEET / SECOND THAT YOU WOULD LIKE TO CONVERT"; FT
7500 V = FT * 0.6818182
7510 CLS : LOCATE 10,10 : PRINT FT; "FEET PER SECOND EQUALS ";
7520 LOCATE 10,40 : PRINT V; "MILES PER HOUR"
7530 V = FT * 1.097283
7540 LOCATE 11,40 : PRINT V; "KILOMETRES PER HOUR"
7550 V = FT * 0.30481
7560 LOCATE 12,40 : PRINT V; "METRES PER SECOND"
7570 GOSUB 11250
7580 RETURN
7590 '
7600 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR SPEED.
7610 '
7620 CLS
7630 LOCATE 2,31 : PRINT "SYMBOL CHART"
7640 LOCATE 5,1 : PRINT "QUANTITY"
7650 LOCATE 5,35 : PRINT "UNIT"
7660 LOCATE 5,70 : PRINT "SYMBOL"
7670 LOCATE 8,1 : PRINT "Speed"
7680 LOCATE 8,15 : PRINT "Miles per Hour"
7690 LOCATE 8,70 : PRINT "mph or m/h"
7700 LOCATE 9,15 : PRINT "Feet per Second"
7710 LOCATE 9,70 : PRINT "ft/s"
7720 LOCATE 11,15 : PRINT "Kilometres per Hour"
7730 LOCATE 11,70 : PRINT "km/h"
7740 LOCATE 12,15 : PRINT "Metres per Second"
7750 LOCATE 12,70 : PRINT "m/s"
7760 GOSUB 11250
7770 CLS:RETURN
7780 '
7790 ' TEMP
7800 ' THIS SECTION CONVERTS IMPERIAL TEMPERATURE TO METRIC
7810 ' TEMPERATURE AND VICE VERSA.
7820 '
7830 CLS
7840 LOCATE 8,20 : PRINT "TEMPERATURE CONVERSION"
7850 LOCATE 10,20 : PRINT "1.   CELCIUS"
7860 LOCATE 11,20 : PRINT "2.   FAHRENHEIT"
7870 LOCATE 12,20 : PRINT "3.   KELVIN"
7880 LOCATE 14,20 : PRINT "4.   SYMBOLS CHART"
7890 LOCATE 15,20 : PRINT "5.   RETURN TO MAIN MENU"
7900 LOCATE 18,20 : INPUT "ENTER YOUR CHOICE"; PICK
7910 IF PICK = 5 THEN CLS:RETURN
7920 IF PICK < 1 OR PICK > 5 THEN CLS : LOCATE 23,10 : PRINT "INVALID ENTRY  --  PLEASE ENTER 1 TO 5" : GOTO 7850
7930 ON PICK GOSUB 7950,8060,8170,8300
7940 GOTO 7830
7950 CLS
7960 LOCATE 10,5 : INPUT "ENTER THE AMOUNT OF CELCIUS DEGREES THAT YOU WOULD LIKE TO CONVERT"; C
7970 IF C < -273.16 THEN LOCATE 15,5:PRINT "SORRY DEGREES CELCIUS CANNOT GO BELOW -273.16":GOTO 7950
7980 CLS
7990 V = C*9/5 + 32
8000 CLS : LOCATE 10,10 : PRINT C; "DEGREES CELCIUS EQUALS ";
8010 LOCATE 10,40 : PRINT V; "DEGREES FAHRENHEIT"
8020 V = C + 273.16
8030 LOCATE 11,40 : PRINT V; "DEGREES KELVIN"
8040 GOSUB 11250
8050 RETURN
8060 CLS
8070 LOCATE 10,5 : INPUT "ENTER THE AMOUNT OF FAHRENHEIT DEGREES THAT YOU WOULD LIKE TO CONVERT"; F
8080 IF F < -459.69 THEN LOCATE 15,5:PRINT "SORRY DEGREES FAHRENHEIT CANNOT GO BELOW -459.69":GOTO 8060
8090 CLS
8100 V = (F - 32) * 5/9
8110 CLS : LOCATE 10,10 : PRINT F; "DEGREES FAHRENHEIT EQUALS ";
8120 LOCATE 10,40 : PRINT V; "DEGREES CELCIUS"
8130 V = ((F - 32) * 5/9) + 273.16
8140 LOCATE 11,40 : PRINT V; "DEGREES KELVIN"
8150 GOSUB 11250
8160 RETURN
8170 CLS
8180 LOCATE 10,5 : INPUT "ENTER THE AMOUNT OF KELVIN DEGREES THAT YOU WOULD LIKE TO CONVERT"; K
8190 IF K < 0 THEN LOCATE 15,5:PRINT "SORRY DEGREES KELVIN CANNOT GO BELOW ZERO":GOTO 8170
8200 V = (K-273.16) / 5 * 9 + 32
8210 CLS : LOCATE 10,10 : PRINT K; "DEGREES KELVIN EQUALS ";
8220 LOCATE 10,40 : PRINT V; "DEGREES FAHRENHEIT"
8230 V = K - 273.16
8240 LOCATE 11,40 : PRINT V; "DEGREES CELCIUS"
8250 GOSUB 11250
8260 RETURN
8270 '
8280 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR TEMPERATURE.
8290 '
8300 CLS
8310 LOCATE 2,31 : PRINT "SYMBOL CHART"
8320 LOCATE 5,1 : PRINT "QUANTITY"
8330 LOCATE 5,35 : PRINT "UNIT"
8340 LOCATE 5,70 : PRINT "SYMBOL"
8350 LOCATE 8,1 : PRINT "Temperature"
8360 LOCATE 8,15 : PRINT "Degrees Fahrenheit"
8370 LOCATE 8,70 : PRINT "F"
8380 LOCATE 10,15 : PRINT "Degrees Celcius"
8390 LOCATE 10,70 : PRINT "C"
8400 LOCATE 12,15 : PRINT "Degrees Kelvin"
8410 LOCATE 12,70 : PRINT "K"
8420 GOSUB 11250
8430 CLS:RETURN
8440 '
8450 ' VOLUME
8460 ' THIS SECTION CONVERTS IMPERIAL VOLUME TO METRIC VOLUME AND
8470 ' VICE VERSA.
8480 '
8490 CLS
8500 LOCATE 5,25 : PRINT "DRY VOLUME CONVERSION"
8510 LOCATE 9,25 : PRINT "1.   CU IN"
8520 LOCATE 10,25 : PRINT "2.   CU FT"
8530 LOCATE 11,25 : PRINT "3.   CU YD"
8540 LOCATE 12,25 : PRINT "4.   BUSHELS"
8550 LOCATE 13,25 : PRINT "5.   CORDS"
8560 LOCATE 14,25 : PRINT "6.   CU CM"
8570 LOCATE 15,25 : PRINT "7.   CU M"
8580 LOCATE 16,25 : PRINT "8.   LITRES"
8590 LOCATE 18,25 : PRINT "9.   SYMBOLS CHART"
8600 LOCATE 19,25 : PRINT "10.  RETURN TO MAIN MENU"
8610 LOCATE 23,15 : INPUT "ENTER THE CONVERSION YOU WISH TO PERFORM"; PICK
8620 IF PICK < 1 OR PICK > 10 THEN CLS : BEEP : LOCATE 23,5 : PRINT "INVALID ENTRY  --  PLEASE ENTER A NUMBER BETWEEN 1 AND 10" : GOTO 8490
8630 IF PICK = 10 THEN CLS:RETURN
8640 ON PICK GOSUB 8660,8750,8880,8970,9040,9150,9240,9350,9450
8650 GOTO 8490
8660 CLS
8670 LOCATE 10,1 : INPUT "HOW MANY CUBIC INCHES WOULD YOU LIKE TO CONVERT"; CI
8680 V = CI * 16.3872
8690 CLS : LOCATE 10,10 : PRINT CI; "CUBIC INCHES EQUALS";
8700 LOCATE 10,40 : PRINT V; "CUBIC CENTIMETRES"
8710 V = CI * 0.000578704
8720 LOCATE 11,40 : PRINT V; "CUBIC FEET"
8730 GOSUB 11250
8740 RETURN
8750 CLS
8760 LOCATE 10,1 : INPUT "HOW MANY CUBIC FEET WOULD YOU LIKE TO CONVERT"; FT
8770 V = FT * 0.0283
8780 CLS : LOCATE 10,10 : PRINT FT; "CUBIC FEET EQUALS";
8790 LOCATE 10,40 : PRINT V; "CUBIC METRES"
8800 V = FT * 28317.08
8810 LOCATE 11,40 : PRINT V; "CUBIC CENTIMETRES"
8820 V = FT * 1728
8830 LOCATE 12,40 : PRINT V; "CUBIC INCHES"
8840 V = FT * 0.037037
8850 LOCATE 13,40 : PRINT V; "CUBIC YARDS"
8860 GOSUB 11250
8870 RETURN
8880 CLS
8890 LOCATE 10,1 : INPUT "HOW MANY CUBIC YARDS WOULD YOU LIKE TO CONVERT"; YD
8900 V = YD * 0.7646
8910 CLS : LOCATE 10,10 : PRINT YD; "CUBIC YARDS EQUALS";
8920 LOCATE 10,40 : PRINT V; "CUBIC METRES"
8930 V = YD * 27
8940 LOCATE 11,40 : PRINT V; "CUBIC FEET"
8950 GOSUB 11250
8960 RETURN
8970 CLS
8980 LOCATE 10,5 : INPUT "HOW MANY BUSHELS WOULD YOU LIKE TO CONVERT"; BU
8990 V = BU * 35.2384
9000 CLS : LOCATE 10,10 : PRINT BU; "BUSHELS EQUALS";
9010 LOCATE 10,40 : PRINT V; "LITRES"
9020 GOSUB 11250
9030 RETURN
9040 CLS
9050 LOCATE 10,1 : INPUT "HOW MANY CORDS WOULD YOU LIKE TO CONVERT"; CRD
9060 V = CRD * 3.6247
9070 CLS : LOCATE 10,10 : PRINT CRD; "CORDS EQUALS";
9080 LOCATE 10,40 : PRINT V; "CUBIC METRES"
9090 V = CRD * 128
9100 LOCATE 11,40 : PRINT V; "CUBIC FEET"
9110 V = CRD * 4.74074
9120 LOCATE 12,40 : PRINT V; "CUBIC YARDS"
9130 GOSUB 11250
9140 RETURN
9150 CLS
9160 LOCATE 10,5 : INPUT "HOW MANY CUBIC CENTIMETRES WOULD YOU LIKE TO CONVERT"; CC
9170 V = CC * 0.061
9180 CLS : LOCATE 10,10 : PRINT CC; "CUBIC CENTIMETRES EQUALS";
9190 LOCATE 10,40 : PRINT V; "CUBIC INCHES"
9200 V = CC * 0.00003531
9210 LOCATE 11,40 : PRINT V; "CUBIC FEET"
9220 GOSUB 11250
9230 RETURN
9240 CLS
9250 LOCATE 10,5 : INPUT "HOW MANY CUBIC METRES WOULD YOU LIKE TO CONVERT"; CM
9260 V = CM * 35.314
9270 CLS : LOCATE 10,10 : PRINT FT; "CUBIC METRES EQUALS";
9280 LOCATE 10,40 : PRINT V; "CUBIC FEET"
9290 V = CM * 1.3079
9300 LOCATE 11,40 : PRINT V; "CUBIC YARDS"
9310 V = CM * 0.275885
9320 LOCATE 12,40 : PRINT V; "CORDS"
9330 GOSUB 11250
9340 RETURN
9350 CLS
9360 LOCATE 10,5 : INPUT "HOW MANY LITRES WOULD YOU LIKE TO CONVERT"; L
9370 V = L * 0.0283781
9380 CLS : LOCATE 10,10 : PRINT BU; "LITRES EQUALS";
9390 LOCATE 10,40 : PRINT V; "BUSHELS"
9400 GOSUB 11250
9410 RETURN
9420 '
9430 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR VOLUME.
9440 '
9450 CLS
9460 LOCATE 2,31 : PRINT "SYMBOL CHART"
9470 LOCATE 5,1 : PRINT "QUANTITY"
9480 LOCATE 5,35 : PRINT "UNIT"
9490 LOCATE 5,70 : PRINT "SYMBOL"
9500 LOCATE 7,1 : PRINT "Volume"
9510 LOCATE 7,15 : PRINT "Cubic Centimetres"
9520 LOCATE 7,70 : PRINT "cc"
9530 LOCATE 8,15 : PRINT "Cubic Metres"
9540 LOCATE 8,70 : PRINT "cu m"
9550 LOCATE 9,15 : PRINT "Litres"
9560 LOCATE 9,70 : PRINT "l"
9570 LOCATE 11,15 : PRINT "Cubic Inches"
9580 LOCATE 11,70 : PRINT "cu in"
9590 LOCATE 12,15 : PRINT "Cubic Feet"
9600 LOCATE 12,70 : PRINT "cu ft"
9610 LOCATE 13,15 : PRINT "Cubic Yards"
9620 LOCATE 13,70 : PRINT "cu yd"
9630 LOCATE 14,15 : PRINT "Bushels"
9640 LOCATE 14,70 : PRINT "bu"
9650 LOCATE 15,15 : PRINT "Cord"
9660 LOCATE 15,70 : PRINT "crd"
9670 GOSUB 11250
9680 CLS:RETURN
9690 '
9700 ' WEIGHT
9710 ' THIS SECTION CONVERTS IMPERIAL WEIGHT TO METRIC WEIGHT AND
9720 ' VICE VERSA.
9730 '
9740 CLS
9750 LOCATE 5,29 : PRINT "WEIGHT CONVERSION"
9760 LOCATE 9,25 : PRINT "1.   OUNCES"
9770 LOCATE 10,25 : PRINT "2.   POUNDS"
9780 LOCATE 11,25 : PRINT "3.   SH TON"
9790 LOCATE 12,25 : PRINT "4.   LG TON"
9800 LOCATE 13,25 : PRINT "5.   HUNDRED WEIGHT"
9810 LOCATE 14,25 : PRINT "6.   GRAMS"
9820 LOCATE 15,25 : PRINT "7.   KILOGRAMS"
9830 LOCATE 16,25 : PRINT "8.   TONNES"
9840 LOCATE 18,25 : PRINT "9.   SYMBOLS CHART"
9850 LOCATE 19,25 : PRINT "10.  RETURN TO MAIN MENU"
9860 LOCATE 23,15 : INPUT "ENTER THE CONVERSION YOU WISH TO PERFORM"; PICK
9870 IF PICK < 1 OR PICK > 10 THEN CLS : LOCATE 23,5 : PRINT "INVALID ENTRY  --  PLEASE ENTER A NUMBER BETWEEN 1 AND 10" : GOTO 9740
9880 IF PICK = 10 THEN CLS:RETURN
9890 ON PICK GOSUB 9910,10020,10150,10280,10410,10560,10650,10820,11000
9900 GOTO 9740
9910 CLS
9920 LOCATE 10,5 : INPUT "HOW MANY OUNCES WOULD YOU LIKE TO CONVERT"; OZ
9930 V = OZ * 28.349
9940 CLS : LOCATE 10,10 : PRINT OZ; "OUNCES EQUALS";
9950 LOCATE 10,40 : PRINT V; "GRAMS"
9960 V = OZ * 0.028349
9970 LOCATE 11,40 : PRINT V; "KILOGRAMS"
9980 V = OZ * 0.0625
9990 LOCATE 12,40 : PRINT V; "POUNDS"
10000 GOSUB 11250
10010 RETURN
10020 CLS
10030 LOCATE 10,5 : INPUT "HOW MANY POUNDS WOULD YOU LIKE TO CONVERT"; LB
10040 V = LB * 453.592
10050 CLS : LOCATE 10,10 : PRINT LB; "POUNDS EQUALS";
10060 LOCATE 10,40 : PRINT V; "GRAMS"
10070 V = LB * 0.45351
10080 LOCATE 11,40 : PRINT V; "KILOGRAMS"
10090 V = LB * 16
10100 LOCATE 12,40 : PRINT V; "OUNCES"
10110 V = LB * 0.01
10120 LOCATE 13,40 : PRINT V; "HUNDRED WEIGHT"
10130 GOSUB 11250
10140 RETURN
10150 CLS
10160 LOCATE 10,5 : INPUT "HOW MANY SHORT TONS WOULD YOU LIKE TO CONVERT"; ST
10170 V= ST * 0.907194
10180 CLS : LOCATE 10,10 : PRINT ST; "SHORT TONS EQUALS";
10190 LOCATE 10,40 : PRINT V; "TONNES"
10200 V= ST * 907.194
10210 LOCATE 11,40 : PRINT V; "KILOGRAMS"
10220 V= ST * 0.892857142#
10230 LOCATE 12,40 : PRINT V; "LONG TONS"
10240 V= ST * 0.05
10250 LOCATE 13,40 : PRINT V; "HUNDRED WEIGHT"
10260 GOSUB 11250
10270 RETURN
10280 CLS
10290 LOCATE 10,5 : INPUT "HOW MANY LONG TONS WOULD YOU LIKE TO CONVERT"; LT
10300 V = LT * 1.0160573#
10310 CLS : LOCATE 10,10 : PRINT LT; "LONG TONS EQUALS";
10320 LOCATE 10,40 : PRINT V; "TONNES"
10330 V = LT * 1016.0573#
10340 LOCATE 11,40 : PRINT V; "KILOGRAMS"
10350 V = LT * 1.12
10360 LOCATE 12,40 : PRINT V; "SHORT TONS"
10370 V = LT * 0.056
10380 LOCATE 13,40 : PRINT V; "HUNDRED WEIGHT"
10390 GOSUB 11250
10400 RETURN
10410 CLS
10420 LOCATE 10,5 : INPUT "HOW MANY HUNDRED WEIGHT WOULD YOU LIKE TO CONVERT"; HW
10430 V = HW * 45.359
10440 CLS : LOCATE 10,10 : PRINT HW; "HUNDRED WEIGHT EQUALS";
10450 LOCATE 10,40 : PRINT V; "KILOGRAMS"
10460 V = HW * 0.045359
10470 LOCATE 11,40 : PRINT V; "TONNES"
10480 V = HW * 100
10490 LOCATE 12,40 : PRINT V; "POUNDS"
10500 V = HW * 0.05
10510 LOCATE 13,40 : PRINT V; "SHORT TONS"
10520 V = HW * 0.044642857#
10530 LOCATE 14,40 : PRINT V; "LONG TONS"
10540 GOSUB 11250
10550 RETURN
10560 CLS
10570 LOCATE 10,5 : INPUT "HOW MANY GRAMS WOULD YOU LIKE TO CONVERT"; G
10580 V = G *0.035273
10590 CLS : LOCATE 10,10 : PRINT G; "GRAMS EQUALS";
10600 LOCATE 10,40 : PRINT V; "OUNCES"
10610 V = G * 0.0022046
10620 LOCATE 11,40 : PRINT V; "POUNDS"
10630 GOSUB 11250
10640 RETURN
10650 CLS
10660 LOCATE 10,5 : INPUT "HOW MANY KILOGRAMS WOULD YOU LIKE TO CONVERT"; KG
10670 V = KG * 2.2046
10680 CLS : LOCATE 10,10 : PRINT KG; "KILOGRAMS EQUALS";
10690 LOCATE 10,40 : PRINT V; "POUNDS"
10700 V = KG * 0.022046
10710 LOCATE 11,40 : PRINT V; "HUNDRED WEIGHT"
10720 V = KG * 0.0011023
10730 LOCATE 12,40 : PRINT V; "SHORT TONS"
10740 V = KG * 0.000984196
10750 LOCATE 13,40 : PRINT V; "LONG TONS"
10760 V = KG * 1000
10770 LOCATE 14,40 : PRINT V; "GRAMS"
10780 V = KG / 1000
10790 LOCATE 15,40 : PRINT V; "TONNES"
10800 GOSUB 11250
10810 RETURN
10820 CLS
10830 LOCATE 10,5 : INPUT "HOW MANY TONNES WOULD YOU LIKE TO CONVERT"; TT
10840 V = TT * 1.1023
10850 CLS : LOCATE 10,10 : PRINT TT; "TONNES EQUALS";
10860 LOCATE 10,40 : PRINT V; "SHORT TONS"
10870 V = TT * 0.984196
10880 LOCATE 11,40 : PRINT V; "LONG TONS"
10890 V = TT * 22.046
10900 LOCATE 12,40 : PRINT V; "HUNDRED WEIGHT"
10910 V = TT * 2204.6
10920 LOCATE 13,40 : PRINT V; "POUNDS"
10930 V = TT * 1000
10940 LOCATE 14,40 : PRINT V; "KILOGRAMS"
10950 GOSUB 11250
10960 RETURN
10970 '
10980 ' THIS SECTION LISTS THE ABBREVIATIONS USED FOR WEIGHT.
10990 '
11000 CLS
11010 LOCATE 2,31 : PRINT "SYMBOL CHART"
11020 LOCATE 5,1 : PRINT "QUANTITY"
11030 LOCATE 5,35 : PRINT "UNIT"
11040 LOCATE 5,70 : PRINT "SYMBOL"
11050 LOCATE 7,1 : PRINT "Weight"
11060 LOCATE 7,15 : PRINT "Ounces"
11070 LOCATE 7,70 : PRINT "oz"
11080 LOCATE 8,15 : PRINT "Pounds"
11090 LOCATE 8,70 : PRINT "lb"
11100 LOCATE 9,15 : PRINT "Ton  --  Short Ton  --  2000 Lbs"
11110 LOCATE 9,70 : PRINT "st"
11120 LOCATE 10,15 : PRINT "     --  Long Ton  --  2240 Lbs"
11130 LOCATE 10,70 : PRINT "lt"
11140 LOCATE 11,15 : PRINT "Hundred Weight"
11150 LOCATE 11,70 : PRINT "hw"
11160 LOCATE 13,15 : PRINT "Grams"
11170 LOCATE 13,70 : PRINT "g"
11180 LOCATE 14,15 : PRINT "Kilograms"
11190 LOCATE 14,70 : PRINT "kg"
11200 LOCATE 15,15 : PRINT "Tonne"
11210 LOCATE 15,70 : PRINT "t"
11220 GOSUB 11250
11230 CLS:RETURN
11240 REM SUBROUTINE TO CONTINUE
11250 LOCATE 25,29:PRINT "HIT ANY KEY TO CONTINUE";
11260 X$=INKEY$:IF X$="" THEN 11260
11270 CLS:RETURN
11280 REM SUBROUTINE TO LIST KNOWN COMMODITIES
11290 CLS
11300 LOCATE 2,34:PRINT "COMMODITY LIST"
11310 LOCATE 5,1:PRINT "CROP"
11320 LOCATE 5,35:PRINT "BUSHEL WEIGHT"
11330 LOCATE 5,65:PRINT "BUSHELS / TONNE"
11340 LOCATE 7,1
11350 PRINT TAB(3)"SOYBEANS";TAB(40)"60";TAB(70)INT((2204.6225#/60)*10+0.5)/10
11360 PRINT TAB(3)"WHEAT";TAB(40)"60";TAB(70)INT((2204.6225#/60)*10+0.5)/10
11370 PRINT TAB(3)"POTATOES";TAB(40)"60";TAB(70)INT((2204.6225#/60)*10+0.5)/10
11380 PRINT TAB(3)"PEAS";TAB(40)"60";TAB(70)INT((2204.6225#/60)*10+0.5)/10
11390 PRINT TAB(3)"CORN";TAB(40)"56";TAB(70)INT((2204.6225#/56)*10+0.5)/10
11400 PRINT TAB(3)"SORGHUM";TAB(40)"56";TAB(70)INT((2204.6225#/56)*10+0.5)/10
11410 PRINT TAB(3)"RYE";TAB(40)"56";TAB(70)INT((2204.6225#/56)*10+0.5)/10
11420 PRINT TAB(3)"FLAX";TAB(40)"56";TAB(70)INT((2204.6225#/56)*10+0.5)/10
11430 PRINT TAB(3)"RAPESEED";TAB(40)"50";TAB(70)INT((2204.6225#/50)*10+0.5)/10
11440 PRINT TAB(3)"CANOLA";TAB(40)"50";TAB(70)INT((2204.6225#/50)*10+0.5)/10
11450 PRINT TAB(3)"MUSTARD SEED";TAB(40)"50";TAB(70)INT((2204.6225#/50)*10+0.5)/10
11460 PRINT TAB(3)"GRAIN BARLEY";TAB(40)"48";TAB(70)INT((2204.6225#/48)*10+0.5)/10
11470 PRINT TAB(3)"BUCKWHEAT";TAB(40)"48";TAB(70)INT((2204.6225#/48)*10+0.5)/10
11480 PRINT TAB(3)"MALTING BARLEY";TAB(40)"34";TAB(70)INT((2204.6225#/34)*10+0.5)/10
11490 PRINT TAB(3)"OATS";TAB(40)"34";TAB(70)INT((2204.6225#/34)*10+0.5)/10
11500 PRINT TAB(3)"SUNFLOWER SEED";TAB(40)"24";TAB(70)INT((2204.6225#/24)*10+0.5)/10
11510 GOSUB 11250
11520 RETURN
```

## QUIZ.BAS

```bas
10 REM QUIZ PROGRAM - DO YOU NEED A COMPUTER?
20 REM TYPE "CTRL Q" AT THE OPENING MENU TO PRODUCE A SUMMARY AND QUIT
30 REM TYPE "CTRL I" AT THE OPENING MENU TO LIST IFORMATION ON SCORING CRITERIA
40 REM COPYRIGHT 1985, RANDOLPH W. ROSS
50 REM COPYWRITE 1985, RIDGETOWN COLLEGE OF AGRICULTURAL TECHNOLOGY
60 REM PERMISSION TO COPY AND DISTRIBUTE AS PUBLIC DOMAIN SOFTWARE GRANTED
70 REM                        PROVIDED THAT
80 REM THIS NOTICE IS NOT REMOVED AND;
90 REM NO FEE, OTHER THAN A SMALL COPYING FEE, IS CHARGED.
100 ON ERROR GOTO 7400
110 CLS:KEY OFF
120 PRIN = 0
130 REM DEFINE RANGE PARAMETERS
140 ONE=50:TWO=35:THREE=25:FOUR=15
150 REM INITIAL SCREEN
160 LOCATE 5,10:PRINT "DO YOU WISH TO USE THE PRINTER?"
170 LOCATE 7,10:PRINT "ENTER - 0 FOR NO"
180 LOCATE 9,10:PRINT "      - 1 FOR YES"
190 LOCATE 11,10:PRINT "      - 2 FOR AT THE OPTION OF THE USER"
200 LOCATE 20,15:PRINT "ENTER 0, 1 or 2";
210 X$=INKEY$:IF X$="" THEN 210
220 PRIN = VAL(X$)
230 PRINT " ";PRIN:FOR TIME = 1 TO 150:NEXT TIME
240 IF PRIN < 0 OR PRIN > 2 THEN CLS:GOTO 160
250 REM OPERATOR'S INFORMATION
260 CLS:LOCATE 5,30
270 PRINT "OPERATOR'S INFORMATION":LOCATE 6,30
280 PRINT "**********************":LOCATE 8,20
290 PRINT "-  AT THE OPENING SCREEN:":LOCATE 10,25
300 PRINT "-  TYPE `CTRL I' FOR INFORMATION ON SCORING CRITERIA":LOCATE 11,25
310 PRINT "-  TYPE `CTRL Q' TO QUIT AND GIVE SUMMARY INFORMATION":LOCATE 18,10
320 PRINT "NOTE:  THIS INFORMATION WILL NOT BE DISPLAYED AGAIN"
330 LOCATE 25,5:PRINT "COPYWRITE - RANDOLPH W. ROSS    THIS PROGRAM IS IN THE PUBLIC DOMAIN";:FOR TIME = 1 TO 500:NEXT TIME
340 LOCATE 25,1:PRINT "                                                                        ";
350 GOSUB 6590
360 A=0:B=0:C=0:D=0:E=0:TOTAL=0:NUMBER=0:COMPUTER=0:NEWSLET=0
370 REM COMPUTER ON THIS FARM.
380 OPEN "AVERAGE.DAT" FOR INPUT AS #1
390 INPUT #1, TOTAL,NUMBER,A,B,C,D,E,COMPUTER,NEWSLET
400 CLOSE
410 REM SHIFT LOCK ON AND CURSOR KEYS DISABLED
420 DEF SEG=0:POKE 1047,96:REM ZERO SEGMENT VERSION
430 SCORE=0:TEMP=0:FLAG=0:EMP=0:EMP1 = 0
440 REM CALL SUBROUTINE FOR OPENING SCREEN AND BORDER
450 GOSUB 6360
460 REM CALL SUBROUTINE TO DISPLAY INSTRUCTIONS
470 GOSUB 6680
480 LOCATE 5,1
490 PRINT "ENTER THE NUMBER OF ACRES OF EACH OF THE FOLLOWING CROP TYPES:"
500 PRINT "PUSH ENTER TO SKIP ANY ITEM"
510 PRINT:PRINT "CORN, SOYBEANS AND CEREALS  _______"
520 PRINT:PRINT "VEGETABLES                  _______"
530 PRINT:PRINT "INTENSIVE HORTICULTURE      _______"
540 PRINT:PRINT "GREENHOUSE                  _______"
550 PRINT:PRINT "ORCHARD                     _______"
560 PRINT:PRINT "ROOT CROPS AND OTHER        _______"
570 LOCATE 8,29
580 INPUT VALUE
590 TEMP1 = INT((VALUE+399)/400):IF TEMP1 > 5 THEN TEMP1 = 5
600 TEMP = TEMP + TEMP1
610 LOCATE 10,29
620 INPUT VALUE
630 TEMP1 = INT((VALUE+49)/50):IF TEMP1 > 5 THEN TEMP1 = 5
640 TEMP = TEMP + TEMP1
650 LOCATE 12,29
660 INPUT VALUE
670 TEMP1 = INT((VALUE+9)/10):IF TEMP1 > 5 THEN TEMP1 = 5
680 TEMP = TEMP + TEMP1
690 LOCATE 14,29
700 INPUT VALUE
710 TEMP1 = INT((VALUE+0.9)/1):IF TEMP1 > 5 THEN TEMP1 = 5
720 TEMP = TEMP + TEMP1
730 LOCATE 16,29
740 INPUT VALUE
750 TEMP1 = INT((VALUE+19)/20):IF TEMP1 > 5 THEN TEMP1 = 5
760 TEMP = TEMP + TEMP1
770 LOCATE 18,29
780 INPUT VALUE
790 TEMP1 = INT((VALUE+199)/200):IF TEMP1 > 5 THEN TEMP1 = 5
800 TEMP = TEMP + TEMP1
810 REM CALL SUBROUTINE TO UPDATE PAGE
820 GOSUB 7000
830 IF FLAG = 1 THEN FLAG=0:GOTO 480
840 LOCATE 5,1
850 PRINT "ENTER THE NUMBER OF EACH OF THE FOLLOWING LIVESTOCK TYPES:"
860 PRINT "PUSH ENTER TO SKIP ANY ITEM"
870 PRINT:PRINT "BREEDING LIVESTOCK (DAIRY)  _______"
880 PRINT:PRINT "FEEDER LIVESTOCK            _______"
890 PRINT:PRINT "POULTRY                     _______"
900 LOCATE 8,29
910 INPUT VALUE
920 TEMP1 = INT((VALUE+99)/100):IF TEMP1 > 5 THEN TEMP1 = 5
930 TEMP = TEMP + TEMP1
940 IF TEMP1>0 THEN BR=1
950 LOCATE 10,29
960 INPUT VALUE
970 TEMP1 = INT((VALUE+999)/1000):IF TEMP1 > 5 THEN TEMP1 = 5
980 TEMP = TEMP + TEMP1
990 IF TEMP1>0 THEN FD=1
1000 LOCATE 12,29
1010 INPUT VALUE
1020 TEMP1 = INT((VALUE+1999)/2000):IF TEMP1 > 5 THEN TEMP1 = 5
1030 TEMP = TEMP + TEMP1
1040 IF TEMP1>0 THEN FD=1
1050 REM CALL SUBROUTINE TO UPDATE PAGE
1060 GOSUB 7000
1070 IF FLAG = 1 THEN FLAG=0:GOTO 840
1080 REM GROSS INCOME
1090 CLS:LOCATE 5,1
1100 PRINT "WHAT IS YOUR APPROXIMATE GROSS INCOME?"
1110 LOCATE 7,8
1120 PRINT "A.   UNDER $100,000"
1130 LOCATE 9,8
1140 PRINT "B.   $100,001 - $200,000"
1150 LOCATE 11,8
1160 PRINT "C.   $200,001 - $400,000"
1170 LOCATE 13,8
1180 PRINT "D.   $400,001 - $1,000,000"
1190 LOCATE 15,8
1200 PRINT "E.   OVER $1,000,000"
1210 REM CALL SUBROUTINE TO ACCEPT ANSWER
1220 GOSUB 7080
1230 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: GOTO 1090
1240 IF X$="F" THEN 1220
1250 REM CALL SUBROUTINE TO ALLOCATE POINTS - TYPE 1
1260 GOSUB 7220
1270 REM EMPLOYEES AND PAYROLL
1280 CLS
1290 LOCATE 5,1
1300 PRINT "HOW MANY FULL TIME EMPLOYEES DO YOU HAVE?"
1310 LOCATE 7,8
1320 PRINT "A.   NONE"
1330 LOCATE 9,8
1340 PRINT "B.   1-5"
1350 LOCATE 11,8
1360 PRINT "C.   6-10"
1370 LOCATE 13,8
1380 PRINT "D.   11-25"
1390 LOCATE 15,8
1400 PRINT "E.   26-75"
1410 LOCATE 17,8
1420 PRINT "F.   OVER 75"
1430 REM CALL SUBROUTINE TO ACCEPT ANSWER
1440 GOSUB 7080
1450 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:GOTO 1090
1460 IF X$="C" OR X$="D" OR X$="E" OR X$="F" THEN EMP = 1
1470 IF X$="B" THEN EMP1 = 1
1480 REM CALL SUBROUTINE TO ALLOCATE POINTS - TYPE 2
1490 GOSUB 7310
1500 TEMP = TEMP * 2
1510 IF X$="A" THEN 1740
1520 LOCATE 5,45
1530 PRINT "HOW OFTEN ARE THEY USUALLY PAID?"
1540 LOCATE 9,50
1550 PRINT "A.   WEEKLY"
1560 LOCATE 11,50
1570 PRINT "B.   BI-WEEKLY"
1580 LOCATE 13,50
1590 PRINT "C.   MONTHLY"
1600 LOCATE 15,50
1610 PRINT "D.   OTHER"
1620 LOCATE 22,55
1630 PRINT "CHOOSE ONE";
1640 X$ = INKEY$: IF X$ = "" THEN 1640
1650 IF X$ = CHR$(27) THEN 420
1660 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: TEMP=0:GOTO 1280
1670 PRINT "  ";X$:FOR TIME = 1 TO 150:NEXT TIME
1680 IF X$<>"A" AND X$<>"B" AND X$<>"C" AND X$<>"D" THEN 1620
1690 IF X$="A" THEN TEMP=TEMP*2
1700 IF X$="C" THEN TEMP=TEMP/2
1710 IF X$="D" THEN TEMP=TEMP*2
1720 TEMP=INT(TEMP+0.9):IF TEMP>15 THEN TEMP=15
1730 SCORE=SCORE+TEMP
1740 CLS
1750 LOCATE 5,1
1760 PRINT "HOW MANY PART TIME EMPLOYEES DO YOU HAVE?"
1770 LOCATE 7,8
1780 PRINT "A.   NONE"
1790 LOCATE 9,8
1800 PRINT "B.   1-5"
1810 LOCATE 11,8
1820 PRINT "C.   6-10"
1830 LOCATE 13,8
1840 PRINT "D.   11-25"
1850 LOCATE 15,8
1860 PRINT "E.   26-75"
1870 LOCATE 17,8
1880 PRINT "F.   OVER 75"
1890 REM CALL SUBROUTINE TO ACCEPT ANSWER
1900 GOSUB 7080
1910 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:GOTO 1280
1920 IF X$="D" OR X$="E" OR X$="F" THEN EMP = 1
1930 IF X$="C" AND EMP1=1 THEN EMP = 1
1940 REM CALL SUBROUTINE TO ALLOCATE POINTS - TYPE 2
1950 GOSUB 7310
1960 TEMP = TEMP * 2
1970 SCORE = SCORE+TEMP
1980 CLS
1990 LOCATE 5,1
2000 PRINT "WHAT TYPE OF ACCOUNTING SYSTEM DO YOU USE?"
2010 PRINT:PRINT "A.   INFORMAL CASHBOOK, SHOEBOX OR NONE"
2020 PRINT:PRINT "B.   FARM CHECK, CANCHECK, ETC."
2030 PRINT:PRINT "C.   FORMAL CASHBOOK (ONTARIO FARM RECORD BOOK)"
2040 PRINT:PRINT "D.   CANFARM OR SIMILAR SYSTEM"
2050 PRINT:PRINT "E.   DOUBLE ENTRY SYSTEM"
2060 REM CALL SUBROUTINE TO ACCEPT ANSWER
2070 GOSUB 7080
2080 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:GOTO 1740
2090 REM CALL SUBROUTINE TO ALLOCATE POINTS - TYPE 2
2100 GOSUB 7310
2110 IF X$="E" THEN TEMP=TEMP+1
2120 IF X$="F" THEN 2070
2130 SCORE=SCORE+TEMP:CLS
2140 LOCATE 5,1
2150 PRINT "WHAT IS THE ORGANIZATIONAL STRUCTURE OF YOUR FARM?"
2160 PRINT:PRINT "A.   INCORPORATED"
2170 PRINT:PRINT "B.   PARTNERSHIP"
2180 PRINT:PRINT "C.   SINGLE OWNER"
2190 PRINT:PRINT "D.   RENTED LAND ONLY"
2200 REM CALL SUBROUTINE TO ACCEPT ANSWER
2210 GOSUB 7080
2220 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:GOTO 1980
2230 IF X$="F" OR X$="E" THEN 2210
2240 IF X$="A" THEN TEMP = TEMP+5
2250 IF X$="B" THEN TEMP = TEMP+3
2260 IF X$="C" THEN TEMP = TEMP+1
2270 SCORE = SCORE + TEMP
2280 CLS
2290 LOCATE 5,1
2300 PRINT "HOW MANY INVOICES (CHEQUES ETC.) ARE PROCESSED PER MONTH?"
2310 PRINT:PRINT "A.   LESS THAN 50"
2320 PRINT:PRINT "B.   50 - 150"
2330 PRINT:PRINT "C.   151 - 500"
2340 PRINT:PRINT "D.   OVER 500"
2350 PRINT:PRINT "E.   NO IDEA"
2360 REM CALL SUBROUTINE TO ACCEPT ANSWER
2370 GOSUB 7080
2380 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2140
2390 IF X$="F" THEN 2370
2400 IF X$="B" THEN TEMP = TEMP+1
2410 IF X$="C" THEN TEMP = TEMP+3
2420 IF X$="D" THEN TEMP = TEMP+5
2430 IF X$="E" THEN TEMP = TEMP-3
2440 SCORE = SCORE + TEMP
2450 CLS
2460 LOCATE 5,1
2470 PRINT "WHO PROCESSES YOUR INVOICES?"
2480 PRINT:PRINT "A.   SELF OR FAMILY MEMBER"
2490 PRINT:PRINT "B.   FARM MANAGER"
2500 PRINT:PRINT "C.   OFFICE STAFF"
2510 PRINT:PRINT "D.   PROFESSIONAL ACCOUNTING SERVICE"
2520 PRINT:PRINT "E.   OTHER"
2530 REM CALL SUBROUTINE TO ACCEPT ANSWER
2540 GOSUB 7080
2550 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2280
2560 IF X$="F" THEN 2540
2570 IF X$="A" THEN TEMP = TEMP+1
2580 IF X$="B" THEN TEMP = TEMP+1
2590 IF X$="C" THEN TEMP = TEMP+2
2600 IF X$="D" THEN TEMP = TEMP+3
2610 IF X$="E" THEN TEMP = TEMP+1
2620 SCORE = SCORE + TEMP
2630 CLS
2640 LOCATE 5,1
2650 PRINT "HOW MUCH DO YOU SPEND IN ANNUAL ACCOUNTING FEES?"
2660 PRINT:PRINT "A.   LESS THAN $1,000"
2670 PRINT:PRINT "B.   $1,001 - $2,000"
2680 PRINT:PRINT "C.   $2,001 - $5,000"
2690 PRINT:PRINT "D.   OVER $5,000"
2700 REM CALL SUBROUTINE TO ACCEPT ANSWER
2710 GOSUB 7080
2720 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2450
2730 IF X$="F" OR X$="E" THEN 2710
2740 IF X$="A" THEN TEMP = TEMP+1
2750 IF X$="B" THEN TEMP = TEMP+2
2760 IF X$="C" THEN TEMP = TEMP+4
2770 IF X$="D" THEN TEMP = TEMP+5
2780 SCORE = SCORE + TEMP
2790 CLS
2800 LOCATE 5,1
2810 PRINT "WHEN NEW TECHNOLOGY APPEARS THAT MIGHT BE USEFUL ON YOUR FARM,","DO YOU CONDSIDER YOURSELF TO BE:"
2820 PRINT:PRINT "A.   AN INNOVATOR - FIRST IN THE AREA TO GET IT"
2830 PRINT:PRINT "B.   AN EARLY USER"
2840 PRINT:PRINT "C.   ABOUT AVERAGE"
2850 PRINT:PRINT "D.   CAUTIOUS"
2860 PRINT:PRINT "E.   SCEPTICAL"
2870 REM CALL SUBROUTINE TO ACCEPT ANSWER
2880 GOSUB 7080
2890 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2630
2900 IF X$="F" THEN 2880
2910 IF X$="A" THEN TEMP = TEMP+8
2920 IF X$="B" THEN TEMP = TEMP+5
2930 IF X$="C" THEN TEMP = TEMP+2
2940 IF X$="D" THEN TEMP = TEMP-1
2950 IF X$="E" THEN TEMP = TEMP-3
2960 SCORE = SCORE + TEMP
2970 CLS
2980 LOCATE 5,1
2990 PRINT "HOW WOULD YOU RANK YOUR KNOWLEDGE OF COMPUTERS?"
3000 PRINT:PRINT "A.   KNOW NOTHING ABOUT THEM"
3010 PRINT:PRINT "B.   BARE MINIMUM"
3020 PRINT:PRINT "C.   ABOUT AVERAGE"
3030 PRINT:PRINT "D.   ABOVE AVERAGE"
3040 PRINT:PRINT "E.   ALMOST AN EXPERT"
3050 REM CALL SUBROUTINE TO ACCEPT ANSWER
3060 GOSUB 7080
3070 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2790
3080 IF X$="F" THEN 3060
3090 IF X$="B" THEN TEMP = TEMP+1
3100 IF X$="C" THEN TEMP = TEMP+2
3110 IF X$="D" THEN TEMP = TEMP+4
3120 IF X$="E" THEN TEMP = TEMP+5
3130 SCORE = SCORE + TEMP
3140 CLS
3150 LOCATE 5,1
3160 PRINT "HOW WOULD YOU RANK YOUR INTEREST IN COMPUTERS?"
3170 PRINT:PRINT "A.   NOT INTERESTED"
3180 PRINT:PRINT "B.   MINIMAL INTEREST"
3190 PRINT:PRINT "C.   AVERAGE INTEREST LEVEL"
3200 PRINT:PRINT "D.   VERY INTERESTED"
3210 PRINT:PRINT "E.   ALMOST FANATICAL ABOUT THEM"
3220 REM CALL SUBROUTINE TO ACCEPT ANSWER
3230 GOSUB 7080
3240 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 2970
3250 IF X$="F" THEN 3230
3260 IF X$="A" THEN TEMP = TEMP-10
3270 IF X$="B" THEN TEMP = TEMP-1
3280 IF X$="C" THEN TEMP = TEMP+1
3290 IF X$="D" THEN TEMP = TEMP+5
3300 IF X$="E" THEN TEMP = TEMP+8
3310 SCORE = SCORE + TEMP
3320 CLS
3330 LOCATE 5,1
3340 PRINT "DO YOU HAVE ANY INTEREST IN COMMODITY FUTURES OR STOCK MARKETS?"
3350 PRINT:PRINT "A.   BOTH TRADE AND KEEP CHARTS"
3360 PRINT:PRINT "B.   DO NOT TRADE BUT KEEP CHARTS"
3370 PRINT:PRINT "C.   TRADE BUT DO NOT KEEP CHARTS"
3380 PRINT:PRINT "D.   INTERESTED BUT HAVE NOT TRADED OR KEPT CHARTS"
3390 PRINT:PRINT "E.   NOT INTERESTED"
3400 REM CALL SUBROUTINE TO ACCEPT ANSWER
3410 GOSUB 7080
3420 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 3140
3430 IF X$="F" THEN 3410
3440 IF X$="A" THEN TEMP = TEMP+5
3450 IF X$="B" THEN TEMP = TEMP+4
3460 IF X$="C" THEN TEMP = TEMP+2
3470 IF X$="D" THEN TEMP = TEMP+1
3480 SCORE = SCORE + TEMP
3490 CLS
3500 IF BR=0 THEN 3850
3510 LOCATE 5,1
3520 PRINT "DO YOU FORMULATE RATIONS FOR YOUR BREEDING HERD?"
3530 PRINT:PRINT "A.   INDIVIDUALLY, BASED ON PRODUCTION CRITERIA"
3540 PRINT:PRINT "B.   IN GROUPS, ACCORDING TO PRODUCTION CRITERIA"
3550 PRINT:PRINT "C.   SINGLE RATION FOR HERD"
3560 PRINT:PRINT "D.   NO, BUT I WOULD LIKE TO"
3570 PRINT:PRINT "E.   NOT NECESSARY"
3580 REM CALL SUBROUTINE TO ACCEPT ANSWER
3590 GOSUB 7080
3600 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 3320
3610 IF X$="F" THEN 3590
3620 IF X$="A" THEN TEMP = TEMP+5
3630 IF X$="B" THEN TEMP = TEMP+3
3640 IF X$="C" THEN TEMP = TEMP+1
3650 IF X$="D" THEN TEMP = TEMP+1
3660 SCORE = SCORE + TEMP
3670 CLS
3680 LOCATE 5,1
3690 PRINT "DO YOU KEEP DETAILED PRODUCTION RECORDS ON YOUR BREEDING HERD?"
3700 PRINT:PRINT "A.   NO, I CAN KEEP THIS INFORMATION IN MY HEAD"
3710 PRINT:PRINT "B.   ABOUT AVERAGE"
3720 PRINT:PRINT "C.   YES, QUITE DETAILED"
3730 PRINT:PRINT "D.   NO, BUT I WOULD LIKE TO"
3740 PRINT:PRINT "E.   SOME, BUT I WOULD LIKE MORE"
3750 REM CALL SUBROUTINE TO ACCEPT ANSWER
3760 GOSUB 7080
3770 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 3490
3780 IF X$="F" THEN 3760
3790 IF X$="B" THEN TEMP = TEMP+1
3800 IF X$="C" THEN TEMP = TEMP+5
3810 IF X$="D" THEN TEMP = TEMP+2
3820 IF X$="E" THEN TEMP = TEMP+4
3830 SCORE = SCORE + TEMP
3840 CLS
3850 IF FD=0 THEN 4030
3860 LOCATE 5,1
3870 PRINT "DO YOU KEEP DETAILED PRODUCTION RECORDS ON YOUR FEEDERS OR POULTRY?"
3880 PRINT:PRINT "A.   NO, I CAN KEEP THIS INFORMATION IN MY HEAD"
3890 PRINT:PRINT "B.   ABOUT AVERAGE"
3900 PRINT:PRINT "C.   YES, QUITE DETAILED"
3910 PRINT:PRINT "D.   NO, BUT I WOULD LIKE TO"
3920 PRINT:PRINT "E.   SOME, BUT I WOULD LIKE MORE"
3930 REM CALL SUBROUTINE TO ACCEPT ANSWER
3940 GOSUB 7080
3950 IF X$ = "R" AND FLAG = 0 AND BR = 0 THEN FLAG = 1:CLS:GOTO 3320
3960 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:CLS:GOTO 3670
3970 IF X$="F" THEN 3940
3980 IF X$="C" THEN TEMP = TEMP+4
3990 IF X$="D" THEN TEMP = TEMP+1
4000 IF X$="E" THEN TEMP = TEMP+3
4010 SCORE = SCORE + TEMP
4020 CLS
4030 LOCATE 5,1
4040 PRINT "HOW BADLY DO YOU WANT A COMPUTER FOR NON-FARM USES (IE:CHILDREN, HOBBY, ETC.)?"
4050 PRINT:PRINT "A.   VERY MUCH"
4060 PRINT:PRINT "B.   A LITTLE BIT"
4070 PRINT:PRINT "C.   NOT SURE"
4080 PRINT:PRINT "D.   ONLY INTERESTED IN FARM USE"
4090 REM CALL SUBROUTINE TO ACCEPT ANSWER
4100 GOSUB 7080
4110 IF X$ = "R" AND FLAG = 0 AND BR = 0 AND FD = 0 THEN FLAG = 1:CLS:GOTO 3320
4120 IF X$ = "R" AND FLAG = 0 AND FD = 0 AND BR = 1 THEN FLAG = 1:CLS:GOTO 3670
4130 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1:CLS:GOTO 3840
4140 IF X$="F" OR X$="E" THEN 4100
4150 IF X$="A" THEN TEMP = TEMP+7
4160 IF X$="B" THEN TEMP = TEMP+2
4170 IF X$="C" THEN TEMP = TEMP+1
4180 SCORE = SCORE + TEMP
4190 CLS
4200 LOCATE 5,1
4210 PRINT "WHAT IS YOUR CURRENT TAX SITUATION?"
4220 PRINT:PRINT "A.   NEVER HAVE TO PAY INCOME TAX"
4230 PRINT:PRINT "B.   PAY LESS THAN $5000 PER YEAR IN INCOME TAX"
4240 PRINT:PRINT "C.   PAY MORE THAN $5000 PER YEAR IN INCOME TAX"
4250 REM CALL SUBROUTINE TO ACCEPT ANSWER
4260 GOSUB 7080
4270 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 4020
4280 IF X$="F" OR X$="E" OR X$="D" THEN 4260
4290 IF X$="B" THEN TEMP = TEMP+2
4300 IF X$="C" THEN TEMP = TEMP+4
4310 SCORE = SCORE + TEMP
4320 CLS
4330 LOCATE 5,1
4340 PRINT "CAN YOU OR A MEMBER OF YOUR FAMILY TYPE?"
4350 PRINT:PRINT "A.   CANNOT TYPE"
4360 PRINT:PRINT "B.   CAN TOUCH TYPE"
4370 PRINT:PRINT "C.   KNOW WHERE ALL THE KEYS ARE BUT THAT'S ABOUT IT"
4380 PRINT:PRINT "D.   FAIRLY GOOD TWO-FINGERED TYPIST"
4390 PRINT:PRINT "E.   SOMEONE ELSE, WHO CAN TYPE, WILL ENTER MY INFORMATION"
4400 REM CALL SUBROUTINE TO ACCEPT ANSWER
4410 GOSUB 7080
4420 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: CLS:GOTO 4190
4430 IF X$="F" THEN 4410
4440 IF X$="A" THEN TEMP = TEMP-1
4450 IF X$="B" THEN TEMP = TEMP+3
4460 IF X$="C" THEN TEMP = TEMP+1
4470 IF X$="D" THEN TEMP = TEMP+2
4480 IF X$="E" THEN TEMP = TEMP+2
4490 SCORE = SCORE + TEMP
4500 CLS
4510 LOCATE 5,1
4520 COMP=0
4530 PRINT "DO YOU CURRENTLY OWN A MICROCOMPUTER?"
4540 GOSUB 9230
4550 IF X$ = "R" AND FLAG = 0 THEN FLAG = 1: SCORE = SCORE - TEMP:TEMP = 0:CLS:GOTO 4320
4560 IF X$="Y" THEN COMP=1
4570 COMPUTER=COMPUTER+COMP
4580 LOCATE 10,1
4590 PRINT "DO YOU CURRENTLY RECEIVE THE FREE AGRICULTURAL COMPUTER EXTENSION NEWSLETTER?"
4600 GOSUB 9230
4610 IF X$<>"Y" AND X$<>"N" THEN 4580
4620 IF X$="Y" THEN CLS:NEWSLET=NEWSLET+1:GOTO 4960
4630 LOCATE 15,1
4640 PRINT "DO YOU WISH TO RECEIVE IT?"
4650 GOSUB 9230
4660 IF X$<>"Y" AND X$<>"N" THEN 4630
4670 CLS
4680 IF X$="N" THEN 4960
4690 LOCATE 5,1
4700 PRINT "PLEASE ENTER THE FOLLOWING INFORMATION:"
4710 PRINT "PUSH ENTER TO SKIP ANY ITEM"
4720 COMP$=" "
4730 PRINT:PRINT "NAME                    ________________________________"
4740 PRINT:PRINT "ADDRESS (LINE1)         ________________________________"
4750 PRINT:PRINT "ADDRESS (LINE2)         ________________________________"
4760 PRINT:PRINT "CITY AND PROVINCE       ________________________________"
4770 PRINT:PRINT "POSTAL CODE             ________________________________"
4780 IF COMP = 1 THEN PRINT:PRINT "BRAND OF COMPUTER OWNED ________________________________"
4790 LOCATE 8,25
4800 LINE INPUT "?";NAM$
4810 LOCATE 10,25
4820 LINE INPUT "?";ADD1$
4830 LOCATE 12,25
4840 LINE INPUT "?";ADD2$
4850 LOCATE 14,25
4860 LINE INPUT "?";CPRO$
4870 LOCATE 16,25
4880 LINE INPUT "?";PC$
4890 IF COMP=0 THEN 4930
4900 LOCATE 18,25
4910 LINE INPUT "?";COMP$
4920 REM STORE ADDRESS INFORMATION
4930 OPEN "ADDRESS.DAT" FOR APPEND AS #2
4940 PRINT #2, NAM$,ADD1$,ADD2$,CPRO$,PC$,COMP$
4950 CLOSE
4960 CLS:LOCATE 5,15
4970 PRINT "HOW DO YOU RATE?"
4980 LOCATE 7,10
4990 IF SCORE < 0 THEN SCORE = 0
5000 PRINT "YOU HAVE SCORED ";SCORE;" POINTS"
5010 LOCATE 8,10
5020 IF NUMBER = 0 THEN AVE = 0:GOTO 5040
5030 AVE = TOTAL/NUMBER
5040 PRINT "THE AVERAGE SCORE FOR THOSE WHO HAVE USED THIS PROGRAM IS ";AVE
5050 TOTAL = TOTAL + SCORE:NUMBER = NUMBER + 1
5060 LOCATE 12,1
5070 IF SCORE < ONE THEN 5140
5080 PRINT "YOU ARE IN THE GROUP OF FARMERS MOST LIKELY TO BE SUCCESSFUL IN COMPUTERIZING"
5090 PRINT "YOUR FARM BUSINESS.  YOU SHOULD SERIOUSLY CONSIDER THE PURCHASE OF A"
5100 PRINT "MICROCOMPUTER, IF YOU HAVE NOT ALREADY DONE SO.  THE AGRICULTURAL COMPUTER"
5110 PRINT "EXTENSION PROGRAM IS AVAILABLE TO ASSIST YOU."
5120 A=A+1
5130 GOTO 5410
5140 IF SCORE < TWO THEN 5210
5150 PRINT "IT'S DEFINITELY WORTH THINKING ABOUT COMPUTERIZING YOUR FARM BUSINESS."
5160 PRINT "YOU CAN PROBABLY JUSTIFY THE PURCHASE OF A FARM COMPUTER SYSTEM, BUT YOU"
5170 PRINT "MAY WISH TO DISCUSS YOUR INDIVIDUAL CIRCUMSTANCES WITH SOMEONE KNOWLEDGEABLE."
5180 PRINT "THE AGRICULTURAL COMPUTER EXTENSION PROGRAM IS AVAILABLE TO ASSIST YOU."
5190 B=B+1
5200 GOTO 5410
5210 IF SCORE < THREE THEN 5300
5220 PRINT "YOUR SITUATION IS MARGINAL.  YOU MAY FIND IT DIFFICULT TO JUSTIFY A COMPUTER"
5230 PRINT "SOLELY FOR FARM USE.  YOUR COMPUTER PURCHASE DECISION SHOULD BE BASED ON OTHER"
5240 PRINT "FACTORS IN ADDITION TO YOUR FARM ENTERPRISE.  THESE FACTORS INCLUDE OTHER"
5250 PRINT "BUSINESS USES, AS WELL AS EDUCATIONAL AND HOBBY USE.  SHOULD YOU WISH"
5260 PRINT "TO DISCUSS YOUR OPTIONS FURTHER, CALL ON THE AGRICULTURAL COMPUTER EXTENSION"
5270 PRINT "PROGRAM."
5280 C=C+1
5290 GOTO 5410
5300 IF SCORE < FOUR THEN 5380
5310 PRINT "IT IS UNLIKELY THAT YOU CAN JUSTIFY THE PURCHASE OF A FARM COMPUTER SYSTEM AT"
5320 PRINT "THE CURRENT TIME.  IF YOU ARE ENTHUSIASTIC ABOUT COMPUTERS, YOU SHOULD"
5330 PRINT "CONSIDER THE PURCHASE OF A SMALL, LOW COST MICROCOMPUTER.  HOWEVER, IT MAY"
5340 PRINT "BE DIFFICULT TO JUSTIFY A LARGE OUTLAY.  CALL ON THE AGRICULTURAL COMPUTER"
5350 PRINT "EXTENSION PROGRAM IF YOU WISH TO DISCUSS THIS FURTHER."
5360 D=D+1
5370 GOTO 5410
5380 PRINT "UNLESS YOU ARE RICH OR A COMPUTER BUFF, HOLD OFF ON THE PURCHASE OF A COMPUTER."
5390 PRINT "UNTIL THEY BECOME STANDARD EQUIPMENT."
5400 E=E+1
5410 IF EMP = 1 THEN PRINT:PRINT "YOU SHOULD SERIOUSLY CONSIDER THE PURCHASE OF A COMPUTERIZED PAYROLL PACKAGE"
5420 IF PRIN = 0 THEN EMP=0:GOTO 6050
5430 IF PRIN = 1 THEN 5520
5440 LOCATE 22,35
5450 PRINT "DO YOU WISH TO HAVE A COPY OF THIS SUMMARY ON PAPER?"
5460 GOSUB 9230
5470 LOCATE 22,35
5480 PRINT "                                                     "
5490 PRINT "                    "
5500 IF X$="N" THEN EMP=0:GOTO 6050
5510 REM PRINT ROUTINE
5520 LPRINT:LPRINT
5530 LPRINT TAB(22);"AGRICULTURAL COMPUTER EXTENSION PROGRAM"
5540 LPRINT TAB(22);"***************************************"
5550 LPRINT:LPRINT TAB(32);"FARM COMPUTER QUIZ"
5560 LPRINT TAB(32);"******************"
5570 LPRINT:LPRINT
5580 LPRINT TAB(33);"HOW DO YOU RATE?"
5590 LPRINT TAB(33);"****************":LPRINT:LPRINT:LPRINT
5600 LPRINT:LPRINT:LPRINT "YOU HAVE SCORED ";SCORE;" POINTS"
5610 LPRINT:LPRINT "THE AVERAGE SCORE FOR THOSE WHO HAVE USED THIS PROGRAM IS ";AVE
5620 LPRINT:LPRINT:LPRINT
5630 IF SCORE < ONE THEN 5690
5640 LPRINT "YOU ARE IN THE GROUP OF FARMERS MOST LIKELY TO BE SUCCESSFUL IN COMPUTERIZING"
5650 LPRINT "YOUR FARM BUSINESS.  YOU SHOULD SERIOUSLY CONSIDER THE PURCHASE OF A"
5660 LPRINT "MICROCOMPUTER, IF YOU HAVE NOT ALREADY DONE SO.  THE AGRICULTURAL COMPUTER"
5670 LPRINT "EXTENSION PROGRAM IS AVAILABLE TO ASSIST YOU."
5680 GOTO 5920
5690 IF SCORE < TWO THEN 5750
5700 LPRINT "IT'S DEFINITELY WORTH THINKING ABOUT COMPUTERIZING YOUR FARM BUSINESS."
5710 LPRINT "YOU CAN PROBABLY JUSTIFY THE PURCHASE OF A FARM COMPUTER SYSTEM, BUT YOU"
5720 LPRINT "MAY WISH TO DISCUSS YOUR INDIVIDUAL CIRCUMSTANCES WITH SOMEONE KNOWLEDGEABLE."
5730 LPRINT "THE AGRICULTURAL COMPUTER EXTENSION PROGRAM IS AVAILABLE TO ASSIST YOU."
5740 GOTO 5920
5750 IF SCORE < THREE THEN 5830
5760 LPRINT "YOUR SITUATION IS MARGINAL.  YOU MAY FIND IT DIFFICULT TO JUSTIFY A COMPUTER"
5770 LPRINT "SOLELY FOR FARM USE.  YOUR COMPUTER PURCHASE DECISION SHOULD BE BASED ON OTHER"
5780 LPRINT "FACTORS IN ADDITION TO YOUR FARM ENTERPRISE.  THESE FACTORS INCLUDE OTHER"
5790 LPRINT "BUSINESS USES, AS WELL AS EDUCATIONAL AND HOBBY USE.  SHOULD YOU WISH"
5800 LPRINT "TO DISCUSS YOUR OPTIONS FURTHER, CALL ON THE AGRICULTURAL COMPUTER EXTENSION"
5810 LPRINT "PROGRAM."
5820 GOTO 5920
5830 IF SCORE < FOUR THEN 5900
5840 LPRINT "IT IS UNLIKELY THAT YOU CAN JUSTIFY THE PURCHASE OF A FARM COMPUTER SYSTEM AT"
5850 LPRINT "THE CURRENT TIME.  IF YOU ARE ENTHUSIASTIC ABOUT COMPUTERS, YOU SHOULD"
5860 LPRINT "CONSIDER THE PURCHASE OF A SMALL, LOW COST MICROCOMPUTER.  HOWEVER, IT MAY"
5870 LPRINT "BE DIFFICULT TO JUSTIFY A LARGE OUTLAY.  CALL ON THE AGRICULTURAL COMPUTER"
5880 LPRINT "EXTENSION PROGRAM IF YOU WISH TO DISCUSS THIS FURTHER."
5890 GOTO 5920
5900 LPRINT "UNLESS YOU ARE RICH OR A COMPUTER BUFF, HOLD OFF ON THE PURCHASE OF A COMPUTER."
5910 LPRINT "UNTIL THEY BECOME STANDARD EQUIPMENT."
5920 IF EMP = 1 THEN EMP = 0:LPRINT:LPRINT "YOU SHOULD SERIOUSLY CONSIDER THE PURCHASE OF A COMPUTERIZED PAYROLL PACKAGE"
5930 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
5940 LPRINT "FOR FURTHER INFORMATION CONTACT:":LPRINT:LPRINT:LPRINT
5950 LPRINT "Randolph W. Ross";TAB(28);"Rob Lindsay";TAB(55);"Barry Billings"
5960 LPRINT "Ridgetown College of";TAB(28);"Guelph Agriculture Centre";TAB(55);"Guelph Agriculture Centre"
5970 LPRINT "Agricultural Technology";TAB(28);"Box 1030";TAB(55);"Box 1030"
5980 LPRINT "RIDGETOWN, Ontario";TAB(28);"GUELPH, Ontario";TAB(55);"GUELPH, Ontario"
5990 LPRINT "N0P 2C0";TAB(28);"N1H 6N1";TAB(55);"N1H 6N1"
6000 LPRINT "(519) 674-5456";TAB(28);"(519) 823-5700";TAB(55)"(519) 823-5700"
6010 LPRINT:LPRINT:LPRINT:LPRINT:LPRINT
6020 GOSUB 9390
6030 LPRINT CHR$(12)
6040 REM UPDATE UP FILE WITH INFORMATION
6050 OPEN "AVERAGE.DAT" FOR OUTPUT AS #1
6060 PRINT #1, TOTAL,NUMBER,A,B,C,D,E,COMPUTER,NEWSLET
6070 CLOSE
6080 REM CLOSING STATEMENT
6090 REM CALL INKEY$ SUBROUTINE
6100 GOSUB 6590
6110 CLS
6120 LOCATE 10,25
6130 PRINT "THANK YOU FOR TAKING THIS QUIZ"
6140 LOCATE 12,31
6150 PRINT "COMMENTS WELCOME"
6160 FOR I=1 TO 1500:NEXT I
6170 GOTO 420
6180 REM SUMMARIZATION SECTION
6190 CLS
6200 LOCATE 5,5
6210 PRINT "SUMMARY"
6220 IF NUMBER = 0 THEN 6240
6230 PRINT:PRINT "AVERAGE SCORE = ";TOTAL/NUMBER
6240 PRINT:PRINT "NUMBER IN TOP CATEGORY =  ";A
6250 PRINT "NUMBER IN 2nd CATEGORY =  ";B
6260 PRINT "NUMBER IN 3rd CATEGORY =  ";C
6270 PRINT "NUMBER IN 4th CATEGORY =  ";D
6280 PRINT "NUMBER IN BOTTOM CATEGORY = ";E
6290 IF NUMBER = 0 THEN 6320
6300 PRINT:PRINT "PER CENT WITH COMPUTERS = ";COMPUTER/NUMBER*100
6310 PRINT:PRINT "PER CENT RECEIVING NEWSLETTER = ";NEWSLET/NUMBER*100
6320 GOSUB 9160
6330 GOTO 420
6340 END
6350 REM SUBROUTINE FOR OPENING SCREEN AND BORDER
6360 CLS
6370 COLOR 7,0,2
6380 GOSUB 9300
6390 LOCATE 8,25
6400 PRINT "CAN YOU USE A PERSONAL COMPUTER"
6410 LOCATE 10,34
6420 PRINT "ON YOUR FARM?"
6430 LOCATE 15,33
6440 PRINT "- A Short Quiz"
6450 LOCATE 18,47
6460 PRINT "FOR MORE INFORMATION CONTACT:"
6470 LOCATE 20,50
6480 PRINT "R.W. ROSS,"
6490 LOCATE 21,50
6500 PRINT "R.C.A.T."
6510 LOCATE 22,50
6520 PRINT "RIDGETOWN, Ontario,"
6530 LOCATE 23,50
6540 PRINT "N0P 2C0.";
6550 REM CALL INKEY$ SUBROUTINE
6560 GOSUB 6590
6570 RETURN
6580 REM INKEY$ SUBROUTINE
6590 LOCATE 25,29
6600 PRINT "HIT ANY KEY TO CONTINUE";
6610 LOCATE 24,1
6620 X$=INKEY$:IF X$="" THEN 6620
6630 IF X$=CHR$(17) THEN GOTO 6190
6640 IF X$=CHR$(9) THEN GOSUB 7510
6650 CLS
6660 RETURN
6670 REM SUBROUTINE FOR RULES
6680 LOCATE 3,30
6690 PRINT "INSTRUCTIONS":PRINT:PRINT
6700 PRINT "HIT THE  ";:COLOR 0,7:PRINT "ENTER";:COLOR 7,0:PRINT "  KEY AFTER EACH SELECTION";
6710 PRINT "THIS KEY IS ON THE RIGHT SIDE OF YOUR KEYBOARD AND LOOKS LIKE THIS:"
6720 LOCATE 9,30
6730 PRINT CHR$(218)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(191)
6740 LOCATE 10,30
6750 PRINT CHR$(179);"       ";CHR$(179)
6760 LOCATE 11,30
6770 PRINT CHR$(179);"       ";CHR$(179)
6780 LOCATE 12,30
6790 PRINT CHR$(179);"       ";CHR$(179)
6800 LOCATE 13,30
6810 PRINT CHR$(179);"       ";CHR$(179)
6820 LOCATE 14,30
6830 PRINT CHR$(179);" ";CHR$(60)CHR$(196)CHR$(196)CHR$(217);"  ";CHR$(179)
6840 LOCATE 15,30
6850 PRINT CHR$(179);"       ";CHR$(179)
6860 LOCATE 16,30
6870 PRINT CHR$(179);"       ";CHR$(179)
6880 LOCATE 17,30
6890 PRINT CHR$(179);"       ";CHR$(179)
6900 LOCATE 18,30
6910 PRINT CHR$(179);"       ";CHR$(179)
6920 LOCATE 19,30
6930 PRINT CHR$(192)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(217)
6940 PRINT:PRINT:PRINT "THE ENTER KEY IS NOT REQUIRED AFTER MOST SINGLE DIGIT ANSWERS.  THIS INCLUDES"
6950 PRINT "ALL ANSWERS REQUIRING A RESPONSE OF THE LETTERS A-F AS WELL AS Y AND N"
6960 REM CALL INKEY$ SUBROUTINE
6970 GOSUB 6590
6980 RETURN
6990 REM SUBROUTINE TO UPDATE PAGE
7000 LOCATE 22,15
7010 PRINT "DO YOU WISH TO CHANGE ANY VARIABLES (Y or N)?";
7020 X$=INKEY$:IF X$="N" OR X$="Y" OR X$="y" OR X$="n" THEN 7030 ELSE 7020
7030 PRINT "  ";X$:FOR TIME = 1 TO 150:NEXT TIME
7040 IF X$="Y" OR X$="y" THEN CLS:TEMP=0:FLAG=1:RETURN
7050 CLS:SCORE=SCORE+TEMP:TEMP=0
7060 RETURN
7070 REM SUBROUTINE TO ACCEPT ANSWERS
7080 LOCATE 22,15
7090 PRINT "CHOOSE ONE";
7100 LOCATE 25,2:PRINT "HIT `ESC' KEY TO START OVER FROM BEGINNING.   HIT `R' TO BACK UP ONE SCREEN.";:LOCATE 22,27
7110 X$=INKEY$:IF X$ = "" THEN 7110
7120 IF X$ = CHR$(27) THEN 420
7130 IF X$ = "R" AND FLAG = 0 THEN SCORE = SCORE - TEMP:TEMP = 0:RETURN
7140 IF X$ = "R" THEN PRINT "SORRY CANNOT RETURN TWO PAGES":FOR TIME = 1 TO 200:NEXT TIME:LOCATE 22,27:PRINT "                             ":GOTO 7080
7150 IF X$="a" OR X$="b" OR X$="c" OR X$="d" OR X$="e" OR X$="f" THEN DEF SEG=0:POKE 1047,96:GOTO 7080
7160 IF X$<>"A" AND X$<> "B" AND X$<>"C" AND X$<>"D" AND X$<>"E" AND X$<>"F" THEN 7080
7170 FLAG = 0
7180 PRINT "  ";X$:FOR TIME= 1 TO 150:NEXT TIME
7190 TEMP = 0
7200 RETURN
7210 REM SUBROUTINE TO ALLOCATE POINTS - TYPE 1
7220 TEMP=0
7230 IF X$="A" THEN TEMP = TEMP + 1
7240 IF X$="B" THEN TEMP = TEMP + 2
7250 IF X$="C" THEN TEMP = TEMP + 3
7260 IF X$="D" THEN TEMP = TEMP + 4
7270 IF X$="E" THEN TEMP = TEMP + 5
7280 SCORE=SCORE+TEMP
7290 RETURN
7300 REM SUBROUTINE TO ALLOCATE POINTS - TYPE 2
7310 TEMP=0
7320 IF X$="A" THEN TEMP = TEMP + 0
7330 IF X$="B" THEN TEMP = TEMP + 1
7340 IF X$="C" THEN TEMP = TEMP + 2
7350 IF X$="D" THEN TEMP = TEMP + 3
7360 IF X$="E" THEN TEMP = TEMP + 4
7370 IF X$="F" THEN TEMP = TEMP + 5
7380 RETURN
7390 REM ERROR TRAPPING ROUTINE
7400 TEMP = 0
7410 IF ERR=53 THEN RESUME 430
7420 IF ERR=62 THEN CLOSE:RESUME 430
7430 PRINT "ERROR";ERR;"IN LINE";ERL:STOP
7440 RESUME
7450 REM HEADER SECTION FOR INFORMATION FUNCTION
7460 CLS
7470 LOCATE 5,10:PRINT "QUESTION":LOCATE 5,45:PRINT "SCORE"
7480 PRINT
7490 RETURN
7500 REM INFORMATION SECTION - SCORING CRITERIA
7510 GOSUB 7460
7520 PRINT "ACRES OF EACH CROP TYPE:"
7530 PRINT "CORN, SOYBEANS AND CEREALS            1 POINT FOR EACH 400 ACRES - MAX. 5"
7540 PRINT "VEGETABLES                            1 POINT FOR EACH  50 ACRES - MAX. 5"
7550 PRINT "INTENSIVE HORTICULTURE                1 POINT FOR EACH  10 ACRES - MAX. 5"
7560 PRINT "GREENHOUSE                            1 POINT FOR EACH   1 ACRE  - MAX. 5"
7570 PRINT "ORCHARD                               1 POINT FOR EACH  20 ACRES - MAX. 5"
7580 PRINT "ROOT CROPS AND OTHER                  1 POINT FOR EACH 200 ACRES - MAX. 5"
7590 PRINT:PRINT "NUMBER EACH LIVESTOCK TYPE:"
7600 PRINT "BREEDING LIVESTOCK (DAIRY)            1 POINT FOR EACH  100 ANIMALS - MAX. 5"
7610 PRINT "FEEDER LIVESTOCK                      1 POINT FOR EACH 1000 ANIMALS - MAX. 5"
7620 PRINT "POULTRY                               1 POINT FOR EACH 2000 ANIMALS - MAX. 5"
7630 GOSUB 6590
7640 GOSUB 7460
7650 PRINT "APPROXIMATE GROSS INCOME?"
7660 PRINT "A.   UNDER $100,000                   1 POINT"
7670 PRINT "B.   $100,001 - $200,000              2 POINTS"
7680 PRINT "C.   $200,001 - $400,000              3 POINTS"
7690 PRINT "D.   $400,001 - $1,000,000            4 POINTS"
7700 PRINT "E.   OVER $1,000,000                  6 POINTS"
7710 PRINT:PRINT "NUMBER OF FULL TIME EMPLOYEES?"
7720 PRINT "A.   NONE                             0 POINTS"
7730 PRINT "B.   1-5                              2 POINTS TIMES PAY FACTOR"
7740 PRINT "C.   6-10                             4 POINTS TIMES PAY FACTOR"
7750 PRINT "D.   11-25                            6 POINTS TIMES PAY FACTOR"
7760 PRINT "E.   26-75                            8 POINTS TIMES PAY FACTOR"
7770 PRINT "F.   OVER 75                         10 POINTS TIMES PAY FACTOR"
7780 PRINT:PRINT TAB(38)"PAY FACTOR = 2 FOR WEEKLY - MAX 15"
7790 PRINT TAB(51)"1 FOR BI-WEEKLY"
7800 PRINT TAB(50)".5 FOR MONTHLY"
7810 PRINT TAB(51)"2 FOR OTHER - MAX. 15"
7820 GOSUB 6590
7830 GOSUB 7460
7840 PRINT "NUMBER OF PART TIME EMPLOYEES?"
7850 PRINT "A.   NONE                             0 POINTS"
7860 PRINT "B.   1-5                              2 POINTS"
7870 PRINT "C.   6-10                             4 POINTS"
7880 PRINT "D.   11-25                            6 POINTS"
7890 PRINT "E.   26-75                            8 POINTS"
7900 PRINT "F.   OVER 75                         10 POINTS"
7910 PRINT:PRINT "TYPE OF ACCOUNTING SYSTEM?"
7920 PRINT "A.   INF. CASHBOOK, SHOEBOX OR NONE   0 POINTS"
7930 PRINT "B.   FARM CHECK, CANCHECK, ETC.       1 POINT"
7940 PRINT "C.   FORMAL CASHBOOK                  2 POINTS"
7950 PRINT "D.   CANFARM OR SIMILAR SYSTEM        3 POINTS"
7960 PRINT "E.   DOUBLE ENTRY SYSTEM              5 POINTS"
7970 GOSUB 6590
7980 GOSUB 7460
7990 PRINT "ORGANIZATIONAL STRUCTURE?"
8000 PRINT "A.   INCORPORATED                     5 POINTS"
8010 PRINT "B.   PARTNERSHIP                      3 POINTS"
8020 PRINT "C.   SINGLE OWNER                     1 POINT"
8030 PRINT "D.   RENTED LAND ONLY                 0 POINTS"
8040 PRINT:PRINT "NUMBER OF INVOICES PER MONTH?"
8050 PRINT "A.   LESS THAN 50                     0 POINTS"
8060 PRINT "B.   50 - 150                         1 POINT"
8070 PRINT "C.   151 - 500                        3 POINTS"
8080 PRINT "D.   OVER 500                         5 POINTS"
8090 PRINT "E.   NO IDEA                         -3 POINTS"
8100 GOSUB 6590
8110 GOSUB 7460
8120 PRINT "WHO PROCESSES INVOICES?"
8130 PRINT "A.   SELF OR FAMILY MEMBER            1 POINT"
8140 PRINT "B.   FARM MANAGER                     1 POINT"
8150 PRINT "C.   OFFICE STAFF                     2 POINTS"
8160 PRINT "D.   PROFESSIONAL ACCOUNTING SERVICE  3 POINTS"
8170 PRINT "E.   OTHER                            1 POINT"
8180 PRINT:PRINT "AMOUNT SPENT ON ACCOUNTING FEES?"
8190 PRINT "A.   LESS THAN $1,000                 1 POINT"
8200 PRINT "B.   $1,001 - $2,000                  2 POINTS"
8210 PRINT "C.   $2,001 - $5,000                  4 POINTS"
8220 PRINT "D.   OVER $5,000                      5 POINTS"
8230 GOSUB 6590
8240 GOSUB 7460
8250 PRINT "TECHNOLOGICAL PROFILE?"
8260 PRINT "A.   AN INNOVATOR                     8 POINTS"
8270 PRINT "B.   AN EARLY USER                    5 POINTS"
8280 PRINT "C.   ABOUT AVERAGE                    2 POINTS"
8290 PRINT "D.   CAUTIOUS                        -1 POINT"
8300 PRINT "E.   SCEPTICAL                       -3 POINTS"
8310 PRINT:PRINT "KNOWLEDGE OF COMPUTERS?"
8320 PRINT "A.   KNOW NOTHING ABOUT THEM          0 POINTS"
8330 PRINT "B.   BARE MINIMUM                     1 POINT"
8340 PRINT "C.   ABOUT AVERAGE                    2 POINTS"
8350 PRINT "D.   ABOVE AVERAGE                    4 POINTS"
8360 PRINT "E.   ALMOST AN EXPERT                 5 POINTS"
8370 GOSUB 6590
8380 GOSUB 7460
8390 PRINT "INTEREST IN COMPUTERS?"
8400 PRINT "A.   NOT INTERESTED                 -10 POINTS"
8410 PRINT "B.   MINIMAL INTEREST                -1 POINT"
8420 PRINT "C.   AVERAGE INTEREST LEVEL           1 POINT"
8430 PRINT "D.   VERY INTERESTED                  5 POINTS"
8440 PRINT "E.   ALMOST FANATICAL ABOUT THEM      8 POINTS"
8450 PRINT:PRINT "INTEREST IN COMMODITY FUTURES OR STOCK MARKETS?"
8460 PRINT "A.   BOTH TRADE AND KEEP CHARTS       5 POINTS"
8470 PRINT "B.   DO NOT TRADE BUT KEEP CHARTS     4 POINTS"
8480 PRINT "C.   TRADE BUT DO NOT KEEP CHARTS     2 POINTS"
8490 PRINT "D.   INTERESTED NOT TRADED ETC.       1 POINT"
8500 PRINT "E.   NOT INTERESTED                   0 POINTS"
8510 GOSUB 6590
8520 GOSUB 7460
8530 PRINT "QUESTION ONLY ASKED IF BREEDING HERD EXISTS!"
8540 PRINT "FORMULATE RATIONS FOR BREEDING HERD?"
8550 PRINT "A.   INDIVIDUALLY BY PRODUCTION       5 POINTS"
8560 PRINT "B.   IN GROUPS BY PRODUCTION          3 POINTS"
8570 PRINT "C.   SINGLE RATION FOR HERD           1 POINT"
8580 PRINT "D.   NO, BUT WOULD LIKE TO            1 POINT"
8590 PRINT "E.   NOT NECESSARY                    0 POINTS"
8600 PRINT:PRINT "QUESTION ONLY ASKED IF BREEDING HERD EXISTS!"
8610 PRINT "PRODUCTION RECORDS ON BREEDING HERD?"
8620 PRINT "A.   NO                               0 POINTS"
8630 PRINT "B.   ABOUT AVERAGE                    1 POINT"
8640 PRINT "C.   YES, QUITE DETAILED              5 POINTS"
8650 PRINT "D.   NO, BUT I WOULD LIKE TO          2 POINTS"
8660 PRINT "E.   SOME, BUT I WOULD LIKE MORE      4 POINTS"
8670 GOSUB 6590
8680 GOSUB 7460
8690 PRINT "QUESTION ONLY ASKED IF FEEDERS OR POULTRY EXIST."
8700 PRINT "PRODUCTION RECORDS ON FEEDERS OR POULTRY?"
8710 PRINT "A.   NO                               0 POINTS"
8720 PRINT "B.   ABOUT AVERAGE                    0 POINTS"
8730 PRINT "C.   YES, QUITE DETAILED              4 POINTS"
8740 PRINT "D.   NO, BUT I WOULD LIKE TO          1 POINT"
8750 PRINT "E.   SOME, BUT I WOULD LIKE MORE      3 POINTS"
8760 PRINT:PRINT "WANT COMPUTER FOR NON-FARM USES?"
8770 PRINT "A.   VERY MUCH                        7 POINTS"
8780 PRINT "B.   A LITTLE BIT                     2 POINTS"
8790 PRINT "C.   NOT SURE                         1 POINT"
8800 PRINT "D.   ONLY INTERESTED IN FARM USE      0 POINTS"
8810 GOSUB 6590
8820 GOSUB 7460
8830 PRINT "CURRENT TAX SITUATION?"
8840 PRINT "A.   NEVER HAVE TO PAY INCOME TAX     0 POINTS"
8850 PRINT "B.   PAY LESS THAN $5000 PER YEAR     2 POINTS"
8860 PRINT "C.   PAY MORE THAN $5000 PER YEAR     4 POINTS"
8870 PRINT:PRINT "TYPING SKILLS?"
8880 PRINT "A.   CANNOT TYPE                     -1 POINTS"
8890 PRINT "B.   CAN TOUCH TYPE                   3 POINTS"
8900 PRINT "C.   KNOW WHERE THE KEYS ARE          1 POINT"
8910 PRINT "D.   FAIR TWO-FINGERED TYPIST         2 POINTS"
8920 PRINT "E.   SOMEONE ELSE WILL ENTER DATA     2 POINTS"
8930 GOSUB 6590
8940 GOSUB 7460
8950 LOCATE 5,10
8960 PRINT "PLEASE UPDATE THIS LIST IF ANY CHANGES ARE MADE!"
8970 LOCATE 10,15
8980 PRINT "THE CURRENT RANGES FOLLOW"
8990 LOCATE 11,20
9000 PRINT "HIGHEST RANGE  >=";ONE;"POINTS"
9010 LOCATE 12,20
9020 PRINT "MID-HIGH RANGE >=";TWO;"POINTS"
9030 LOCATE 13,20
9040 PRINT "MIDDLE RANGE   >=";THREE;"POINTS"
9050 LOCATE 14,20
9060 PRINT "MID-LOW RANGE  >=";FOUR;"POINTS"
9070 LOCATE 15,20
9080 PRINT "LOWEST RANGE   < ";FOUR;"POINTS"
9090 LOCATE 16,15
9100 PRINT "SCORE CANNOT BE LESS THAN ZERO!"
9110 LOCATE 20,10
9120 PRINT "NOTE THESE RANGES ARE DEFINED IN LINE 140"
9130 GOSUB 6590
9140 RETURN
9150 REM SUBROUTINE TO QUIT PROGRAM
9160 LOCATE 22,15
9170 PRINT "DO YOU WISH TO QUIT (Y or N)?";
9180 X$=INKEY$:IF X$="N" OR X$="Y" OR X$="y" OR X$="n" THEN 9190 ELSE 9180
9190 PRINT "  ";X$:FOR TIME = 1 TO 150:NEXT TIME
9200 IF X$="Y" OR X$="y" THEN CLS:SYSTEM
9210 RETURN
9220 REM SUBROUTINE ANSWER YES/NO QUESTION
9230 PRINT "ENTER Y OR N ";
9240 X$=INKEY$:IF X$="N" OR X$="Y" OR X$="y" OR X$="n" OR X$="R" THEN 9250 ELSE 9240
9250 IF X$="y" THEN X$="Y"
9260 IF X$="n" THEN X$="N"
9270 PRINT "  ";X$;:FOR TIME = 1 TO 150:NEXT TIME
9280 RETURN
9290 REM SUBROUTINE TO DRAW A.C.E. LOGO
9300 PRINT TAB(5);CHR$(218)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(191)
9310 PRINT TAB(5);CHR$(179)"ACE      "CHR$(179)
9320 PRINT TAB(5);CHR$(179)"         "CHR$(179)
9330 PRINT TAB(5);CHR$(179)"    O    "CHR$(179)
9340 PRINT TAB(5);CHR$(179)"         "CHR$(179)
9350 PRINT TAB(5);CHR$(179)"         "CHR$(179)
9360 PRINT TAB(5);CHR$(192)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(196)CHR$(217)
9370 RETURN
9380 REM SUBROUTINE TO DRAW A.C.E. LOGO
9390 LPRINT TAB(25);"-----------"
9400 LPRINT TAB(25);"|ACE      |";TAB(45);"Y O U R"
9410 LPRINT TAB(25);"|         |"
9420 LPRINT TAB(25);"|    O    |";TAB(45);"A. C. E."
9430 LPRINT TAB(25);"|         |"
9440 LPRINT TAB(25);"|         |";TAB(45);"T E A M"
9450 LPRINT TAB(25);"-----------"
9460 RETURN
```

## TREEFIX.BAS

```bas
100 ' TREEFIX.BAS -- by Bob Raven -- adapted by PC Magazine
101 PRINT "DO YOU WISH TO PRINT ON PRINTER? (Y OR N)";:INPUT YN$
110 CLS:OPEN "I",#1,"TREE1.LST"
120 IF EOF(1) THEN CLOSE:KILL "TREE1.LST":SYSTEM
130 INPUT #1,A$
140 B$=MID$(A$,2,4)
150 IF B$<>"IREC" THEN 170 ELSE PRINT A$:PRINT "\(ROOT)":IF YN$="Y" THEN LPRINT A$:LPRINT "\(ROOT)"
160 INPUT #1,A$:A$=RIGHT$(A$,LEN(A$)-6):GOTO 190
170 IF B$<>"Path" GOTO 120
180 A$=MID$(A$,8):K=0
190 FOR J=1 TO LEN(A$)
200 C$=MID$(A$,J,1)
210 IF C$="\" THEN K=K+1:D$=MID$(A$,J) ELSE 220
220 NEXT J
230 PRINT TAB(12*K-11);CHR$(124);STRING$(11,45);D$
231 IF YN$="Y" THEN LPRINT TAB(12*K-11);CHR$(124);STRING$(11,45);D$
240 M=M+1:IF M<21 THEN 120
250 PRINT "Hit any key to continue...";
260 IF INKEY$="" THEN 260 ELSE CLS:M=0:GOTO 120
```

## USLE.BAS

```bas
10   CLS:PRINT:PRINT:PRINT
20   PRINT"This program was prepared by:"
30   PRINT"                                S. Elwynn Taylor and Minoru Amemiya"
40   PRINT"                                Agronomy Extension"
50   PRINT"                                Iowa State University, Ames"
60   VPOS=10:HPOS=20
70   PRINT"...AND JUSTICE FOR ALL"
80   PRINT"Programs and activities of Cooperative Extension Service are available to all"
90   PRINT"potential clientele without regard to race, color, sex or national origin."
100   PRINT"Anyone who feels discriminated against should send a complaint within 180 days"
110   PRINT"to the Secretary of Agriculture, Washington, D.C. 20250"
120   PRINT:PRINT:PRINT
130   PRINT"Cooperative Extension Service, Iowa State University of Science and Technology"
140   PRINT"and the United States Department of Agriculture cooperating. Robert L. Crom,"
150   PRINT"director, Ames, Iowa. Distributed in furtherance of the Acts of Congress of"
160   PRINT"May 8 and June 30, 1914."
170   PRINT:PRINT"These programs may not be duplicated or reprinted without permission."
180 PRINT:PRINT "Press any key to continue"
190 A$=INPUT$(1)
500   IMAGE1$="###.##     \                                       \"
510   IMAGE2$="###.###    \                                       \"
520   IMAGE3$="!          \                                      \"
530   IMAGE4$="###.##     \                                    \ ##"
540   IMAGE5$="   #       #.##    ##.##     ####   ####.##   ###.## +###.##       ###"
550   IMAGE6$="\                \ ###.##"
560   IMAGE7$="\          \ ##.##"
570   IMAGE8$="\                            \ ##.##"
580   IMAGE9$="\                     \ ####.##"
590   IMAGE10$="\                       \ #### \  \"
600   IMAGE11$="\                                                            \ .##"
610   IMAGE12$="\                               \ ##. !"
790   CLS
800   INPUT"      Is your printer PARALLEL (1) or SERIAL (2)?",THING$
810   IF THING$="?" THEN 22900
820   IF SRING$<>"" THEN THING$=VAL(SRING$)
822   IF SRING$="?" THEN SRING$="Y"
824   GOSUB 60400
826   IF ANSWER$="Y" THEN 62800
840   INPUT"           Do you want to see general instructions?",ANSWER$
842   IF ANSWER$="?" THEN ANSWER$="Y"
844   GOSUB 60000
846   IF ANSWER$="Y" THEN 62800
900   RAINFALL=175
910   NUM.SEGS=1
920   EROD(1)=.32
930   PRACTICE=1
940   TOLER=5
950   TILLAGE=.33
960   COVER=.24
965   B.F=.035
970   OPION$="N"
9000   CLS
9010   SRING$=""
10000   PRINT"************************ UNIVERSAL SOIL LOSS EQUATION ************************"
10005   PRINT"               (Complex Slopes after Foster & Wischmeier, 1974)
10007   PRINT"For help on any entry press: ?."
10010   PRINT
10014   ANSWER$=OPION$
10020   PRINT USING IMAGE3$;OPION$,"Are you using percent residues option?";
10022   INPUT" :  ",SRING$
10025   IF SRING$="?" THEN GOTO 63100
10027   IF SRING$<>"" THEN ANSWER$=SRING$
10030   GOSUB 60000
10040   OPION$=ANSWER$
10100   PRINT USING IMAGE1$;RAINFALL,"Enter rainfall factor";
10200   INPUT": ",SRING$
10250   IF SRING$="?" THEN GOTO 63000
10300   IF SRING$<>"" THEN RAINFALL=VAL(SRING$)
10400   PRINT USING IMAGE1$;NUM.SEGS,"Enter number of segments";
10500   INPUT": ",SRING$
10510   IF SRING$="?" THEN GOTO 63200
10600   IF SRING$<>"" THEN NUM.SEGS=VAL(SRING$)
10700   FOR J=1 TO NUM.SEGS
10800     PRINT USING IMAGE4$;EROD(J),"Enter soil erodibility factor. Segment ",J;
10900     INPUT": ",SRING$
10910     IF SRING$="?" THEN GOTO 63300
11000     IF SRING$<>"" THEN EROD(J)=VAL(SRING$)
11100     PRINT USING IMAGE4$;SEG.LN(J),"Enter length of slope (feet). Segment ",J;
11200     INPUT": ",SRING$
11210     IF SRING$="?" THEN GOTO 63400
11300     IF SRING$<>"" THEN SEG.LN(J)=VAL(SRING$)
11400     PRINT USING IMAGE4$;GRAD(J),"Enter slope steepness (%).     Segment",J;
11500     INPUT": ",SRING$
11510     IF SRING$="?" THEN GOTO 62600
11600     IF SRING$<>"" THEN GRAD(J)=VAL(SRING$)
11700     NEXT J
11800   PRINT USING IMAGE1$;PRACTICE,"Enter supporting practice factor";
11900   INPUT": ",SRING$
11910   IF SRING$="?" THEN GOTO 62100
12000   IF SRING$<>"" THEN PRACTICE=VAL(SRING$)
12100   PRINT USING IMAGE1$;TOLER,"Enter tolerance ";
12200   INPUT": ",SRING$
12210   IF SRING$="?" THEN GOTO 62200
12300   IF SRING$<>"" THEN TOLER=VAL(SRING$)
12310   IF OPION$="N" THEN 13000
12400   PRINT USING IMAGE1$;TILLAGE,"Enter conventional tillage factor";
12500   INPUT": ",SRING$
12510   IF SRING$="?" THEN GOTO 62300
12600   IF SRING$<>"" THEN TILLAGE=VAL(SRING$)
12700   PRINT USING IMAGE1$;RESIDUE,"Enter percent residue";
12800   INPUT": ",SRING$
12810   IF SRING$="?" THEN GOTO 62400
12820   IF SRING$<>"" THEN RESIDUE=VAL(SRING$)
12825   SRING$=""
12830   PRINT USING IMAGE2$;B.F,"Enter  B  factor if needed";
12840   INPUT":  ",SRING$
12850   IF SRING$="?" THEN GOTO 63600
12860   IF SRING$<>"" THEN B.F=VAL(SRING$)
12905   COVER=EXP(-1*(B.F*(RESIDUE-3)))*TILLAGE
12910   GOTO 13300
13000   PRINT USING IMAGE1$;COVER,"Enter cover and management factor";
13100   INPUT": ",SRING$
13130   IF SRING$="?" THEN GOTO 62500
13200   IF SRING$<>"" THEN COVER=VAL(SRING$)
13300   SUM.DELTA=0
13350   U(0)=0
13355   LAST.M=0
13358   SUM.U=0
13360   SUM.LN(0)=0
13400   FOR J=1 TO NUM.SEGS
13500     SUM.LN(J)=SUM.LN(J-1)+SEG.LN(J)
13900     THETA=ATN(GRAD(J)*.01)
14000     STEEP(J)=65.41001*(SIN(THETA)^2)+4.56*SIN(THETA)+.065
14100     M=.2
14200     IF GRAD(J)>=1 THEN M=.3
14300     IF GRAD(J)>=3.1 THEN M=.4
14400     IF GRAD(J)>=5 THEN M=.5
14500     U2=STEEP(J)*(SUM.LN(J)^(M+1))/(72.6^M)
14600     U1=STEEP(J)*(SUM.LN(J-1)^(LAST.M+1))/(72.6^LAST.M)
14650     U(J)=(U2-U1)
14655     SUM.U=SUM.U+U(J)
14700     DELTA(J)=EROD(J)*(U2-U1)
14710     IF DELTA(J)>0 THEN 14800
14715     PRINT
14720     PRINT"Slope ";J;" is invalid"
14730     NUM.SEGS=J-1
14740     PRINT"Using only the first ";NUM.SEGS;" segments"
14750     GOTO 15000
14800     SUM.DELTA=SUM.DELTA+DELTA(J)
14850     LAST.M=M
14900     NEXT J
15000   KLS=SUM.DELTA/SUM.LN(NUM.SEGS)
15050   LS=SUM.U/SUM.LN(NUM.SEGS)
15100   AVE.LOSS=RAINFALL*COVER*PRACTICE*KLS
15150   MAXC=TOLER/(PRACTICE*RAINFALL*KLS)
15160   MINCOV=3-(LOG(MAXC/TILLAGE)/B.F)
15162   IF MINCOV<0 THEN MINCOV=0
15200   FOR J=1 TO NUM.SEGS
15300     SEGCON(J)=DELTA(J)/SUM.DELTA
15400     NEXT J
15450   PRINT
15500   PRINT"Average soil loss is ";INT(AVE.LOSS+.5);" tons per acre"
15510   PRINT
15520   PRINT USING IMAGE9$;"KLS for entire slope is",KLS
15530   PRINT USING IMAGE10$;"Length of entire slope is",SUM.LN(NUM.SEGS),"feet"
15532   PRINT USING IMAGE9$;"U  for entire slope is",SUM.U
15534   PRINT USING IMAGE9$;"LS for entire slope is",LS
15540   PRINT USING IMAGE11$;"To achieve tolerance, C (cover and management) must not exceed",MAXC
15545   IF OPION$="N" THEN 15600
15550   PRINT USING IMAGE12$;"Average residue cover required is",MINCOV,"%"
15600   PRINT
15650   PRINT"Segment  Segment  Segment  Segment  Segment  Segment  Segment  Percent of"
15660   PRINT" Number     K      Slope    Length   U2-U1    DELTA     KLS    Total Erosion"
15670   PRINT"----------------------------------------------------------------------------"
15700   FOR J=1 TO NUM.SEGS
15800     PRINT USING IMAGE5$;J,EROD(J),GRAD(J),SEG.LN(J),U(J),DELTA(J),DELTA(J)  /SEG.LN(J),SEGCON(J)*100
15900     NEXT J
15910   PRINT
15920   PRINT
18000   INPUT"Do you want the values printed? ",ANSWER$
18050   GOSUB 60000
18100   IF ANSWER$="N" THEN 19900
18105   IF THING$="2" THEN 18110
18107   OPEN "O",#1,"LPT1"
18109   GOTO 18130
18110   OPEN#1 AS OUTPUT,".printer"
18130   PRINT#1,
18140   PRINT#1,"*********************** UNIVERSAL SOIL LOSS EQUATION ************************"
18142   PRINT#1,"               (Complex Slopes after Foster & Wischmeier, 1974)
18144   IF OPION$="N" THEN 18150
18145   PRINT#1,"                         ** % RESIDUE OPTION USED **"
18150   PRINT#1,
18160   PRINT#1, USING IMAGE6$;"Rainfall factor is",RAINFALL
18170   PRINT#1, USING IMAGE8$;"Supporting practice factor is ",PRACTICE
18180   PRINT#1, USING IMAGE7$;"Tolerance is ",TOLER
18182   IF OPION$="N" THEN 18200
18190   PRINT#1, USING IMAGE8$;"Conventional tillage factor is ",TILLAGE
18195   PRINT#1, USING IMAGE8$;"Percent residue cover is",RESIDUE
18197   GOTO 18310
18200   PRINT#1, USING IMAGE8$;"Cover and management factor is ",COVER
18310   PRINT#1,
18510   PRINT#1,
18520   PRINT#1,"Average soil loss is ";INT(AVE.LOSS+.5);" tons per acre"
18521   PRINT#1,
18522   PRINT#1, USING IMAGE9$;"KLS for entire slope is",KLS
18524   PRINT#1, USING IMAGE10$;"Length of entire slope is",SUM.LN(NUM.SEGS),"feet"
18526   PRINT#1, USING IMAGE9$;"U  for entire slope is",SUM.U
18528   PRINT#1, USING IMAGE9$;"LS for entire slope is",LS
18529   PRINT#1, USING IMAGE11$;"To achieve tolerance C (cover and management) must not exceed",MAXC
18535   PRINT#1,
18540   PRINT#1,"Segment  Segment  Segment  Segment  Segment  Segment  Segment    Percent of"
18550   PRINT#1," Number     K      Slope    Length   U2-U1    DELTA     KLS    Total Erosion"
18560   PRINT#1,"----------------------------------------------------------------------------"
18570   PRINT#1,
18580   FOR J=1 TO NUM.SEGS
18590     PRINT#1, USING IMAGE5$;J,EROD(J),GRAD(J),SEG.LN(J),U(J),DELTA(J),DELTA   (J)/SEG.LN(J),SEGCON(J)*100
18600     NEXT J
18650   PRINT#1,CHR$(12):REM form feed
18700   CLOSE#1
19900   INPUT"Do you want to go again ? ",ANSWER$
19910   GOSUB 60000
19920   IF ANSWER$="Y" THEN 9000
19925   SYSTEM
19930   END
22900   PRINT
22902   PRINT"This program is designed to use the IDS560 printer connected"
22904   PRINT"to the SERIAL (2) port, or an APPLE DOT MATRIX printer,"
22906   PRINT"connected to a PARALLEL (1) port.    PRESS: RETURN":A$=INPUT$(1)
22908   GOTO 790
50000   REM *******************************************************************
50100   REM *** Error handling subroutine
50150   REM *******************************************************************
50200   PRINT CHR$(7)
50300   IF ERR=255 THEN PRINT"control-c encountered":END
50400   PRINT"INVALID DATA ENTRY CAUSED AN ERROR"
50600   PRINT
50800   GOTO 10100
59900   REM *****************************************************************
60000   REM *** this subroutine checks to see if the string passed in by
60100   REM *** "answer$" is an affirmative string.
60200   REM *****************************************************************
60300   SRING$=ANSWER$
60400   ANSWER$="N"
60500   IF SRING$="Y" THEN ANSWER$="Y"
60600   IF SRING$="y" THEN ANSWER$="Y"
60700   IF SRING$="OK" THEN ANSWER$="Y"
60800   IF SRING$="ok" THEN ANSWER$="Y"
60900   IF SRING$="OKAY" THEN ANSWER$="Y"
61000   IF SRING$="okay" THEN ANSWER$="Y"
61100   IF SRING$="YES" THEN ANSWER$="Y"
61200   IF SRING$="yes" THEN ANSWER$="Y"
61300   RETURN
62000   PRINT
62010   PRINT"After reading, press RETURN   The first program statement will be displayed,"
62011   PRINT"however all of the data you have entered have been retained.":A$=INPUT$(1)
62020   GOTO 9000
62086   PRINT
62100   PRINT
62110   PRINT"Supporting Practice Factor (P).  See tables 13, 14, and 15 of Handbook 537.
62120   PRINT
62130   PRINT"The value, in general, may be entered as 0.5 where contour practices are used"
62140   PRINT"and as a 1.0 if fields are planted fence row to fence row."
62190   GOTO 62000
62200   PRINT
62210   PRINT"Soil TOLERANCE to erosion in annual tons per acre as found in Soil Survey"
62212   PRINT"Reports or in the SCS Field Office Technical Guide."
62290   GOTO 62000
62300   PRINT
62310   PRINT"Conventional tillage factors determined for IOWA (common crops)"
62320   PRINT"Selected conventional tillage factors for Iowa (from SCS-IOWA Field Office"
62330   PRINT"Technical Guide, section I-C-1, Table V-b)."
62340   PRINT"       CROP                  FALL TILLAGE     SPRING TILLAGE"
62342   PRINT"       ----                  ------------     --------------"
62344   PRINT"Corn after Corn                    0.33              0.29"
62346   PRINT"Corn after Corn (silage)           0.43              0.36"
62348   PRINT"Corn after Meadow                  0.17              0.13"
62350   PRINT"Corn after Soybeans                0.40              0.36"
62352   PRINT"Small Grain after Corn             0.10              0.09"
62354   PRINT"Soybeans (>20 inches) after Corn   0.35              0.30"
62356   PRINT"Soybeans (<20 inches) after Corn   0.27              0.24"
62390   GOTO 62000
62400   PRINT
62410   PRINT"The % RESIDUE is a measured amount.  It should not be an estimate.  The program"
62420   PRINT"uses IOWA research data by Laflin et al. 1981."
62430   PRINT"Iowa results have shown that residue is much more effective in reducing"
62432   PRINT"erosion than is indicated in Handbook No. 537.  The effectiveness appears as"
62434   PRINT"the exponent in the expression at statement No. 12905 & 15160 of this program."
62490   GOTO 62000
62500   PRINT
62510   PRINT"Cover and Management value (C factor) tables are found in the SCS"
62512   PRINT"Field Office Technical Guide."
62590   GOTO 62000
62600   PRINT
62610   PRINT"Slope gradient (or steepness) is measured in percent, it may be field measured"
62620   PRINT"for each segment by clinometer or determined from topographic map."
62630   PRINT"sheets.  Lacking these, the soil number includes a character indicating the"
62640   PRINT"average slope for soil types in Iowa:"
62642   PRINT"              Map Symbol      Slope       Average"
62644   PRINT"              ----------      -----       -------"
62646   PRINT"                  A            0-2           1"
62648   PRINT"                  B            2-5           3.5"
62650   PRINT"                  C            5-9           7"
62652   PRINT"                  D            9-14         11.5"
62654   PRINT"                  E           14-18         16"
62656   PRINT"                  F           18-25         21.5"
62690   GOTO 62000
62700   PRINT
62800   PRINT"This program solves the Universal Soil Loss Equation as given in:"
62802   PRINT"Agricultural Handbook Number 537, U. S. Department of Agriculture, 1978."
62804   PRINT"The user should have avilable: Handbook 537 together with the current US"
62806   PRINT"Field Office Tech. Guide for the state and appropriate Soil Survey reports."
62807   PRINT
62808   PRINT"For details of complex slope computations, refer to Foster, G. R. and W. H."
62810   PRINT"Wischmeier,  1974  Evaluating Irregular Slopes for Soil Loss Prediction,"
62812   PRINT"Trans. ASAE 17(2):306-309."
62814   PRINT
62816   PRINT
62847   PRINT
62848   PRINT"This program will prompt for data.  A defalt (sample) value is displayed to"
62850   PRINT"the LEFT of the prompt, if you wish to use the displayed value,simply tap the"
62852   PRINT"RETURN key, otherwise key in the desired value."
62854   PRINT
62856   PRINT"Comments reguarding each prompt may be viewed by entering a question mark (?)."
62890   PRINT
62892   PRINT"                 PRESS ANY KEY TO RETURN TO PROGRAM"
62894   A$=INPUT$(1)
62896   GOTO 900
63000   PRINT
63010   PRINT" RAINFALL factor; see figure 1 of Agricultural Handbook 537."
63012   PRINT" Values in Iowa range from 140 in the NW to 190 in the SE.  Usually a value of"
63014   PRINT" 150 is appropriate for the NW half and 175 for the SE half of Iowa"
63020   GOTO 62000
63100   PRINT
63110   PRINT"Selection of the % residue option permits use of measured residue cover"
63112   PRINT"in determining the cover and management value (C-factor).  Procedures for"
63113   PRINT"measuring % residue cover may be implemented by several appropriate methods"
63115   PRINT"as described on page 50 of Agricultural Handbook number 537 and also as"
63120   PRINT"described by Laflen, J. M., M. Amemiya, and E. A. Hintz. 1981. Measuring"
63121   PRINT"crop residue cover.  Journal of Soil and Water Conservation 36(6):341-343."
63122   PRINT"Residue should be actually measured and not estimated.  However, for training"
63124   PRINT"puposes it may be assumed that 3% residue is left by a Moldboard,"
63126   PRINT"                              18%    by      Offset disk"
63128   PRINT"                              20%    by      Till plant"
63130   PRINT"                              22%    by      Chisel"
63132   PRINT"                              65%    by      Slot plant"
63190   GOTO 62000
63200   PRINT
63210   PRINT"The number of slope segments may range from 1 to as many as desired by the user."
63220   PRINT"A single segment indicates USLE as it appears in Handbook # 537."
63290   GOTO 62000
63300   PRINT
63310   PRINT"K is the erodibility factor for the soil.  Refer to Soil Survey report or the"
63312   PRINT"SCS Field Office Technical Guide; or use figure 3 of Handbook #537."
63390   GOTO 62000
63400   PRINT
63410   PRINT"The length of the slope, measured in feet, may be determined in the field or"
63420   PRINT"from a map.  On very steep slopes there will be some error in the latter method."
63490   GOTO 62000
63500   PRINT
63510   PRINT"   "
63590   GOTO 62000
63600   PRINT
63610   PRINT"The B factor is the coefficient describing the effectiveness of residue for"
63620   PRINT"reducing soil loss.  The relationship given in Handbook 537 is approximated by"
63630   PRINT"a value of .025.  Iowa research suggests that residue is considerably more"
63640   PRINT"effective and that the value should be near .035 (for Iowa)."
63999   GOTO 62000
18530   IF OPION$="N" THEN 18535
18532   PRINT#1, USING IMAGE12$;"Average residue cover required is",MINCOV,"%"
```

## WOODHEAT.BAS

```bas
10  REM  NEW SHELTER'S WOOD HEAT PROGRAM, WRITTEN BY ROGER RAWLINGS
20  DIM K$(1),W$(10): GOSUB 320: PRINT "WOOD HEAT PROGRAM": PRINT
30  PRINT "THE FOLLOWING QUESTIONS APPLY": PRINT "TO THE FUEL THAT YOU WANT TO": PRINT "REPLACE WITH WOOD.": PRINT
40  PRINT "NUMBER OF FUEL UNITS YOU USED": PRINT "FOR HEAT LAST YEAR=": INPUT FU: PRINT : PRINT "BTU'S PER FUEL UNIT=": INPUT BU
50  PRINT : PRINT "TOTAL COST OF THIS MUCH FUEL=": INPUT FC: GOSUB 320
60  PRINT "EFFICIENCY OF YOUR PRESENT": PRINT "HEATING SYSTEM=": INPUT SE:BT = (BU * FU) * SE: GOSUB 320: GOSUB 370
70  GOSUB 320: PRINT "PERCENTAGE OF YOUR PRESENT": PRINT "HEATER'S HEAT OUTPUT TO BE": PRINT "DELIVERED BY THE WOODBURNING": PRINT "DEVICE="
80  INPUT PH:PH = PH / 100: IF PH <= 0 OR PH > 1 THEN  GOTO 80
90  GOSUB 330
100  GOSUB 320: PRINT "WHAT KIND OF WOOD WILL YOU": PRINT "BURN IN THE NEW WOODBURNING": PRINT "DEVICE?"
110  PRINT : PRINT "1) HARDWOODS (SUCH AS OAK)": PRINT : PRINT "2) SOFTWOODS (SUCH AS PINE)": PRINT : PRINT "3) A MIXTURE OF HARDWOODS": PRINT "   AND SOFTWOODS"
120  INPUT W: ON W GOTO 130,140,150
130 BC = 19000000.#:W$ = "HARDWOOD": GOTO 160
140 BC = 15000000.#:W$ = "SOFTWOOD": GOTO 160
150 BC = 17000000.#:W$ = "MIXED WOOD"
160  GOSUB 380: GOSUB 320: PRINT "COST OF A CORD OF": PRINT W$;"=": INPUT CC
170 CO = BT / BC:WC = CO * CC: GOSUB 340
180  GOSUB 320: PRINT "YOU WILL NEED TO BURN": PRINT CO;" CORDS OF": PRINT W$;" TO REPLACE": PRINT PH * 100;"% OF YOUR PRESENT HEATER'S": PRINT "HEAT OUTPUT."
190  PRINT : PRINT "THIS MUCH ";W$: PRINT "WILL COST $";WC: PRINT "A SAVINGS OF $";AS: PRINT "COMPARED TO THE $";FC: PRINT "YOU NOW SPEND FOR THE": PRINT "SAME AMOUNT OF HEAT."
200  GOSUB 300: GOSUB 390: IF AS <= 0 THEN  GOTO 220
210  PRINT "WILL PAY FOR ITSELF": PRINT "IN "; INT (PB * 10) / 10;" YEARS": GOSUB 400: GOSUB 300: GOTO 230
220  PRINT "WILL NEVER PAY FOR ITSELF": GOSUB 400: GOSUB 300
230  PRINT "OPTIONS=": PRINT : PRINT "1) CHANGE TYPE AND/OR COST OF": PRINT "   THE WOODBURNER": PRINT "2) CHANGE THE AMOUNT OF HEAT YOU": PRINT "   WANT FROM THE WOODBURNER"
240  PRINT "3) CHANGE THE TYPE AND/OR PRICE": PRINT "   OF WOOD YOU WILL BURN": PRINT "4) CHANGE AMOUNT YOU PAID": PRINT "   FOR YOUR PRESENT FUEL": PRINT "5) QUIT"
250  PRINT : INPUT OP: ON OP GOTO 290,260,100,260,410
260  PRINT : PRINT "ENTER NEW VALUE=": INPUT X: GOSUB 320: ON OP GOTO 0,270,0,280,0
270 BT = BT / PH:FC = FC / PH:PH = X / 100: GOSUB 330: GOTO 170
280 FC = X * PH: GOTO 170
290  GOSUB 320:BC = BC / WE: GOSUB 370: GOSUB 380: GOSUB 340: GOSUB 320: GOTO 170
300  PRINT : PRINT "PRESS THE RETURN OR ENTER KEY": INPUT K$
310  REM  LINE 320: USE YOUR COMPUTER'S SCREEN-CLEARING COMMAND
320  CLS:RETURN
330 BT = BT * PH:FC = FC * PH: RETURN
340 AS = FC - WC: IF AS = 0 THEN  GOTO 360
350 PB = CW / AS: RETURN
360 PB = 0: RETURN
370  PRINT "SYSTEM EFFICIENCY OF THE": PRINT "WOODBURNING DEVICE YOU": PRINT "WANT TO INSTALL=": INPUT WE: PRINT : PRINT "COST OF BUYING AND": PRINT "INSTALLING IT=": INPUT CW: RETURN
380 BC = BC * WE: RETURN
390  PRINT "A WOODBURNING DEVICE WITH": PRINT "A SYSTEM EFFICIENCY OF ";WE: RETURN
400  PRINT "IF YOUR PRESENT FUEL COSTS": PRINT "$";FC / PH;" A YEAR AND YOU": PRINT "SWITCH TO ";W$;" COSTING": PRINT "$";CC;" PER CORD.": RETURN
410  GOSUB 320: PRINT "(END)": END
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0461

     Volume in drive A has no label
     Directory of A:\

    AVERAGE  DAT       128   1-23-86  12:04a
    CONV     BAS      3929  10-09-85   9:03a
    FILES461 TXT      1199   2-10-86   9:36a
    LUMBER   BAS      3456   8-15-85   2:56p
    LUMBER   DOC      2688   8-15-85   2:42p
    METRICON BAS     34502   9-15-85  12:18a
    METRICON EXE     75520   9-15-85  12:36a
    QUIZ     BAS     32558   9-12-85   1:43p
    QUIZ     EXE     69632   9-15-85  12:33a
    README   DOC      1024  10-09-85   9:40a
    SHRINK   EXE     43904   9-11-85   1:55a
    SILENCE  COM      7424   8-21-85  12:29p
    TREEFIX  BAS       624   4-04-85   1:41p
    TREEFIX  BAT        78   4-04-85   1:15p
    USLE     BAS     17920   8-14-85   1:18p
    USLE     DOC      7808   8-15-85   2:43p
    WOODHEAT BAS      2747   3-15-85   1:13p
    WOODHEAT TXT     20352   3-15-85   1:09p
           18 file(s)     325493 bytes
                           29696 bytes free
