---
layout: page
title: "PC-SIG Library Disk #2169"
permalink: /software/pcx86/sw/misc/pcsig/2000-2999/DISK2169/
machines:
  - id: ibm5170
    type: pcx86
    config: /machines/pcx86/ibm/5170/cga/1024kb/rev3/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #2169"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5170" %}

{% comment %}info_begin{% endcomment %}

## Information about "HOME CONTROL"

    Have your computer turn your lights and appliances on and off!  Build
    yourself a computerized home.
    
    Someday all homes will be intelligent.  You can get on-board now with
    the HOME CONTROL PRIMER, an excellent introduction to home automation.
    Light and appliance control is accomplished by using the X-10
    Powerhouse Computer Interface. The HOME CONTROL PRIMER consists of three
    independent programs:
    
    ~ SENDX10 turns lights and appliances on/off from the DOS command line
    or from batch files.
    
    ~ SYNCHX10 synchronizes the clock inside the X-10 Powerhouse Computer
    Interface with that of your PC.
    
    ~ GAMEPORT explores the analog and digital input capabilities of the PC
    gameport.  The Game Port Monitor lets you hook up sensors to your PC's
    game port so you can begin to experiment with environment monitoring.
    You can measure light intensity in a room, monitor windows and doors,
    measure temperature, and observe human movement through open spaces.
    
    Taken together, these programs will teach you the fundamentals of
    computerized home control.  Move your home into the 21st Century today!
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## EXAMP1.BAS

```bas
5 CLS:PRINT "SETTING THE REAL TIME CLOCK ":PRINT
10 GOSUB 20000
20 INPUT "ENTER THE DAY (1=SUN .. 7=SAT) ";DAY
30 INPUT "ENTER THE HOURS ( 0..23 )      ";HOURS
40 INPUT "ENTER THE MINUTES ( 0..59 )    ";MINS
50 GOSUB 20100:IF STATUS =-1 THEN PRINT " **** ERROR ****":END
60 PRINT:PRINT "THE TIME HAS BEEN SET ":END
19999 CLS:PRINT"ERROR RUNNING INTO UTILITIES......":END
```

## EXAMP2.BAS

```bas
5 CLS:PRINT "SETTING THE BASE HOUSECODE  ":PRINT
10 GOSUB 20000
20 INPUT "ENTER BASE HOUSECODE ";A$:BASECODE=ASC(A$)-64
30 GOSUB 20200:IF STATUS =-1 THEN PRINT " **** ERROR ****":END
40 PRINT:PRINT "THE BASE HOUSECODE HAS BEEN SET ":END
19999 CLS:PRINT"ERROR RUNNING INTO UTILITIES......":END
```

## EXAMP3.BAS

```bas
10 GOSUB 20000
20 GOSUB 20300:IF STATUS =-1 THEN PRINT " *** ERROR *** ":END
30 PRINT " DAY = ";DAY;" HOURS = ";HOURS;" MINS = ";MINS
40 END
```

## EXAMP4.BAS

```bas
10 GOSUB 20000
20 LEVEL=0
30 INPUT "ENTER THE FUNCTION (1=OFF 2=ON 3=DIM) ";FUNCTION
40 IF FUNCTION=3 THEN PRINT :INPUT "ENTER LEVEL ";LEVEL
50 PRINT:INPUT" ENTER THE HOUSECODE (A-P) ";A$:HOUSECODE=ASC(A$)-64
60 PRINT:INPUT "ENTER THE VALUE FOR HIUNIT (0=NONE ) ";HIUNIT
70 PRINT:INPUT "ENTER THE VALUE FOR LOUNIT (0=NONE ) ";LOUNIT
80 GOSUB 20500:IF STATUS =-1 THEN PRINT " **** ERROR ****":END
90 PRINT "THE X-10 MESSAGE HAS BEEN SENT ":END
19999 CLS:PRINT"ERROR RUNNING INTO UTILITIES......":END
```

## EXAMP5.BAS

```bas
5 CLS:PRINT "DOWNLOADING GRAPHICS DATA   ":PRINT
10 GOSUB 20000
20 INPUT "ENTER THE ADDRESS (0-255)";ADDRESS
30 PRINT:INPUT "DATA 1 ";GRAPH (0,ADDRESS)
40 PRINT:INPUT "DATA 2 ";GRAPH (1,ADDRESS)
50 GOSUB 21000 :IF STATUS =-1 THEN PRINT " *** ERROR *** ":END
60 PRINT " DONE ":END
```

## EXAMP6.BAS

```bas
5 CLS:PRINT "UPLOADING GRAPHICS DATA   ":PRINT
10 GOSUB 20000
20 GOSUB 21100:IF STATUS =-1 THEN PRINT " *** ERROR *** ":END
30 PRINT " DONE ":END
```

## EXAMP7.BAS

```bas
5 CLS:PRINT "DOWNLOADING TIMER EVENT  ":PRINT
10 GOSUB 20000
20 INPUT "ENTER THE ADDRESS (0-127)";ADDRESS
30 FOR X=0 TO 7
40 PRINT "ENTER DATA FOR BYTE ";20+X;:INPUT TIM (X, ADDRESS): NEXT X
50 GOSUB 20800 :IF STATUS =-1 THEN PRINT " *** ERROR *** ":END
60 PRINT " DONE ":END
```

## EXAMP8.BAS

```bas
5 CLS:PRINT "UPLOADING TIMER EVENT  ":PRINT
10 GOSUB 20000
20 GOSUB 20900:IF STATUS =-1 THEN PRINT " *** ERROR *** ":END
30 PRINT " DONE ":END
```

## X10.BAS

```bas
19999 CLS:PRINT"ERROR RUNNING INTO UTILITIES......":END
20000 '*************** SETUP ROUTINE ************************
20001 '
20002 ' (C) 1986 X10 (USA) inc          GD - 07/05/86
20003 '
20005 DIM CODCONV(16) , TIM( 7,128)
20008 DIM GRAPH(2,256)
20015 OPEN "COM1:600,N,8,1,CS,DS" AS #1
20020 DAYCONV(1)=64:DAYCONV(2)=1:DAYCONV(3)=2
20030 DAYCONV(4)=4:DAYCONV(5)=8:DAYCONV(6)=16:DAYCONV(7)=32
20040 HOURS=0:MINS=0
20050 CODCONV(1)=96:CODCONV(2)=224:CODCONV(3)=32:CODCONV(4)=160:CODCONV(5)=16
20060 CODCONV(6)=144:CODCONV(7)=80:CODCONV(8)=208:CODCONV(9)=112
20070 CODCONV(10)=240:CODCONV(11)=48:CODCONV(12)=176:CODCONV(13)=0
20080 CODCONV(14)=128:CODCONV(15)=64:CODCONV(16)=192
20099 RETURN
20100 '************ SET TIME IN INTERFACE *******************
20101 '
20110 GOSUB 30300
20125 A=2:GOSUB 30000
20130 A=MINS:GOSUB 30000
20140 A=HOURS:GOSUB 30100
20150 A=DAYCONV(DAY):GOSUB 30100
20160 CHKSUM=(CHKSUM AND 255):A=CHKSUM:GOSUB 30600
20170 GOTO 30200
20200 '************ SET BASE HOUSECODE **********************
20201 '
20210 GOSUB 30300
20220 A=0:GOSUB 30000
20230 A=CODCONV(BASECODE):GOSUB 30000
20240 GOTO 30200
20300 '**************** READ THE TIME ***********************
20301 '
20310 GOSUB 30300
20320 A=4:GOSUB 30000
20370 GOSUB 30500:IF STATUS = -1 THEN RETURN
20375 STATUS=A
20380 GOSUB 30400:IF STATUS=-1 THEN RETURN
20385 MINS=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20390 HOURS=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20395 BTEMP=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20396 C=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20400 FOR BASECODE=1 TO 16:IF HTEMP=CODCONV(BASECODE) THEN 20420
20410 NEXT BASECODE
20420 FOR DAY=1 TO 7:IF BTEMP=DAYCONV(DAY) THEN RETURN
20430 NEXT DAY
20440 RETURN
20500 '****************** X10 DIRECT ************************
20501 '
20510 IF FUNCTION=2 THEN FTEMP=3:GOTO 20540
20520 IF FUNCTION=3 THEN FTEMP=5+(240 AND (LEVEL*16)):GOTO 20540
20530 FTEMP=2
20540 HTEMP=CODCONV(HOUSECODE)
20560 GOSUB 30300
20570 A=1:GOSUB 30000:A=FTEMP:GOSUB 30000:A=HTEMP:GOSUB 30100:A=HIUNIT:GOSUB 30100
20580 A=LOUNIT:GOSUB 30100:A=(CHKSUM AND 255):GOSUB 30000
20590 GOSUB 30200:IF STATUS=-1 THEN RETURN
20600 GOSUB 30500:IF STATUS=-1 THEN RETURN
20610 STATUS =A
20660 GOSUB 30400:IF STATUS=-1 THEN RETURN
20670 FTEMP=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20680 HIUNIT=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20685 LOUNIT=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20690 BTEMP=A:A=(FTEMP AND 240)
20695 FOR HOUSECODE=1 TO 16
20700 IF A<>CODCONV(HOUSECODE) THEN NEXT HOUSECODE
20710 FOR BASECODE=1 TO 16
20720 IF BTEMP<>CODCONV(BASECODE) THEN NEXT BASECODE
20740 A=(FTEMP AND 15):FUNCTION=1:IF A=4 THEN FUNCTION=3
20750 IF A=3 THEN FUNCTION =2
20760 RETURN
20800 '************** DOWNLOAD TIMER EVENT ******************
20801 '
20810 ADDR1=((ADDRESS) AND 31)*8:ADDR2=((ADDRESS) AND 96)/32
20820 GOSUB 30300:A=3:GOSUB 30000
20830 A=ADDR1:GOSUB 30000:A=ADDR2:GOSUB 30000
20840 A=TIM(0,ADDRESS):GOSUB 30000
20845 FOR XTEMP=1 TO 7
20847 A=TIM(XTEMP,ADDRESS):GOSUB 30100
20848 NEXT XTEMP
20850 A=CHKSUM AND 255 :GOSUB 30000
20860 GOTO 30200
20900 '*************** UPLOAD TIMER EVENTS ******************
20901 '
20910 GOSUB 30300:A=5:GOSUB 30000
20920 GOSUB 30500:IF STATUS=-1 THEN RETURN
20925 EVENT =0
20930 GOSUB 30400:IF STATUS=-1 THEN RETURN
20935 IF A=255 THEN 20970
20940 FOR XTEMP=0 TO 6
20945 TIM(XTEMP,EVENT)=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
20947 NEXT XTEMP:TIM(7,EVENT)=A
20948 EVENT=EVENT+1:IF EVENT < 128 THEN 20930
20950 GOTO 30400:IF STATUS=-1 THEN RETURN
20970 FOR XTEMP=0 TO 7:TIM(XTEMP,EVENT)=0
20980 NEXT XTEMP:GOTO 20948
21000 '************** DOWNLOAD GRAPHICS DATA ****************
21001 '
21010 ADDR1=((ADDRESS) AND 127)*2:ADDR2=4+(((ADDRESS) AND 128)/128)
21020 GOSUB 30300:A=3:GOSUB 30000
21030 A=ADDR1:GOSUB 30000:A=ADDR2:GOSUB 30000
21040 A=GRAPH(0,ADDRESS):GOSUB 30000
21050 A=GRAPH(1,ADDRESS):GOSUB 30100
21060 A=CHKSUM AND 255 :GOSUB 30000
21070 GOTO 30200
21100 '**************** UPLOAD GRAPHICS DATA ****************
21101 '
21110 GOSUB 30300:A=6:GOSUB 30000
21120 GOSUB 30500:IF STATUS=-1 THEN RETURN
21125 EVENT=0
21130 GOSUB 30400::IF STATUS=-1 THEN RETURN
21135 IF A=255 THEN GRAPH(1,EVENT)=0:GRAPH(2,EVENT)=0:GOTO 21150
21140 GRAPH(0,EVENT)=A:GOSUB 30400:IF STATUS=-1 THEN RETURN
21141 GRAPH(1,EVENT)=A
21150 EVENT=EVENT+1:IF EVENT < 256 THEN 21130
21160 GOTO 30400
30000 CHKSUM=0
30100 A1=LOF(1):CHKSUM=CHKSUM+A:GOSUB 30600
30110 IF LOF(1)<>A1 THEN 30110
30120 RETURN
30200 '***************** GET ACKN ***************************
30201 '
30220 GOSUB 30500:IF STATUS=-1 THEN RETURN
30250 STATUS=A
30270 RETURN
30300 '***************** START CODE *************************
30301 '
30310 FOR XTEMP=1 TO 16
30320 A=255:GOSUB 30600: NEXT XTEMP
30330 RETURN
30400 '***************** GET RS 232 DATA ********************
30405 COUNT=0
30410 IF COUNT> 1500 THEN STATUS=-1:RETURN
30420 IF LOF(1) = 256  THEN COUNT=COUNT+1:GOTO 30410
30430 GOSUB 30700:A=ASC(A$):RETURN
30500 '***************** GET IN SYNC ************************
30501 '
30510 STATUS=0:COUNT=0 :XTEMP =LOF(1)
30520 IF COUNT> 1500 THEN STATUS=-1:RETURN
30530 IF LOF(1) =XTEMP	THEN COUNT=COUNT+1:GOTO 30520
30540 GOSUB 30700 : A=ASC(A$) : IF A=255 THEN XTEMP=XTEMP+1 : GOTO 30520
30550 RETURN
30600 ' **************** O/P TO PORT ***************
30601 '
30610 PRINT # 1,CHR$(A);:RETURN
30700 ' **************** I/P TO PORT ***************
30701 '
30710 A$=INPUT$(1,#1):RETURN
```

## X10TEST.BAS

```bas
10000 DEFINT A-Z                : REM make everybody here integers
10100 REM ----- this stuff must be done just once at the start
10200 INTNUM = &H63             : REM X10 interrupt number
10300 REM fetch the X10 interrupt vector
10400 DEF SEG=0
10500 INTOFFS = 4*INTNUM        : REM figure vector offset
10700 XOFF=256*PEEK(INTOFFS+3)+PEEK(INTOFFS+2)  : REM get X10 vector
10800 XSEG=256*PEEK(INTOFFS+3)+PEEK(INTOFFS+2)
10900 IF (XOFF <> 0) AND (XSEG <> 0) GOTO 11500
11000 PRINT "X10DRIVE isn't installed!"
11100 STOP
11500 X10=&H104                 : REM set BASIC CALL entry offset
11600 DEF SEG=XSEG              : REM  ... and segment
12000 REM ----- now for the useful code
12100 HOUSE=ASC("A")            : REM CALL parameter must be an integer!
12200 UNIT=1                    : REM unit from 0 - 16
12300 ACTION=1                  : REM action from 0 - 6
12400 PRINT "Making the call!"
12500 CALL X10(HOUSE,UNIT,ACTION)
12600 PRINT "Return code is",HOUSE
12700 REM ----- repeat the above as needed
12800 STOP
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #2169

     Volume in drive A has no label
     Directory of A:\

    BBSMSGS1 TXT     12654   6-01-88   1:50a
    BBSMSGS2 TXT      6930   6-22-88  10:48p
    BBSMSGS3 TXT     11959   6-26-88  10:52p
    BBSMSGS4 TXT     32968   6-23-88   9:05a
    BBSMSGS5 TXT      8827   6-29-88   7:54p
    CONTENTS TXT      2261  12-16-89   2:24p
    EXAMP1   BAS       384   4-12-88   6:51a
    EXAMP2   BAS       256   4-12-88   6:52a
    EXAMP3   BAS       128   4-12-88   6:52a
    EXAMP4   BAS       512   4-12-88   6:53a
    EXAMP5   BAS       256   4-12-88   6:53a
    EXAMP6   BAS       128   4-12-88   6:54a
    EXAMP7   BAS       256   4-12-88   6:54a
    EXAMP8   BAS       128   4-12-88   6:55a
    GAMEPORT DOC      8448  12-16-89   2:01p
    GAMEPORT EXE     54116  11-18-89  11:49a
    HCSVIDEO TXT      2458   6-22-88  10:33p
    SENDX10  DOC      4684  12-16-89   2:06p
    SENDX10  EXE     47078  11-18-89  11:45a
    SYNCHX10 DOC      4418  12-16-89   2:10p
    SYNCHX10 EXE     45870  11-18-89  11:47a
    TSRX10   TXT       912  11-20-89   3:45p
    X10      BAS      5632   4-13-88   7:50a
    X10COCO  TXT      1808   5-01-88   6:24p
    X10GUIDE TXT     12843   4-14-88   8:59a
    X10TEST  BAS      1042   2-18-88   9:49p
    HOMECNTL REF      1093  12-16-89   1:31p
    GO       BAT        38  10-19-87   3:56p
    GO       TXT       925   4-26-90   4:38a
    FILE2169 TXT      2591   4-26-90   5:56p
           30 file(s)     271603 bytes
                           33792 bytes free
