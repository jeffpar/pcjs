940 REM The IBM Personal Computer Colorbar
950 REM Version 1.00 (C)Copyright IBM Corp 1981
960 REM Licensed Material - Program Property of IBM
970 REM Author - Glenn Stuart Dardick
975 DEF SEG: POKE 106,0
980 SAMPLE$="NO"
990 GOTO 1010
1000 SAMPLE$="YES"
1010 KEY OFF:SCREEN 0,1:COLOR 15,0:WIDTH 40:CLS:LOCATE 5,19:PRINT "IBM"
1020 LOCATE 7,12,0:PRINT "Personal Computer"
1030 COLOR 10,0:LOCATE 10,9,0:PRINT CHR$(213)+STRING$(21,205)+CHR$(184)
1040 LOCATE 11,9,0:PRINT CHR$(179)+"       COLORBAR      "+CHR$(179)
1050 LOCATE 12,9,0:PRINT CHR$(179)+STRING$(21,32)+CHR$(179)
1060 LOCATE 13,9,0:PRINT CHR$(179)+"    Version 1.00     "+CHR$(179)
1070 LOCATE 14,9,0:PRINT CHR$(212)+STRING$(21,205)+CHR$(190)
1080 COLOR 15,0:LOCATE 17,7,0:PRINT "(C) Copyright IBM Corp 1981"
1090 COLOR 14,0:LOCATE 23,7,0:PRINT "Press space bar to continue"
1100 IF INKEY$ <> "" THEN GOTO 1100
1110 CMD$ = INKEY$
1120 IF CMD$ = " " THEN GOTO 1150
1130 IF CMD$ = CHR$(27) THEN GOTO 1490
1140 GOTO 1110
1150 COLOR 7,0:CLS
1160 KEY OFF
1170 SCREEN 0,1
1180 WIDTH 40
1190 COLOR 0,0
1200 CLS
1210 FOR I = 0 TO 7
1220 FOR J=2 TO 11
1230 LOCATE J,I*5+1
1240 COLOR I,I:PRINT CHR$(219);CHR$(219);CHR$(219);
1250 NEXT J
1260 NEXT I
1270 LOCATE 12,1
1280 COLOR 15,0
1290 PRINT "blk  blue grn  cyan red  mag  brn white";
1300 LOCATE 13,1
1310 COLOR 15,0
1320 PRINT "                                       ";
1330 FOR I = 0 TO 7
1340 FOR J=14 TO 22
1350 LOCATE J,I*5+1
1360 COLOR I+8,I+8:PRINT CHR$(219);CHR$(219);CHR$(219);
1370 NEXT J
1380 NEXT I
1390 LOCATE 23,1
1400 COLOR 15,0
1410 PRINT "dark lght lght lght lght lght yel  int";
1420 LOCATE 24,1,0
1430 COLOR 15,0
1440 PRINT "grey blue grn  cyan red  mag      white";
1450 LOCATE 1,1
1460 PRINT "Press ESC key to exit";
1470 IF INKEY$<>CHR$(27)THEN GOTO 1470
1480 'DELETE
1490 SCREEN 0
1500 COLOR 7,0
1510 CLS
1520 IF SAMPLE$="YES" THEN CHAIN "SAMPLES",1000
